Перем мДлинаСуток;

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция РассчитатьКалендарныеДниБолезни(ДатаНачалаСобытия, ДатаНачала, ДатаОкончания, ПричинаНетрудоспособности, СлучайУходаЗаБольнымРебенком) Экспорт 
	
	ДнейОплаты = 0;
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) Тогда
		Если ДатаНачалаСобытия <> ДатаНачала Тогда
			ДнейКОплате = (ДатаОкончания - ДатаНачалаСобытия) / мДлинаСуток + 1;
			ОплаченоРанее = (ДатаНачала - ДатаНачалаСобытия) / мДлинаСуток;
		Иначе
			ДнейКОплате = (ДатаОкончания - ДатаНачала) / мДлинаСуток + 1;
			ОплаченоРанее = 0;
		КонецЕсли;
		
		// учтем ограничение периода оплаты по одному страховому случаю
		Если ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПособиеПриДолечивании Тогда
			ДнейОплаты = Макс(Мин(ДнейКОплате, 24) - ОплаченоРанее,0) // п.2 ст.6 Федерального закона от 29 декабря 2006 г. № 255-ФЗ
		ИначеЕсли ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаВзрослым Тогда
			ДнейОплаты = Макс(Мин(ДнейКОплате, 7) - ОплаченоРанее,0) // пп. 6 п.5 ст.6 Федерального закона от 29 декабря 2006 г. № 255-ФЗ
		ИначеЕсли СлучайУходаЗаБольнымРебенком = Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо15тиЛетАмбулаторно
			Или СлучайУходаЗаБольнымРебенком = Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо15тиЛетВСтационаре Тогда
			ДнейОплаты = Макс(Мин(ДнейКОплате, 15) - ОплаченоРанее,0) // пп. 2 п.5 ст.6 Федерального закона от 29 декабря 2006 г. № 255-ФЗ
		Иначе
			ДнейОплаты = ДнейКОплате
		КонецЕсли;
	КонецЕсли;

	Возврат ДнейОплаты
	
КонецФункции

Функция ВернутьТекстВТПоСостояниюРаботников()
	
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаСостояний.Период КАК Период,
	|	ТаблицаСостояний.Регистратор КАК Регистратор,
	|	ТаблицаСостояний.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСостояний.Активность,
	|	ТаблицаСостояний.Сотрудник КАК Сотрудник,
	|	ТаблицаСостояний.Организация,
	|	ТаблицаСостояний.Состояние КАК Состояние,
	|	ТаблицаСостояний.ПериодЗавершения КАК ПериодЗавершения,
	|	ТаблицаСостояний.СостояниеЗавершения,
	|	ТаблицаСостояний.ПервичныйДокумент,
	|	ТаблицаСостояний.ВидДополнительногоОтпуска,
	|	ТаблицаСостояний.ДатаНачалаДоп
	|ПОМЕСТИТЬ ВТТаблицаСостояний
	|ИЗ
	|	РегистрСведений.СостояниеРаботниковОрганизаций КАК ТаблицаСостояний
	|ГДЕ
	|	(&НеОтбиратьПоСотруднику
	|			ИЛИ ТаблицаСостояний.Сотрудник = &Сотрудник)
	|	И (&НеОтбиратьПоОрганизации
	|			ИЛИ ТаблицаСостояний.Организация В
	|				(ВЫБРАТЬ
	|					СписокГоловныхОрганизаций.Ссылка
	|				ИЗ
	|					ВТГоловныеОрганизации КАК СписокГоловныхОрганизаций))
	|	И (НЕ ТаблицаСостояний.Регистратор ССЫЛКА Документ.ПереносДанных)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	Период,
	|	НомерСтроки,
	|	Сотрудник,
	|	Состояние,
	|	ПериодЗавершения";
	
КонецФункции

Функция ВернутьТекстВТПоИсправляемымДокументам(ПараметрыЗапроса, СоответствиеДокументовИсправлений)
	
	МДСостояний = Метаданные.РегистрыСведений.СостояниеРаботниковОрганизаций;
	
	СписокДляВыбора = Новый СписокЗначений;
	СпискиРегистраторов = глЗначениеПеременной("глСпискиРегистраторов");
	СпискиРегистраторов.Свойство("СостояниеРаботниковОрганизаций", СписокДляВыбора);
	СтрокаТипов = "";
	Для каждого ЭлементСписка Из СписокДляВыбора Цикл
		ИмяДокумента = ЭлементСписка.Значение.ПустаяСсылка().Метаданные().Имя; // из списка значений получаем менеджер документа
		СтрокаТипов = СтрокаТипов + ?(Не ЗначениеЗаполнено(СтрокаТипов),"",",") + "ДокументСсылка." + ИмяДокумента;
	КонецЦикла;

	ТаблицаСостояний = Новый ТаблицаЗначений;
	ТаблицаСостояний.Колонки.Добавить("Регистратор",	Новый ОписаниеТипов(СтрокаТипов));
	ТаблицаСостояний.Колонки.Добавить("Период",			Новый ОписаниеТипов("Дата"));
	ТаблицаСостояний.Колонки.Добавить("НомерСтроки",	Новый ОписаниеТипов("Число"));
	Для Каждого Измерение Из МДСостояний.Измерения Цикл
		ТаблицаСостояний.Колонки.Добавить(Измерение.Имя, Измерение.Тип);
	КонецЦикла;
	Для Каждого Ресурс Из МДСостояний.Ресурсы Цикл
		ТаблицаСостояний.Колонки.Добавить(Ресурс.Имя, Ресурс.Тип);
	КонецЦикла;
	Для Каждого Реквизит Из МДСостояний.Реквизиты Цикл
		ТаблицаСостояний.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из СоответствиеДокументовИсправлений Цикл
		ДвиженияИсправляемогоДокумента = КлючИЗначение.Значение.ДвиженияИсправляемогоДокумента.Получить();
		
		Если ДвиженияИсправляемогоДокумента = Неопределено ИЛИ ДвиженияИсправляемогоДокумента.Получить("СостояниеРаботниковОрганизаций") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СостоянияРаботников = ДвиженияИсправляемогоДокумента["СостояниеРаботниковОрганизаций"];
		Для Каждого Состояние Из СостоянияРаботников Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаСостояний.Добавить(), Состояние);
		КонецЦикла;
	КонецЦикла;
	
	ПараметрыЗапроса.Вставить("ТаблицаСостояний",	ТаблицаСостояний);
	
	Возврат
	"ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ ВТТаблицаСостояний
	|ИЗ
	|	&ТаблицаСостояний КАК ТаблицаСостояний
	|
	|ГДЕ
	|	(&НеОтбиратьПоСотруднику
	|			ИЛИ ТаблицаСостояний.Сотрудник = &Сотрудник)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	Период,
	|	НомерСтроки,
	|	Сотрудник,
	|	Состояние,
	|	ПериодЗавершения";
	
КонецФункции

// Формирует запрос по регистру СостояниеРаботниковОрганизаций
//
// Параметры:
//	Нет
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапрос(Организации = Неопределено, ПодразделениеОрганизации, Регистраторы, ДатаНачала, ДатаОкончания, МаксимальноеКоличествоСобытий = 0, ТолькоСОтметкой = Ложь, Сотрудник = Неопределено, СоответствиеДокументовИсправлений = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистраторы",				Регистраторы);
	Запрос.УстановитьПараметр("ДатаНачала",					НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания",				КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("СписокОрганизаций",			Организации);
	Запрос.УстановитьПараметр("ПодразделениеОрганизации",	ПодразделениеОрганизации);
	Запрос.УстановитьПараметр("Сотрудник",					Сотрудник);
	Запрос.УстановитьПараметр("ОтбиратьСобытияПоДокументу",	ЗначениеЗаполнено(Регистраторы));
	Запрос.УстановитьПараметр("НеОтбиратьПоПодразделению",	Не ЗначениеЗаполнено(ПодразделениеОрганизации));
	Запрос.УстановитьПараметр("НеОтбиратьПоСотруднику",		Не ЗначениеЗаполнено(Сотрудник));
	Запрос.УстановитьПараметр("НеОтбиратьПоОрганизации",	Не ЗначениеЗаполнено(Организации));
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Определим периоды смены (реальные или намеченные) состояния работников за указанный пользователем интервал.
	// Выбираем намеченные события, зарегистрированные тем же регистратором.
	// Составим список возможных расчетных документов за соответствующий период регистрации
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Организации.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Организации.Ссылка
	|		ИНАЧЕ Организации.ГоловнаяОрганизация
	|	КОНЕЦ КАК Ссылка
	|ПОМЕСТИТЬ ВТГоловныеОрганизации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В(&СписокОрганизаций)";
	Запрос.Выполнить();
	
	Если СоответствиеДокументовИсправлений = Неопределено Тогда
		Запрос.Текст = ВернутьТекстВТПоСостояниюРаботников();
	Иначе
		Запрос.Текст = ВернутьТекстВТПоИсправляемымДокументам(Запрос.Параметры, СоответствиеДокументовИсправлений);
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СостояниеРаботников.Сотрудник КАК Сотрудник,
	|	СостояниеРаботников.Сотрудник.Физлицо КАК ФизЛицо,
	|	СостояниеРаботников.Период КАК ДатаНачала,
	|	ЕСТЬNULL(МИНИМУМ(ДОБАВИТЬКДАТЕ(СостояниеБудущее.Период, ДЕНЬ, -1)), БудущиеНамеченныеСобытия.Период) КАК ДатаОкончания,
	|	СостояниеРаботников.Состояние КАК Состояние,
	|	СостояниеРаботников.Регистратор КАК КадровыйДокумент,
	|	ОтпускаРаботниковОрганизации.ВидДополнительногоОтпуска,
	|	ОтпускаРаботниковОрганизации.ДатаНачалаДоп,
	|	ОтпускаРаботниковОрганизации.РабочийГодС,
	|	ОтпускаРаботниковОрганизации.РабочийГодПо,
	|	СостояниеРаботников.Организация
	|ПОМЕСТИТЬ ВТСостояниеРаботников
	|ИЗ
	|	ВТТаблицаСостояний КАК СостояниеРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтпускаОрганизаций.РаботникиОрганизации КАК ОтпускаРаботниковОрганизации
	|		ПО СостояниеРаботников.Регистратор = ОтпускаРаботниковОрганизации.Ссылка
	|			И СостояниеРаботников.Период = ОтпускаРаботниковОрганизации.ДатаНачала
	|			И СостояниеРаботников.Сотрудник = ОтпускаРаботниковОрганизации.Сотрудник
	|			И (&НеОтбиратьПоСотруднику
	|				ИЛИ СостояниеРаботников.Сотрудник = &Сотрудник)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СостояниеРаботников.Сотрудник КАК Сотрудник,
	|			СостояниеРаботников.Период КАК Период,
	|			СостояниеРаботников.Период КАК ДатаНачала
	|		ИЗ
	|			ВТТаблицаСостояний КАК СостояниеРаботников
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			СостояниеРаботников.Сотрудник,
	|			СостояниеРаботников.ПериодЗавершения,
	|			СостояниеРаботников.Период
	|		ИЗ
	|			ВТТаблицаСостояний КАК СостояниеРаботников
	|				ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаСостояний КАК СостояниеРаботниковПроверка
	|				ПО (СостояниеРаботниковПроверка.Период > СостояниеРаботников.Период)
	|					И (СостояниеРаботниковПроверка.Период <= СостояниеРаботников.ПериодЗавершения)
	|					И СостояниеРаботников.Сотрудник = СостояниеРаботниковПроверка.Сотрудник
	|		ГДЕ
	|			СостояниеРаботников.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|			И СостояниеРаботниковПроверка.Сотрудник ЕСТЬ NULL ) КАК СостояниеБудущее
	|		ПО СостояниеРаботников.Период < СостояниеБудущее.Период
	|			И СостояниеРаботников.Сотрудник = СостояниеБудущее.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МИНИМУМ(ДОБАВИТЬКДАТЕ(НамеченныеСобытия.ДатаИзменения, ДЕНЬ, -1)) КАК Период,
	|			СостояниеРаботников.Регистратор КАК Регистратор,
	|			СостояниеРаботников.НомерСтроки КАК НомерСтроки
	|		ИЗ
	|			ВТТаблицаСостояний КАК СостояниеРаботников
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НамеченныеСобытияПоПерсоналу КАК НамеченныеСобытия
	|				ПО СостояниеРаботников.Регистратор = НамеченныеСобытия.Регистратор
	|					И СостояниеРаботников.Период <= НамеченныеСобытия.ДатаИзменения
	|					И (ВЫБОР
	|						КОГДА СостояниеРаботников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускЕжегодный)
	|								И НамеченныеСобытия.ПланируемоеСобытие = ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналу.ОтпускЕжегодный)
	|							ТОГДА ИСТИНА
	|						КОГДА СостояниеРаботников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный)
	|								И НамеченныеСобытия.ПланируемоеСобытие = ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналу.ОтпускУчебный)
	|							ТОГДА ИСТИНА
	|						КОГДА СостояниеРаботников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебныйНеоплачиваемый)
	|								И НамеченныеСобытия.ПланируемоеСобытие = ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналу.ОтпускУчебныйНеоплачиваемый)
	|							ТОГДА ИСТИНА
	|						КОГДА СостояниеРаботников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускБезСохраненияЗарплаты)
	|								И НамеченныеСобытия.ПланируемоеСобытие = ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналу.ОтпускУчебныйНеоплачиваемый)
	|							ТОГДА ИСТИНА
	|						КОГДА СостояниеРаботников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Командировка)
	|								И НамеченныеСобытия.ПланируемоеСобытие = ЗНАЧЕНИЕ(Перечисление.НамеченныеСобытияПоПерсоналу.Командировка)
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ ЛОЖЬ
	|					КОНЕЦ)
	|					И СостояниеРаботников.Сотрудник = НамеченныеСобытия.Сотрудник
	|		ГДЕ
	|			((НЕ &ОтбиратьСобытияПоДокументу)
	|					ИЛИ СостояниеРаботников.Регистратор В (&Регистраторы))
	|			И (&ОтбиратьСобытияПоДокументу
	|					ИЛИ СостояниеРаботников.Регистратор.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СостояниеРаботников.Регистратор,
	|			СостояниеРаботников.НомерСтроки) КАК БудущиеНамеченныеСобытия
	|		ПО СостояниеРаботников.Регистратор = БудущиеНамеченныеСобытия.Регистратор
	|			И СостояниеРаботников.НомерСтроки = БудущиеНамеченныеСобытия.НомерСтроки
	|ГДЕ
	|	(НЕ СостояниеРаботников.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает), ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.НеРаботает), ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускПоУходуЗаРебенком)))
	|	И ((НЕ &ОтбиратьСобытияПоДокументу)
	|			ИЛИ СостояниеРаботников.Регистратор В (&Регистраторы))
	|	И (&ОтбиратьСобытияПоДокументу
	|			ИЛИ СостояниеРаботников.Регистратор.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)
	|
	|СГРУППИРОВАТЬ ПО
	|	СостояниеРаботников.Организация,
	|	СостояниеРаботников.Регистратор,
	|	СостояниеРаботников.Состояние,
	|	СостояниеРаботников.Период,
	|	БудущиеНамеченныеСобытия.Период,
	|	ОтпускаРаботниковОрганизации.ВидДополнительногоОтпуска,
	|	ОтпускаРаботниковОрганизации.ДатаНачалаДоп,
	|	ОтпускаРаботниковОрганизации.РабочийГодС,
	|	ОтпускаРаботниковОрганизации.РабочийГодПо,
	|	СостояниеРаботников.Сотрудник,
	|	СостояниеРаботников.Сотрудник.Физлицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник";
	Запрос.Выполнить();
	
	Запрос.Текст = АнализНеявокПереопределяемый.ВернутьТекстВТСписокРасчетныхДокументов();
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ОсновныеНачисления.Ссылка) КАК ВидРасчета
	|ПОМЕСТИТЬ ВТНачислениеКомандировки
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработку)
	|	И ОсновныеНачисления.ВидВремени = ЗНАЧЕНИЕ(Перечисление.ВидыВремени.ЦелодневноеНеотработанное)
	|	И ОсновныеНачисления.ОбозначениеВТабелеУчетаРабочегоВремени = ЗНАЧЕНИЕ(Справочник.КлассификаторИспользованияРабочегоВремени.Командировка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОсновныеНачисления.Ссылка) КАК ВидРасчета
	|ПОМЕСТИТЬ ВТНачислениеУчебногоОтпуска
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуДляОтпускаПоКалендарнымДням), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуДляОтпускаПоШестидневке))
	|	И ОсновныеНачисления.ВидВремени = ЗНАЧЕНИЕ(Перечисление.ВидыВремени.ЦелодневноеНеотработанное)
	|	И ОсновныеНачисления.ОбозначениеВТабелеУчетаРабочегоВремени В (ЗНАЧЕНИЕ(Справочник.КлассификаторИспользованияРабочегоВремени.ПовышениеКвалификацииВДругойМестности), ЗНАЧЕНИЕ(Справочник.КлассификаторИспользованияРабочегоВремени.ПовышениеКвалификации), ЗНАЧЕНИЕ(Справочник.КлассификаторИспользованияРабочегоВремени.ОтпускНаОбучение))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОсновныеНачисления.Ссылка) КАК ВидРасчета
	|ПОМЕСТИТЬ ВТНачислениеВынужденныйПрогул
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработку)
	|	И ОсновныеНачисления.ВидВремени = ЗНАЧЕНИЕ(Перечисление.ВидыВремени.ЦелодневноеНеотработанное)
	|	И ОсновныеНачисления.ОбозначениеВТабелеУчетаРабочегоВремени = ЗНАЧЕНИЕ(Справочник.КлассификаторИспользованияРабочегоВремени.ВынужденныйПрогул)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ОсновныеНачисления.Ссылка) КАК ВидРасчета
	|ПОМЕСТИТЬ ВТНачислениеГосударственныеОбязанности
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработку)
	|	И ОсновныеНачисления.ВидВремени = ЗНАЧЕНИЕ(Перечисление.ВидыВремени.ЦелодневноеНеотработанное)
	|	И ОсновныеНачисления.ОбозначениеВТабелеУчетаРабочегоВремени = ЗНАЧЕНИЕ(Справочник.КлассификаторИспользованияРабочегоВремени.ГосударственныеОбязанности)";
	Запрос.Выполнить();
	
	ТекстЗапроса = АнализНеявокПереопределяемый.ВернутьТекстОсновногоЗапроса();
	
	Если МаксимальноеКоличествоСобытий = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПЕРВЫЕ 1","")
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПЕРВЫЕ 1"," ПЕРВЫЕ " + МаксимальноеКоличествоСобытий)
	КонецЕсли;
	Если Не ТолькоСОтметкой Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И РасчетныеИКадровыеДанные.Отметка","")
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапрос()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТКИ

// Заполняет т.ч. обработки кадровыми и расчетными документами
//
// Параметры:
//	Нет
//
// Возвращаемое значение:
//	Булево - Истина, если успешно выполнено заполнение, иначе Ложь
//
Функция Автозаполнение() Экспорт

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ttk_ОбщегоНазначения.СообщитьОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Необходимо указать организацию!"));
		Возврат Ложь;
	КонецЕсли;
	
	Если ОтбиратьСобытияПоДокументу Тогда
		ДатаН = КадровыйДокументОтбора.Дата;
		РезультатЗапроса = СформироватьЗапрос(Организация, ПодразделениеОрганизации, КадровыйДокументОтбора, ДатаН, ДатаН);
	Иначе
		РезультатЗапроса = СформироватьЗапрос(Организация, ПодразделениеОрганизации, Неопределено, ДатаНачалаОтбора, ДатаОкончанияОтбора);
	КонецЕсли;

	Неявки.Загрузить(РезультатЗапроса.Выгрузить());
	ГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);
	
	// обработаем табличную часть, проверяя на дублирование документов
	ПредыдущийКадровыйДокумент	= Неопределено;
	ПредыдущийСотрудник			= Неопределено;
	ПредыдущаяНеявка			= Неопределено;
	ПредыдущаяДатаНачала		= Неопределено;
	
	// структура для заполнения реквизитов расчетного документа
	ДанныеРасчетногоДокумента = Новый Структура("Сотрудник, Физлицо, ДатаНачала, ДатаОкончания, ПричинаНетрудоспособности, ПроцентОплаты, 
												|ОграничениеПособия, ДатаНачалаСобытия, ПроцентОплатыБезЛьгот, ОграничениеПособияБезЛьгот, 
												|ПрименятьЛьготыПриНачисленииПособия, ВыплатаЗаСчетФедеральногоБюджета");
												
	ДанныеРасчетногоДокумента.Вставить("Организация", 		Организация);
	ДанныеРасчетногоДокумента.Вставить("ПериодРегистрации", ПериодРегистрации);
	
	Для Каждого СтрокаТаблицы Из Неявки Цикл
		
		Если ПредыдущийКадровыйДокумент = СтрокаТаблицы.КадровыйДокумент
			 И ПредыдущийСотрудник = СтрокаТаблицы.Сотрудник
			 И ПредыдущаяНеявка = СтрокаТаблицы.Неявка
			 И ПредыдущаяДатаНачала = СтрокаТаблицы.ДатаНачала Тогда
			
			// полностью повторились ключевые реквизиты - отметим как дубль
			СтрокаТаблицы.ДублирующаясяСтрока = Истина;
			СтрокаТаблицы.Отметка = Ложь;
		КонецЕсли;
		
		Если ТипЗнч(СтрокаТаблицы.РасчетныйДокумент) = Тип("ДокументСсылка.НачислениеПоБольничномуЛисту") И НЕ ЗначениеЗаполнено(СтрокаТаблицы.РасчетныйДокумент) Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ДатаНачалаСобытия) Тогда
				СтрокаТаблицы.ДатаНачалаСобытия = СтрокаТаблицы.ДатаНачала;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ДатаНачалаОплаты) Тогда
				СтрокаТаблицы.ДатаНачалаОплаты = СтрокаТаблицы.ДатаНачала;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ПериодРасчетаСреднегоЗаработкаНачало) Тогда
				СтрокаТаблицы.ПериодРасчетаСреднегоЗаработкаНачало = ДобавитьМесяц(НачалоМесяца(СтрокаТаблицы.ДатаНачалаСобытия), - 12);
				СтрокаТаблицы.ПериодРасчетаСреднегоЗаработкаОкончание = НачалоМесяца(СтрокаТаблицы.ДатаНачалаСобытия) - 1;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ДанныеРасчетногоДокумента, СтрокаТаблицы);
			НачислениеПоБольничномуЛистуСервис.ПрочитатьДанныеОЛьготах(ДанныеРасчетногоДокумента);
			НачислениеПоБольничномуЛистуСервис.ПрочитатьРазмерыПособия(ДанныеРасчетногоДокумента, Ложь, Ложь);
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеРасчетногоДокумента);
			
		ИначеЕсли (ТипЗнч(СтрокаТаблицы.РасчетныйДокумент) = Тип("ДокументСсылка.НачислениеОтпускаРаботникамОрганизаций")
			или ТипЗнч(СтрокаТаблицы.РасчетныйДокумент) = Тип("ДокументСсылка.ОплатаПоСреднемуЗаработку"))
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.РасчетныйДокумент) Тогда	
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ПериодРасчетаСреднегоЗаработкаНачало) Тогда
				СтрокаТаблицы.ПериодРасчетаСреднегоЗаработкаНачало = ДобавитьМесяц(НачалоМесяца(СтрокаТаблицы.ДатаНачалаСобытия), - 12);
				СтрокаТаблицы.ПериодРасчетаСреднегоЗаработкаОкончание = НачалоМесяца(СтрокаТаблицы.ДатаНачалаСобытия) - 1;
			КонецЕсли;
			
		КонецЕсли;
		
		// Запоминаем текущие значения ключевых реквизитов
		ПредыдущийКадровыйДокумент	= СтрокаТаблицы.КадровыйДокумент;
		ПредыдущийСотрудник			= СтрокаТаблицы.Сотрудник;
		ПредыдущаяНеявка			= СтрокаТаблицы.Неявка;
		ПредыдущаяДатаНачала		= СтрокаТаблицы.ДатаНачала;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Процедура создает документы по пустым строкам табличной части
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
Процедура СоздатьДокументы() Экспорт
	
	ГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Неявки.НомерСтроки КАК НомерСтроки,
	|	Неявки.Сотрудник,
	|	Неявки.ФизЛицо,
	|	Неявки.Неявка,
	|	Неявки.ДатаНачалаСобытия,
	|	ВЫБОР
	|		КОГДА Неявки.Неявка = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Неявки.ПорядокРасчетаОтпуска
	|	КОНЕЦ КАК ПорядокРасчетаОсновногоОтпуска,
	|	ВЫБОР
	|		КОГДА Неявки.Неявка = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный)
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		КОГДА Неявки.ДатаНачала <> Неявки.ДатаНачалаДоп
	|			ТОГДА Неявки.ДатаНачала
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК ДатаНачалаОсновногоОтпуска,
	|	ВЫБОР
	|		КОГДА Неявки.Неявка = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный)
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		КОГДА Неявки.ДатаНачалаДоп = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Неявки.ДатаОкончания
	|		КОГДА Неявки.ДатаНачала <> Неявки.ДатаНачалаДоп
	|			ТОГДА ДОБАВИТЬКДАТЕ(Неявки.ДатаНачалаДоп, ДЕНЬ, -1)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК ДатаОкончанияОсновногоОтпуска,
	|	ВЫБОР
	|		КОГДА Неявки.Неявка = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный)
	|			ТОГДА Неявки.ВидРасчета
	|		ИНАЧЕ Неявки.ВидДополнительногоОтпуска
	|	КОНЕЦ КАК ВидРасчетаДополнительногоОтпуска,
	|	ВЫБОР
	|		КОГДА Неявки.Неявка = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный)
	|			ТОГДА Неявки.ДатаНачала
	|		ИНАЧЕ Неявки.ДатаНачалаДоп
	|	КОНЕЦ КАК ДатаНачалаДополнительногоОтпуска,
	|	ВЫБОР
	|		КОГДА Неявки.Неявка = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный)
	|			ТОГДА Неявки.ДатаОкончания
	|		КОГДА Неявки.ДатаНачалаДоп <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Неявки.ДатаОкончания
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК ДатаОкончанияДополнительногоОтпуска,
	|	Неявки.РабочийГодС,
	|	Неявки.РабочийГодПо,
	|	Неявки.ПериодРасчетаСреднегоЗаработкаНачало,
	|	Неявки.ПериодРасчетаСреднегоЗаработкаОкончание,
	|	Неявки.ПроцентОплаты КАК ПроцентОплатыДополнительногоОтпуска
	|ПОМЕСТИТЬ ВТОтпуска
	|ИЗ
	|	&Неявки КАК Неявки
	|ГДЕ
	|	Неявки.Отметка
	|	И Неявки.Неявка В (ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный), ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускЕжегодный))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки";
	Запрос.УстановитьПараметр("Неявки",Неявки.Выгрузить());
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Отпуска.НомерСтроки КАК НомерСтроки,
	|	Отпуска.Сотрудник,
	|	Отпуска.ФизЛицо,
	|	Отпуска.ПорядокРасчетаОсновногоОтпуска,
	|	Отпуска.ДатаНачалаОсновногоОтпуска,
	|	Отпуска.ДатаНачалаСобытия,
	|	Отпуска.ДатаОкончанияОсновногоОтпуска,
	|	ДниОсновногоОтпуска.КалендарныеДни КАК КоличествоДнейОсновногоОтпуска,
	|	Отпуска.ВидРасчетаДополнительногоОтпуска,
	|	Отпуска.ДатаНачалаДополнительногоОтпуска,
	|	Отпуска.ДатаОкончанияДополнительногоОтпуска,
	|	ДниДопОтпуска.КалендарныеДни КАК КоличествоДнейДополнительногоОтпуска,
	|	Отпуска.РабочийГодС,
	|	Отпуска.РабочийГодПо,
	|	Отпуска.ПериодРасчетаСреднегоЗаработкаНачало,
	|	Отпуска.ПериодРасчетаСреднегоЗаработкаОкончание,
	|	Отпуска.ПроцентОплатыДополнительногоОтпуска,
	|	ВЫБОР
	|		КОГДА Отпуска.ВидРасчетаДополнительногоОтпуска.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуДляОтпускаПоКалендарнымДням), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуДляОтпускаПоШестидневке))
	|			ТОГДА """"
	|		КОГДА Отпуска.Неявка = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный)
	|			ТОГДА ""для учебного отпуска необходимо указать вид расчета по среднему заработку для отпуска""
	|		КОГДА Отпуска.ДатаНачалаДополнительногоОтпуска = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА """"
	|		ИНАЧЕ ""для дополнительного отпуска необходимо указать вид расчета по среднему заработку для отпуска""
	|	КОНЕЦ КАК СообщениеОбОшибке,
	|	ВЫБОР
	|		КОГДА Отпуска.ДатаНачалаОсновногоОтпуска = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОсновнойОтпуск,
	|	ВЫБОР
	|		КОГДА Отпуска.ДатаНачалаДополнительногоОтпуска = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ДополнительныйОтпуск
	|ИЗ
	|	ВТОтпуска КАК Отпуска
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Отпуска.НомерСтроки КАК НомерСтроки,
	|			СУММА(КалендарьОсновногоОтпуска.КалендарныеДни) КАК КалендарныеДни
	|		ИЗ
	|			ВТОтпуска КАК Отпуска
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК КалендарьОсновногоОтпуска
	|				ПО (КалендарьОсновногоОтпуска.ДатаКалендаря МЕЖДУ Отпуска.ДатаНачалаОсновногоОтпуска И Отпуска.ДатаОкончанияОсновногоОтпуска)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Отпуска.НомерСтроки) КАК ДниОсновногоОтпуска
	|		ПО Отпуска.НомерСтроки = ДниОсновногоОтпуска.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВЫБОР
	|				КОГДА Отпуска.Неявка = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ОтпускУчебный)
	|					ТОГДА КОЛИЧЕСТВО(КалендарьДопОтпуска.ДатаКалендаря)
	|				ИНАЧЕ СУММА(КалендарьДопОтпуска.КалендарныеДни)
	|			КОНЕЦ КАК КалендарныеДни,
	|			Отпуска.НомерСтроки КАК НомерСтроки
	|		ИЗ
	|			ВТОтпуска КАК Отпуска
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК КалендарьДопОтпуска
	|				ПО (КалендарьДопОтпуска.ДатаКалендаря МЕЖДУ Отпуска.ДатаНачалаДополнительногоОтпуска И Отпуска.ДатаОкончанияДополнительногоОтпуска)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Отпуска.Неявка,
	|			Отпуска.НомерСтроки) КАК ДниДопОтпуска
	|		ПО Отпуска.НомерСтроки = ДниДопОтпуска.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	ДанныеОтпусков = Запрос.Выполнить().Выгрузить();
	ДанныеОтпусков.Индексы.Добавить("НомерСтроки");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеНачисления.Ссылка КАК ВидРасчета
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.НулеваяСумма)
	|	И ОсновныеНачисления.ОбозначениеВТабелеУчетаРабочегоВремени = ЗНАЧЕНИЕ(Справочник.КлассификаторИспользованияРабочегоВремени.ПростойПоВинеРаботника)
	|	И ОсновныеНачисления.ВидВремени = ЗНАЧЕНИЕ(Перечисление.ВидыВремени.ЦелодневноеНеотработанное)";
	
	ВРНеоплачиваемыйПростой = Неопределено;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВРНеоплачиваемыйПростой = Выборка.ВидРасчета;
	КонецЕсли;
	
	СписокКадровыхДокументов = Неявки.НайтиСтроки(Новый Структура("Отметка", Истина));
	
	МассивДокументовИсправлений = Новый Массив;
	СоответствиеДокументовИсправлений = Новый Соответствие;
	
	Для Каждого СтрокаТаблицы Из СписокКадровыхДокументов Цикл
		Если СтрокаТаблицы.КадровыйДокумент = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		МетаданныеРегистратора = СтрокаТаблицы.КадровыйДокумент.Метаданные();
		Если МетаданныеРегистратора.Реквизиты.Найти("ИсправляемыйДокумент") <> Неопределено Тогда
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Док.ИсправляемыйДокумент,
			|	Док.ДвиженияИсправляемогоДокумента
			|ИЗ
			|	Документ." + МетаданныеРегистратора.Имя + " КАК Док
			|ГДЕ
			|	Док.Ссылка = &Ссылка";
			
			Ссылка = СтрокаТаблицы.КадровыйДокумент;
			Пока Ссылка <> Неопределено И Не Ссылка.Пустая() Цикл
				Запрос.УстановитьПараметр("Ссылка",	Ссылка);
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					Ссылка = Выборка.ИсправляемыйДокумент;
					Если Не Ссылка.Пустая() Тогда
						МассивДокументовИсправлений.Добавить(Ссылка);
						ДокументИсправление = Новый Структура;
						ДокументИсправление.Вставить("ИсходныйДокумент",				СтрокаТаблицы.КадровыйДокумент);
						ДокументИсправление.Вставить("ДвиженияИсправляемогоДокумента",	Выборка.ДвиженияИсправляемогоДокумента);
						СоответствиеДокументовИсправлений.Вставить(Ссылка, ДокументИсправление);
					КонецЕсли;
				Иначе
					Ссылка = Неопределено;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	РасчетныеДокументыДляИсправления = Новый Соответствие;
	Если МассивДокументовИсправлений.Количество() > 0 Тогда
		РезультатЗапроса = СформироватьЗапрос(Организация, ПодразделениеОрганизации, МассивДокументовИсправлений, ДатаНачалаОтбора, ДатаОкончанияОтбора, , , , СоответствиеДокументовИсправлений);
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Не Выборка.Отметка
				И ЗначениеЗаполнено(Выборка.КадровыйДокумент)
				И ЗначениеЗаполнено(Выборка.РасчетныйДокумент) Тогда
				
				ИсходныйДокумент = СоответствиеДокументовИсправлений[Выборка.КадровыйДокумент].ИсходныйДокумент;
				
				СтруктураРасчетногоДокумента = Новый Структура;
				СтруктураРасчетногоДокумента.Вставить("КадровыйДокумент",			Выборка.КадровыйДокумент);
				СтруктураРасчетногоДокумента.Вставить("РасчетныйДокумент",			Выборка.РасчетныйДокумент);
				СтруктураРасчетногоДокумента.Вставить("СовпадаетПериодРегистрации",	Выборка.ПериодРегистрации = ПериодРегистрации);
				
				РасчетныеДокументыДляИсправления.Вставить(ИсходныйДокумент, СтруктураРасчетногоДокумента);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	Для Каждого СтрокаТаблицы Из Неявки Цикл
		Если Не СтрокаТаблицы.Отметка
			ИЛИ Не ЗначениеЗаполнено(СтрокаТаблицы.КадровыйДокумент)
			ИЛИ ЗначениеЗаполнено(СтрокаТаблицы.РасчетныйДокумент) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураРасчетногоДокумента = РасчетныеДокументыДляИсправления.Получить(СтрокаТаблицы.КадровыйДокумент);
		Если СтруктураРасчетногоДокумента = Неопределено Тогда
			РасчетныйДокументДляИсправления = Неопределено;
			
		Иначе
			РасчетныйДокументДляИсправления = СтруктураРасчетногоДокумента.РасчетныйДокумент;
			
			Если СтруктураРасчетногоДокумента.СовпадаетПериодРегистрации Тогда
				Расшифровки = Новый Массив;
				Расшифровки.Добавить(Новый Структура("Представление,Расшифровка", СтрокаТаблицы.КадровыйДокумент, СтрокаТаблицы.КадровыйДокумент));
				
				РодительскаяСтрока = ОбработкаКомментариев.ДобавитьСообщение("Невозможно создание нового расчетного документа, так как текущий кадровый документ был исправлен, и для исправленного документа был зарегистрирован расчетный документ в текущем периоде",	Перечисления.ВидыСообщений.Раздел);
				
				Расшифровки = Новый Массив;
				Расшифровки.Добавить(Новый Структура("Представление,Расшифровка", СтрокаТаблицы.КадровыйДокумент, СтрокаТаблицы.КадровыйДокумент));
				ОбработкаКомментариев.ДобавитьСообщение("Текущий кадровый документ: ",	Перечисления.ВидыСообщений.Информация, Расшифровки, РодительскаяСтрока);
				
				Расшифровки = Новый Массив;
				Расшифровки.Добавить(Новый Структура("Представление,Расшифровка", СтруктураРасчетногоДокумента.КадровыйДокумент, СтруктураРасчетногоДокумента.КадровыйДокумент));
				ОбработкаКомментариев.ДобавитьСообщение("Исходный кадровый документ: ",	Перечисления.ВидыСообщений.Информация, Расшифровки, РодительскаяСтрока);
				
				Расшифровки = Новый Массив;
				Расшифровки.Добавить(Новый Структура("Представление,Расшифровка", СтрокаТаблицы.КадровыйДокумент, РасчетныйДокументДляИсправления));
				ОбработкаКомментариев.ДобавитьСообщение("Расчетный документ: ",			Перечисления.ВидыСообщений.Ошибка, Расшифровки, РодительскаяСтрока);
				
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ТипРасчетногоДокумента	= ТипЗнч(СтрокаТаблицы.РасчетныйДокумент);
		ВидНеявки = СтрокаТаблицы.Неявка;
		СтрокаСообщенияОбОшибке = Строка(СтрокаТаблицы.Сотрудник) + " " + ВидНеявки + " от " + Формат(СтрокаТаблицы.ДатаНачала,"ДФ=дд.ММ.гггг")+": ";
		
		Если ТипРасчетногоДокумента = Тип("ДокументСсылка.НачислениеПоБольничномуЛисту") Тогда
			
			РасчетныйДокумент = Документы.НачислениеПоБольничномуЛисту.СоздатьДокумент();
			РасчетныйДокумент.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВРеглУчетеНачислений.КакБазовыеНачисления;
			НачислениеПоБольничномуЛистуПереопределяемый.ЗаполнитьДополнительныеРеквизиты(РасчетныйДокумент, , Истина);
			Если ЗначениеЗаполнено(РасчетныйДокументДляИсправления) Тогда
				РасчетныйДокумент.ЗаполнитьПоПерерассчитываемомуДокументу(РасчетныйДокументДляИсправления, СтрокаТаблицы.Сотрудник);
			КонецЕсли;
			РасчетныйДокумент.ПериодРегистрации = ПериодРегистрации;
			ЗаполнитьЗначенияСвойств(РасчетныйДокумент,СтрокаТаблицы);
			
		ИначеЕсли ТипРасчетногоДокумента = Тип("ДокументСсылка.ОплатаПоСреднемуЗаработку") Тогда
			
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ВидРасчета) 	
				 Или СтрокаТаблицы.ВидРасчета.СпособРасчета <> Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработку Тогда
				ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаСообщенияОбОшибке + "необходимо указать вид расчета по среднему заработку");
				Продолжить;
			КонецЕсли;
			
			РасчетныйДокумент = Документы.ОплатаПоСреднемуЗаработку.СоздатьДокумент();
			Если ЗначениеЗаполнено(РасчетныйДокументДляИсправления) Тогда
				РасчетныйДокумент.ЗаполнитьПоПерерассчитываемомуДокументу(РасчетныйДокументДляИсправления, СтрокаТаблицы.Сотрудник);
			КонецЕсли;
			РасчетныйДокумент.ПериодРегистрации 		= ПериодРегистрации;
			РасчетныйДокумент.СпособРегистрацииВремени 	= Перечисления.СпособыРегистрацииВремени.РегистрацияДляЦелойСмены;
			ЗаполнитьЗначенияСвойств(РасчетныйДокумент,СтрокаТаблицы);
			
		ИначеЕсли ТипРасчетногоДокумента = Тип("ДокументСсылка.НачислениеОтпускаРаботникамОрганизаций") Тогда
			
			СтрокаДанных = ДанныеОтпусков.Найти(СтрокаТаблицы.НомерСтроки,"НомерСтроки");
			Если СтрокаДанных <> Неопределено Тогда
				Если ЗначениеЗаполнено(СтрокаДанных.СообщениеОбОшибке) Тогда
					ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаСообщенияОбОшибке + СтрокаДанных.СообщениеОбОшибке);
					Продолжить;
				КонецЕсли;
				РасчетныйДокумент = Документы.НачислениеОтпускаРаботникамОрганизаций.СоздатьДокумент();
				Если ЗначениеЗаполнено(РасчетныйДокументДляИсправления) Тогда
					РасчетныйДокумент.ЗаполнитьПоПерерассчитываемомуДокументу(РасчетныйДокументДляИсправления, СтрокаТаблицы.Сотрудник);
				КонецЕсли;
				РасчетныйДокумент.ПериодРегистрации	= ПериодРегистрации;
				ЗаполнитьЗначенияСвойств(РасчетныйДокумент,СтрокаДанных);
			КонецЕсли;
			
		ИначеЕсли ВидНеявки = Перечисления.СостоянияРаботникаОрганизации.Простой
				  И ЗначениеЗаполнено(СтрокаТаблицы.ВидПростоя) Тогда
			// оплачиваемый простой
			РасчетныйДокумент = Документы.РегистрацияПростоевРаботниковОрганизаций.СоздатьДокумент();
			Если ЗначениеЗаполнено(РасчетныйДокументДляИсправления) Тогда
				РасчетныйДокумент.ЗаполнитьПоПерерассчитываемомуДокументу(РасчетныйДокументДляИсправления, СтрокаТаблицы.Сотрудник);
			КонецЕсли;
			РасчетныйДокумент.ПериодРегистрации			= ПериодРегистрации;
			РасчетныйДокумент.СпособРегистрацииВремени	= Перечисления.СпособыРегистрацииВремени.РегистрацияДляЦелойСмены;
			ЗаполнитьЗначенияСвойств(РасчетныйДокумент,СтрокаТаблицы);
			
		Иначе // прочие невыходы
			
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ВидРасчета)
				 И ВидНеявки = Перечисления.СостоянияРаботникаОрганизации.Простой Тогда
				СтрокаТаблицы.ВидРасчета = ВРНеоплачиваемыйПростой;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ВидРасчета) 
				 Или СтрокаТаблицы.ВидРасчета.СпособРасчета <> Перечисления.СпособыРасчетаОплатыТруда.НулеваяСумма Тогда
				ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаСообщенияОбОшибке+"необходимо выбрать вид расчета по нулевой сумме");
				Продолжить;
			КонецЕсли;
			
			РасчетныйДокумент = Документы.НевыходыВОрганизациях.СоздатьДокумент();
			Если ЗначениеЗаполнено(РасчетныйДокументДляИсправления) Тогда
				РасчетныйДокумент.ЗаполнитьПоПерерассчитываемомуДокументу(РасчетныйДокументДляИсправления, СтрокаТаблицы.Сотрудник);
			КонецЕсли;
			РасчетныйДокумент.ПериодРегистрации = ПериодРегистрации;
			РасчетныйДокумент.СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЦелойСмены;
			
			СтрокаНачисления = РасчетныйДокумент.Начисления.Добавить();
			СтрокаНачисления.Авторасчет			= Истина;
			СтрокаНачисления.Сотрудник			= СтрокаТаблицы.Сотрудник;
			СтрокаНачисления.Физлицо			= СтрокаТаблицы.Физлицо;
			СтрокаНачисления.ДатаНачала 		= СтрокаТаблицы.ДатаНачала;
			СтрокаНачисления.ДатаОкончания 		= СтрокаТаблицы.ДатаОкончания;
			СтрокаНачисления.ДатаНачалаСобытия 	= СтрокаТаблицы.ДатаНачала;
			
			// Определим подразделение работника на дату события
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Сотрудник",			СтрокаТаблицы.Сотрудник);
			Запрос.УстановитьПараметр("Организация",		ГоловнаяОрганизация);
			Запрос.УстановитьПараметр("ДатаАктуальности",	СтрокаТаблицы.ДатаНачала);
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА РаботникиОрганизации.ПериодЗавершения <= &ДатаАктуальности
			|				И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
			|			ТОГДА РаботникиОрганизации.ПодразделениеОрганизацииЗавершения
			|		ИНАЧЕ РаботникиОрганизации.ПодразделениеОрганизации
			|	КОНЕЦ КАК ПодразделениеОрганизации
			|ИЗ
			|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаАктуальности, Сотрудник = &Сотрудник) КАК РаботникиОрганизации";
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				СтрокаНачисления.ПодразделениеОрганизации = Выборка.ПодразделениеОрганизации;
			КонецЕсли;
			
			СтрокаНачисления.ВидРасчета = СтрокаТаблицы.ВидРасчета;
			
			РазницаВМесяцах = (Год(СтрокаНачисления.ДатаОкончания)*12 + Месяц(СтрокаНачисления.ДатаОкончания)) - (Год(СтрокаНачисления.ДатаНачала)*12 + Месяц(СтрокаНачисления.ДатаНачала));
			Если РазницаВМесяцах > 0 Тогда
				ПроведениеРасчетов.РазбитьСтрокуНачисленийНаПомесячныеЗаписи(СтрокаНачисления, РасчетныйДокумент.Начисления);
			КонецЕсли;
			
		КонецЕсли;
		
		РасчетныйДокумент.Организация	= Организация;
		РасчетныйДокумент.Ответственный	= глЗначениеПеременной("глТекущийПользователь");
		ДатаКЗаписи = ТекущаяДата();
		#Если Клиент Тогда
			Если ИспользованиеРабочейДаты = РежимРабочейДаты.Назначать Тогда
				ДатаКЗаписи = НачалоДня(РабочаяДата) + 3600 * Час(ДатаКЗаписи) + 60 * Минута(ДатаКЗаписи) + Секунда(ДатаКЗаписи);
			КонецЕсли;
		#КонецЕсли
		РасчетныйДокумент.Дата = ДатаКЗаписи;
		РасчетныйДокумент.УстановитьНовыйНомер();
		РасчетныйДокумент.Записать();
		
		СтрокаТаблицы.РасчетныйДокумент	= РасчетныйДокумент.Ссылка;
		
	КонецЦикла;
	
	ОбработкаКомментариев.ПоказатьСообщения();
	
КонецПроцедуры // СоздатьДокументы()

// Процедура рассчитывает отмеченные документы
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
Процедура РассчитатьДокументы() Экспорт
	
	СтрокиКРасчету = Неявки.НайтиСтроки(Новый Структура("Отметка",Истина));
	КоличествоСтрок =  СтрокиКРасчету.Количество();
	Если КоличествоСтрок = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РассчитываемыеТаблицы = Новый Структура("Начисления,РасчетСреднего",Истина,Истина);
	Заголовок = ОбщегоНазначения.КомментарийРасчета("Расчет документов");
	
	Для Каждого СтрокаТаблицы Из СтрокиКРасчету Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицы.РасчетныйДокумент) Тогда
			
			// этот документ не рассчитываем
			Если ТипЗнч(СтрокаТаблицы.РасчетныйДокумент) = Тип("ДокументСсылка.ОтпускПоУходуЗаРебенком") Тогда 
				Продолжить
			КонецЕсли;
			
			// проверим можно ли что-то делать с документом
			ОписаниеПричиныОтказа = "";
			Если ПроведениеРасчетов.ДокументНельзяИзменятьЗаднимЧислом(СтрокаТаблицы.РасчетныйДокумент, ОписаниеПричиныОтказа) Тогда
				ОбщегоНазначения.КомментарийРасчета(СтрЗаменить(ОписаниеПричиныОтказа,"Документ ",Строка(СтрокаТаблицы.РасчетныйДокумент)), , "Открыть", СтрокаТаблицы.РасчетныйДокумент,Перечисления.ВидыСообщений.ВажнаяИнформация);
				Продолжить;
			КонецЕсли;
			
			РасчетныйДокумент = СтрокаТаблицы.РасчетныйДокумент.ПолучитьОбъект();
			Если РасчетныйДокумент.ПометкаУдаления Тогда
				Попытка
					РасчетныйДокумент.УстановитьПометкуУдаления(Ложь);
				Исключение
					ОбщегоНазначения.КомментарийРасчета(Строка(СтрокаТаблицы.РасчетныйДокумент) + ": не удалось отменить пометку документа на уделение!", , "Открыть", СтрокаТаблицы.РасчетныйДокумент,Перечисления.ВидыСообщений.Ошибка);
					Продолжить;
				КонецПопытки;
			ИначеЕсли РасчетныйДокумент.Проведен Тогда
				Попытка
					РасчетныйДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения)
				Исключение
					ОбщегоНазначения.КомментарийРасчета(Строка(СтрокаТаблицы.РасчетныйДокумент) + ": не удалось отменить проведение документа!", , "Открыть", СтрокаТаблицы.РасчетныйДокумент,Перечисления.ВидыСообщений.Ошибка);
					Продолжить;
				КонецПопытки;
			КонецЕсли;
			
			// авторасчет документа
			Попытка
				Если ТипЗнч(РасчетныйДокумент) = Тип("ДокументОбъект.НевыходыВОрганизациях") Тогда
					РасчетныйДокумент.Рассчитать();
				Иначе
					РасчетныйДокумент.Рассчитать(РассчитываемыеТаблицы);
				КонецЕсли;
				РасчетныйДокумент.Записать(РежимЗаписиДокумента.Запись);
				ОбщегоНазначения.КомментарийРасчета(Строка(РасчетныйДокумент) + " обработан успешно. ", Заголовок, "Открыть", СтрокаТаблицы.РасчетныйДокумент);
			Исключение
				ОбщегоНазначения.КомментарийРасчета(Строка(РасчетныйДокумент) + ": рассчитать не удалось!", , "Открыть", СтрокаТаблицы.РасчетныйДокумент,Перечисления.ВидыСообщений.Ошибка);
			КонецПопытки;
			
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // РассчитатьДокументы()

// Процедура проводит отмеченные документы
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
Процедура ПровестиДокументы() Экспорт

	Заголовок = ОбщегоНазначения.КомментарийРасчета("Проведение документов");
	Для Каждого СтрокаТаблицы Из Неявки Цикл
		Если СтрокаТаблицы.Отметка и ЗначениеЗаполнено(СтрокаТаблицы.РасчетныйДокумент) Тогда
			РасчетныйДокумент = СтрокаТаблицы.РасчетныйДокумент.ПолучитьОбъект();
			Если РасчетныйДокумент.ПометкаУдаления Тогда
				РасчетныйДокумент.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;
			Попытка
				РасчетныйДокумент.Записать(РежимЗаписиДокумента.Проведение);
				ОбщегоНазначения.КомментарийРасчета("Документ №" + РасчетныйДокумент.Номер + " от " + РасчетныйДокумент.Дата + " обработан успешно. ", Заголовок, "Открыть", СтрокаТаблицы.РасчетныйДокумент);
				СтрокаТаблицы.Отметка = Не СтрокаТаблицы.Отметка;
			Исключение
				ОбщегоНазначения.КомментарийРасчета("Документ №" + РасчетныйДокумент.Номер + " от " + РасчетныйДокумент.Дата + " провести не удалось!", , "Открыть", СтрокаТаблицы.РасчетныйДокумент,Перечисления.ВидыСообщений.Ошибка);
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ПровестиДокументы()

////////////////////////////////////////////////////////////////////////////////
// ИНИЦИАЛИЗАЦИЯ ОБЪЕКТА 

мДлинаСуток = 86400; // в секундах