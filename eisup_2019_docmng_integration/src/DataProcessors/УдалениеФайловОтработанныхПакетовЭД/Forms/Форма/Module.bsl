
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуФайловОтработанныхПакетовЭД();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуФайловОтработанныхПакетовЭД()
	
	ОтработанныеПакеты.Очистить();
	
	ЗапросПакетов = Новый Запрос;
	УдаляемыеСтатусыПакетов = Новый СписокЗначений;
	УдаляемыеСтатусыПакетов.Добавить(Перечисления.СтатусыПакетовЭД.Отменен);
	УдаляемыеСтатусыПакетов.Добавить(Перечисления.СтатусыПакетовЭД.Доставлен);
	УдаляемыеСтатусыПакетов.Добавить(Перечисления.СтатусыПакетовЭД.Распакован);
	ЗапросПакетов.УстановитьПараметр("СтатусПакета", УдаляемыеСтатусыПакетов);
		
	ЗапросПакетов.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	                      |	ЭДПрисоединенныеФайлы.Ссылка КАК Документ,
	                      |	ЭДПрисоединенныеФайлы.ВладелецФайла.СтатусПакета КАК Статус,
	                      |	ЛОЖЬ КАК Выбран,
	                      |	ЭДПрисоединенныеФайлы.ДатаСоздания КАК ДатаПолучения,
	                      |	ЭДПрисоединенныеФайлы.ВладелецФайла.Организация КАК Организация,
	                      |	ЭДПрисоединенныеФайлы.ВладелецФайла.Контрагент КАК Контрагент,
	                      |	ЭДПрисоединенныеФайлы.ВладелецФайла.Направление КАК Направление
	                      |ИЗ
	                      |	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	                      |ГДЕ
	                      |	ЭДПрисоединенныеФайлы.ВладелецФайла ССЫЛКА Документ.ПакетЭД
	                      |	И ЭДПрисоединенныеФайлы.ПометкаУдаления = ЛОЖЬ
	                      |	И ВЫРАЗИТЬ(ЭДПрисоединенныеФайлы.ВладелецФайла КАК Документ.ПакетЭД).СтатусПакета В (&СтатусПакета)";
	ЗначениеВРеквизитФормы(ЗапросПакетов.Выполнить().Выгрузить(), "ОтработанныеПакеты");
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеОтмеченныеПЭД(Команда)
	
	ОпределитьУдаляемыеДанные();
	ОбновитьДанныеПоТаблицам(Неопределено);
	
КонецПроцедуры

&НаСервере
Функция ОпределитьУдаляемыеДанные(ПараметрУдаления = Истина)
	
	УдаляемыеДанные = ОтработанныеПакеты;
	Если ПараметрУдаления Тогда
		УдалитьДанные(УдаляемыеДанные);
	Иначе
		Возврат УдаляемыеДанные;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УдалитьДанные(ТаблицаУдаления)
		
	Для Каждого СтрокаУдаления Из ТаблицаУдаления Цикл
		Если СтрокаУдаления.Выбран Тогда
			ОбъектУдаления = СтрокаУдаления.Документ.ПолучитьОбъект();
			ОбъектУдаления.ПометкаУдаления = Истина;
			ОбъектУдаления.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоТаблицам(Команда)	
	
	ЗаполнитьТаблицуФайловОтработанныхПакетовЭД();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	УстановитьЗначенияФлажков(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияФлажков(ПараметрУстановки)
	
	ДанныеУстановки = ОпределитьУдаляемыеДанные(Ложь);
	Для Каждого Строка Из ДанныеУстановки Цикл
		Строка.Выбран = ПараметрУстановки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	
	УстановитьЗначенияФлажков(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтработанныеПакетыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныеДокументыКлиент.ОткрытьЭДДляПросмотра(Элементы.ОтработанныеПакеты.ТекущиеДанные.Документ);	
КонецПроцедуры
