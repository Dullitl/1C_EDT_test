
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	мРолиПользователя = абс_БизнесПроцессы.ПолучитьСписокДоступныхРолейПользователя();
	
	Если мРолиПользователя.Найти(Справочники.РолиИсполнителей.РуководительПД) = Неопределено Тогда
		
		ttk_ОбщегоНазначения.СообщитьОбОшибке("Недостаточно прав.", Отказ);
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьИсполнителей();
	
КонецПроцедуры

&НаСервере 
Процедура ПрочитатьИсполнителей()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Исполнитель,
	|	ВложенныйЗапрос.абс_ИндексИсполнителя КАК Порядок
	|ИЗ
	|	(ВЫБРАТЬ
	|		РолиИИсполнители.Исполнитель КАК Исполнитель,
	|		РолиИИсполнители.абс_ИндексИсполнителя КАК абс_ИндексИсполнителя
	|	ИЗ
	|		РегистрСведений.РолиИИсполнители КАК РолиИИсполнители
	|	ГДЕ
	|		(НЕ РолиИИсполнители.Роль = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.СуперПользователь))
	|		И РолиИИсполнители.Роль = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.СогласованиеПД)) КАК ВложенныйЗапрос
	|ГДЕ
	|	(НЕ ВложенныйЗапрос.Исполнитель В
	|				(ВЫБРАТЬ
	|					РолиИИсполнители.Исполнитель
	|				ИЗ
	|					РегистрСведений.РолиИИсполнители КАК РолиИИсполнители
	|				ГДЕ
	|					РолиИИсполнители.Роль = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.СуперПользователь)
	|				СГРУППИРОВАТЬ ПО
	|								РолиИИсполнители.Исполнитель))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Исполнитель,
	|	ВложенныйЗапрос.абс_ИндексИсполнителя");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Объект.ИсполнителиПД.Очистить();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаИсполнитель = Объект.ИсполнителиПД.Добавить();
		
		СтрокаИсполнитель.Исполнитель = Выборка.Исполнитель;
		СтрокаИсполнитель.Порядок = Выборка.Порядок;
		
	КонецЦикла;	

КонецПроцедуры


&НаКлиенте
Процедура Сохранить(Команда)
	
	ЗаписатьИзменения();
	
	ПрочитатьИсполнителей();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзменения()
	
	Для Каждого СтрокаПД Из Объект.ИсполнителиПД Цикл
		
		Если НЕ СтрокаПД.ЗаписьИзменена Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.РолиИИсполнители.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Исполнитель.Установить(СтрокаПД.Исполнитель);
		НаборЗаписей.Отбор.Роль.Установить(Справочники.РолиИсполнителей.СогласованиеПД);
		
		ЗаписьНабора = НаборЗаписей.Добавить();
		
		ЗаписьНабора.Исполнитель = СтрокаПД.Исполнитель;
		ЗаписьНабора.Роль = Справочники.РолиИсполнителей.СогласованиеПД;
		
		ЗаписьНабора.абс_ИндексИсполнителя = СтрокаПД.Порядок;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура ИсполнителиПДПорядокПриИзменении(Элемент)
	
	Элементы.ИсполнителиПД.ТекущиеДанные.ЗаписьИзменена = Истина;
	
КонецПроцедуры

