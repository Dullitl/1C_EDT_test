
Процедура УсловиеКонтрагентСогласованДЭБПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат =  Контрагент.абс_СтатусКонтрагента = Перечисления.абсСтатусыКонтрагентов.Закрыт;
КонецПроцедуры

Процедура ДействиеСогласованиеДЭБПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	
	СформироватьГрупповуюЗадачу(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи);
	
КонецПроцедуры

Процедура СформироватьГрупповуюЗадачу(ТочкаМаршрута, ФормируемыеЗадачи)
	
	ФормируемыеЗадачи.Очистить();
	
	ЗапросИсполнителей = Новый Запрос("ВЫБРАТЬ
									  |	РолиИИсполнители.Исполнитель
									  |ИЗ
									  |	РегистрСведений.РолиИИсполнители КАК РолиИИсполнители
									  |ГДЕ
									  |	РолиИИсполнители.Роль = &РольИсполнителей");
									  
	РольИсполнителей = Неопределено;									  
	Если ТочкаМаршрута = БизнесПроцессы.абсЗавершениеОтношенийСКонтрагентами.ТочкиМаршрута.ДействиеСогласованиеДЭБ Тогда	
		//АБС ВСТАВКА №37747 НАЧАЛО «3 февраля 2014 г.», Пополитов
		РольИсполнителей = Справочники.РолиИсполнителей.СогласованиеБухгалтером;
		//\\АБС ВСТАВКА №37747 КОНЕЦ
	КонецЕсли;
	
	Если РольИсполнителей = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросИсполнителей.УстановитьПараметр("РольИсполнителей", РольИсполнителей);  	
	ВыборкаИсполнителей = ЗапросИсполнителей.Выполнить().Выбрать();   	
	Пока ВыборкаИсполнителей.Следующий() Цикл
		НоваяЗадача = Задачи.абсЗадачаДоговора.СоздатьЗадачу();		
		НоваяЗадача.БизнесПроцесс 		= ЭтотОбъект.Ссылка;
		НоваяЗадача.ТочкаМаршрута 		= ТочкаМаршрута;
		НоваяЗадача.Дата 				= ТекущаяДата();
		//НоваяЗадача.Наименование 		= ТочкаМаршрута.НаименованиеЗадачи + ", " + Строка(ЭтотОбъект.Ссылка.Контрагент); 		
		НоваяЗадача.Наименование = СокрЛП(Контрагент.Наименование) + " "  
			+ ТочкаМаршрута.НаименованиеЗадачи + ".";		
		НоваяЗадача.Исполнитель			= ВыборкаИсполнителей.Исполнитель; 		
		НоваяЗадача.ОбъектЗадачи		= Контрагент;  		
		НоваяЗадача.Записать();  		
		ФормируемыеЗадачи.Добавить(НоваяЗадача);   		
	КонецЦикла; 
	
	// Добавим задачи для суперпользователей
	абс_БизнесПроцессы.ДобавитьЗадачиСуперПользователя(ЭтотОбъект.Ссылка, Контрагент, ТочкаМаршрута, ФормируемыеЗадачи);
	
КонецПроцедуры

Процедура ДействиеСогласованиеДЭБОбработкаИнтерактивнойАктивации(ТочкаМаршрутаБизнесПроцесса, Задача, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;	
	КонтрагентОбъект = Контрагент.ПолучитьОбъект(); 	
	ФормаКонтрагента = Контрагент.ПолучитьФорму("ФормаЭлемента",,Контрагент);
	//ФормаКонтрагента.КлючУникальности = Задача;     	
	Если ФормаКонтрагента.Открыта() Тогда
		ФормаКонтрагента.Активизировать();
	Иначе
		ФормаКонтрагента.Открыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗавершениеОтказПриЗавершении(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	ЗавершитьБП(Контрагент.абс_СтатусКонтрагента);
	
КонецПроцедуры

Процедура ЗавершениеУспешноПриЗавершении(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	ЗавершитьБП(Контрагент.абс_СтатусКонтрагента);
	
КонецПроцедуры
                                                        
Процедура ЗавершитьБП(СтатусЗавершения)
	
	// Закроем задачи проверки контрагента по договорам
	СтатусДоговора = Неопределено;
	
	Если СтатусЗавершения = Перечисления.абсСтатусыКонтрагентов.Активный Тогда
		СтатусДоговора = Перечисления.абсСтатусыДоговоров.ПроверкаДФМ;
	Иначе
		СтатусДоговора = Перечисления.абсСтатусыДоговоров.Отказ;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	абсЗадачаДоговора.Ссылка,
	                      |	абсЗадачаДоговора.ОбъектЗадачи КАК ДоговорКонтрагента
	                      |ИЗ
	                      |	Задача.абсЗадачаДоговора КАК абсЗадачаДоговора
	                      |ГДЕ
	                      |	абсЗадачаДоговора.ОбъектЗадачи ССЫЛКА Справочник.ДоговорыКонтрагентов
	                      |	И абсЗадачаДоговора.ОбъектЗадачи.Владелец = &Контрагент
	                      |	И абсЗадачаДоговора.ТочкаМаршрута = &ТочкаМаршрута
	                      |	И абсЗадачаДоговора.Выполнена = ЛОЖЬ
	                      |	И абсЗадачаДоговора.ОбъектЗадачи.абс_СтатусДоговора = ЗНАЧЕНИЕ(Перечисление.абсСтатусыДоговоров.ПроверкаКонтрагента)");
						  
						  
	Запрос.УстановитьПараметр("Контрагент"		, Контрагент);
	Запрос.УстановитьПараметр("ТочкаМаршрута"	, БизнесПроцессы.абсСогласованиеДоговоров.ТочкиМаршрута.ДействиеПроверкаКонтрагента);     	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл  		
		// Сначала изменим статус в карточке договора
		// потом в регистре
		// потом выполним задачу
		ДоговорОбъект = Выборка.ДоговорКонтрагента.ПолучитьОбъект();  		
		ДоговорОбъект.абс_СтатусДоговора = СтатусДоговора; 		
		ДоговорОбъект.Записать(); 		
		ДоговорОбъект.ЗаписатьНовыйСтатус(ДоговорОбъект.абс_СтатусДоговора, "Контрагент перешел в статус " + СокрЛП(ДоговорОбъект.абс_СтатусДоговора));   		
		ЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();   		
		ЗадачаОбъект.ВыполнитьЗадачу();			
	КонецЦикла;
		
КонецПроцедуры



