Процедура УстановитьСтатусКонтрагента(БПСсылка, НовыйСтатус) Экспорт
	Набор = РегистрыСведений.абсПротоколСогласования.СоздатьНаборЗаписей();
	
	Набор.Отбор.Объект.Установить(БПСсылка.Контрагент);
	Набор.Отбор.БизнесПроцесс.Установить(БПСсылка);
	
	Набор.Прочитать();
	
	НоваяЗапись = Набор.Добавить();
	
	НоваяЗапись.Период = ТекущаяДата();
	НоваяЗапись.Объект 			= БПСсылка.Контрагент;
	НоваяЗапись.БизнесПроцесс 	= БПСсылка;
	
	НоваяЗапись.СтатусСогласования = НовыйСтатус;
	
	НоваяЗапись.Пользователь 	= глЗначениеПеременной("глТекущийПользователь");
	
	Набор.Записать();	
	
КонецПроцедуры

// Убрать в общий модуль
Функция НайтиБПНовыйКонтрагент(КонтрагентСсылка) Экспорт
	ЗапросБП = Новый Запрос("ВЫБРАТЬ
	                        |	абсНовыйКонтрагент.Ссылка КАК БПНовыйКонтрагент
	                        |ИЗ
	                        |	БизнесПроцесс.абсНовыйКонтрагент КАК абсНовыйКонтрагент
	                        |ГДЕ
	                        |	абсНовыйКонтрагент.Контрагент = &КонтрагентСсылка
	                        |	И абсНовыйКонтрагент.Завершен = ЛОЖЬ");
							
	ЗапросБП.УстановитьПараметр("КонтрагентСсылка", КонтрагентСсылка);
	
	ВыборкаБП = ЗапросБП.Выполнить().Выбрать();
	
	Если ВыборкаБП.Следующий() Тогда
		Возврат ВыборкаБП.БПНовыйКонтрагент;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Убрать в общий модуль
Функция ДобавитьБПНовыйКонтрагент(КонтрагентСсылка) Экспорт
	НовыйБП = БизнесПроцессы.абсНовыйКонтрагент.СоздатьБизнесПроцесс();
	
	НовыйБП.Дата = ТекущаяДата();
	
	НовыйБП.Контрагент 					= КонтрагентСсылка;
	НовыйБП.ПользовательИнициаторБП 	= глЗначениеПеременной("глТекущийПользователь");
	
	// Заполнить список согласования
	НовыйБП.ЗаполнитьСписокСогласованияПоПользователям();
	
	// Проверим список согласования
	Если Не НовыйБП.ПроверитьСписокСогласования() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		НовыйБП.Записать();
		
		УстановитьСтатусКонтрагента(НовыйБП.Ссылка, Перечисления.абсСтатусыКонтрагентов.Подготовка);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат НовыйБП;
		
КонецФункции
