Процедура РедактироватьБюджетныеАналитики(ДокументОбъект) Экспорт
											
	ФормаРедактированияАналитик = ПолучитьОбщуюФорму("абс_ФормаРедактированияБюджетныхАналитик");
	
	//+ Машута А. 09.10.2018 http://sd.ttk.ru/sd/operator/#uuid:task$1413990
	//Вынес установку доступности элементов формы редактирования аналитик в отдельную процедуру
	УстановитьВидимостьДоступностьЭлементовФормыРедактированияАналитик(ДокументОбъект, ФормаРедактированияАналитик);
	//- Машута А. 09.10.2018
	// {{KM WARE Погорелов В.М. Заявка № 07.10.2015 начало
	// добавлен новый реквизит kwr_БюджетнаяСтатьяСторно для документа ПеремещениеТоваров
	// {{Старый код:
	//ЗаполнитьЗначенияСвойств(ФормаРедактированияАналитик, ДокументОбъект, "абс_ЦФУ, абс_ЦФО, Организация, абс_БюджетнаяСтатья, абс_ТипКонтрагента, абс_КВ, абс_ТЭО, абс_ТипСети, абс_ТипРасхода");
	// }}Новый код:
	СписокРеквизитов = "абс_ЦФУ, абс_ЦФО, Организация, абс_БюджетнаяСтатья, абс_ТипКонтрагента, абс_КВ, абс_ТЭО, абс_ТипСети, абс_ТипРасхода";
	Если ДокументОбъект.Метаданные().Реквизиты.Найти("kwr_БюджетнаяСтатьяСторно") <> Неопределено Тогда
		СписокРеквизитов = СписокРеквизитов + ", kwr_БюджетнаяСтатьяСторно"; 	
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ФормаРедактированияАналитик, ДокументОбъект, СписокРеквизитов);
	// }}KM WARE Погорелов В.М. Заявка № 07.10.2015 окончание
	
	//абс_Стрельцов+ добавлено: 10.10.2012
	//---------------------------------------------------------------------------------------------------------
	Если ДокументОбъект.Метаданные().ТабличныеЧасти.Найти("абс_РаспределениеПоПроектам") <> Неопределено Тогда
		ФормаРедактированияАналитик.абс_РаспределениеПоПроектам = ДокументОбъект.абс_РаспределениеПоПроектам.Выгрузить();
	КонецЕСли;
	//---------------------------------------------------------------------------------------------------------
	//абс_Стрельцов-
	
	ФормаРедактированияАналитик.ДокументСсылка = ДокументОбъект.Ссылка;
	
	РезультатЗаполнения = ФормаРедактированияАналитик.ОткрытьМодально();
	
	Если РезультатЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.абс_ЦФУ					, ФормаРедактированияАналитик.абс_ЦФУ);
	ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.абс_ЦФО					, ФормаРедактированияАналитик.абс_ЦФО);
	ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.Организация				, ФормаРедактированияАналитик.Организация);
	ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.абс_БюджетнаяСтатья		, ФормаРедактированияАналитик.абс_БюджетнаяСтатья);
	ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.абс_ТипКонтрагента		, ФормаРедактированияАналитик.абс_ТипКонтрагента);
	ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.абс_КВ					, ФормаРедактированияАналитик.абс_КВ);
	ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.абс_ТЭО					, ФормаРедактированияАналитик.абс_ТЭО);
	ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.абс_ТипСети				, ФормаРедактированияАналитик.абс_ТипСети);
	ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.абс_ТипРасхода			, ФормаРедактированияАналитик.абс_ТипРасхода);
	
	ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.абс_БезКонтроляМаппинга	, ФормаРедактированияАналитик.абс_БезКонтроляМаппинга);
	
	// {{KM WARE Погорелов В.М. 07.10.2015 начало
	// заполнение реквизита kwr_БюджетнаяСтатьяСторно 
	Если ДокументОбъект.Метаданные().Реквизиты.Найти("kwr_БюджетнаяСтатьяСторно") <> Неопределено Тогда
		ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект.kwr_БюджетнаяСтатьяСторно,
			ФормаРедактированияАналитик.kwr_БюджетнаяСтатьяСторно);				
	КонецЕсли;
	// }}KM WARE Погорелов В.М. 07.10.2015 окончание
	
	//абс_Стрельцов+ добавлено: 10.10.2012
	//---------------------------------------------------------------------------------------------------------
	Если ДокументОбъект.Метаданные().ТабличныеЧасти.Найти("абс_РаспределениеПоПроектам") <> Неопределено Тогда
		ДокументОбъект.абс_РаспределениеПоПроектам.Загрузить(ФормаРедактированияАналитик.абс_РаспределениеПоПроектам);
	КонецЕСли;
	//---------------------------------------------------------------------------------------------------------
	//абс_Стрельцов-
	
	//ЗаполнитьЗначенияСвойств(ДокументОбъект, ФормаРедактированияАналитик, "абс_ЦФУ, абс_ЦФО, Организация, абс_БюджетнаяСтатья, абс_ТипКонтрагента, абс_КВ, абс_ТЭО, абс_ТипСети, абс_ТипРасхода");
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПлатежноеПоручениеИсходящее")
		или ТипЗнч(ДокументОбъект) = Тип("ДокументСсылка.ПлатежноеПоручениеИсходящее") 
		или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПлатежноеПоручениеВходящее")
		или ТипЗнч(ДокументОбъект) = Тип("ДокументСсылка.ПлатежноеПоручениеВходящее") Тогда     		
		
		СтатьяДДС = ПолучитьСтатьюДДС(ФормаРедактированияАналитик.абс_БюджетнаяСтатья);
		
		// Романова Н.Г. #7780485 14.03.17 Если статья ДДС не найдена, то в ПП она не заменяется пустым значением.
		Если СтатьяДДС = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка() Тогда 
			СтатьяДДС = ДокументОбъект.СтатьяДвиженияДенежныхСредств;			
		КонецЕсли;	
		//- Романова Н.Г. #7780485
			
		врМетаДокумент = ДокументОбъект.Метаданные();
		Для каждого Реквизит Из врМетаДокумент.Реквизиты Цикл
			Если ТипЗнч(ДокументОбъект[Реквизит.Имя]) = Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств") и не ДокументОбъект[Реквизит.Имя] = СтатьяДДС Тогда
				ПрисвоитьЗначениеРеквизитаСПроверкой(ДокументОбъект[Реквизит.Имя], СтатьяДДС);
			КонецЕсли;	
		КонецЦикла;
		Для каждого ИмяТЧ Из врМетаДокумент.ТабличныеЧасти Цикл			
			Для каждого Реквизит Из ИмяТЧ.Реквизиты Цикл
				Для каждого Строка Из ДокументОбъект[ИмяТЧ.Имя] Цикл 					
					Если ТипЗнч(Строка[Реквизит.Имя]) = Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств") и не Строка[Реквизит.Имя] = СтатьяДДС Тогда
						ПрисвоитьЗначениеРеквизитаСПроверкой(Строка[Реквизит.Имя], СтатьяДДС);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
			
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПрисвоитьЗначениеРеквизитаСПроверкой(РеквизитПриемник, РеквизитИсточник)
	
	Если НЕ РеквизитПриемник = РеквизитИсточник Тогда
		РеквизитПриемник = РеквизитИсточник;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСтатьюДДС(врБюджетнаяСтатья,врНазначениеПлатежа = Неопределено) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ДДС.БюджетнаяСтатья,
	               |	ДДС.НазначениеПлатежа КАК НазначениеПлатежа,
	               |	ДДС.СтатьяДДС
	               |ИЗ
	               |	РегистрСведений.абс_СоответствияСтатейДДСИУправленческихСтатейЗатрат КАК ДДС
	               |ГДЕ
	               |	ДДС.БюджетнаяСтатья В(&БюджетнаяСтатья)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НазначениеПлатежа";
	
	Запрос.УстановитьПараметр("БюджетнаяСтатья",врБюджетнаяСтатья);	
	
	врТЗ = Запрос.Выполнить().Выгрузить();
	Если врТЗ.Количество() > 1 и не врНазначениеПлатежа = Неопределено Тогда 		
		МассивДДС = врТЗ.НайтиСтроки(Новый Структура("НазначениеПлатежа",врНазначениеПлатежа));
		Если МассивДДС.Количество() > 0 Тогда
			Возврат МассивДДС[0].СтатьяДДС;	
		КонецЕсли;	
	ИначеЕсли врТЗ.Количество() = 1 Тогда			
		МассивДДС = врТЗ.ВыгрузитьКолонку("СтатьяДДС");
		Возврат МассивДДС[0];
	Иначе  
		Возврат Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

//+ Машута А. 09.10.2018 http://sd.ttk.ru/sd/operator/#uuid:task$1413990
Процедура УстановитьВидимостьДоступностьЭлементовФормыРедактированияАналитик(ДокументОбъект, ФормаРедактированияАналитик)
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПлатежноеПоручениеИсходящее")
		или ТипЗнч(ДокументОбъект) = Тип("ДокументСсылка.ПлатежноеПоручениеИсходящее") Тогда     		
		мТекущийПользователь = глЗначениеПеременной("глТекущийПользователь");
		мРолиПользователя = абс_БизнесПроцессы.ПолучитьСписокДоступныхРолейПользователя(мТекущийПользователь);	
		врТолькоПросмотр  = мРолиПользователя.Найти(Справочники.РолиИсполнителей.СотрудникДК) = Неопределено
							и Константы.абс_СоответствияСтатейДДСИУправленческихСтатейЗатратВклКонтроль.Получить();			
		ФормаРедактированияАналитик.ЭлементыФормы.абс_БюджетнаяСтатья.ТолькоПросмотр = врТолькоПросмотр;
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.КорректировкаДолга") Тогда
		ФормаРедактированияАналитик.ЭлементыФормы.абс_ТипКонтрагента.ТолькоПросмотр = Истина;	
	КонецЕсли;
	
КонецПроцедуры
//- Машута А. 09.10.2018
