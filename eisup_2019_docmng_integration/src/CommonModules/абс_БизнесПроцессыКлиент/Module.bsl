&НаКлиенте
Функция ВвестиВнутреннийНомерТелефона(СтруктураПользователя) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтруктураПользователя.Пользователь) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтруктураПользователя.ВнутреннийНомер) Тогда
		
		ТекВнутреннийНомер = 0;
		
		Если ВвестиСтроку(ТекВнутреннийНомер, "Введите свой внутренний номер телефона:", 7, Ложь) Тогда
			
			абс_БизнесПроцессыПривелегированный.ЗаписатьВнутреннийНомерПользователя(СтруктураПользователя.Пользователь, ТекВнутреннийНомер);
						
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

//Сторчевой А.Н. 7747248 {
Процедура ДобавитьПолеВводаСНадписьюНаПанель(Форма, Лево, Верх, ИмяРеквизита, ТекстНадписи, ШиринаНадписи, ШиринаПоляВвода = 0, Панель) Экспорт
	
	Надпись = ДобавитьЭлементУправления(Форма, Тип("Надпись"), "Надпись" + ИмяРеквизита,
		Новый Структура("Лево, Верх, Ширина, Высота, Заголовок", Лево, Верх, ШиринаНадписи, 19, ТекстНадписи), Панель);
		
	Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Панель, ГраницаЭлементаУправления.Лево);
	Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Право, Панель, ГраницаЭлементаУправления.Лево);
	Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Верх, Панель, ГраницаЭлементаУправления.Верх);
	Надпись.УстановитьПривязку(ГраницаЭлементаУправления.Низ, Панель, ГраницаЭлементаУправления.Верх);
	
	ШиринаПоля = ?(ШиринаПоляВвода = 0, Панель.Ширина - ШиринаНадписи - Лево * 2 - 2, ШиринаПоляВвода);
	
	Поле = ДобавитьЭлементУправления(Форма, Тип("ПолеВвода"), ИмяРеквизита,
		Новый Структура("Лево, Верх, Ширина, Высота, Данные", Лево + ШиринаНадписи, Верх, ШиринаПоля, 19, ИмяРеквизита), Панель);
	
	СкопироватьПривязку(ГраницаЭлементаУправления.Лево, Надпись, Поле);
	СкопироватьПривязку(ГраницаЭлементаУправления.Верх, Надпись, Поле);
	СкопироватьПривязку(ГраницаЭлементаУправления.Низ, Надпись, Поле);
	Поле.УстановитьПривязку(ГраницаЭлементаУправления.Право, Панель, ?(ШиринаПоляВвода = 0, ГраницаЭлементаУправления.Право, ГраницаЭлементаУправления.Лево));
	
КонецПроцедуры // ДобавитьПолеВводаСНадписьюНаПанель()

Функция ДобавитьЭлементУправления(Форма, ТипЭлемента, ИмяЭлемента, Свойства, Владелец = Неопределено) Экспорт
	
	ЭлементУправления = Форма.ЭлементыФормы.Добавить(ТипЭлемента, ИмяЭлемента, Истина, ?(Владелец = Неопределено, Форма.Панель, Владелец));
	ЗаполнитьЗначенияСвойств(ЭлементУправления, Свойства);
	
	Возврат ЭлементУправления;
	
КонецФункции // ДобавитьЭлементУправления()

//Сторчевой А.Н. D2016-1222-3124 {
Функция ДобавитьФлажокНаПанель(Форма, Лево, Верх, Ширина = 0, ИмяРеквизита, Заголовок, ЗаголовокСлева = Истина, Панель) Экспорт
	
	Результат = ДобавитьЭлементУправления(Форма, Тип("Флажок"), ИмяРеквизита,
		Новый Структура("Лево, Верх, Ширина, Высота, Заголовок, ПоложениеЗаголовка, Данные",
			Лево,
			Верх,
			?(Ширина = 0, Панель.Ширина - Лево * 2 - 2, Ширина),
			19,
			Заголовок,
			?(ЗаголовокСлева, ПоложениеЗаголовка.ЗаголовокСлева, ПоложениеЗаголовка.ЗаголовокСправа),
			ИмяРеквизита
		),
		Панель
	);
		
	Результат.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Панель, ГраницаЭлементаУправления.Лево);
	Результат.УстановитьПривязку(ГраницаЭлементаУправления.Право, Панель, ГраницаЭлементаУправления.Лево);
	Результат.УстановитьПривязку(ГраницаЭлементаУправления.Верх, Панель, ГраницаЭлементаУправления.Верх);
	Результат.УстановитьПривязку(ГраницаЭлементаУправления.Низ, Панель, ГраницаЭлементаУправления.Верх);

	Возврат Результат;
	
КонецФункции // ДобавитьФлажокНаПанель()	
// } Сторчевой А.Н. D2016-1222-3124

Процедура СкопироватьПривязку(Граница, ЭлементИсточник, ЭлементПриемник) Экспорт
	
	Перем ПервыйЭлемент, ГраницаПервогоЭлемента;
	Перем ВторойЭлемент, ГраницаВторогоЭлемента;
	
	ЭлементИсточник.ПолучитьПривязку(Граница, ПервыйЭлемент, ГраницаПервогоЭлемента, ВторойЭлемент, ГраницаВторогоЭлемента);
	ЭлементПриемник.УстановитьПривязку(Граница, ПервыйЭлемент, ГраницаПервогоЭлемента, ВторойЭлемент, ГраницаВторогоЭлемента);
	
КонецПроцедуры // СкопироватьПривязку()
// } Сторчевой А.Н. 7747248

//+++ Григорьев Д.В. (#77117167)
Процедура ИзменитьДатуДокумента(СсылкаНаДокумент) Экспорт
	
	мРолиПользователя = абс_БизнесПроцессы.ПолучитьСписокДоступныхРолейПользователя();
	
	Если НЕ ЗначениеЗаполнено(мРолиПользователя.Найти(Справочники.РолиИсполнителей.РедактированиеДатыРасходаЗРС)) Тогда		
		ttk_ОбщегоНазначения.СообщитьОбОшибке("Не достаточно прав доступа для выполнения операции.");
		Возврат;		
	КонецЕсли;
	
	ТекДокумент = СсылкаНаДокумент; //ЭлементыФормы.ДокументСписок.ТекущиеДанные.Ссылка;
	Если Не ЗначениеЗаполнено(ТекДокумент) Тогда
    	Возврат;
	КонецЕсли;	
	
	Если Не ТекДокумент.Статус = Перечисления.абсСтатусыПлатежей.Подготовка Тогда
		Сообщить("В текущем статусе дату документа изменить нельзя. Изменения возможны только в статусе ""Подготовка""!", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	ТекДата = ТекДокумент.Дата;	
	Если Не ВвестиДату(ТекДата, "Введите новую дату документа", ЧастиДаты.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекДата <= ТекДокумент.Дата Тогда
		Сообщить("Дату документа возможно изменить только на будущее!",СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	             	
	//контроль по ПП
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.Ссылка
		|ИЗ
		|	Документ.ПлатежноеПоручениеИсходящее.РасшифровкаПлатежа КАК ПлатежноеПоручениеИсходящееРасшифровкаПлатежа
		|ГДЕ
		|	(ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.ДокументРасчетовСКонтрагентом = &ДокументОснование
		|			ИЛИ ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.Ссылка.ДокументОснование = &ДокументОснование)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.Ссылка";						  
    Запрос.УстановитьПараметр("ДокументОснование",ТекДокумент);
	Если Запрос.Выполнить().Пустой() Тогда	
		абс_БизнесПроцессыПривелегированный.ИзменитьДатуДокуммента(ТекДокумент, ТекДата, Истина);
	Иначе	
		Сообщить("По документу '" + Строка(СсылкаНаДокумент) + "' существуют платежные документы. Изменение даты документа невозможно.", СтатусСообщения.Информация);
		Возврат;
	КонецЕсли;		
	
КонецПроцедуры
//+++ Григорьев Д.В. (#77117167)