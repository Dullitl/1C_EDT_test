#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМЫ СПИСКА

Функция РежимВводаПоЦФО(мРежимВводаПоказателей) Экспорт
	
	Возврат мРежимВводаПоказателей <> Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
	
КонецФункции

// Устанавливает свертку панелей и видимость реквизитов формы в зависимости от выбранной организационной структуры 
//
Процедура ВидОрганизационнойСтруктурыПредприятияПриИзменении(мРежимВводаПоказателей, ЭлементыФормы) Экспорт
	
	РежимВводаПоЦФО = РежимВводаПоЦФО(мРежимВводаПоказателей);
	
	ИспользоватьУправленческийУчетЗарплаты = глЗначениеПеременной("глИспользоватьУправленческийУчетЗарплаты");
	Если ИспользоватьУправленческийУчетЗарплаты Тогда
		ЭлементыФормы.ДействияФормы.Кнопки.ПорядокФормированияСМ.Кнопки.ПоЦФО.Пометка			= РежимВводаПоЦФО;
		ЭлементыФормы.ДействияФормы.Кнопки.ПорядокФормированияСМ.Кнопки.ПоОрганизациям.Пометка	= Не РежимВводаПоЦФО;
	КонецЕсли;
	
	ЭлементыФормы.ПанельОрганизации.Свертка = ?(РежимВводаПоЦФО, РежимСверткиЭлементаУправления.Верх, РежимСверткиЭлементаУправления.Нет);
		
КонецПроцедуры // ВидОрганизационнойСтруктурыПредприятияПриИзменении

Процедура ПереключитьРежимВводаПоказателейПоОрганизациям(мРежимВводаПоказателей, мОрганизация, ЭтаФорма, Отбор)
	
	мРежимВводаПоказателей = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
	
	Если Не ЗначениеЗаполнено(Отбор.Организация.Значение) Тогда
		Если мОрганизация = Неопределено Тогда
			// заполним организацию
			РаботаСДиалогами.ЗаполнениеОтбораПоОрганизацииПоУмолчанию(ЭтаФорма, Отбор.Организация, ЭтаФорма.ЭлементыФормы.Организация, Неопределено, Ложь, глЗначениеПеременной("глТекущийПользователь"));
			
			Отбор.Организация.Использование = Истина;
			Если Не ЗначениеЗаполнено(Отбор.Организация.Значение) Тогда
				Отбор.Организация.ВидСравнения	= ВидСравнения.НеРавно;
			КонецЕсли;
		Иначе
			Отбор.Организация.Значение = мОрганизация;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПереключитьРежимВводаПоказателейПоЦФО(мРежимВводаПоказателей, мОрганизация, Отбор)
	
	мОрганизация = Отбор.Организация.Значение;
	Отбор.Организация.Значение = Справочники.Организации.ПустаяСсылка();
	
	мРежимВводаПоказателей = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности;
	
КонецПроцедуры

Процедура ДействияФормыПоОрганизациям(мРежимВводаПоказателей, мОрганизация, ЭтаФорма, Отбор) Экспорт
	
	ПереключитьРежимВводаПоказателейПоОрганизациям(мРежимВводаПоказателей, мОрганизация, ЭтаФорма, Отбор);
	
	ВидОрганизационнойСтруктурыПредприятияПриИзменении(мРежимВводаПоказателей, ЭтаФорма.ЭлементыФормы);
	
КонецПроцедуры

Процедура ДействияФормыПоЦФО(мРежимВводаПоказателей, мОрганизация, ЭтаФорма, Отбор) Экспорт
	
	ПереключитьРежимВводаПоказателейПоЦФО(мРежимВводаПоказателей, мОрганизация, Отбор);
	
	ВидОрганизационнойСтруктурыПредприятияПриИзменении(мРежимВводаПоказателей, ЭтаФорма.ЭлементыФормы);
	
КонецПроцедуры

#КонецЕсли
