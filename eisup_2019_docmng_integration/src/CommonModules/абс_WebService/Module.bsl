//////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Получает обработку из хранилища внешних обработок
Функция ПолучитьОбработкуИзХранилища(ВнОбработка, ОписаниеОшибки = "") Экспорт
	
	Если НЕ ТипЗнч(ВнОбработка) = Тип("СправочникСсылка.ВнешниеОбработки") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Неопределено;
		
	Попытка
		ИмяФайла = ПолучитьИмяВременногоФайла("epf");
		ДвоичныеДанные = ВнОбработка.ХранилищеВнешнейОбработки.Получить();
		ДвоичныеДанные.Записать(ИмяФайла);
		Результат = ВнешниеОбработки.Создать(ИмяФайла, Ложь);
	Исключение
		ОписаниеОшибки = "Ошибка создания внешней обработки: " + ОписаниеОшибки();
		СообщитьОбОшибке(ОписаниеОшибки, "ПолучитьОбработкуИзХранилища()");
		Результат = Неопределено;
	КонецПопытки;

	Возврат Результат;

КонецФункции // ПолучитьОбработкуИзХранилища()

// Сообщение с записью в журнал регистрации
Процедура СообщитьОбОшибке(ОписаниеОшибки, ИмяСобытия = "Ошибка") Экспорт
	
	Сообщить(ОписаниеОшибки, СтатусСообщения.ОченьВажное);
	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки);
	
КонецПроцедуры

// Возвращает обработчик WebService последней версии (либо из конфигурации, либо из внешних обработок)
Функция ПолучитьОбработчикWebService() Экспорт
	
	абс_ОбработчикWebService = Обработки.абс_ОбработчикWebService.Создать();
	
	// Получаем обработку их хранилища (если зарегистрирована)
	абс_ОбработчикWebServiceПараметры = Константы.абс_ОбработчикWebService.Получить().Получить();
	Если ЗначениеЗаполнено(абс_ОбработчикWebServiceПараметры) Тогда
		абс_ОбработчикWebServiceСсылка = абс_ОбработчикWebServiceПараметры["Ссылка"];
		Если ЗначениеЗаполнено(абс_ОбработчикWebServiceСсылка) Тогда
			абс_ОбработчикWebServiceХран = абс_WebService.ПолучитьОбработкуИзХранилища(абс_ОбработчикWebServiceСсылка);
			// Сравниваем версии
			Если абс_ОбработчикWebServiceХран <> Неопределено И абс_ОбработчикWebServiceХран.Версия > абс_ОбработчикWebService.Версия Тогда
				абс_ОбработчикWebService = абс_ОбработчикWebServiceХран;	
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат абс_ОбработчикWebService;
	
КонецФункции	