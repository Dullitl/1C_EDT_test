////////////////////////////////////////////////////////////////////////////////
//  Методы, позволяющие начать и закончить замер времени выполнения ключевой операции
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Активизирует замер времени выполнения ключевой операции.
//
// Параметры:
//  КлючеваяОперация - СправочникСсылка.КлючевыеОперации - ключевая операция. 
//  При вызове с сервера аргумент игнорируется.
//
// Возвращаемое значение:
//  Дата или число - время начала с точностью до миллисекунд или секунд в зависимости от версии платформы.
//
Функция НачатьЗамерВремени(КлючеваяОперация = Неопределено) Экспорт
	
	ВремяНачала = 0;
	Если абс_БСПОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительности() Тогда
		ДатаНачала = ЗначениеТаймера(Ложь);
		ВремяНачала = ЗначениеТаймера();
		#Если Клиент Тогда
			Если КлючеваяОперация = Неопределено Тогда
				ВызватьИсключение НСтр("ru = 'Не указана ключевая операция.'");
			КонецЕсли;
			Если ОценкаПроизводительностиЗамерВремени = Неопределено Тогда
				ОценкаПроизводительностиЗамерВремени = Новый Структура;
				ОценкаПроизводительностиЗамерВремени.Вставить("Замеры", Новый Соответствие);
				
				ТекущийПериодЗаписи = абс_БСПОценкаПроизводительностиВызовСервераПолныеПрава.ПериодЗаписи();
				ОценкаПроизводительностиЗамерВремени.Вставить("ПериодЗаписи", ТекущийПериодЗаписи);
				
				ДатаИВремяНаСервере = абс_БСПОценкаПроизводительностиВызовСервераПолныеПрава.ДатаИВремяНаСервере();
				ДатаИВремяНаКлиенте = ТекущаяДата();
				ОценкаПроизводительностиЗамерВремени.Вставить(
					"СмещениеДатыКлиента", 
					ДатаИВремяНаСервере - ДатаИВремяНаКлиенте);
				
				ПодключитьОбработчикОжидания("абс_БСПЗаписатьРезультатыАвто", ТекущийПериодЗаписи, Истина);
			КонецЕсли;
			Замеры = ОценкаПроизводительностиЗамерВремени["Замеры"]; 
			
			БуферКлючевойОперации = Замеры.Получить(КлючеваяОперация);
			Если БуферКлючевойОперации = Неопределено Тогда
				БуферКлючевойОперации = Новый Соответствие;
				Замеры.Вставить(КлючеваяОперация, БуферКлючевойОперации);
			КонецЕсли;
			
			СмещениеДатыКлиента = ОценкаПроизводительностиЗамерВремени["СмещениеДатыКлиента"];
			ДатаНачала = ДатаНачала + СмещениеДатыКлиента;
			НачатыйЗамер = БуферКлючевойОперации.Получить(ДатаНачала);
			Если НачатыйЗамер = Неопределено Тогда
				БуферЗамера = Новый Соответствие;
				БуферЗамера.Вставить("ВремяНачала", ВремяНачала);
				БуферКлючевойОперации.Вставить(ДатаНачала, БуферЗамера);
			КонецЕсли;
			
			ПодключитьОбработчикОжидания("абс_БСПЗакончитьЗамерВремениАвто", 0.1, Истина);
		#КонецЕсли
	КонецЕсли;

	Возврат ВремяНачала;
	
КонецФункции

// Процедура завершает замер времени на сервере и записывает результат на сервере
// Параметры:
//  КлючеваяОперацияСсылка - СправочникСсылка.КлючевыеОперации
//  ВремяНачала - Число или Дата
Процедура ЗакончитьЗамерВремени(КлючеваяОперацияСсылка, ВремяНачала) Экспорт
	ВремяОкончания = ЗначениеТаймера();		
    Длительность = (ВремяОкончания - ВремяНачала);
	Если ТипЗнч(ВремяОкончания) = Тип("Число") Тогда
		ДатаНачала = ЗначениеТаймера(Ложь) - (Длительность / 1000);
	Иначе	
		ДатаНачала = ВремяНачала;
	КонецЕсли;	
	абс_БСПОценкаПроизводительностиВызовСервераПолныеПрава.ЗафиксироватьДлительностьКлючевойОперации(
		КлючеваяОперацияСсылка, 
		Длительность, 
		ДатаНачала,
		ЗначениеТаймера(Ложь));
КонецПроцедуры

// Функция вызывается при старте замера времени и его завершении.
// ТекущаяДата вместо ТекущаяДатаСеанса используется осмысленно.
// Но нужно помнить, что если время начала замера получено на клиенте, 
// то и время конца замера нужно вычислять на клиенте. Для сервера то же самое.
//
// Возвращаемое значение:
//  Дата - время начала замера.
Функция ЗначениеТаймера(ВысокаяТочность = Истина) Экспорт
	
	Перем ЗначениеТаймера;
	Если ВысокаяТочность Тогда
		
		ЗначениеТаймера = ТекущаяУниверсальнаяДатаВМиллисекундах() / 1000.0;
		Возврат ЗначениеТаймера;
		
	Иначе
		
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Возврат ТекущаяДатаСеанса();
#Иначе
		Возврат ТекущаяДата();
#КонецЕсли
		
	КонецЕсли;
	
КонецФункции

// Ключ параметра регламентного задания, соответствующий локальному каталогу экспорта
Функция ЛокальныйКаталогЭкспортаКлючЗадания() Экспорт
	Возврат "ЛокальныйКаталогЭкспорта";		
КонецФункции

// Ключ параметра регламентного задания, соответствующий ftp каталогу экспорта
Функция FTPКаталогЭкспортаКлючЗадания() Экспорт
	Возврат "FTPКаталогЭкспорта";		
КонецФункции

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
// Процедура записывает данные в журнал регистрации
//
// Параметры:
//  ИмяСобытия - Строка
//  Уровень - УровеньЖурналаРегистрации
//  ТекстСообщения - Строка
//
Процедура ЗаписатьВЖурналРегистрации(ИмяСобытия, Уровень, ТекстСообщения) Экспорт
	
	ЗаписьЖурналаРегистрации(ИмяСобытия,
		Уровень,
		,
		"Оценка производительности",
		ТекстСообщения);
	
КонецПроцедуры
#КонецЕсли

// Получает имя дополнительного свойства не проверять приоритеты при записи ключевой операции
//
// Возвращаемое значение:
//  Строка - имя дополнительного свойства
//
Функция НеПроверятьПриоритет() Экспорт
	Возврат "НеПровеятьПриоритет";	
КонецФункции



Функция ПолучитьКлючевыеОперацииПоСтатусам(НачальныйСтатус, КонечныйСтатус) Экспорт
	
	ХранилищеКэша = абс_БСПОценкаПроизводительностиВызовСервераПолныеПрава.ПолучитьКэшКлючевыхОпераций();
	
	абс_БСПКэшКлючевыхОпераций = ХранилищеКэша.Получить();
	
	МассивКлючевыхОпераций = Новый Массив;
	
	СтрокиКлючевыхОпераций = абс_БСПКэшКлючевыхОпераций.ТаблицаПереходов.НайтиСтроки(Новый Структура("НачальныйСтатус, КонечныйСтатус", НачальныйСтатус, КонечныйСтатус));
	Для Каждого СтрокаОперации Из СтрокиКлючевыхОпераций Цикл
		МассивКлючевыхОпераций.Добавить(СтрокаОперации.Ссылка);
	КонецЦикла;
	
	СтрокаОперации = абс_БСПКэшКлючевыхОпераций.ОперацииПереходаКСтатусу.Найти(КонечныйСтатус, "КонечныйСтатус");
	Если СтрокаОперации<>Неопределено Тогда
		МассивКлючевыхОпераций.Добавить(СтрокаОперации.Ссылка);
	КонецЕсли;
	
	Возврат МассивКлючевыхОпераций;
	
КонецФункции

