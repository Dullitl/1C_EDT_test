

///////////////////////////////////////////////////////////////////////////////////
// Процедуры для подготовки и записи движений документа при проведении с клиента
// {{KM WARE Погорелов В.М. Заявка № 14.10.2015 начало
// Данное изменение нужно для увеличения параметров процедуры, и добавление параметров в ДополнительныеСвойства 
// {{Старый код:
//Процедура ВыполнитьПроведениеДокумента(Знач ДокументСсылка, РежимПроведения=Неопределено, ЭтоНовый = Ложь, Отказ = Ложь) Экспорт
//	
//	ДополнительныеСвойства = Новый Структура;
//	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый);
//	
//	абс_ПроведениеДокументовСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(ДокументСсылка, ДополнительныеСвойства, РежимПроведения);
// }}Новый код:
Процедура ВыполнитьПроведениеДокумента(Знач ДокументСсылка, РежимПроведения=Неопределено,
	ЭтоНовый = Ложь, Отказ = Ложь, ДополнительныеСвойства=Неопределено) Экспорт
	
	Если ДополнительныеСвойства = Неопределено Тогда
		ДополнительныеСвойства = Новый Структура;
	КонецЕсли;
	
	ДляПроведения = Новый Структура;
	ДляПроведения.Вставить("Ссылка"             , ДокументСсылка);
	ДляПроведения.Вставить("РежимПроведения"    , РежимПроведения);
	ДляПроведения.Вставить("МетаданныеДокумента", ДокументСсылка.Метаданные());
	
	ДополнительныеСвойства.Вставить("ЭтоНовый"     , ЭтоНовый);
	ДополнительныеСвойства.Вставить("ДляПроведения", ДляПроведения);
// }}KM WARE Погорелов В.М. Заявка № 14.10.2015 окончание 

	ЗаполнитьСтруктуруНаборовЗаписейДвижений(ДополнительныеСвойства);
	
	МенеджерДокумента = Документы[ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента.Имя];
	МенеджерДокумента.ПроведениеДокумента(ДополнительныеСвойства, Отказ);
	
КонецПроцедуры

Процедура ЗаполнитьСтруктуруНаборовЗаписейДвижений(ДополнительныеСвойства) Экспорт
	
	ДокументСсылка = ДополнительныеСвойства.ДляПроведения.Ссылка;
	МетаданныеДокумента = ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента;
	
	СтруктураДвижений = Новый Структура;
	
	Для Каждого МетаданныеРегистра Из МетаданныеДокумента.Движения Цикл
		
		ИмяРегистра = МетаданныеРегистра.Имя;
		
		НаборЗаписей = Неопределено;
		Если Метаданные.РегистрыБухгалтерии.Найти(ИмяРегистра)<>Неопределено Тогда
			НаборЗаписей = РегистрыБухгалтерии[ИмяРегистра].СоздатьНаборЗаписей();
		ИначеЕсли Метаданные.РегистрыНакопления.Найти(ИмяРегистра)<>Неопределено Тогда
			НаборЗаписей = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей();
		ИначеЕсли Метаданные.РегистрыСведений.Найти(ИмяРегистра)<>Неопределено Тогда
			НаборЗаписей = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();
		КонецЕсли;
		
		Если НаборЗаписей<>Неопределено Тогда
			// {{KM WARE Погорелов В.М. Заявка № 14.10.2015 начало
			// отбор по регистратору
			// {{Старый код:
			//НаборЗаписей.Отбор.Установить(ДокументСсылка);
			// }}Новый код:
			НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
			// }}KM WARE Погорелов В.М. Заявка № 14.10.2015 окончание 
			СтруктураДвижений.Вставить(ИмяРегистра, НаборЗаписей);
		КонецЕсли;
		
	КонецЦикла;
	
	ДополнительныеСвойства.Вставить("Движения", СтруктураДвижений);
	
КонецПроцедуры
