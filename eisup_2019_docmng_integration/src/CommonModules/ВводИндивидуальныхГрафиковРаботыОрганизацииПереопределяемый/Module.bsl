
Процедура ВнестиПечатныеФормы(СтруктураМакетов) Экспорт

	СтруктураМакетов.Вставить("Т13", "Форма Т-13");
	
КонецПроцедуры

#Если Клиент Тогда
	
// Возвращает "длинное" описание печатной формы
//
// Параметры
//  ИмяМакета - строка, идентификатор печатной формы
//
// Возвращаемое значение:
//   строка
//
Функция ПолучитьОписаниеПечатнойФормы(ИмяМакета) Экспорт

	Если ИмяМакета = "Т13" Тогда
		Возврат " Унифицированная форма Т-13"
	Иначе
		Возврат ""
	КонецЕсли;

КонецФункции // ПолучитьОписаниеПечатнойФормы()

// Для переданного документа формирует указанную печатную форму
//
// Параметры
//  ИмяМакета - строка, идентификатор печатной формы
//
// Возвращаемое значение:
//   табличный документ или Неопределено
//
Функция СформироватьПечатнуюФорму(ИмяМакета, ДокументОбъект, СписокСотрудников) Экспорт

	Если ИмяМакета = "Т13" Тогда
		
		ТабДокумент = Новый ТабличныйДокумент;	
		
		Отчет = Отчеты.УнифицированнаяФормаТ13.Создать();
		
		ФильтрСоответствия = Новый Соответствие;
		
		ФильтрСоответствия.Вставить("Организация", ДокументОбъект.Организация);
		ФильтрСоответствия.Вставить("Подразделение", ДокументОбъект.ПодразделениеОрганизации);
		
		ФильтрСоответствия.Вставить("ОтборОрганизации", ЗначениеЗаполнено(ДокументОбъект.Организация));
		ФильтрСоответствия.Вставить("ОтборПодразделения", ЗначениеЗаполнено(ДокументОбъект.ПодразделениеОрганизации));
		
		ФильтрСоответствия.Вставить("ВидСравненияОрганизации", ВидСравнения.Равно);
		ФильтрСоответствия.Вставить("ВидСравненияПодразделения", ВидСравнения.Равно);
		
		ФильтрСоответствия.Вставить("ПериодРегистрации", ДокументОбъект.ПериодРегистрации);
		
		Если СписокСотрудников.Количество() = 1 Тогда
			Если ЗначениеЗаполнено(СписокСотрудников[0].Значение) Тогда
				ФильтрСоответствия.Вставить("Работник", СписокСотрудников[0].Значение);
				ФильтрСоответствия.Вставить("ВидСравненияРаботника", ВидСравнения.Равно);
				ФильтрСоответствия.Вставить("ОтборРаботника", Истина);
			КонецЕсли;
		ИначеЕсли СписокСотрудников.Количество() > 1 Тогда
			ФильтрСоответствия.Вставить("Работник", СписокСотрудников);
			ФильтрСоответствия.Вставить("ВидСравненияРаботника", ВидСравнения.ВСписке);
			ФильтрСоответствия.Вставить("ОтборРаботника", Истина);
		КонецЕсли;
		
		Отчет.УстановитьФильтр(ФильтрСоответствия);
		ТекстОшибки = "";
		Отчет.СформироватьОтчет(ТабДокумент, "Табель", ТекстОшибки);
		Если ТекстОшибки <> "" Тогда
			Если ДокументОбъект.СпособВводаДанных = Перечисления.СпособыВводаДанныхОВремени.ПоДням  Тогда
				ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
				ОбработкаКомментариев.УдалитьСообщения();
			КонецЕсли;
			ОбработкаКомментариев.ДобавитьСообщение(ТекстОшибки, Перечисления.ВидыСообщений.ВажнаяИнформация);
			ОбработкаКомментариев.ПоказатьСообщения();
			Возврат Неопределено;
		Иначе
			Возврат ТабДокумент;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции // СформироватьПечатнуюФорму()

#КонецЕсли


