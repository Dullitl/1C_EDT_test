
Процедура ЗаписатьУзелДереваXMLВXML(СтрокаДерева, ПотокXML, ПрефиксПространстваИмен = "xsi", URIПространстваИмен = "http://www.w3.org/2001/XMLSchema") Экспорт
	
	Если ТипЗнч(СтрокаДерева) = Тип("СтрокаДереваЗначений") Тогда
		
		Если Не ПустаяСтрока(СтрокаДерева.Имя) Тогда
			
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Имя);
			ПотокXML.ЗаписатьСоответствиеПространстваИмен(ПрефиксПространстваИмен, URIПространстваИмен);
			СписокАтрибутов = ПроцедурыПерсонифицированногоУчета.НайтиАтрибуты(СтрокаДерева);
			
			Если СписокАтрибутов <> Неопределено Тогда
				Для каждого СтрокаСАтрибутом Из СписокАтрибутов.Строки Цикл
					ПотокXML.ЗаписатьАтрибут(СтрокаСАтрибутом.Имя, СтрокаСАтрибутом.Значение);
				КонецЦикла;
			КонецЕсли;
			
			ПотокXML.ЗаписатьТекст(СтрокаДерева.Значение);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого Лист из СтрокаДерева.Строки Цикл
		Если ПроцедурыПерсонифицированногоУчета.СтрокаСАтрибутами(Лист) Тогда
			Продолжить;
		КонецЕсли;
		ЗаписатьУзелДереваXMLВXML(Лист, ПотокXML, ПрефиксПространстваИмен, URIПространстваИмен);
	КонецЦикла;
	
	Если ТипЗнч(СтрокаДерева) = Тип("СтрокаДереваЗначений") Тогда
		Если Не ПустаяСтрока(СтрокаДерева.Имя) Тогда
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

Функция КодОКАТОизСуммыОКАТОиКПП(ОКАТО_КПП) Экспорт

	Возврат СокрЛП(Лев(ОКАТО_КПП,11))

КонецФункции // ОКАТОизСуммыОКАТОиКПП()

Функция КППизСуммыОКАТОиКПП(ОКАТО_КПП) Экспорт

	Возврат СокрЛП(Прав(ОКАТО_КПП,9))

КонецФункции // КППизСуммыОКАТОиКПП()

// Проверяет, написана ли строка только русскими и латинскими буквами 
//
// Параметры:
//  СтрокаПараметр - строка, проверяемая строка.
//
// Возвращаемое значение:
//		Истина - строка состоит из допустимых символов
//		Ложь - в строке встречаются недопустимые символы
// 
// Описание:
//		Строка проверяется на наличие только русских и латинских букв;
//		кроме того,	допускаются дефис, "Ё", "ё".
//
Функция СтрокаНаписанаРусскимиИлиЛатинскими(Знач СтрокаПараметр) Экспорт
	
	СтрокаПараметр = СокрЛП(СтрокаПараметр);	

	КоличествоСимволов = СтрДлина(СтрокаПараметр);
	
	Если  КоличествоСимволов > 0 Тогда
		
		ПервыйСимвол = КодСимвола(Лев(СтрокаПараметр,1)); 
		Русские = (ПервыйСимвол >= 192) или (ПервыйСимвол = 184) или (ПервыйСимвол = 168);
		
		СписокДопустимыхЗначений = Новый Соответствие;
		СписокДопустимыхЗначений.Вставить(184,Истина);   // ё
		СписокДопустимыхЗначений.Вставить(168,Истина);   // Ё
		СписокДопустимыхЗначений.Вставить(45,Истина);   //  "-"

		Для Сч = 1 По КоличествоСимволов Цикл
			
			Код = КодСимвола(Сред(СтрокаПараметр,Сч));   
			
			// Большие латинские буквы: 65 - 90
			// Маленькие латинские буквы: 97 - 122
			// Русские буквы: 192 и больше
			
			//русские:
			Если Русские Тогда
				
				Если (СписокДопустимыхЗначений[Код] = Неопределено) и (Код < 192)  Тогда
					Возврат Ложь;
				КонецЕсли;

			// латинские:	
			ИначеЕсли (Код <> 45) и ((Код < 65) или	(Код > 90) и (Код < 97) или (Код > 122))  Тогда    
				Возврат Ложь;
			КонецЕсли;
				
		КонецЦикла;
		
		Возврат Истина;

	Иначе 	

		Возврат Истина;

	КонецЕсли;
	
КонецФункции // СтрокаНаписанаРусскимиИлиЛатинскими()

Функция СуммаОКАТОиКПП(КодПоОКАТО, КПП) Экспорт 

	Возврат РегламентированнаяОтчетность.ДополнитьСтроку(СокрЛП(КодПоОКАТО), 11, " ", 1) + "/" + РегламентированнаяОтчетность.ДополнитьСтроку(СокрЛП(КПП), 9, " ", 1)

КонецФункции // СуммаОКАТОиКПП()

////////////////////////////////////////////////////////////////////////////////

Процедура ПеренумероватьСправкиДокумента(ДокументОбъект, НомерПервойСправки) Экспорт

	ТаблицаДоходов = ДокументОбъект.СведенияОДоходах.Выгрузить();
	ТаблицаВычетов = ДокументОбъект.СведенияОВычетах.Выгрузить();
	ТаблицаНалогов = ДокументОбъект.СуммыНалогов.Выгрузить();
	ДокументОбъект.СведенияОДоходах.Очистить();
	ДокументОбъект.СведенияОВычетах.Очистить();
	ДокументОбъект.СуммыНалогов.Очистить();
	СтруктураПоиска = Новый Структура("НомерСправки");
	Для каждого СтрокаТЧ Из ДокументОбъект.СотрудникиОрганизации Цикл
		СтруктураПоиска.НомерСправки = СтрокаТЧ.НомерСправки;
		СтрокаТЧ.НомерСправки = НомерПервойСправки;
		Для каждого СтрокаМассива Из ТаблицаДоходов.НайтиСтроки(СтруктураПоиска) Цикл
			НоваяСтрока = ДокументОбъект.СведенияОДоходах.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаМассива);
			НоваяСтрока.НомерСправки = НомерПервойСправки;
		КонецЦикла;
		Для каждого СтрокаМассива Из ТаблицаВычетов.НайтиСтроки(СтруктураПоиска) Цикл
			НоваяСтрока = ДокументОбъект.СведенияОВычетах.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаМассива);
			НоваяСтрока.НомерСправки = НомерПервойСправки;
		КонецЦикла;
		Для каждого СтрокаМассива Из ТаблицаНалогов.НайтиСтроки(СтруктураПоиска) Цикл
			НоваяСтрока = ДокументОбъект.СуммыНалогов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаМассива);
			НоваяСтрока.НомерСправки = НомерПервойСправки;
		КонецЦикла;
		НомерПервойСправки = НомерПервойСправки + 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрочитатьДанныеСотрудника(ОписаниеСправки, Дата, НалоговыйПериод) Экспорт

	ФизЛицо = ОписаниеСправки.Сотрудник;
	
	Если ЗначениеЗаполнено(ФизЛицо) Тогда
		ТаблицаСотрудников = Новый ТаблицаЗначений;
		ТаблицаСотрудников.Колонки.Добавить("Сотрудник",Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
		ТаблицаСотрудников.Колонки.Добавить("Ставка",Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавки"));
		ЗаполнитьЗначенияСвойств(ТаблицаСотрудников.Добавить(), ОписаниеСправки);
		ЗаполнитьЗначенияСвойств(ОписаниеСправки, ДанныеСотрудников(ТаблицаСотрудников, Дата, НалоговыйПериод)[0]);
	Иначе
		ОписаниеСправки.АдресЗарубежом = "";
		ОписаниеСправки.АдресРФ = "";
		ОписаниеСправки.ВидДокумента = "";
		ОписаниеСправки.Гражданство = "";
		ОписаниеСправки.ДатаРождения = "";
		ОписаниеСправки.Имя = "";
		ОписаниеСправки.Отчество = "";
		ОписаниеСправки.Фамилия = "";
		ОписаниеСправки.ИНН = "";
		ОписаниеСправки.НомерДокумента = "";
		ОписаниеСправки.СерияДокумента = "";
		ОписаниеСправки.СтатусНалогоплательщика = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьДанныеСправки(ОписаниеСправки, СведенияОДоходах, СведенияОВычетах, СуммыНалогов, НалоговыйПериод, Организация, ГоловнаяОрганизация, КодОП, Дата, ПоказыватьПоКодуОКТМО = Ложь) Экспорт

	ФизЛицо = ОписаниеСправки.Сотрудник;
	Ставка = ОписаниеСправки.Ставка;
	
	Если Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(ФизЛицо) Или (НалоговыйПериод < 2011 и Не ЗначениеЗаполнено(Ставка)) Тогда
		Возврат
	КонецЕсли;
	
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	ТаблицаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаСотрудников.Колонки.Добавить("Ставка", Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавки"));
	ТаблицаСотрудников.Колонки.Добавить("НомерСправки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8, 0)));
	ЗаполнитьЗначенияСвойств(ТаблицаСотрудников.Добавить(), ОписаниеСправки);
	СтруктураДанных = ДанныеОДоходах(ТаблицаСотрудников, НалоговыйПериод, Организация, ГоловнаяОрганизация, КодОП, Дата, ПоказыватьПоКодуОКТМО);
	Для каждого СтрокаТЗ Из СтруктураДанных.Доходы Цикл
		ЗаполнитьЗначенияСвойств(СведенияОДоходах.Добавить(),СтрокаТЗ);
	КонецЦикла; 
	Для каждого СтрокаТЗ Из СтруктураДанных.Вычеты Цикл
		ЗаполнитьЗначенияСвойств(СведенияОВычетах.Добавить(),СтрокаТЗ);
	КонецЦикла; 
	Если НалоговыйПериод < 2011 Тогда
		ЗаполнитьЗначенияСвойств(ОписаниеСправки, СтруктураДанных.Итоги[0]);
	Иначе
		ПерваяСтрока = Истина;
		Для каждого СтрокаТЗ Из СтруктураДанных.Итоги Цикл
			ЗаполнитьЗначенияСвойств(СуммыНалогов.Добавить(),СтрокаТЗ);
			Если ПерваяСтрока Тогда
				ЗаполнитьЗначенияСвойств(ОписаниеСправки, СтрокаТЗ, "ДатаУведомления,КодНалоговогоОрганаУведомления,НомерУведомления");
				ПерваяСтрока = Ложь;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеОДоходах(ТаблицаСотрудников, НалоговыйПериод, Организация, ГоловнаяОрганизация, КодОП, Дата, ПоказыватьПоКодуОКТМО = Ложь) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылки", ТаблицаСотрудников);
	Запрос.УстановитьПараметр("ДатаЗакона229ФЗ", ПроведениеРасчетов.ДатаЗакона229ФЗ());
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ГодНП",НалоговыйПериод);
	НачалоНалоговогоПериода = Дата(НалоговыйПериод, 1, 1);
	КонецНалоговогоПериода = КонецГода(НачалоНалоговогоПериода);
	Запрос.УстановитьПараметр("КонецНП",КонецНалоговогоПериода);
	Запрос.УстановитьПараметр("НачалоНП", НачалоНалоговогоПериода);
	Запрос.УстановитьПараметр("НачалоСледующегоНП", КонецНалоговогоПериода + 1); // начало следующего налогового периода
	Запрос.УстановитьПараметр("ДатаПодачиСведений", Дата);
	Запрос.УстановитьПараметр("ДатаДоПодачиСведений", Дата - 1);
	ЧитатьСводныеДанные = КодОП = Неопределено;
	Запрос.УстановитьПараметр("ЧитатьСводныеДанные", ЧитатьСводныеДанные);
	Запрос.УстановитьПараметр("ЧитатьСводныеДанныеПоСтавкам", ЧитатьСводныеДанные Или НалоговыйПериод > 2010);
	Запрос.УстановитьПараметр("РазделятьИтогиПоСтавкам", НалоговыйПериод > 2010);
	Запрос.УстановитьПараметр("ОКАТО_КПП", КодОП);
	Запрос.УстановитьПараметр("ОКТМО_КПП", КодОП);
	Если ЧитатьСводныеДанные Тогда
		Запрос.УстановитьПараметр("КодПоОКАТО", Неопределено);
		Запрос.УстановитьПараметр("КПП", Неопределено);
		Запрос.УстановитьПараметр("КодПоОКТМО", Неопределено);
		Запрос.УстановитьПараметр("КПП", Неопределено);
	Иначе
		Запрос.УстановитьПараметр("КодПоОКАТО",КодОКАТОизСуммыОКАТОиКПП(КодОП));
		Запрос.УстановитьПараметр("КПП",КППизСуммыОКАТОиКПП(КодОП));
		Запрос.УстановитьПараметр("КодПоОКТМО",КодОКАТОизСуммыОКАТОиКПП(КодОП));
		Запрос.УстановитьПараметр("КПП",КППизСуммыОКАТОиКПП(КодОП));
	КонецЕсли;
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.УстановитьПараметр("ПустойКодПоОКТМО","");
	Запрос.УстановитьПараметр("ПустойКПП","");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СотрудникиОрганизаций.Сотрудник КАК Физлицо,
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.Ставка
	|ПОМЕСТИТЬ ВТФизлица
	|ИЗ
	|	&Ссылки КАК СотрудникиОрганизаций";
	Запрос.Выполнить();
	НачМесяца = НачалоНалоговогоПериода;
	ДатыПоМесяцамТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период";
	ДатыПоМесяцамТекст = ДатыПоМесяцамТекст + "
	|ПОМЕСТИТЬ ВТПериодыНалогаНаДоходы";
	Пока НачМесяца < НачалоМесяца(КонецНалоговогоПериода) Цикл
		НачМесяца = НачалоМесяца(КонецМесяца(НачМесяца) + 1);
		ДатыПоМесяцамТекст = ДатыПоМесяцамТекст +"
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	Запрос.Текст = ДатыПоМесяцамТекст;
	Запрос.Выполнить();
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Статусы.ФизЛицо,
	|	Статусы.Статус
	|ПОМЕСТИТЬ ВТРезидентствоФизлицНаКонецГода
	|ИЗ
	|	РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ.СрезПоследних(
	|			&КонецНП,
	|			ФизЛицо В
	|				(ВЫБРАТЬ
	|					ФизическиеЛица.ФизЛицо
	|				ИЗ
	|					ВТФизлица КАК ФизическиеЛица)) КАК Статусы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыРегистра.ФизЛицо КАК ФизЛицо,
	|	ПериодыРегистра.МесяцНалоговогоПериода,
	|	ЕСТЬNULL(СтатусНалогоплательщика.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) КАК Статус
	|ПОМЕСТИТЬ ВТРезидентствоФизлицНаКонецМесяца
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Физлица.ФизЛицо КАК ФизЛицо,
	|		Периоды.Период КАК МесяцНалоговогоПериода,
	|		МАКСИМУМ(СтатусНалогоплательщика.Период) КАК Период
	|	ИЗ
	|		ВТФизлица КАК Физлица
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПериодыНалогаНаДоходы КАК Периоды
	|			ПО (ИСТИНА)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|			ПО Физлица.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|				И (СтатусНалогоплательщика.Период <= КОНЕЦПЕРИОДА(Периоды.Период, МЕСЯЦ))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Физлица.ФизЛицо,
	|		Периоды.Период) КАК ПериодыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|		ПО ПериодыРегистра.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|			И ПериодыРегистра.Период = СтатусНалогоплательщика.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Физлица.ФизЛицо,
	|	Периоды.Период КАК МесяцНалоговогоПериода,
	|	ВЫБОР
	|		КОГДА Периоды.Период < &ДатаЗакона229ФЗ
	|			ТОГДА ЕСТЬNULL(РезидентствоФизлицНаКонецГода.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент))
	|		ИНАЧЕ РезидентствоФизлицНаКонецМесяца.Статус
	|	КОНЕЦ КАК ТекущийСтатус,
	|	ЕСТЬNULL(РезидентствоФизлицНаКонецГода.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) КАК Статус
	|ПОМЕСТИТЬ ВТСтатусы
	|ИЗ
	|	ВТФизлица КАК Физлица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПериодыНалогаНаДоходы КАК Периоды
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаКонецМесяца КАК РезидентствоФизлицНаКонецМесяца
	|		ПО Физлица.ФизЛицо = РезидентствоФизлицНаКонецМесяца.ФизЛицо
	|			И (Периоды.Период = РезидентствоФизлицНаКонецМесяца.МесяцНалоговогоПериода)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаКонецГода КАК РезидентствоФизлицНаКонецГода
	|		ПО Физлица.ФизЛицо = РезидентствоФизлицНаКонецГода.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо,
	|	МИНИМУМ(НДФЛИмущественныеВычетыФизлиц.Регистратор) КАК Регистратор
	|ПОМЕСТИТЬ ВТДокументыУведомлений
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|			И (&ЧитатьСводныеДанныеПоСтавкам
	|				ИЛИ СотрудникиОрганизаций.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|			И (НДФЛИмущественныеВычетыФизлиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛИмущественныеВычетыФизлиц.Год = &ГодНП)
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|ГДЕ
	|	НДФЛИмущественныеВычетыФизлиц.Регистратор ССЫЛКА Документ.НДФЛПодтверждениеПраваНаИмущественныйВычет
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыУведомлений.НомерСправки,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ДатаУведомления, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаУведомления,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.КодНалоговогоОргана, """") КАК КодНалоговогоОрганаУведомления,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.НомерВходящегоДокумента, """") КАК НомерУведомления
	|ПОМЕСТИТЬ ВТУведомления
	|ИЗ
	|	ВТДокументыУведомлений КАК ДокументыУведомлений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НДФЛПодтверждениеПраваНаИмущественныйВычет.РаботникиОрганизации КАК НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации
	|		ПО ДокументыУведомлений.Регистратор = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.Ссылка
	|			И ДокументыУведомлений.ФизЛицо = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
	|	НДФЛСведенияОДоходах.КодДохода,
	|	НДФЛСведенияОДоходах.КодВычета,
	|	СУММА(НДФЛСведенияОДоходах.СуммаДохода) КАК СуммаДохода,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛСведенияОДоходах.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ НДФЛСведенияОДоходах.СуммаВычета
	|		КОНЕЦ) КАК СуммаВычета,
	|	Статусы.ТекущийСтатус,
	|	Статусы.Статус
	|ПОМЕСТИТЬ ВТДоходы
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|			ПО НДФЛСведенияОДоходах.ФизЛицо = Статусы.ФизЛицо
	|				И (НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, МЕСЯЦ) = Статусы.МесяцНалоговогоПериода)
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛСведенияОДоходах.ФизЛицо
	|			И (НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛСведенияОДоходах.ОбособленноеПодразделение = &Организация)
	|			И (ГОД(НДФЛСведенияОДоходах.Период) = &ГодНП)
	|			И (НДФЛСведенияОДоходах.ПериодРегистрации < &ДатаПодачиСведений)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛСведенияОДоходах.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ = &КодПоОКАТО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛСведенияОДоходах.КПП <> &ПустойКПП
	|						ТОГДА НДФЛСведенияОДоходах.КПП
	|					КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|			И (&ЧитатьСводныеДанныеПоСтавкам
	|				ИЛИ СотрудникиОрганизаций.Ставка = ВЫБОР
	|					КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|						ТОГДА ВЫБОР
	|								КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|										И Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|							КОНЕЦ
	|					КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|						ТОГДА ВЫБОР
	|								КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|								КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОГДА НДФЛСведенияОДоходах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				КОНЕЦ)
	|			И (НЕ НДФЛСведенияОДоходах.КодДохода.НеОтражаетсяВОтчетности2010)
	|ГДЕ
	|	НДФЛСведенияОДоходах.КодДохода ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	НДФЛСведенияОДоходах.КодДохода,
	|	НДФЛСведенияОДоходах.КодВычета,
	|	МЕСЯЦ(НДФЛСведенияОДоходах.Период),
	|	Статусы.ТекущийСтатус,
	|	Статусы.Статус
	|
	|ИМЕЮЩИЕ
	|	СУММА(НДФЛСведенияОДоходах.СуммаДохода) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки КАК НомерСправки,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета КАК КодВычета,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПримененныйВычет КАК СуммаВычета,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизЛицо
	|ПОМЕСТИТЬ ВТВычеты
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК НДФЛПредоставленныеСтандартныеВычетыФизЛиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизЛицо
	|			И (&ЧитатьСводныеДанныеПоСтавкам
	|				ИЛИ СотрудникиОрганизаций.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение = &Организация)
	|			И (ГОД(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода) = &ГодНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ = &КодПоОКАТО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КПП <> &ПустойКПП
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КПП
	|					КОГДА ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|ГДЕ
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета ЕСТЬ НЕ NULL 
	|	И НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПримененныйВычет <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный,
	|	НДФЛИмущественныеВычетыФизлиц.Размер,
	|	НДФЛИмущественныеВычетыФизлиц.Период,
	|	НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|			И (&ЧитатьСводныеДанныеПоСтавкам
	|				ИЛИ СотрудникиОрганизаций.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|			И (НДФЛИмущественныеВычетыФизлиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение = &Организация)
	|			И (НДФЛИмущественныеВычетыФизлиц.Год = &ГодНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ = &КодПоОКАТО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛИмущественныеВычетыФизлиц.КПП <> &ПустойКПП
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.КПП
	|					КОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|ГДЕ
	|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный ЕСТЬ НЕ NULL 
	|	И НДФЛИмущественныеВычетыФизлиц.Размер <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыИВычеты.НомерСправки,
	|	СУММА(ДоходыИВычеты.ОбщаяСуммаДохода) КАК ОбщаяСуммаДохода,
	|	СУММА(ДоходыИВычеты.ОблагаемаяСуммаДохода) КАК ОблагаемаяСуммаДохода,
	|	ВЫБОР
	|		КОГДА НЕ &РазделятьИтогиПоСтавкам
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА ДоходыИВычеты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА ДоходыИВычеты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА ДоходыИВычеты.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка
	|ПОМЕСТИТЬ ВТИтогиДоходов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Доходы.НомерСправки КАК НомерСправки,
	|		Доходы.СуммаДохода КАК ОбщаяСуммаДохода,
	|		Доходы.СуммаДохода - Доходы.СуммаВычета КАК ОблагаемаяСуммаДохода,
	|		Доходы.КодДохода.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|		Доходы.КодДохода КАК КодДохода,
	|		Доходы.Статус КАК Статус
	|	ИЗ
	|		ВТДоходы КАК Доходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Вычеты.НомерСправки,
	|		0,
	|		-Вычеты.СуммаВычета,
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13),
	|		ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.КодДоходаПоУмолчанию),
	|		Статусы.Статус
	|	ИЗ
	|		ВТВычеты КАК Вычеты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|			ПО Вычеты.ФизЛицо = Статусы.ФизЛицо
	|				И (НАЧАЛОПЕРИОДА(Вычеты.МесяцНалоговогоПериода, МЕСЯЦ) = Статусы.МесяцНалоговогоПериода)) КАК ДоходыИВычеты
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыИВычеты.НомерСправки,
	|	ВЫБОР
	|		КОГДА НЕ &РазделятьИтогиПоСтавкам
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА ДоходыИВычеты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА ДоходыИВычеты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА ДоходыИВычеты.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправкиОДоходах.НомерСправки КАК НомерСправки,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК НачисленоЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК УдержаноЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
	|					ТОГДА -НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ВозвращеноЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
	|						И НДФЛРасчетыСБюджетом.Период < &НачалоСледующегоНП
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ПереданоНаВзысканиеЗаТекущийГод,
	|	ВЫБОР
	|		КОГДА НЕ &РазделятьИтогиПоСтавкам
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка
	|ПОМЕСТИТЬ ВТНалоги
	|ИЗ
	|	ВТФизлица КАК СправкиОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|			ПО НДФЛРасчетыСБюджетом.ФизЛицо = Статусы.ФизЛицо
	|				И (НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) = Статусы.МесяцНалоговогоПериода)
	|		ПО СправкиОДоходах.ФизЛицо = НДФЛРасчетыСБюджетом.ФизЛицо
	|			И (НДФЛРасчетыСБюджетом.Период < &ДатаПодачиСведений)
	|			И (НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &Организация)
	|			И (НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ &КодПоОКАТО = ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ &КПП = ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.КПП <> &ПустойКПП
	|						ТОГДА НДФЛРасчетыСБюджетом.КПП
	|					КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ)
	|			И (&ЧитатьСводныеДанныеПоСтавкам
	|				ИЛИ СправкиОДоходах.Ставка = ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|						ТОГДА ВЫБОР
	|								КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|										И Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|							КОНЕЦ
	|					КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|						ТОГДА ВЫБОР
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	СправкиОДоходах.НомерСправки,
	|	ВЫБОР
	|		КОГДА НЕ &РазделятьИтогиПоСтавкам
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход КАК Сумма
	|ПОМЕСТИТЬ ВТПеречисленоНДФЛСводно
	|ИЗ
	|	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Обороты(
	|			&НачалоНП,
	|			&ДатаДоПодачиСведений,
	|			,
	|			Организация = &Организация
	|				И ФизЛицо В
	|					(ВЫБРАТЬ
	|						ФизическиеЛица.ФизЛицо
	|					ИЗ
	|						ВТФизлица КАК ФизическиеЛица)
	|				И (МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)) КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &РазделятьИтогиПоСтавкам
	|			ТОГДА ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|						ТОГДА ВЫБОР
	|								КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|							КОНЕЦ
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|						ТОГДА ВЫБОР
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|							И Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.ПустаяСсылка)
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка,
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	СУММА(НДФЛРасчетыСБюджетом.Налог) КАК Налог
	|ПОМЕСТИТЬ ВТУдержаноНДФЛПоМесяцам
	|ИЗ
	|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|		ПО НДФЛРасчетыСБюджетом.ФизЛицо = Статусы.ФизЛицо
	|			И (НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) = Статусы.МесяцНалоговогоПериода)
	|ГДЕ
	|	НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
	|	И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
	|	И НДФЛРасчетыСБюджетом.Период < &ДатаПодачиСведений
	|	И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
	|	И НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &Организация
	|	И НДФЛРасчетыСБюджетом.ФизЛицо В
	|			(ВЫБРАТЬ
	|				ФизическиеЛица.ФизЛицо
	|			ИЗ
	|				ВТФизлица КАК ФизическиеЛица)
	|	И &КодПоОКАТО = ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
	|				ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
	|			КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|				ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
	|			ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|		КОНЕЦ
	|	И &КПП = ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.КПП <> &ПустойКПП
	|				ТОГДА НДФЛРасчетыСБюджетом.КПП
	|			КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|				ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП
	|			ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|				ТОГДА ВЫБОР
	|						КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|								И Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОНЕЦ
	|			КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|				ТОГДА ВЫБОР
	|						КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|						КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.ПустаяСсылка)
	|					КОНЕЦ
	|			КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|		КОНЕЦ В (ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30))
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	ВЫБОР
	|		КОГДА &РазделятьИтогиПоСтавкам
	|			ТОГДА ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|						ТОГДА ВЫБОР
	|								КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|							КОНЕЦ
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|						ТОГДА ВЫБОР
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|							И Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.ПустаяСсылка)
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода
	|ПОМЕСТИТЬ ВТПеречисленоНДФЛПоМесяцам
	|ИЗ
	|	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Обороты(
	|			&НачалоНП,
	|			&ДатаДоПодачиСведений,
	|			,
	|			Организация = &Организация
	|				И ФизЛицо В
	|					(ВЫБРАТЬ
	|						ФизическиеЛица.ФизЛицо
	|					ИЗ
	|						ВТФизлица КАК ФизическиеЛица)
	|				И (МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|				И ОКАТО_КПП = &ОКАТО_КПП) КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НЕ &РазделятьИтогиПоСтавкам
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|	КОНЕЦ КАК СтавкаПоСтатусу,
	|	ЕСТЬNULL(УдержаноНДФЛПоСтавке30.Налог, 0) КАК УдержаноНДФЛПоСтавке30,
	|	ЕСТЬNULL(УдержаноНДФЛПоСтавке13.Налог, 0) КАК УдержаноНДФЛПоСтавке13,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход
	|ПОМЕСТИТЬ ВТПеречисленоПоСтавке13_30
	|ИЗ
	|	ВТПеречисленоНДФЛПоМесяцам КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержаноНДФЛПоМесяцам КАК УдержаноНДФЛПоСтавке13
	|		ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо = УдержаноНДФЛПоСтавке13.ФизЛицо
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода = УдержаноНДФЛПоСтавке13.МесяцНалоговогоПериода
	|			И (УдержаноНДФЛПоСтавке13.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержаноНДФЛПоМесяцам КАК УдержаноНДФЛПоСтавке30
	|		ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо = УдержаноНДФЛПоСтавке30.ФизЛицо
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода = УдержаноНДФЛПоСтавке30.МесяцНалоговогоПериода
	|			И (УдержаноНДФЛПоСтавке30.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|		ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо = Статусы.ФизЛицо
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода = Статусы.МесяцНалоговогоПериода
	|ГДЕ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка В (ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Перечислено.Ставка,
	|	Перечислено.ФизЛицо,
	|	СУММА(Перечислено.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТПеречисленоНДФЛ
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка КАК Ставка,
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо КАК ФизЛицо,
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход КАК Сумма
	|	ИЗ
	|		ВТПеречисленоНДФЛПоМесяцам КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|	ГДЕ
	|		НЕ РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка В (ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30),
	|		ПеречисленоПоСтавке13_30.ФизЛицо,
	|		ВЫБОР
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА 0
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА ПеречисленоПоСтавке13_30.СуммаРасход
	|			ИНАЧЕ ВЫРАЗИТЬ(ПеречисленоПоСтавке13_30.СуммаРасход * ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 / (ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13) КАК ЧИСЛО(22, 0))
	|		КОНЕЦ
	|	ИЗ
	|		ВТПеречисленоПоСтавке13_30 КАК ПеречисленоПоСтавке13_30
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13),
	|		ПеречисленоПоСтавке13_30.ФизЛицо,
	|		ВЫБОР
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА 0
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 = 0
	|				ТОГДА ПеречисленоПоСтавке13_30.СуммаРасход
	|			ИНАЧЕ ПеречисленоПоСтавке13_30.СуммаРасход - (ВЫРАЗИТЬ(ПеречисленоПоСтавке13_30.СуммаРасход * ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 / (ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13) КАК ЧИСЛО(22, 0)))
	|		КОНЕЦ
	|	ИЗ
	|		ВТПеречисленоПоСтавке13_30 КАК ПеречисленоПоСтавке13_30
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПеречисленоПоСтавке13_30.СтавкаПоСтатусу,
	|		ПеречисленоПоСтавке13_30.ФизЛицо,
	|		ВЫБОР
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА ПеречисленоПоСтавке13_30.СуммаРасход
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТПеречисленоПоСтавке13_30 КАК ПеречисленоПоСтавке13_30) КАК Перечислено
	|
	|СГРУППИРОВАТЬ ПО
	|	Перечислено.Ставка,
	|	Перечислено.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.НомерСправки КАК НомерСправки,
	|	Доходы.Месяц КАК Месяц,
	|	Доходы.КодДохода,
	|	ВЫБОР
	|		КОГДА Доходы.СуммаВычета = 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		ИНАЧЕ Доходы.КодВычета
	|	КОНЕЦ КАК КодВычета,
	|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода,
	|	СУММА(Доходы.СуммаВычета) КАК СуммаВычета
	|ПОМЕСТИТЬ ВТДоходыПоМесяцам
	|ИЗ
	|	ВТДоходы КАК Доходы
	|ГДЕ
	|	Доходы.Месяц ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.Месяц,
	|	Доходы.НомерСправки,
	|	Доходы.КодДохода,
	|	ВЫБОР
	|		КОГДА Доходы.СуммаВычета = 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		ИНАЧЕ Доходы.КодВычета
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Доходы.НомерСправки,
	|	Доходы.КодДохода,
	|	Доходы.Месяц
	|ПОМЕСТИТЬ ВТОсобыеМесяцы
	|ИЗ
	|	ВТДоходыПоМесяцам КАК Доходы
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.НомерСправки,
	|	Доходы.КодДохода,
	|	Доходы.Месяц
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА Доходы.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) = 2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Месяц,
	|	МАКСИМУМ(Доходы.КодВычета) КАК КодВычета
	|ПОМЕСТИТЬ ВТСтрокиСУказаннымВычетом
	|ИЗ
	|	ВТОсобыеМесяцы КАК МесяцыОсобыхВычетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыПоМесяцам КАК Доходы
	|		ПО МесяцыОсобыхВычетов.НомерСправки = Доходы.НомерСправки
	|			И МесяцыОсобыхВычетов.КодДохода = Доходы.КодДохода
	|			И МесяцыОсобыхВычетов.Месяц = Доходы.Месяц
	|ГДЕ
	|	Доходы.КодВычета <> ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Месяц,
	|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода
	|ПОМЕСТИТЬ ВТДоходыИзСтрокБезВычетов
	|ИЗ
	|	ВТОсобыеМесяцы КАК МесяцыОсобыхВычетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыПоМесяцам КАК Доходы
	|		ПО МесяцыОсобыхВычетов.НомерСправки = Доходы.НомерСправки
	|			И МесяцыОсобыхВычетов.КодДохода = Доходы.КодДохода
	|			И МесяцыОсобыхВычетов.Месяц = Доходы.Месяц
	|ГДЕ
	|	Доходы.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.НомерСправки КАК НомерСправки,
	|	Доходы.Месяц КАК Месяц,
	|	Доходы.КодДохода,
	|	Доходы.КодДохода.КодДляОтчетности КАК КодДоходаКодДляОтчетности,
	|	Доходы.КодВычета КАК КодВычета,
	|	Доходы.СуммаДохода + ВЫБОР
	|		КОГДА СтрокиСУказаннымВычетом.НомерСправки ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ДоходыИзСтрокБезВычетов.СуммаДохода, 0)
	|	КОНЕЦ КАК СуммаДохода,
	|	Доходы.СуммаВычета КАК СуммаВычета
	|ИЗ
	|	ВТДоходыПоМесяцам КАК Доходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсобыеМесяцы КАК СтрокиБезКодаВычета
	|		ПО Доходы.НомерСправки = СтрокиБезКодаВычета.НомерСправки
	|			И Доходы.КодДохода = СтрокиБезКодаВычета.КодДохода
	|			И Доходы.Месяц = СтрокиБезКодаВычета.Месяц
	|			И (Доходы.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыИзСтрокБезВычетов КАК ДоходыИзСтрокБезВычетов
	|		ПО Доходы.НомерСправки = ДоходыИзСтрокБезВычетов.НомерСправки
	|			И Доходы.Месяц = ДоходыИзСтрокБезВычетов.Месяц
	|			И Доходы.КодДохода = ДоходыИзСтрокБезВычетов.КодДохода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиСУказаннымВычетом КАК СтрокиСУказаннымВычетом
	|		ПО Доходы.НомерСправки = СтрокиСУказаннымВычетом.НомерСправки
	|			И Доходы.Месяц = СтрокиСУказаннымВычетом.Месяц
	|			И Доходы.КодДохода = СтрокиСУказаннымВычетом.КодДохода
	|			И Доходы.КодВычета = СтрокиСУказаннымВычетом.КодВычета
	|ГДЕ
	|	СтрокиБезКодаВычета.НомерСправки ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки,
	|	Месяц,
	|	КодДоходаКодДляОтчетности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вычеты.НомерСправки КАК НомерСправки,
	|	Вычеты.КодВычета,
	|	СУММА(Вычеты.СуммаВычета) КАК СуммаВычета,
	|	Вычеты.КодВычета.КодДляОтчетности2011 + Вычеты.КодВычета.КодДляОтчетности2010 КАК КодВычетаКод
	|ИЗ
	|	ВТВычеты КАК Вычеты
	|ГДЕ
	|	Вычеты.КодВычета ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Вычеты.НомерСправки,
	|	Вычеты.КодВычета,
	|	Вычеты.КодВычета.КодДляОтчетности2011 + Вычеты.КодВычета.КодДляОтчетности2010
	|
	|ИМЕЮЩИЕ
	|	СУММА(Вычеты.СуммаВычета) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки,
	|	КодВычетаКод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки КАК НомерСправки,
	|	ВЫБОР
	|		КОГДА &РазделятьИтогиПоСтавкам
	|			ТОГДА ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|		ИНАЧЕ СотрудникиОрганизаций.Ставка
	|	КОНЕЦ КАК Ставка,
	|	ИтогиДоходов.ОбщаяСуммаДохода,
	|	ИтогиДоходов.ОблагаемаяСуммаДохода,
	|	Налоги.НачисленоЗаТекущийГод КАК Исчислено,
	|	Налоги.УдержаноЗаТекущийГод - Налоги.ВозвращеноЗаТекущийГод КАК Удержано,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА &ЧитатьСводныеДанные
	|				ТОГДА ПеречисленоНДФЛСводно.Сумма
	|			ИНАЧЕ ПеречисленоНДФЛ.Сумма
	|		КОНЕЦ, 0) КАК Перечислено,
	|	ВЫБОР
	|		КОГДА Налоги.НачисленоЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод - Налоги.ПереданоНаВзысканиеЗаТекущийГод > 0
	|			ТОГДА Налоги.НачисленоЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод - Налоги.ПереданоНаВзысканиеЗаТекущийГод
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Задолженность,
	|	ВЫБОР
	|		КОГДА Налоги.НачисленоЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод - Налоги.ПереданоНаВзысканиеЗаТекущийГод < 0
	|			ТОГДА -(Налоги.НачисленоЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод - Налоги.ПереданоНаВзысканиеЗаТекущийГод)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ИзлишнеУдержано,
	|	Уведомления.ДатаУведомления,
	|	Уведомления.КодНалоговогоОрганаУведомления,
	|	Уведомления.НомерУведомления
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.НДФЛСтавки КАК Ставки
	|		ПО (&РазделятьИтогиПоСтавкам)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУведомления КАК Уведомления
	|		ПО СотрудникиОрганизаций.НомерСправки = Уведомления.НомерСправки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогиДоходов КАК ИтогиДоходов
	|		ПО СотрудникиОрганизаций.НомерСправки = ИтогиДоходов.НомерСправки
	|			И (НЕ &РазделятьИтогиПоСтавкам
	|				ИЛИ ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)) = ИтогиДоходов.Ставка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНалоги КАК Налоги
	|		ПО СотрудникиОрганизаций.НомерСправки = Налоги.НомерСправки
	|			И (НЕ &РазделятьИтогиПоСтавкам
	|				ИЛИ ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)) = Налоги.Ставка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПеречисленоНДФЛСводно КАК ПеречисленоНДФЛСводно
	|		ПО СотрудникиОрганизаций.ФизЛицо = ПеречисленоНДФЛСводно.ФизЛицо
	|			И (ВЫБОР
	|				КОГДА &РазделятьИтогиПоСтавкам
	|					ТОГДА ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|				ИНАЧЕ СотрудникиОрганизаций.Ставка
	|			КОНЕЦ = ПеречисленоНДФЛСводно.Ставка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПеречисленоНДФЛ КАК ПеречисленоНДФЛ
	|		ПО СотрудникиОрганизаций.ФизЛицо = ПеречисленоНДФЛ.ФизЛицо
	|			И (ВЫБОР
	|				КОГДА &РазделятьИтогиПоСтавкам
	|					ТОГДА ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|				ИНАЧЕ СотрудникиОрганизаций.Ставка
	|			КОНЕЦ = ПеречисленоНДФЛ.Ставка)
	|ГДЕ
	|	(НЕ &РазделятьИтогиПоСтавкам
	|			ИЛИ ИтогиДоходов.НомерСправки ЕСТЬ НЕ NULL 
	|				И (ИтогиДоходов.ОбщаяСуммаДохода <> 0
	|					ИЛИ ИтогиДоходов.ОблагаемаяСуммаДохода <> 0)
	|			ИЛИ Налоги.НомерСправки ЕСТЬ НЕ NULL 
	|				И (Налоги.НачисленоЗаТекущийГод <> 0
	|					ИЛИ Налоги.НачисленоЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод - Налоги.ПереданоНаВзысканиеЗаТекущийГод <> 0)
	|			ИЛИ ВЫБОР
	|					КОГДА &ЧитатьСводныеДанные
	|						ТОГДА ПеречисленоНДФЛСводно.Сумма
	|					ИНАЧЕ ПеречисленоНДФЛ.Сумма
	|				КОНЕЦ ЕСТЬ НЕ NULL 
	|				И ВЫБОР
	|					КОГДА &ЧитатьСводныеДанные
	|						ТОГДА ПеречисленоНДФЛСводно.Сумма
	|					ИНАЧЕ ПеречисленоНДФЛ.Сумма
	|				КОНЕЦ <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки";
	
	Если ПоказыватьПоКодуОКТМО Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ОКАТО", "ОКТМО")
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Результаты = Запрос.ВыполнитьПакет();
	ВсегоЗапросов = Результаты.Количество();
	
	Возврат Новый Структура("Доходы, Вычеты, Итоги", Результаты[ВсегоЗапросов - 3].Выгрузить(), Результаты[ВсегоЗапросов - 2].Выгрузить(), Результаты[ВсегоЗапросов - 1].Выгрузить());
	
КонецФункции

Функция ДанныеПодписывающихЛиц(Организация, ДатаАктуальности) Экспорт

	СтруктураДанных = Новый Структура("ГлавныйБухгалтер, Телефон, СправкуПодписал, ДолжностьПодписавшегоЛица");
	
	Запрос = Новый Запрос;
	
	Если ОбщегоНазначенияПереопределяемый.ЭтоЮрЛицо(Организация) Тогда
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность КАК ДолжностьПодписавшегоЛица,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо КАК ГлавныйБухгалтер,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо КАК СправкуПодписал,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование
		|ИЗ
		|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
		|			&Дата,
		|			СтруктурнаяЕдиница = &Организация
		|				И ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер)) КАК ОтветственныеЛицаОрганизацийСрезПоследних";
		Запрос.УстановитьПараметр("Дата", ДатаАктуальности - 1);
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий(); 
		ЗаполнитьЗначенияСвойств(СтруктураДанных,Выборка);
		Запрос.УстановитьПараметр("ВидТелефона", Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	Иначе
		ИндивидуальныйПредприниматель = ОбщегоНазначенияЗК.ПолучитьЗначениеРеквизита(Организация,"ИндивидуальныйПредприниматель");
		СтруктураДанных.ГлавныйБухгалтер = ИндивидуальныйПредприниматель;
		СтруктураДанных.СправкуПодписал = ИндивидуальныйПредприниматель;
		Запрос.УстановитьПараметр("Организация", ИндивидуальныйПредприниматель);
		Запрос.УстановитьПараметр("ВидТелефона", Справочники.ВидыКонтактнойИнформации.ТелефонСлужебный);
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(КонтактнаяИнформация.Представление КАК СТРОКА(300)) КАК ТелефонОрганизации
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Организация
	|	И КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|	И КонтактнаяИнформация.Вид = &ВидТелефона";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураДанных.Телефон = СокрЛП(Выборка.ТелефонОрганизации);
	КонецЕсли;
	
	Возврат СтруктураДанных

КонецФункции // ДанныеРуководителяОрганизации()

Функция ДанныеСотрудников(ТаблицаСотрудников, Дата, НалоговыйПериод) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылки", ТаблицаСотрудников);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("КонецНП",КонецГода(Дата(НалоговыйПериод,1,1)));
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СотрудникиОрганизаций.Сотрудник КАК Физлицо,
	|	СотрудникиОрганизаций.Ставка
	|ПОМЕСТИТЬ ВТФизлица
	|ИЗ
	|	&Ссылки КАК СотрудникиОрганизаций";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФИОФизЛицСрезПоследних.ФизЛицо,
	|	ФИОФизЛицСрезПоследних.Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя,
	|	ФИОФизЛицСрезПоследних.Отчество
	|ПОМЕСТИТЬ ВТФИОФизЛиц
	|ИЗ
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|			&Дата,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					ВТФизлица КАК Физлица)) КАК ФИОФизЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГражданствоФизЛицСрезПоследних.ФизЛицо,
	|	ГражданствоФизЛицСрезПоследних.Страна
	|ПОМЕСТИТЬ ВТГражданство
	|ИЗ
	|	РегистрСведений.ГражданствоФизЛиц.СрезПоследних(
	|			&Дата,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					ВТФизлица КАК Физлица)) КАК ГражданствоФизЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусФизЛиц.ФизЛицо,
	|	СтатусФизЛиц.Статус
	|ПОМЕСТИТЬ ВТСтатусы
	|ИЗ
	|	РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ.СрезПоследних(
	|			&КонецНП,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					ВТФизлица КАК Физлица)) КАК СтатусФизЛиц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер
	|ПОМЕСТИТЬ ВТПаспортныеДанные
	|ИЗ
	|	РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(
	|			&Дата,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					ВТФизлица КАК Физлица)) КАК ПаспортныеДанныеФизЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Физлица.Физлицо КАК Сотрудник,
	|	Физлица.Ставка,
	|	ФИОФизЛицСрезПоследних.Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя,
	|	ФИОФизЛицСрезПоследних.Отчество,
	|	"","" + АдресаРегистрации.Поле1 + "","" + АдресаРегистрации.Поле2 + "","" + АдресаРегистрации.Поле3 + "","" + АдресаРегистрации.Поле4 + "","" + АдресаРегистрации.Поле5 + "","" + АдресаРегистрации.Поле6 + "","" + АдресаРегистрации.Поле7 + "","" + АдресаРегистрации.Поле8 + "","" + АдресаРегистрации.Поле9 + "","" + ВЫБОР
	|		КОГДА АдресаРегистрации.ТипДома В (ЗНАЧЕНИЕ(Перечисление.ТипыДомов.Дом), ЗНАЧЕНИЕ(Перечисление.ТипыДомов.ПустаяСсылка))
	|			ТОГДА ""дом""
	|		ИНАЧЕ ""владение""
	|	КОНЕЦ + "","" + ВЫБОР
	|		КОГДА АдресаРегистрации.ТипКорпуса В (ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.Корпус), ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.ПустаяСсылка))
	|			ТОГДА ""корпус""
	|		ИНАЧЕ ""строение""
	|	КОНЕЦ + "","" + ВЫБОР
	|		КОГДА АдресаРегистрации.ТипКвартиры В (ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.Квартира), ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.ПустаяСсылка))
	|			ТОГДА ""кв.""
	|		ИНАЧЕ ""оф.""
	|	КОНЕЦ КАК АдресРФ,
	|	АдресаИностранцев.Представление КАК АдресЗарубежом,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид КАК ВидДокумента,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия КАК СерияДокумента,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер КАК НомерДокумента,
	|	ФизическиеЛица.ДатаРождения,
	|	ФизическиеЛица.ИНН,
	|	ФизическиеЛица.Наименование КАК Наименование,
	|	ЕСТЬNULL(Гражданство.Страна, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) КАК Гражданство,
	|	ЕСТЬNULL(Статусы.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) КАК СтатусНалогоплательщика
	|ИЗ
	|	ВТФизлица КАК Физлица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО Физлица.Физлицо = ФизическиеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаИностранцев
	|		ПО Физлица.Физлицо = АдресаИностранцев.Объект
	|			И (АдресаИностранцев.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ИнострАдресФизЛица))
	|			И (АдресаИностранцев.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаРегистрации
	|		ПО Физлица.Физлицо = АдресаРегистрации.Объект
	|			И (АдресаРегистрации.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресФизЛица))
	|			И (АдресаРегистрации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПаспортныеДанные КАК ПаспортныеДанныеФизЛицСрезПоследних
	|		ПО Физлица.Физлицо = ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИОФизЛиц КАК ФИОФизЛицСрезПоследних
	|		ПО Физлица.Физлицо = ФИОФизЛицСрезПоследних.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГражданство КАК Гражданство
	|		ПО Физлица.Физлицо = Гражданство.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|		ПО Физлица.Физлицо = Статусы.ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	Возврат Запрос.Выполнить().Выгрузить()
	
КонецФункции

// Формирует имя файла в установленном формате для организации документа
//
// Параметры
// 	ЭтоЮрЛицо - булево, дополнительная характеристика организации
//
// Возвращаемое значение:
//  Строка с именем файла
//
Функция ИмяФайлаОбмена(Организация, Дата, ЭтоЮрЛицо = Неопределено, КодНО = Неопределено) Экспорт

	Если ЭтоЮрЛицо = Неопределено Тогда
		ЭтоЮрЛицо = ОбщегоНазначенияПереопределяемый.ЭтоЮрЛицо(Организация);
	КонецЕсли;
	
	Параметры = ОбщегоНазначенияЗК.ПолучитьЗначенияРеквизитов(Организация, "КодИФНСПолучателя, КодИМНС, ИНН, КПП");
	
	Возврат "NO_NDFL2"
			+ "_" + ?(ЗначениеЗаполнено(Параметры.КодИФНСПолучателя), Параметры.КодИФНСПолучателя, ?(ЗначениеЗаполнено(КодНО),КодНО,Параметры.КодИМНС))
			+ "_" + ?(ЗначениеЗаполнено(КодНО),КодНО,Параметры.КодИМНС)
			+ "_" + ?(ЭтоЮрЛицо, СокрЛП(Параметры.ИНН) + СокрЛП(Параметры.КПП), СокрЛП(Параметры.ИНН))
			+ "_" + Формат(Дата, "ДФ=ггггММдд")
			+ "_" + Новый УникальныйИдентификатор();

КонецФункции // ИмяФайлаОбмена()

Функция КодНалоговогоОрганаПоОКАТО_КППиОрганизации(Организация, ОрганизацияЭтоЮрЛицо, ОКАТО_КПП) Экспорт
	
	Если ОрганизацияЭтоЮрЛицо И ЗначениеЗаполнено(ОКАТО_КПП) Тогда
		ДанныеОрганизации = ОбщегоНазначенияЗК.ПолучитьЗначенияРеквизитов(Организация,"КодИМНС,КПП");
		КПП = КППизСуммыОКАТОиКПП(ОКАТО_КПП);
		Если КПП = ДанныеОрганизации.КПП Тогда
			КодНО = ?(ЗначениеЗаполнено(ДанныеОрганизации.КодИМНС), ДанныеОрганизации.КодИМНС, Лев(КПП, 4))
		Иначе
			КодНО = Лев(КПП, 4)
		КонецЕсли;
	Иначе 	
		КодНО = ОбщегоНазначенияЗК.ПолучитьЗначениеРеквизита(Организация,"КодИМНС");
	КонецЕсли;
	
	Возврат КодНО
	
КонецФункции 

Функция ОписаниеДоходовПоКодам(Организация, ГоловнаяОрганизация, ОрганизацияЭтоЮрЛицо, НалоговыйПериод, ДатаСведений, ФизЛицо = Неопределено, Ставка = Неопределено, ПоказыватьКодыОКТМО = Ложь) Экспорт

	Возврат СправкиПоНДФЛПолныеПрава.ОписаниеДоходовОрганизации(Организация, ГоловнаяОрганизация, ОрганизацияЭтоЮрЛицо, НалоговыйПериод, ДатаСведений, ФизЛицо, Ставка, ПоказыватьКодыОКТМО)

КонецФункции // ОписаниеДоходов()

Функция СписокОКАТОиКПП(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Наименование + "": "" + Организации.КодПоОКАТО + "", "" + Организации.КПП КАК Представление,
	|	Организации.КодПоОКАТО,
	|	Организации.КПП,
	|	1 КАК Порядок
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Владелец.Наименование + "", "" + ПодразделенияОрганизаций.Наименование + "": "" + ПодразделенияОрганизаций.КодПоОКАТО + "", "" + ПодразделенияОрганизаций.КПП,
	|	ПодразделенияОрганизаций.КодПоОКАТО,
	|	ПодразделенияОрганизаций.КПП,
	|	2
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Организация
	|	И ПодразделенияОрганизаций.КодПоОКАТО <> &ПустойКодПоОКАТО
	|	И (ПодразделенияОрганизаций.КодПоОКАТО <> ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.КодПоОКАТО, &ПустойКодПоОКАТО)
	|			ИЛИ ПодразделенияОрганизаций.КПП <> ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.КПП, &ПустойКодПоОКАТО))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Представление";
	
	НаборыДанных = Запрос.Выполнить().Выгрузить(); 
	
	СписокОКАТОиКПП = Новый СписокЗначений;
	
	Для Каждого СтрокаЗапроса Из НаборыДанных Цикл
		СписокОКАТОиКПП.Добавить(СуммаОКАТОиКПП(СтрокаЗапроса.КодПоОКАТО, СтрокаЗапроса.КПП), СтрокаЗапроса.Представление);
	КонецЦикла;
	
	Возврат СписокОКАТОиКПП;
	
КонецФункции // СписокОКАТОиКПП
 
Функция СписокОКТМОиКПП(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПустойКодПоОКТМО","");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Наименование + "": "" + Организации.КодПоОКТМО + "", "" + Организации.КПП КАК Представление,
	|	Организации.КодПоОКТМО,
	|	Организации.КПП,
	|	1 КАК Порядок
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Владелец.Наименование + "", "" + ПодразделенияОрганизаций.Наименование + "": "" + ПодразделенияОрганизаций.КодПоОКТМО + "", "" + ПодразделенияОрганизаций.КПП,
	|	ПодразделенияОрганизаций.КодПоОКТМО,
	|	ПодразделенияОрганизаций.КПП,
	|	2
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Организация
	|	И ПодразделенияОрганизаций.КодПоОКТМО <> &ПустойКодПоОКТМО
	|	И (ПодразделенияОрганизаций.КодПоОКТМО <> ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.КодПоОКТМО, &ПустойКодПоОКТМО)
	|			ИЛИ ПодразделенияОрганизаций.КПП <> ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.КПП, &ПустойКодПоОКТМО))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Представление";
	
	НаборыДанных = Запрос.Выполнить().Выгрузить(); 
	
	СписокОКТМОиКПП = Новый СписокЗначений;
	
	Для Каждого СтрокаЗапроса Из НаборыДанных Цикл
		СписокОКТМОиКПП.Добавить(СуммаОКАТОиКПП(СтрокаЗапроса.КодПоОКТМО, СтрокаЗапроса.КПП), СтрокаЗапроса.Представление);
	КонецЦикла;
	
	Возврат СписокОКТМОиКПП;
	
КонецФункции

Функция СоответствиеОКАТОиОКТМО(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВсеКоды.КодПоОКАТО,
	|	МАКСИМУМ(ВсеКоды.КодПоОКТМО) КАК КодПоОКТМО
	|ИЗ
	|	(ВЫБРАТЬ
	|		Организации.КодПоОКАТО КАК КодПоОКАТО,
	|		Организации.КодПоОКТМО КАК КодПоОКТМО
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.Ссылка = &Организация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПодразделенияОрганизаций.КодПоОКАТО,
	|		ПодразделенияОрганизаций.КодПоОКТМО
	|	ИЗ
	|		Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|	ГДЕ
	|		ПодразделенияОрганизаций.Владелец = &Организация
	|		И (ПодразделенияОрганизаций.КодПоОКАТО <> &ПустойКодПоОКАТО
	|				ИЛИ ПодразделенияОрганизаций.КодПоОКТМО <> &ПустойКодПоОКАТО)
	|		И (ПодразделенияОрганизаций.КодПоОКАТО <> &ПустойКодПоОКАТО
	|					И ПодразделенияОрганизаций.КодПоОКАТО <> ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				ИЛИ ПодразделенияОрганизаций.КодПоОКТМО <> &ПустойКодПоОКАТО
	|					И ПодразделенияОрганизаций.КодПоОКТМО <> ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.КодПоОКТМО, &ПустойКодПоОКАТО))) КАК ВсеКоды
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеКоды.КодПоОКАТО";
	
	НаборДанных = Запрос.Выполнить().Выгрузить();
	НаборДанных.Индексы.Добавить("КодПоОКАТО");
	НаборДанных.Индексы.Добавить("КодПоОКТМО");
	
	Возврат НаборДанных;
	
КонецФункции

Функция ПреобразоватьОКАТОвОКТМО(КодПоОКАТО, ТаблицаСоответствияОКАТОиОКТМО) Экспорт 

	Если Не ЗначениеЗаполнено(ТаблицаСоответствияОКАТОиОКТМО) Тогда
		Возврат "";
	КонецЕсли;
	
	Строки = ТаблицаСоответствияОКАТОиОКТМО.НайтиСтроки(Новый Структура("КодПоОКАТО",КодПоОКАТО));
    Если Строки.Количество() > 0 Тогда
		Возврат Строки[0].КодПоОКТМО;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции 
 
Функция ПреобразоватьОКТМОвОКАТО(КодПоОКТМО, ТаблицаСоответствияОКАТОиОКТМО) Экспорт 

	Если Не ЗначениеЗаполнено(ТаблицаСоответствияОКАТОиОКТМО) Тогда
		Возврат "";
	КонецЕсли;
	
	Строки = ТаблицаСоответствияОКАТОиОКТМО.НайтиСтроки(Новый Структура("КодПоОКТМО",КодПоОКТМО));
    Если Строки.Количество() > 0 Тогда
		Возврат Строки[0].КодПоОКАТО;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции 
 
Функция ПреобразоватьОКАТО_КППвОКТМО_КПП(ОКАТО_КПП, ТаблицаСоответствияОКАТОиОКТМО) Экспорт 

	Возврат СуммаОКАТОиКПП(ПреобразоватьОКАТОвОКТМО(КодОКАТОизСуммыОКАТОиКПП(ОКАТО_КПП), ТаблицаСоответствияОКАТОиОКТМО),КППизСуммыОКАТОиКПП(ОКАТО_КПП));
	
КонецФункции 
 
Функция ПреобразоватьОКТМО_КППвОКАТО_КПП(ОКТМО_КПП, ТаблицаСоответствияОКАТОиОКТМО) Экспорт 

	Возврат СуммаОКАТОиКПП(ПреобразоватьОКТМОвОКАТО(КодОКАТОизСуммыОКАТОиКПП(ОКТМО_КПП), ТаблицаСоответствияОКАТОиОКТМО),КППизСуммыОКАТОиКПП(ОКТМО_КПП));
	
КонецФункции 

Процедура ОчиститьКодыОКТМО(НаборЗаписейУчета, ИмяПоляПериода = "МесяцНалоговогоПериода", ИмяПоляОКТМО = "КодПоОКТМО") Экспорт

	ДатаНачалаУчетаПоОКТМО = ДобавитьМесяц(ПроведениеРасчетов.ДатаПереходаНаКодыОКТМО(), -12);
	Для Каждого Запись Из НаборЗаписейУчета Цикл
		Если Запись[ИмяПоляПериода] < ДатаНачалаУчетаПоОКТМО Тогда
			Запись[ИмяПоляОКТМО] = "";
		КонецЕсли;
	КонецЦикла

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

Функция СоответствиеДоходовИМассивовВычетов(НалоговыйПериод = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(НалоговыйПериод) Тогда
		НалоговыйПериод = Год(ОбщегоНазначенияЗК.ПолучитьРабочуюДату())
	КонецЕсли;
	
	// Все виды авторских 2201-2209
	СписокАвторскихВознаграждений = Новый Массив;
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2201);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2202);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2203);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2204);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2205);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2206);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2207);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2208);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2209);
	
	СоответствиеДоходовИВычетов = Новый Соответствие;
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2761, Справочники.ВычетыНДФЛ.Код506); // Суммы материальной помощи, оказываемой инвалидам общественными организациями инвалидов, п.28 ст.217
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2720, Справочники.ВычетыНДФЛ.Код501); // Стоимость подарков
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2730, Справочники.ВычетыНДФЛ.Код502); // Стоимость призов на соревнованиях, определяемая согласно п.28 ст.217 НК
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2740, Справочники.ВычетыНДФЛ.Код505); // Стоимость выигрышей и призов, полученных в мероприятиях в целях рекламы, согл. п.28 ст.217 НК РФ
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2760, Справочники.ВычетыНДФЛ.Код503); //Суммы материальной помощи
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.МатпомощьПриРожденииРебенка, Справочники.ВычетыНДФЛ.МатпомощьПриРожденииРебенка); //Суммы материальной помощи
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2770, Справочники.ВычетыНДФЛ.Код504); //Суммы возмещения работникам стоимости медикаментов
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2790, Справочники.ВычетыНДФЛ.Код507); //Суммы помощи и подарков ветеранам ВОВ, п.33 ст.217
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2791, Справочники.ВычетыНДФЛ.Код509); //Доходы, полученные работниками в натуральной форме в качестве оплаты труда от организаций - с/х товаропроизводителей и крестьянских х-в
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1010, Справочники.ВычетыНДФЛ.Код601); //Дивиденды
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1211, Справочники.ВычетыНДФЛ.Код607); //Взносы в пенс.фонды
	Вычет620 = Справочники.ВычетыНДФЛ.НайтиПоКоду("620");
	Если ЗначениеЗаполнено(Вычет620) Тогда
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код4800, Вычет620); 
	КонецЕсли;
	
	Если НалоговыйПериод > 2011 Тогда // с 2012 года
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код307);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код208);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код212);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код216);
		Если ЗначениеЗаполнено(Вычет620) Тогда
			СписокВычетов.Добавить(Вычет620); 
		КонецЕсли;
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1530, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код308);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код217);
		Если ЗначениеЗаполнено(Вычет620) Тогда
			СписокВычетов.Добавить(Вычет620); 
		КонецЕсли;
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1531, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код206);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код205);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код209);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1532, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		Если ЗначениеЗаполнено(Вычет620) Тогда
			СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1533_2010, Вычет620); 
		КонецЕсли;
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код305);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код210);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1535, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1536, Справочники.ВычетыНДФЛ.Код309);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1537, Справочники.ВычетыНДФЛ.Код211); // РЕПО
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1538, Справочники.ВычетыНДФЛ.Код215); //займы ценными бумагами
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код213);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код214);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1539, СписокВычетов); //РЕПО
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1541, СписокВычетов); //РЕПО
	ИначеЕсли НалоговыйПериод > 2010 Тогда // с 2011 года
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код307);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код204);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код208);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код212);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код216);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1530, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код308);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код217);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1531, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код206);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код205);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код209);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1532, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		Если ЗначениеЗаполнено(Вычет620) Тогда
			СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1533_2010, Вычет620); 
		КонецЕсли;
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код305);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код210);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1535, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1536, Справочники.ВычетыНДФЛ.Код309);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1537, Справочники.ВычетыНДФЛ.Код211); // РЕПО
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1538, Справочники.ВычетыНДФЛ.Код215); //займы ценными бумагами
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код213);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код214);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1539, СписокВычетов); //РЕПО
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1541, СписокВычетов); //РЕПО
	ИначеЕсли НалоговыйПериод > 2009 Тогда  // с 2010 года
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код307);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код204);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код208);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код209);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1530, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код308);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код309);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1531, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1533_2010, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код206);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код205);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код305);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1532, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1533, Справочники.ВычетыНДФЛ.Код306); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1535, Справочники.ВычетыНДФЛ.Код305); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1536, Справочники.ВычетыНДФЛ.Код309);
	Иначе
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1530, Справочники.ВычетыНДФЛ.Код307); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1531, Справочники.ВычетыНДФЛ.Код308); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1532, Справочники.ВычетыНДФЛ.Код305); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1533, Справочники.ВычетыНДФЛ.Код306); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1535, Справочники.ВычетыНДФЛ.Код305); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый Массив;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код309);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код310);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1536,СписокВычетов);
	КонецЕсли;
	
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2010, Справочники.ВычетыНДФЛ.Код403); // Договора подряда
	СписокВычетов = Новый Массив;
	СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код405);
	СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код404);
	Для каждого КодДоходаАвторскихВознаграждений Из СписокАвторскихВознаграждений Цикл
		СоответствиеДоходовИВычетов.Вставить(КодДоходаАвторскихВознаграждений,СписокВычетов);
	КонецЦикла;
	
	Для каждого Элемент Из СоответствиеДоходовИВычетов Цикл
		Если ТипЗнч(Элемент.Значение) = Тип("СправочникСсылка.ВычетыНДФЛ") Тогда
			СписокВычетов = Новый Массив;
			СписокВычетов.Добавить(Элемент.Значение);
			СоответствиеДоходовИВычетов[Элемент.Ключ] = СписокВычетов
		КонецЕсли;
	КонецЦикла;
	
	Возврат СоответствиеДоходовИВычетов
	
КонецФункции 

////////////////////////////////////////////////////////////////////////////////

#Если ТолстыйКлиентОбычноеПриложение Тогда
	
Процедура ПриНачалеРаботыСистемы() Экспорт

	//Если Не ПравоДоступа("Изменение", Метаданные.РегистрыНакопления.НДФЛСведенияОДоходах) Или 
	//	Не ПравоДоступа("Использование", Метаданные.Обработки.ПереходВУчетеПоНДФЛНаКодыОКТМО) Тогда
	//	Возврат
	//КонецЕсли;
	//
	//Запрос = Новый Запрос;
	//Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	//Запрос.Текст = 
	//"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	//|	Организации.Ссылка,
	//|	Организации.КодПоОКАТО
	//|ИЗ
	//|	Справочник.Организации КАК Организации
	//|ГДЕ
	//|	Организации.КодПоОКАТО <> &ПустойКодПоОКАТО
	//|	И Организации.КодПоОКТМО = &ПустойКодПоОКАТО
	//|
	//|ОБЪЕДИНИТЬ ВСЕ
	//|
	//|ВЫБРАТЬ ПЕРВЫЕ 1
	//|	ПодразделенияОрганизаций.Ссылка,
	//|	ПодразделенияОрганизаций.КодПоОКАТО
	//|ИЗ
	//|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	//|ГДЕ
	//|	ПодразделенияОрганизаций.КодПоОКАТО <> &ПустойКодПоОКАТО
	//|	И ПодразделенияОрганизаций.КодПоОКТМО = &ПустойКодПоОКАТО";
	//Результат = Запрос.Выполнить();
	//Если Не Результат.Пустой() Тогда
	//	Обработки.ПереходВУчетеПоНДФЛНаКодыОКТМО.Создать().ПолучитьФорму().Открыть();
	//	Возврат;
	//КонецЕсли;

	//Если ОбщегоНазначенияЗК.ПолучитьРабочуюДату() < ДобавитьМесяц(ПроведениеРасчетов.ДатаПереходаНаКодыОКТМО(), 4) Тогда
	//	Запрос.Текст = 
	//	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	//	|	НДФЛСведенияОДоходах.Регистратор
	//	|ИЗ
	//	|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	//	|ГДЕ
	//	|	НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
	//	|	И НДФЛСведенияОДоходах.КодПоОКТМО = &ПустойКодПоОКАТО
	//	|	И НДФЛСведенияОДоходах.Период МЕЖДУ ДАТАВРЕМЯ(2013, 1, 1) И ДАТАВРЕМЯ(2013, 12, 31, 23, 59, 59)";
	//	Результат = Запрос.Выполнить();
	//	Если Не Результат.Пустой() Тогда
	//		Обработки.ПереходВУчетеПоНДФЛНаКодыОКТМО.Создать().ПолучитьФорму().Открыть();
	//	КонецЕсли;
	//КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

Процедура НастроитьПолеВводаОКАТО(ПолеВвода, Организация, СписокОКАТОиКПП) Экспорт 

	Если Не ЗначениеЗаполнено(СписокОКАТОиКПП) Тогда
		СписокОКАТОиКПП = СписокОКАТОиКПП(Организация);
	КонецЕсли;
	
	ПолеВвода.КнопкаВыбора = Ложь;
	
	// Если не нашли данных - ничего не делаем
	Если СписокОКАТОиКПП.Количество() > 0 Тогда
		
		МаксимальнаяДлина = 40;
		Для Каждого ЭлементСпискаЗначений Из СписокОКАТОиКПП Цикл
			МаксимальнаяДлина = Макс(МаксимальнаяДлина, СтрДлина(ЭлементСпискаЗначений.Представление));
		КонецЦикла;
		
		ПолеВвода.КнопкаВыбора = Истина;
		ПолеВвода.ШиринаСпискаВыбора = МаксимальнаяДлина;
		
	КонецЕсли; 

КонецПроцедуры

Процедура НастроитьПолеВводаОКТМО(ПолеВвода, Организация, СписокОКТМОиКПП) Экспорт 

	Если Не ЗначениеЗаполнено(СписокОКТМОиКПП) Тогда
		СписокОКТМОиКПП = СписокОКТМОиКПП(Организация);
	КонецЕсли;
	
	ПолеВвода.КнопкаВыбора = Ложь;
	
	// Если не нашли данных - ничего не делаем
	Если СписокОКТМОиКПП.Количество() > 0 Тогда
		
		МаксимальнаяДлина = 40;
		Для Каждого ЭлементСпискаЗначений Из СписокОКТМОиКПП Цикл
			МаксимальнаяДлина = Макс(МаксимальнаяДлина, СтрДлина(ЭлементСпискаЗначений.Представление));
		КонецЦикла;
		
		ПолеВвода.КнопкаВыбора = Истина;
		ПолеВвода.ШиринаСпискаВыбора = МаксимальнаяДлина;
		
	КонецЕсли; 

КонецПроцедуры

Процедура НастроитьПанельОКАТОиОКТМО(Панель, НалоговыйПериод) Экспорт 
	
	Если НалоговыйПериод < Год(ПроведениеРасчетов.ДатаПереходаНаКодыОКТМО()) Тогда
		Панель.ТекущаяСтраница = Панель.Страницы.ОКАТО
	Иначе
		Панель.ТекущаяСтраница = Панель.Страницы.ОКТМО
	КонецЕсли;
	
КонецПроцедуры

Процедура НастроитьКолонкиОКАТОиОКТМО(ТабличноеПоле, НалоговыйПериод, ИмяКолонкиОКАТО = "КодПоОКАТО", ИмяКолонкиОКТМО = "КодПоОКТМО") Экспорт 
	
	ГодПереходаНаКодыОКТМО = Год(ПроведениеРасчетов.ДатаПереходаНаКодыОКТМО());
	
	ТабличноеПоле.Колонки[ИмяКолонкиОКАТО].Видимость = НалоговыйПериод < ГодПереходаНаКодыОКТМО;
	ТабличноеПоле.Колонки[ИмяКолонкиОКТМО].Видимость = НалоговыйПериод >= ГодПереходаНаКодыОКТМО - 1;
	
	ТабличноеПоле.Колонки[ИмяКолонкиОКТМО].ИзменятьНастройку = ТабличноеПоле.Колонки[ИмяКолонкиОКТМО].Видимость;
	ТабличноеПоле.Колонки[ИмяКолонкиОКАТО].ИзменятьНастройку = ТабличноеПоле.Колонки[ИмяКолонкиОКАТО].Видимость;
	
КонецПроцедуры

Процедура ОбновитьИтогиТекущейСправки(НомерСправки = Неопределено, СтатусНалогоплательщика, СведенияОДоходах, СведенияОВычетах, СуммыНалогов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОДоходах.СуммаДохода,
	|	СведенияОДоходах.СуммаВычета,
	|	СведенияОДоходах.КодДохода
	|ПОМЕСТИТЬ ВТСведенияОДоходах
	|ИЗ
	|	&Доходы КАК СведенияОДоходах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сведения.СуммаВычета
	|ПОМЕСТИТЬ ВТСведенияОВычетах
	|ИЗ
	|	&Вычеты КАК Сведения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Сведения.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА Сведения.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА Сведения.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА Сведения.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка,
	|	СУММА(Сведения.ОблагаемаяСуммаДохода) КАК ОблагаемаяСуммаДохода,
	|	СУММА(Сведения.ОбщаяСуммаДохода) КАК ОбщаяСуммаДохода
	|ИЗ
	|	(ВЫБРАТЬ
	|		СведенияОДоходах.СуммаДохода - СведенияОДоходах.СуммаВычета КАК ОблагаемаяСуммаДохода,
	|		ДоходыНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|		СведенияОДоходах.СуммаДохода КАК ОбщаяСуммаДохода,
	|		СведенияОДоходах.КодДохода КАК КодДохода
	|	ИЗ
	|		ВТСведенияОДоходах КАК СведенияОДоходах
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоходыНДФЛ КАК ДоходыНДФЛ
	|			ПО СведенияОДоходах.КодДохода = ДоходыНДФЛ.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-СведенияОВычетах.СуммаВычета,
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13),
	|		0,
	|		ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.КодДоходаПоУмолчанию)
	|	ИЗ
	|		ВТСведенияОВычетах КАК СведенияОВычетах) КАК Сведения
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА Сведения.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА Сведения.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА Сведения.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА Сведения.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ";
	Запрос.УстановитьПараметр("Доходы", СведенияОДоходах);
	Запрос.УстановитьПараметр("Вычеты", СведенияОВычетах);
	Запрос.УстановитьПараметр("Статус", СтатусНалогоплательщика);
	Запрос.УстановитьПараметр("НеРезидент", СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент);
	
	СтруктураПоиска = Новый Структура();
	Если НомерСправки <> Неопределено Тогда
		СтруктураПоиска.Вставить("НомерСправки", НомерСправки);
	КонецЕсли;
	СтруктураПоиска.Вставить("Ставка");
	МассивСтавок = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивСтавок.Добавить(Выборка.Ставка);
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Выборка);
		СтрокиИтогов = СуммыНалогов.НайтиСтроки(СтруктураПоиска);
		Если СтрокиИтогов.Количество() > 0 Тогда
			СтрокаИтогов = СтрокиИтогов[0];
		Иначе
			СтрокаИтогов = СуммыНалогов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаИтогов, СтруктураПоиска);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаИтогов, Выборка);
	КонецЦикла;
	Если НомерСправки <> Неопределено Тогда
		СтруктураПоиска.Удалить("Ставка");
		СтрокиИтогов = СуммыНалогов.НайтиСтроки(СтруктураПоиска);
	Иначе
		СтрокиИтогов = СуммыНалогов;
	КонецЕсли;
	Для каждого СтрокаТЧ Из СтрокиИтогов Цикл
	    Если МассивСтавок.Найти(СтрокаТЧ.Ставка) = Неопределено Тогда
			СтрокаТЧ.ОбщаяСуммаДохода = 0;
			СтрокаТЧ.ОблагаемаяСуммаДохода = 0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьДанныеСправки(НомерСправки, ТабличнаяЧасть) Экспорт

    Для каждого СтрокаТЧ Из ТабличнаяЧасть.НайтиСтроки(Новый Структура("НомерСправки",НомерСправки)) Цикл
		ТабличнаяЧасть.Удалить(СтрокаТЧ)
	КонецЦикла;
	
КонецПроцедуры

Функция ВводитсяАдресЗарубежом(ОписаниеСотрудника) Экспорт

	Возврат ОписаниеСотрудника.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент Или ОписаниеСотрудника.Гражданство <> Справочники.КлассификаторСтранМира.Россия

КонецФункции // ВводитсяАдресЗарубежом()

Функция СоответствиеДоходовИВычетов(НалоговыйПериод = Неопределено) Экспорт
	
	СоответствиеДоходовИВычетов = СоответствиеДоходовИМассивовВычетов(НалоговыйПериод);
	Для каждого Элемент Из СоответствиеДоходовИВычетов Цикл
		СписокВычетов = Новый СписокЗначений;
		СписокВычетов.ЗагрузитьЗначения(Элемент.Значение);
		СоответствиеДоходовИВычетов[Элемент.Ключ] = СписокВычетов
	КонецЦикла;
	
	Возврат СоответствиеДоходовИВычетов
	
КонецФункции 

Функция СписокВычетовДляДохода(КодДохода, СоответствиеДоходовИВычетов, АвторскиеВознаграждения, СтатусНалогоплательщика) Экспорт

	Если Не ЗначениеЗаполнено(КодДохода) Тогда
		Возврат Новый СписокЗначений;	// Пустой список
	КонецЕсли; 

	// Всем подряд могут предоставляться только вычеты 3хх на доходы по ценным бумагам, 5хх на доходы 27X0 и вычет по дивидендам
	// остальные вычеты - только резидентам
	Если КодДохода = Справочники.ДоходыНДФЛ.Код2010 Или АвторскиеВознаграждения.Найти(КодДохода) <> Неопределено Тогда // Договора подряда

		Если СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент Тогда 
			Возврат Новый СписокЗначений;
		КонецЕсли;
		
	КонецЕсли;
	
	Вычеты = СоответствиеДоходовИВычетов[КодДохода];
	
	Если Вычеты = Неопределено Тогда
		Возврат Новый СписокЗначений;
	ИначеЕсли ТипЗнч(Вычеты) = Тип("СправочникСсылка.ВычетыНДФЛ") Тогда
		СписокВычетов = Новый СписокЗначений;
		СписокВычетов.Добавить(Вычеты);
		Возврат СписокВычетов;
	Иначе	
		Возврат Вычеты;
	КонецЕсли;

КонецФункции 

Функция СписокСтандартныхВычетов() Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВычетыНДФЛ.Ссылка,
	|	ВычетыНДФЛ.Код,
	|	ВычетыНДФЛ.Наименование,
	|	ВычетыНДФЛ.КодДляОтчетности2011 + ВычетыНДФЛ.КодДляОтчетности2010 КАК Порядок
	|ИЗ
	|	Справочник.ВычетыНДФЛ КАК ВычетыНДФЛ
	|ГДЕ
	|	ВычетыНДФЛ.ГруппаВычета <> ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок");
	Выборка = Запрос.Выполнить().Выбрать();		
	
	СписокВыбораСтандартныхВычетов = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		СписокВыбораСтандартныхВычетов.Добавить(Выборка.Ссылка,СокрЛП(Выборка.Код) + " (" + СокрЛП(Выборка.Наименование) + ")");
	КонецЦикла;

	Возврат СписокВыбораСтандартныхВычетов
	
КонецФункции // СписокСтандартныхВычетов()

////////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьДанныеСотрудника(НалоговыйПериод, Режим, ДанныеФизЛица, Доходы, Вычеты, Итоги, Дата, Отказ, ОбработкаКомментариев, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеИменСтран, СоответствиеСтавокДоходов = Неопределено) Экспорт
	
	СтрОшибки = "";
	СтрПредупрСводнойСправки = "";
	
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.НомерСправки) Тогда
		СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- не указан номер справки!";
	КонецЕсли;
	
	Если Режим <> "СводнаяСправка" И НалоговыйПериод < 2011 И Не ЗначениеЗаполнено(ДанныеФизЛица.Ставка) Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- не указана ставка налогообложения!";
	КонецЕсли;
	
	// ИНН
	Если Не ПустаяСтрока(ДанныеФизЛица.ИНН) И Не РегламентированнаяОтчетность.ИННСоответствуетТребованиям(ДанныеФизЛица.ИНН, Перечисления.ЮрФизЛицо.ФизЛицо) Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- указан неверный ИНН!";	
	КонецЕсли;
	
	// ФИО
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.Фамилия) Или Не ЗначениеЗаполнено(ДанныеФизЛица.Имя) Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- должны быть указаны, как минимум, фамилия и имя физического лица!";
	КонецЕсли; 
	Если ДанныеФизЛица.Гражданство = Справочники.КлассификаторСтранМира.Россия Тогда
		Если Не РегламентированнаяОтчетность.СтрокаНаписанаПоРусски(ДанныеФизЛица.Фамилия+ДанныеФизЛица.Имя+ДанныеФизЛица.Отчество) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- ФИО физического лица должно быть введено русскими буквами!";
		КонецЕсли; 
	ИначеЕсли ЗначениеЗаполнено(ДанныеФизЛица.Гражданство) Тогда
		Если Не СтрокаНаписанаРусскимиИлиЛатинскими(СтрЗаменить(ДанныеФизЛица.Фамилия+ДанныеФизЛица.Имя+ДанныеФизЛица.Отчество, " ", "")) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- ФИО физического лица нерезидента должно быть введено только русскими или только латинскими буквами!";
		КонецЕсли; 
	КонецЕсли;
	
	// Гражданство
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.Гражданство) Тогда
		СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- не указано гражданство!";
	КонецЕсли;
	
	// СтатусНалогоплательщика
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.СтатусНалогоплательщика) Тогда
		СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- не указан статус налогоплательщика!";
	КонецЕсли;
	
	// Удостоверение личности
	КодДокумента = СоответствиеДокументовУдостоверяющиеЛичность[ДанныеФизЛица.ВидДокумента];
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.ВидДокумента) Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- не указан документ, удостоверяющий личность!";
	Иначе
		Если Не ЗначениеЗаполнено(КодДокумента) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- указан неверный код документа, удостоверяющего личность!";
		Иначе
			Если Число(КодДокумента) < 1 или Число(КодДокумента)>99 Тогда
				СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- указан неверный код документа, удостоверяющего личность!";
			ИначеЕсли Найти(",21,03,07,08,10,11,12,13,14,15,18,23,24,91,", "," + КодДокумента + ",") = 0 Тогда
				СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- указан документ, не допустимый в справке 2-НДФЛ!";
			ИначеЕсли	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС05
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС10
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС11
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС12
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС13
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС14
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС91 Тогда
				// Справа от последнего пробела расположен номер документа, слева - серия
				СерияДок = ДанныеФизЛица.СерияДокумента;
				НомерДок = ДанныеФизЛица.НомерДокумента;
				
				СтрОшибкиПроверки = "";
				Если Не ПроцедурыПерсонифицированногоУчета.ПроверитьСериюУдостоверенияЛичности(ДанныеФизЛица.ВидДокумента, СерияДок, СтрОшибкиПроверки) Тогда
					СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- Ошибка в серии удостоверения личности: " + НРег(СтрОшибкиПроверки);
				КонецЕсли; 
				Если Не ПроцедурыПерсонифицированногоУчета.ПроверитьНомерУдостоверенияЛичности(ДанныеФизЛица.ВидДокумента, НомерДок, СтрОшибкиПроверки) Тогда
					СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- Ошибка в номере удостоверения личности: " + НРег(СтрОшибкиПроверки);
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	// Дата рождения
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.ДатаРождения) Тогда 
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- не указана дата рождения!";
	ИначеЕсли ДанныеФизЛица.ДатаРождения > Дата тогда	
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- неверно указана дата рождения!";
	КонецЕсли;	
		
	// Адреса
	Если ДанныеФизЛица.СтатусНалогоплательщика <> Перечисления.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент И ДанныеФизЛица.Гражданство = Справочники.КлассификаторСтранМира.Россия Тогда 			
		Если Не ЗначениеЗаполнено(ДанныеФизЛица.АдресРФ) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- не указан адрес регистрации!";
		КонецЕсли;
	ИначеЕсли Не ЗначениеЗаполнено(ДанныеФизЛица.АдресРФ) Тогда // Адрес нерезидента
		Если ПустаяСтрока(ДанныеФизЛица.АдресЗарубежом) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - для физического лица нерезидента или иностранца должен быть заполнен ""Адрес физ. лица за пределами РФ""!";
		КонецЕсли;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ДанныеФизЛица.АдресРФ) Тогда
		СтруктураАдресаДляПроверки = РегламентированнаяОтчетность.РазложитьАдрес(ДанныеФизЛица.АдресРФ);
		Если ПустаяСтрока(СтруктураАдресаДляПроверки.Индекс) тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - в адресе регистрации не указан индекс!";
			СтруктураАдресаДляПроверки.Индекс = "000000";
		КонецЕсли;
		Если Не РегламентированнаяОтчетность.АдресСоответствуетТребованиям(СтруктураАдресаДляПроверки) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - указанный адрес регистрации не соответствует требованиям ФНС! Возможно, в адресе присутствуют латинские или недопустимые символы";
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеФизЛица.АдресЗарубежом) Тогда
		СтруктураАдреса = УправлениеКонтактнойИнформацией.ПолучитьСтруктуруАдресаИзСтроки(ДанныеФизЛица.АдресЗарубежом);
		Если Не ОбщегоНазначенияЗК.ТолькоЦифрыВСтроке(СоответствиеИменСтран[СтруктураАдреса.Страна]) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - неверно указана страна проживания нерезидента (возможно, не заполнен справочник ""Классификатор стран мира"")!";
		КонецЕсли;
	КонецЕсли;
	
	// доходы и вычеты
	Если Доходы.НайтиСтроки(Новый Структура("КодДохода", Справочники.ДоходыНДФЛ.ПустаяСсылка())).Количество() > 0 Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- в таблице доходов присутствуют строки, в которых не указан код дохода!";
	КонецЕсли;
	
	Если Вычеты.НайтиСтроки(Новый Структура("КодВычета", Справочники.ВычетыНДФЛ.ПустаяСсылка())).Количество() > 0 Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- в таблице вычетов присутствуют строки, в которых не указан код вычета!";
	КонецЕсли;
	
	Если Вычеты.НайтиСтроки(Новый Структура("СуммаВычета", 0)).Количество() > 0 Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- в таблице вычетов присутствуют строки, в которых не указана сумма вычета!";
	КонецЕсли;
	
	Если Режим <> "СводнаяСправка" И (Вычеты.Найти(Справочники.ВычетыНДФЛ.Код311, "КодВычета") <> Неопределено Или Вычеты.Найти(Справочники.ВычетыНДФЛ.Код312, "КодВычета") <> Неопределено Или Вычеты.Найти(Справочники.ВычетыНДФЛ.Код318, "КодВычета") <> Неопределено) Тогда
		
		Если Не ЗначениеЗаполнено(ДанныеФизЛица.КодНалоговогоОрганаУведомления) Или Не ЗначениеЗаполнено(ДанныеФизЛица.НомерУведомления) Или Не ЗначениеЗаполнено(ДанныеФизЛица.ДатаУведомления) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - не указаны данные уведомления о праве на имущественный вычет!";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеФизЛица.КодНалоговогоОрганаУведомления) И Не ОбщегоНазначенияЗК.ТолькоЦифрыВСтроке(ДанныеФизЛица.КодНалоговогоОрганаУведомления) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - неверно указан код налогового органа, выдавшего уведомление о праве на имущественный вычет!";
		КонецЕсли;
	
	КонецЕсли;
	
	// итоги
	Если НалоговыйПериод < 2011 Тогда
		
		Если ДанныеФизЛица.ОбщаяСуммаДохода <> Доходы.Итог("СуммаДохода") Тогда
			СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- общая сумма дохода не совпадает с итогом таблицы доходов!";
		КонецЕсли;
		
		Если ДанныеФизЛица.ОблагаемаяСуммаДохода <> Доходы.Итог("СуммаДохода") - Доходы.Итог("СуммаВычета") - Вычеты.Итог("СуммаВычета") Тогда
			СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- сумма налоговой базы не совпадает с итогами таблиц доходов и вычетов!";
		КонецЕсли;
		
	Иначе
		
		Если Итоги.НайтиСтроки(Новый Структура("Ставка", Перечисления.НДФЛСтавки.ПустаяСсылка())).Количество() > 0 Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- в таблице итогов присутствуют строки, в которых не указана ставка налога!";
		КонецЕсли;
		
		ПерваяСтавка = Истина;
		Для каждого ЗначениеПеречисления Из Перечисления.НДФЛСтавки Цикл
			СтрокаИтогов = Итоги.Найти(ЗначениеПеречисления, "Ставка");
			ДоходыПоСтавке = ИзвлечьДоходыПоСтавке(ЗначениеПеречисления, ДанныеФизЛица.СтатусНалогоплательщика, Доходы, СоответствиеСтавокДоходов);
		    Если СтрокаИтогов <> Неопределено Или ДоходыПоСтавке.Количество() > 0 Тогда
				
				ОбщаяСуммаДохода = 0; ОблагаемаяСуммаДохода = 0;
				Если СтрокаИтогов <> Неопределено Тогда
					ОбщаяСуммаДохода = СтрокаИтогов.ОбщаяСуммаДохода;
					ОблагаемаяСуммаДохода = СтрокаИтогов.ОблагаемаяСуммаДохода;
				КонецЕсли;
				
				Если ОбщаяСуммаДохода <> ДоходыПоСтавке.Итог("СуммаДохода") Тогда
					СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- общая сумма дохода по ставке " + ЗначениеПеречисления + " не совпадает с итогом таблицы доходов!";
				КонецЕсли;
				
				Если ОблагаемаяСуммаДохода <> ДоходыПоСтавке.Итог("СуммаДохода") - ДоходыПоСтавке.Итог("СуммаВычета") - ?(ПерваяСтавка, Вычеты.Итог("СуммаВычета"),0) Тогда
					СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- сумма налоговой базы по ставке " + ЗначениеПеречисления + " не совпадает с итогами таблиц доходов и вычетов!";
				КонецЕсли;
				
				ПерваяСтавка = Ложь;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// выводим сообщения об ошибках
	Если ЗначениеЗаполнено(СтрОшибки + СтрПредупрСводнойСправки) Тогда
		
		Если Режим <> "СводнаяСправка" Тогда
			СтрОшибки =  "В сведениях о доходах сотрудника (справка № " + ДанныеФизЛица.НомерСправки + ")" + ?(Не ПустаяСтрока(ДанныеФизЛица.Фамилия+ДанныеФизЛица.Имя+ДанныеФизЛица.Отчество), " " + ДанныеФизЛица.Фамилия + " " + ДанныеФизЛица.Имя + " " + ДанныеФизЛица.Отчество, " <ФИО не указано>") + " обнаружены ошибки:" + СтрОшибки + СтрПредупрСводнойСправки;
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрОшибки, Отказ);
		Иначе
			СтрОшибки =  "В сведениях о доходах сотрудника (справка № " + ДанныеФизЛица.НомерСправки + ")" + ?(Не ПустаяСтрока(ДанныеФизЛица.Фамилия+ДанныеФизЛица.Имя+ДанныеФизЛица.Отчество), " " + ДанныеФизЛица.Фамилия + " " + ДанныеФизЛица.Имя + " " + ДанныеФизЛица.Отчество, " <ФИО не указано>") + " обнаружены ошибки:" + СтрОшибки;
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрОшибки, Отказ);
			Если Не ЗначениеЗаполнено(СтрОшибки) Тогда
				СтрПредупрСводнойСправки =  "В сведениях о доходах сотрудника (справка № " + ДанныеФизЛица.НомерСправки + ")" + ?(Не ПустаяСтрока(ДанныеФизЛица.Фамилия+ДанныеФизЛица.Имя+ДанныеФизЛица.Отчество), " " + ДанныеФизЛица.Фамилия + " " + ДанныеФизЛица.Имя + " " + ДанныеФизЛица.Отчество, " <ФИО не указано>") + " обнаружены ошибки:" + СтрПредупрСводнойСправки;
			КонецЕсли;
			ОбработкаКомментариев.ДобавитьСообщение(СтрПредупрСводнойСправки, Перечисления.ВидыСообщений.Ошибка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьКлассификаторыВСоответствия(НалоговыйПериод, СоответствиеКодовСтран = Неопределено, СоответствиеИменСтран = Неопределено, СоответствиеДокументовУдостоверяющиеЛичность = Неопределено, СоответствиеКодовРегионов = Неопределено, СоответствиеКодовДоходов = Неопределено, СоответствиеКодовВычетов = Неопределено, СоответствиеСтавокДоходов = Неопределено) Экспорт 
	
	Запрос = Новый Запрос;

	Если СоответствиеКодовСтран <> Неопределено И СоответствиеИменСтран <> Неопределено Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КлассификаторСтранМира.Ссылка,
		|	КлассификаторСтранМира.Код,
		|	КлассификаторСтранМира.Наименование
		|ИЗ
		|	Справочник.КлассификаторСтранМира КАК КлассификаторСтранМира";
		Выборка = Запрос.Выполнить().Выбрать();
		СоответствиеКодовСтран = Новый Соответствие;
		СоответствиеИменСтран = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			СоответствиеКодовСтран.Вставить(Выборка.Ссылка,Выборка.Код);
			СоответствиеИменСтран.Вставить(Выборка.Наименование,Выборка.Код)
		КонецЦикла;
	КонецЕсли;
	
	Если СоответствиеДокументовУдостоверяющиеЛичность <> Неопределено Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокументыУдостоверяющиеЛичность.Ссылка,
		|	ДокументыУдостоверяющиеЛичность.КодИМНС
		|ИЗ
		|	Справочник.ДокументыУдостоверяющиеЛичность КАК ДокументыУдостоверяющиеЛичность";
		Выборка = Запрос.Выполнить().Выбрать();
		СоответствиеДокументовУдостоверяющиеЛичность = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			СоответствиеДокументовУдостоверяющиеЛичность.Вставить(Выборка.Ссылка,Выборка.КодИМНС)
		КонецЦикла;
	КонецЕсли;
	
	Если СоответствиеКодовРегионов <> Неопределено Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	АдресныйКлассификатор.Наименование,
		|	АдресныйКлассификатор.Сокращение,
		|	АдресныйКлассификатор.КодРегионаВКоде
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
		|ГДЕ
		|	АдресныйКлассификатор.ТипАдресногоЭлемента = 1";
		Выборка = Запрос.Выполнить().Выбрать();
		СоответствиеКодовРегионов = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			СоответствиеКодовРегионов.Вставить(СокрЛП(Выборка.Наименование) + " " + СокрЛП(Выборка.Сокращение), Формат(Выборка.КодРегионаВКоде,"ЧЦ=2; ЧДЦ=0; ЧВН="))
		КонецЦикла;
	КонецЕсли;
	
	Если СоответствиеКодовДоходов <> Неопределено Или СоответствиеСтавокДоходов <> Неопределено Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоходыНДФЛ.Ссылка,
		|	ДоходыНДФЛ.КодДляОтчетности,
		|	ДоходыНДФЛ.СтавкаНалогообложенияРезидента
		|ИЗ
		|	Справочник.ДоходыНДФЛ КАК ДоходыНДФЛ";
		Выборка = Запрос.Выполнить().Выбрать();
		СоответствиеКодовДоходов = Новый Соответствие;
		СоответствиеСтавокДоходов = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			СоответствиеКодовДоходов.Вставить(Выборка.Ссылка, Выборка.КодДляОтчетности);
			СоответствиеСтавокДоходов.Вставить(Выборка.Ссылка, Выборка.СтавкаНалогообложенияРезидента)
		КонецЦикла;
	КонецЕсли;
	
	Если СоответствиеКодовВычетов <> Неопределено Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВычетыНДФЛ.Ссылка,
		|	ВычетыНДФЛ.КодДляОтчетности2010,
		|	ВычетыНДФЛ.КодДляОтчетности2011
		|ИЗ
		|	Справочник.ВычетыНДФЛ КАК ВычетыНДФЛ";
		Выборка = Запрос.Выполнить().Выбрать();
		СоответствиеКодовВычетов = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			Если НалоговыйПериод = 2010 Тогда
				СоответствиеКодовВычетов.Вставить(Выборка.Ссылка, Выборка.КодДляОтчетности2010)
			Иначе
				СоответствиеКодовВычетов.Вставить(Выборка.Ссылка, Выборка.КодДляОтчетности2011)
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьОбщиеДанныеСправки(СтруктураДанных, Отказ, ОбработкаКомментариев, ПроверятьКодОКТМО = Ложь) Экспорт

	Если Не ЗначениеЗаполнено(СтруктураДанных.НалоговыйПериод) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан налоговый период, за который формируются сведения!", Отказ);
	ИначеЕсли СтруктураДанных.НалоговыйПериод < 2010 Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("В документе поддерживаются налоговые периоды с 2010 года!", Отказ);
	КонецЕсли;
	
	ЭтоЮрЛицо = СтруктураДанных.ЭтоЮрЛицо;
	Если Не ЗначениеЗаполнено(СтруктураДанных.Организация) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("Не указана организация!"), Отказ);
	Иначе
		
		Если Не ЗначениеЗаполнено(СтруктураДанных.НазваниеОрганизации) Тогда
			Если ЭтоЮрЛицо Тогда
				ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указаны полное и сокращенное наименования " + ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("организации") + "!", Отказ);
			ИначеЕсли ПустаяСтрока(СтруктураДанных.Фамилия) Или ПустаяСтрока(СтруктураДанных.Имя) Тогда
				ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Для физического лица-работодателя должны быть указаны, как минимум, фамилия и имя!", Отказ);
			КонецЕсли;
		ИначеЕсли Не ЭтоЮрЛицо Тогда
			Если Не РегламентированнаяОтчетность.СтрокаНаписанаПоРусски(СтруктураДанных.Фамилия + СтруктураДанных.Имя + СтруктураДанных.Отчество) Тогда
				ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Для физического лица-работодателя Фамилия, Имя, Отчество должно быть введены русскими буквами!", Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если СтруктураДанных.Признак <> "СводнаяСправка" Тогда
			Если Не ЗначениеЗаполнено(СтруктураДанных.ИННорг) Тогда
				ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан ИНН " + ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("организации") + "!", Отказ);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтруктураДанных.КодИФНС) Тогда
				ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан код ИФНС-получателя справки!", Отказ);
			КонецЕсли;
		Иначе 	
			Если Не ЗначениеЗаполнено(СтруктураДанных.ИННорг) Тогда
				ОбработкаКомментариев.ДобавитьСообщение("Не указан ИНН " + ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("организации") + "!", Перечисления.ВидыСообщений.Ошибка);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураДанных.ИННорг) Тогда
			Если ЭтоЮрЛицо Тогда
				Если Не РегламентированнаяОтчетность.ИННСоответствуетТребованиям(СтруктураДанных.ИННорг, Перечисления.ЮрФизЛицо.ЮрЛицо ) Тогда
					ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Для " + ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("организации") + " неверно введен ИНН!", Отказ);
				КонецЕсли; 
			Иначе
				Если Не РегламентированнаяОтчетность.ИННСоответствуетТребованиям(СтруктураДанных.ИННорг, Перечисления.ЮрФизЛицо.ФизЛицо) Тогда
					ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Для физического лица-работодателя неверно введен ИНН!", Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураДанных.Признак <> "СводнаяСправка" Тогда
		Если ПроверятьКодОКТМО Тогда
			Если Не ЗначениеЗаполнено(СтрЗаменить(СтруктураДанных.ОКТМО,"-","")) Тогда
				ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан код по ОКТМО, по которому формируются сведения!", Отказ);
			КонецЕсли;
		Иначе
			Если Не ЗначениеЗаполнено(СтруктураДанных.ОКАТО) Тогда
				ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан код по ОКАТО, по которому формируются сведения!", Отказ);
			КонецЕсли;
		КонецЕсли;
		Если ЭтоЮрЛицо И Не ЗначениеЗаполнено(СтруктураДанных.КПП) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан КПП, по которому формируются сведения!", Отказ);
		КонецЕсли;
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(СтруктураДанных.ФИОПодписавшего) Тогда
		ОбработкаКомментариев.ДобавитьСообщение("Не указано лицо, подписавшее справку!", Перечисления.ВидыСообщений.Ошибка);
	КонецЕсли;
	
	Если ЭтоЮрЛицо И Не ЗначениеЗаполнено(СтруктураДанных.ДолжностьПодписавшегоЛица) Тогда
		ОбработкаКомментариев.ДобавитьСообщение("Не указана должность подписавшего лица!", Перечисления.ВидыСообщений.Ошибка);
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеНалоговогоАгентаДля2НДФЛ(Организация, НалоговыйПериод, ОКАТО_КПП, ОКТМО_КПП, КодИФНС, Дата, Телефон, ДолжностьПодписавшегоЛица, СправкуПодписал) Экспорт

	СтруктураДанныхНА = Новый Структура("Организация, НалоговыйПериод, НалоговыйПериодСтрокой, Дата, Телефон, Признак, ЭтоЮрЛицо, НазваниеОрганизации, Фамилия, Имя, Отчество, КодИФНС, ИННорг, КПП, ОКАТО, ОКТМО, ДолжностьПодписавшегоЛица, ФИОПодписавшего", Организация, НалоговыйПериод, Формат(НалоговыйПериод,"ЧЦ=4; ЧДЦ=0; ЧГ=0"), Дата, Телефон);
	ЭтоЮрЛицо = ОбщегоНазначенияПереопределяемый.ЭтоЮрЛицо(Организация);
	СтруктураДанныхНА.ЭтоЮрЛицо = ЭтоЮрЛицо;
	ДанныеОрганизации = ОбщегоНазначенияЗК.ПолучитьЗначенияРеквизитов(Организация, "ИНН, КодИМНС, НаименованиеПолное, НаименованиеСокращенное" + ?(ЭтоЮрЛицо,"", ",ИндивидуальныйПредприниматель"));
	СтруктураДанныхНА.КодИФНС = ?(ЗначениеЗаполнено(КодИФНС), КодИФНС, ДанныеОрганизации.КодИМНС);
	СтруктураДанныхНА.ИННорг = ДанныеОрганизации.ИНН;
	СтруктураДанныхНА.ОКАТО = КодОКАТОизСуммыОКАТОиКПП(ОКАТО_КПП);
	СтруктураДанныхНА.ОКТМО = РегламентированнаяОтчетность.ДополнитьСтроку(КодОКАТОизСуммыОКАТОиКПП(ОКТМО_КПП), 11, "-", 1);
	СтруктураДанныхНА.КПП = КППизСуммыОКАТОиКПП(?(ЗначениеЗаполнено(СтрЗаменить(ОКТМО_КПП,"/","")), ОКТМО_КПП, ОКАТО_КПП));
	Если ЭтоЮрЛицо Тогда
		СтруктураДанныхНА.НазваниеОрганизации = ?(ЗначениеЗаполнено(ДанныеОрганизации.НаименованиеСокращенное), ДанныеОрганизации.НаименованиеСокращенное, ДанныеОрганизации.НаименованиеПолное);
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИндивидуальныйПредприниматель", ДанныеОрганизации.ИндивидуальныйПредприниматель);
		Запрос.УстановитьПараметр("ДатаКадровыхСведений", КонецДня(Дата));
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ФИОФизЛицСрезПоследних.Фамилия,
		|	ФИОФизЛицСрезПоследних.Имя,
		|	ФИОФизЛицСрезПоследних.Отчество
		|ИЗ
		|	РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаКадровыхСведений, ФизЛицо = &ИндивидуальныйПредприниматель) КАК ФИОФизЛицСрезПоследних";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Фамилия = Выборка.Фамилия;
			Имя = Выборка.Имя;
			Отчество = Выборка.Отчество;
		Иначе
			Фамилия = "";
			Имя = "";
			Отчество = "";
			ОбщегоНазначенияЗК.ФамилияИнициалыФизЛица(ОбщегоНазначенияЗК.ПолучитьЗначениеРеквизита(ДанныеОрганизации.ИндивидуальныйПредприниматель,"Наименование"),Фамилия,Имя,Отчество);
		КонецЕсли;
		СтруктураДанныхНА.НазваниеОрганизации = Фамилия + " " + Имя + " " + Отчество;
		СтруктураДанныхНА.Фамилия = Фамилия;
		СтруктураДанныхНА.Имя = Имя;
		СтруктураДанныхНА.Отчество = Отчество;
	КонецЕсли;
	СтруктураДанныхНА.ДолжностьПодписавшегоЛица = ОбщегоНазначенияЗК.ПолучитьПредставленияОбъектов(ДолжностьПодписавшегоЛица);
	Если ЗначениеЗаполнено(СправкуПодписал) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СправкуПодписал", СправкуПодписал);
		Запрос.УстановитьПараметр("Дата", Дата);
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ФИОПодписавшего.Фамилия + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОПодписавшего.Имя, 1, 1) <> """"
		|				ТОГДА "" "" + ПОДСТРОКА(ФИОПодписавшего.Имя, 1, 1) + "".""
		|			ИНАЧЕ """"
		|		КОНЕЦ + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОПодписавшего.Отчество, 1, 1) <> """"
		|				ТОГДА "" "" + ПОДСТРОКА(ФИОПодписавшего.Отчество, 1, 1) + "".""
		|			ИНАЧЕ """"
		|		КОНЕЦ, ДанныеДокумента.Наименование) КАК ФИОПодписавшего
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ФизЛицо = &СправкуПодписал) КАК ФИОПодписавшего
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &СправкуПодписал";
	    Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтруктураДанныхНА.ФИОПодписавшего = Выборка.ФИОПодписавшего
		КонецЕсли;
	КонецЕсли;
	
    Возврат СтруктураДанныхНА
	
КонецФункции // ДанныеНалоговогоАгентаДля2НДФЛ()

Функция ИзвлечьДоходыПоСтавке(Ставка, СтатусНалогоплательщика, ДоходыПоВидам, СоответствиеСтавокДоходов) Экспорт
	
	ТаблицаПодходящихДоходов = ДоходыПоВидам.СкопироватьКолонки();

	ЭтоНеРезидент = СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент;
	ЭтоРезидент = СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.Резидент;
	Для каждого СтрокаТЗ Из ДоходыПоВидам Цикл
	
		СтавкаДохода = Перечисления.НДФЛСтавки.Ставка13;
		СтавкаНалогообложенияРезидента = СоответствиеСтавокДоходов[СтрокаТЗ.КодДохода];
		
		Если СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13 Тогда
			Если ЭтоНеРезидент Тогда
				СтавкаДохода = Перечисления.НДФЛСтавки.Ставка30
			Иначе
				СтавкаДохода = Перечисления.НДФЛСтавки.Ставка13;
			КонецЕсли;
		ИначеЕсли ЭтоРезидент Тогда
			Если СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09 Тогда
				СтавкаДохода = Перечисления.НДФЛСтавки.Ставка09;
			ИначеЕсли СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35 Тогда
				СтавкаДохода = Перечисления.НДФЛСтавки.Ставка35;
			КонецЕсли;
		ИначеЕсли СтрокаТЗ.КодДохода = Справочники.ДоходыНДФЛ.Код1010 Тогда
			СтавкаДохода = Перечисления.НДФЛСтавки.Ставка15
		Иначе
			СтавкаДохода = Перечисления.НДФЛСтавки.Ставка30
		КонецЕсли;
		
		Если СтавкаДохода = Ставка Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаПодходящихДоходов.Добавить(), СтрокаТЗ);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат	ТаблицаПодходящихДоходов

КонецФункции // ИзвлечьДоходыПоСтавке()

////////////////////////////////////////////////////////////////////////////////

Процедура ВывестиРазделВСправку(ТабДокумент, Раздел, ЭтоПоследнийРаздел, ОбластьНалоговыйАгент, ОбластьПримечание, ШапкаВторойСтраницы)
	
	ВыводимыеОбласти = Новый Массив;
	ВыводимыеОбласти.Добавить(Раздел);
	ВыводимыеОбласти.Добавить(ОбластьНалоговыйАгент);
	
	ВывестиПодвалЛиста = Не ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти);
	Если Не ВывестиПодвалЛиста И ЭтоПоследнийРаздел Тогда
		ВыводимыеОбласти.Добавить(ОбластьПримечание);
		ВывестиПодвалЛиста = Не ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти);
	КонецЕсли;
	Если ВывестиПодвалЛиста Тогда
		ТабДокумент.Вывести(ОбластьНалоговыйАгент);
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ШапкаВторойСтраницы.Параметры.НомерСтраницы = ШапкаВторойСтраницы.Параметры.НомерСтраницы + 1;
		ТабДокумент.Вывести(ШапкаВторойСтраницы);
	КонецЕсли;
	
	ТабДокумент.Вывести(Раздел);
	
КонецПроцедуры

Процедура ВывестиТабличныеДанные(МассивСтрок, ДокументРезультат, ОбластиМакета, Знач МинимумСтрок, КоличествоКолонок = 2, ПроверяемоеПоле = "")

	ВсегоСтрок = МассивСтрок.Количество();
	
	Для каждого Область Из ОбластиМакета Цикл
		ФормированиеПечатныхФормЗК.ОчиститьПараметрыОбластиТабличногоДокумента(Область);
	КонецЦикла;
	
	ЛеваяЧастьМакета = ОбластиМакета[0];
	
	// Вычислим высоту таблицы
	ВысотаТаблицы = ?(ВсегоСтрок < МинимумСтрок, ВсегоСтрок, ?(ВсегоСтрок > МинимумСтрок * КоличествоКолонок, ОбщегоНазначенияЗК.ЦелМаксимальное(ВсегоСтрок / КоличествоКолонок), МинимумСтрок));
	
	Если КоличествоКолонок = 2 И ЗначениеЗаполнено(ПроверяемоеПоле) И ВсегоСтрок > ВысотаТаблицы Тогда // иногда строки доходов не надо переносить в правый столбец печ.формы
		ПерваяСтрокаВторойКолонки = ВысотаТаблицы + 1;
		Для Сч = ПерваяСтрокаВторойКолонки По ВсегоСтрок Цикл
			Если ЗначениеЗаполнено(МассивСтрок[Сч - 1][ПроверяемоеПоле]) Тогда
				Прервать;
			Иначе
				ВысотаТаблицы = ВысотаТаблицы + 1
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для Сч = 1 По ВысотаТаблицы Цикл
		
		// перенесем данные
		ЛеваяЧастьМакета.Параметры.Заполнить(МассивСтрок[Сч - 1]);
		Если ВсегоСтрок > МинимумСтрок Тогда  // правая часть
			Для СчКолонок = 2 По КоличествоКолонок Цикл
				Если ВысотаТаблицы * (СчКолонок - 1) + Сч <= ВсегоСтрок Тогда
					ОбластиМакета[СчКолонок - 1].Параметры.Заполнить(МассивСтрок[ВысотаТаблицы * (СчКолонок - 1) + Сч - 1]);
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
		
		// выведем области макета
		ДокументРезультат.Вывести(ЛеваяЧастьМакета);	
		Для СчКолонок = 2 По КоличествоКолонок Цикл
			ДокументРезультат.Присоединить(ОбластиМакета.Получить(СчКолонок - 1));	
		КонецЦикла;
		
		Для каждого Область Из ОбластиМакета Цикл
			ФормированиеПечатныхФормЗК.ОчиститьПараметрыОбластиТабличногоДокумента(Область);
		КонецЦикла;
		
	КонецЦикла;
	
	// довыведем пустые строки раздела
	Если ВысотаТаблицы < МинимумСтрок Тогда
		Для Сч = ВысотаТаблицы + 1 По МинимумСтрок Цикл
			ДокументРезультат.Вывести(ЛеваяЧастьМакета);	
			Для СчКолонок = 2 По КоличествоКолонок Цикл
				ДокументРезультат.Присоединить(ОбластиМакета[СчКолонок - 1]);	
			КонецЦикла; 
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ВывестиТабличныеДанные()

Функция Печать2010(Отказ, СтруктураДанныхНА, ТаблицаДанныхСотрудников, ТаблицаДанныхОДоходах, ТаблицаДанныхОВычетах, ТаблицаИтоговыхДанных, СоответствиеКодовСтран, СоответствиеИменСтран, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеКодовРегионов, СоответствиеКодовДоходов, СоответствиеКодовВычетов)

	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ПустаяСтруктураАдреса = Новый Структура("Индекс,КодРегиона,Район,Город,НаселенныйПункт,Улица,Дом,Корпус,Квартира");
	СтруктураОтбора = Новый Структура("НомерСправки");
	
	ТекущаяДата = ОбщегоНазначенияЗК.ПолучитьРабочуюДату();
	
	Если Не Отказ Тогда
		
		ТабДокумент = Новый ТабличныйДокумент;
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаПоНДФЛВНалоговыйОрган_Справка2_НДФЛ";
		
		Справка = ПолучитьОбщийМакет("Форма2НДФЛс2010");
		
		ОбластьМакетаШапка = Справка.ПолучитьОбласть("Шапка"); // Шапка документа.
		ОбластьМакетаСередина = Справка.ПолучитьОбласть("Середина"); // Середина документа.
		ОбластьМакетаПодвал	= Справка.ПолучитьОбласть("Подвал");// Подвал документа
		ОбластьМакетаСтрокаВычетов = Справка.ПолучитьОбласть("СтрокаВычетов"); 
		ОбластьМакетаСтрокаДоходов = Справка.ПолучитьОбласть("СтрокаДоходов"); 
		ОбластиМакетаДляДоходов = Новый Массив;
		ОбластиМакетаДляДоходов.Добавить(Справка.ПолучитьОбласть("СтрокаДоходов|ЛеваяЧасть"));
		ОбластиМакетаДляДоходов.Добавить(Справка.ПолучитьОбласть("СтрокаДоходов|ПраваяЧасть"));
		ОбластиМакетаДляВычетов = Новый Массив;
		Для Сч = 1 По 4 Цикл
			ОбластиМакетаДляВычетов.Добавить(Справка.ПолучитьОбласть("СтрокаВычетов|Вычеты"+Сч));
		КонецЦикла;
		
		ОбластьМакетаШапка.Параметры.Заполнить(СтруктураДанныхНА);  // Раздел 1
		Если СтруктураДанныхНА.Признак = "СводнаяСправка" Тогда
			ОбластьМакетаШапка.Параметры.Признак = ""
		КонецЕсли;;
		
		ОбластьМакетаПодвал.Параметры.Заполнить(СтруктураДанныхНА); // подписи
		
	КонецЕсли;
	
	Для каждого СтрокаДанных Из ТаблицаДанныхСотрудников Цикл 
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаДанных);
		ДоходыПоВидам = ТаблицаДанныхОДоходах.Скопировать(ТаблицаДанныхОДоходах.НайтиСтроки(СтруктураОтбора));
		ВычетыПоВидам = ТаблицаДанныхОВычетах.Скопировать(ТаблицаДанныхОВычетах.НайтиСтроки(СтруктураОтбора));
		
		// Проверка корректности выводимых данных
		ПроверитьДанныеСотрудника(2010, СтруктураДанныхНА.Признак, СтрокаДанных, ДоходыПоВидам, ВычетыПоВидам, Неопределено, ТекущаяДата, Отказ, ОбработкаКомментариев, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеИменСтран);
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ДоходыПоВидам.Колонки.Добавить("КодДоходаСтрокой", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(4)));
		ДоходыПоВидам.Колонки.Добавить("КодВычетаСтрокой", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(3)));
		ДоходыПоВидам.Сортировать("Месяц, КодДохода, СуммаДохода Убыв, КодВычета");
		ПредыдущийМесяц = -1;
		ПредыдущийКодДохода = "";
		Для каждого СтрокаТЗ Из ДоходыПоВидам Цикл
			Если ПредыдущийМесяц <> СтрокаТЗ.Месяц Или ПредыдущийКодДохода <> СтрокаТЗ.КодДохода Тогда
				СтрокаТЗ.КодДоходаСтрокой = СоответствиеКодовДоходов[СтрокаТЗ.КодДохода];
				ПредыдущийМесяц = СтрокаТЗ.Месяц;
				ПредыдущийКодДохода = СтрокаТЗ.КодДохода;
			Иначе
				СтрокаТЗ.Месяц = 0;
			КонецЕсли;
			СтрокаТЗ.КодВычетаСтрокой = СоответствиеКодовВычетов[СтрокаТЗ.КодВычета];
		КонецЦикла;
		ВычетыПоВидам.Колонки.Добавить("КодВычетаСтрокой", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(3)));
		Для каждого СтрокаТЗ Из ВычетыПоВидам Цикл
			СтрокаТЗ.КодВычетаСтрокой = СоответствиеКодовВычетов[СтрокаТЗ.КодВычета];
		КонецЦикла;
		
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда  // если справок несколько - разобьем на отдельные страницы
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		// Заполнение данных в параметрах макета
		
		ОбластьМакетаШапка.Параметры.Заполнить(СтрокаДанных);  // Раздел 2
		ОбластьМакетаШапка.Параметры.Статус = ?(СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент Или СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.УчастникПрограммыПоПереселениюСоотечественников Или СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.ЧленЭкипажаСуднаПодФлагомРФ,2,?(СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.ВысококвалифицированныйИностранныйСпециалист,3,1));
		ОбластьМакетаШапка.Параметры.Гражданство = СоответствиеКодовСтран[СтрокаДанных.Гражданство];
		ОбластьМакетаШапка.Параметры.КодУдЛичн = СоответствиеДокументовУдостоверяющиеЛичность[СтрокаДанных.ВидДокумента];
		
		Если ЗначениеЗаполнено(СтрокаДанных.АдресРФ) Тогда
			СтруктураАдреса = РегламентированнаяОтчетность.РазложитьАдрес(СтрокаДанных.АдресРФ);
			ОбластьМакетаШапка.Параметры.Заполнить(СтруктураАдреса);  
			ОбластьМакетаШапка.Параметры.КодРегиона = СоответствиеКодовРегионов[СтруктураАдреса.Регион]
		Иначе // очищаем адресные данные
			ОбластьМакетаШапка.Параметры.Заполнить(ПустаяСтруктураАдреса);  
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаДанных.АдресЗарубежом) Тогда
			СтруктураАдреса = УправлениеКонтактнойИнформацией.ПолучитьСтруктуруАдресаИзСтроки(СтрокаДанных.АдресЗарубежом);
			ОбластьМакетаШапка.Параметры.КодСтр = СоответствиеИменСтран[СтруктураАдреса.Страна];
			ОбластьМакетаШапка.Параметры.АдрТекст = СтруктураАдреса.Представление;
		Иначе // очищаем адресные данные
			ОбластьМакетаШапка.Параметры.КодСтр = "";
			ОбластьМакетаШапка.Параметры.АдрТекст = "";
		КонецЕсли;
		
		ОбластьМакетаПодвал.Параметры.Заполнить(СтрокаДанных); // реквизиты Уведомления об имущ.вычете Раздела 4 и суммы Раздела 5
		
		// Вывод подготовленной справки в табличный документ
		
		ТабДокумент.Вывести(ОбластьМакетаШапка);
		
		// Раздел 3
		Если ДоходыПоВидам.Количество() = 0 Тогда
			Для Сч = 1 По 9 Цикл
				ТабДокумент.Вывести(ОбластьМакетаСтрокаДоходов);
			КонецЦикла;
		Иначе
			ВывестиТабличныеДанные(ДоходыПоВидам, ТабДокумент, ОбластиМакетаДляДоходов, 9, 2);
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакетаСередина);
		
		// Раздел 4
		ВычетыПоВидам.Сортировать("КодВычета");
		Если ВычетыПоВидам.Количество() = 0 Тогда
			ТабДокумент.Вывести(ОбластьМакетаСтрокаВычетов);
		Иначе
			ВывестиТабличныеДанные(ВычетыПоВидам, ТабДокумент, ОбластиМакетаДляВычетов, 1, 4);
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакетаПодвал);
		
	КонецЦикла;
	
	Возврат ТабДокумент;

КонецФункции // Печать2010()

Функция ПечатьС2011года(Отказ, НалоговыйПериод, ИмяМакета, СтруктураДанныхНА, ТаблицаДанныхСотрудников, ТаблицаДанныхОДоходах, ТаблицаДанныхОВычетах, ТаблицаИтоговыхДанных, СоответствиеКодовСтран, СоответствиеИменСтран, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеКодовРегионов, СоответствиеКодовДоходов, СоответствиеКодовВычетов, СоответствиеСтавокДоходов)

	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ПустаяСтруктураАдреса = Новый Структура("Индекс,КодРегиона,Район,Город,НаселенныйПункт,Улица,Дом,Корпус,Квартира");
	СтруктураОтбора = Новый Структура("НомерСправки");
	
	ТекущаяДата = ОбщегоНазначенияЗК.ПолучитьРабочуюДату();
	ВременныйТабДокумент = Новый ТабличныйДокумент;
	
	Если Не Отказ Тогда
		
		ТабДокумент = Новый ТабличныйДокумент;
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаПоНДФЛВНалоговыйОрган_Справка2_НДФЛ";
		
		Справка = ПолучитьОбщийМакет(ИмяМакета);
		
		ОбластьМакетаШапкаПервойСтраницы = Справка.ПолучитьОбласть("ШапкаПервойСтраницы"); 
		ОбластьМакетаШапкаВторойСтраницы = Справка.ПолучитьОбласть("ШапкаВторойСтраницы");
		ОбластьМакетаЗаголовокРаздела3 = Справка.ПолучитьОбласть("ЗаголовокРаздела3"); 
		ОбластьМакетаЗаголовокРаздела4 = Справка.ПолучитьОбласть("ЗаголовокРаздела4");
		ОбластьМакетаПодвалРаздела4	= Справка.ПолучитьОбласть("ПодвалРаздела4");
		ОбластьМакетаРаздел5 = Справка.ПолучитьОбласть("Раздел5");
		ОбластьМакетаНалоговыйАгент = Справка.ПолучитьОбласть("НалоговыйАгент");
		ОбластьМакетаПримечание = Справка.ПолучитьОбласть("Примечание");
		
		ОбластьМакетаСтрокаВычетов = Справка.ПолучитьОбласть("СтрокаВычетов"); 
		ОбластьМакетаСтрокаДоходов = Справка.ПолучитьОбласть("СтрокаДоходов"); 
		ОбластиМакетаДляДоходов = Новый Массив;
		ОбластиМакетаДляДоходов.Добавить(Справка.ПолучитьОбласть("СтрокаДоходов|ЛеваяЧасть"));
		ОбластиМакетаДляДоходов.Добавить(Справка.ПолучитьОбласть("СтрокаДоходов|ПраваяЧасть"));
		ОбластиМакетаДляВычетов = Новый Массив;
		Для Сч = 1 По 4 Цикл
			ОбластиМакетаДляВычетов.Добавить(Справка.ПолучитьОбласть("СтрокаВычетов|Вычеты"+Сч));
		КонецЦикла;
		
		ОбластьМакетаШапкаПервойСтраницы.Параметры.Заполнить(СтруктураДанныхНА);  // Раздел 1
		Если СтруктураДанныхНА.Признак = "СводнаяСправка" Или Не ЗначениеЗаполнено(ОбластьМакетаШапкаПервойСтраницы.Параметры.Признак) Тогда
			ОбластьМакетаШапкаПервойСтраницы.Параметры.Признак = "1"
		КонецЕсли;
		
		ОбластьМакетаНалоговыйАгент.Параметры.Заполнить(СтруктураДанныхНА); // подписи
		
	КонецЕсли;
	
	Для каждого СтрокаДанных Из ТаблицаДанныхСотрудников Цикл 
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаДанных);
		ДоходыПоВидам = ТаблицаДанныхОДоходах.Скопировать(ТаблицаДанныхОДоходах.НайтиСтроки(СтруктураОтбора));
		ВычетыПоВидам = ТаблицаДанныхОВычетах.Скопировать(ТаблицаДанныхОВычетах.НайтиСтроки(СтруктураОтбора));
		Итоги = ТаблицаИтоговыхДанных.Скопировать(ТаблицаИтоговыхДанных.НайтиСтроки(СтруктураОтбора));
		Итоги.Сортировать("Ставка");
		
		// Проверка корректности выводимых данных
		ПроверитьДанныеСотрудника(НалоговыйПериод, СтруктураДанныхНА.Признак, СтрокаДанных, ДоходыПоВидам, ВычетыПоВидам, Итоги, ТекущаяДата, Отказ, ОбработкаКомментариев, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеИменСтран, СоответствиеСтавокДоходов);
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ДоходыПоВидам.Колонки.Добавить("КодДоходаСтрокой", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(4)));
		ДоходыПоВидам.Колонки.Добавить("КодВычетаСтрокой", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(3)));
		ДоходыПоВидам.Сортировать("Месяц, КодДохода, СуммаДохода Убыв, КодВычета");
		ПредыдущийМесяц = -1;
		ПредыдущийКодДохода = "";
		Для каждого СтрокаТЗ Из ДоходыПоВидам Цикл
			Если ПредыдущийМесяц <> СтрокаТЗ.Месяц Или ПредыдущийКодДохода <> СтрокаТЗ.КодДохода Тогда
				СтрокаТЗ.КодДоходаСтрокой = СоответствиеКодовДоходов[СтрокаТЗ.КодДохода];
				ПредыдущийМесяц = СтрокаТЗ.Месяц;
				ПредыдущийКодДохода = СтрокаТЗ.КодДохода;
			Иначе
				СтрокаТЗ.Месяц = 0;
			КонецЕсли;
			СтрокаТЗ.КодВычетаСтрокой = СоответствиеКодовВычетов[СтрокаТЗ.КодВычета];
		КонецЦикла;
		ВычетыПоВидам.Колонки.Добавить("КодВычетаСтрокой", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(3)));
		Для каждого СтрокаТЗ Из ВычетыПоВидам Цикл
			СтрокаТЗ.КодВычетаСтрокой = СоответствиеКодовВычетов[СтрокаТЗ.КодВычета];
		КонецЦикла;
		
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда  // если справок несколько - разобьем на отдельные страницы
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		// Заполнение данных в параметрах макета
		
		ОбластьМакетаШапкаПервойСтраницы.Параметры.Заполнить(СтрокаДанных);  // Раздел 2
		ОбластьМакетаШапкаПервойСтраницы.Параметры.Статус = ?(СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент Или СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.УчастникПрограммыПоПереселениюСоотечественников Или СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.ЧленЭкипажаСуднаПодФлагомРФ,2,?(СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.ВысококвалифицированныйИностранныйСпециалист,3,1));
		ОбластьМакетаШапкаПервойСтраницы.Параметры.Гражданство = СоответствиеКодовСтран[СтрокаДанных.Гражданство];
		ОбластьМакетаШапкаПервойСтраницы.Параметры.КодУдЛичн = СоответствиеДокументовУдостоверяющиеЛичность[СтрокаДанных.ВидДокумента];
		
		Если ЗначениеЗаполнено(СтрокаДанных.АдресРФ) Тогда
			СтруктураАдреса = РегламентированнаяОтчетность.РазложитьАдрес(СтрокаДанных.АдресРФ);
			ОбластьМакетаШапкаПервойСтраницы.Параметры.Заполнить(СтруктураАдреса);  
			ОбластьМакетаШапкаПервойСтраницы.Параметры.КодРегиона = СоответствиеКодовРегионов[СтруктураАдреса.Регион]
		Иначе // очищаем адресные данные
			ОбластьМакетаШапкаПервойСтраницы.Параметры.Заполнить(ПустаяСтруктураАдреса);  
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаДанных.АдресЗарубежом) Тогда
			СтруктураАдреса = УправлениеКонтактнойИнформацией.ПолучитьСтруктуруАдресаИзСтроки(СтрокаДанных.АдресЗарубежом);
			ОбластьМакетаШапкаПервойСтраницы.Параметры.КодСтр = СоответствиеИменСтран[СтруктураАдреса.Страна];
			ОбластьМакетаШапкаПервойСтраницы.Параметры.АдрТекст = СтруктураАдреса.Представление;
		Иначе // очищаем адресные данные
			ОбластьМакетаШапкаПервойСтраницы.Параметры.КодСтр = "";
			ОбластьМакетаШапкаПервойСтраницы.Параметры.АдрТекст = "";
		КонецЕсли;
		
		ОбластьМакетаПодвалРаздела4.Параметры.Заполнить(СтрокаДанных); // реквизиты Уведомления об имущ.вычете Раздела 4
		
		// Вывод подготовленной справки в табличный документ
		
		ОбластьМакетаШапкаВторойСтраницы.Параметры.Заполнить(ОбластьМакетаШапкаПервойСтраницы.Параметры);  // последующие страницы
		ТабДокумент.Вывести(ОбластьМакетаШапкаПервойСтраницы);
		
		ПерваяСтавка = Истина;
		ОбластьМакетаШапкаВторойСтраницы.Параметры.НомерСтраницы = 1;
		ВыведеноИтогов = 0;
		ВсегоИтогов = Итоги.Количество();
		Для каждого СтрокаИтогов Из Итоги Цикл
		
			ВыведеноИтогов = ВыведеноИтогов + 1;
			
			ВременныйТабДокумент.Очистить();
			
			// Раздел 3
			ОбластьМакетаЗаголовокРаздела3.Параметры.Заполнить(СтрокаИтогов); 
			ВременныйТабДокумент.Вывести(ОбластьМакетаЗаголовокРаздела3);
			
			ДоходыПоСтавке = ИзвлечьДоходыПоСтавке(СтрокаИтогов.Ставка, СтрокаДанных.СтатусНалогоплательщика, ДоходыПоВидам, СоответствиеСтавокДоходов);
			Если ДоходыПоСтавке.Количество() = 0 Тогда
				ВременныйТабДокумент.Вывести(ОбластьМакетаСтрокаДоходов);
			Иначе
				ВывестиТабличныеДанные(ДоходыПоСтавке, ВременныйТабДокумент, ОбластиМакетаДляДоходов, 1, 2, "КодДоходаСтрокой");
			КонецЕсли;
			ВывестиРазделВСправку(ТабДокумент, ВременныйТабДокумент, Ложь, ОбластьМакетаНалоговыйАгент, ОбластьМакетаПримечание, ОбластьМакетаШапкаВторойСтраницы);
			
			// Раздел 4
			Если ПерваяСтавка Тогда
			
				ВременныйТабДокумент.Очистить();
				
				ВременныйТабДокумент.Вывести(ОбластьМакетаЗаголовокРаздела4);
				ВычетыПоВидам.Сортировать("КодВычета");
				Если ВычетыПоВидам.Количество() = 0 Или СтрокаИтогов.Ставка <> Перечисления.НДФЛСтавки.Ставка13 Тогда
					ВременныйТабДокумент.Вывести(ОбластьМакетаСтрокаВычетов);
				Иначе
					ВывестиТабличныеДанные(ВычетыПоВидам, ВременныйТабДокумент, ОбластиМакетаДляВычетов, 1, 4);
				КонецЕсли;
				
				ВременныйТабДокумент.Вывести(ОбластьМакетаПодвалРаздела4);
				ВывестиРазделВСправку(ТабДокумент, ВременныйТабДокумент, Ложь, ОбластьМакетаНалоговыйАгент, ОбластьМакетаПримечание, ОбластьМакетаШапкаВторойСтраницы);
				
				ПерваяСтавка = Ложь;
				
			КонецЕсли;
			
			// Раздел 5
			ВременныйТабДокумент.Очистить();
			
			ОбластьМакетаРаздел5.Параметры.Заполнить(СтрокаИтогов); 
			ВременныйТабДокумент.Вывести(ОбластьМакетаРаздел5);
			ВывестиРазделВСправку(ТабДокумент, ВременныйТабДокумент, ВыведеноИтогов = ВсегоИтогов, ОбластьМакетаНалоговыйАгент, ОбластьМакетаПримечание, ОбластьМакетаШапкаВторойСтраницы);
			
		КонецЦикла;
		
		ТабДокумент.Вывести(ОбластьМакетаНалоговыйАгент);
		ТабДокумент.Вывести(ОбластьМакетаПримечание);
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции // Печать2011()

Функция ПечатьСправок2НДФЛ(Ссылка, СтруктураДанныхНА, ТаблицаДанныхСотрудников, ТаблицаДанныхОДоходах, ТаблицаДанныхОВычетах, ТаблицаИтоговыхДанных) Экспорт

	ГодПереходаНаОКТМО = Год(ПроведениеРасчетов.ДатаПереходаНаКодыОКТМО());
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	ДанныеДокумента = ОбщегоНазначенияЗК.ПолучитьЗначенияРеквизитов(Ссылка, "СпециальныйДокумент2011года, СпециальныйДокумент2013года");
	
	Отказ = Ложь;
	
	ПроверитьОбщиеДанныеСправки(СтруктураДанныхНА, Отказ, ОбработкаКомментариев, СтруктураДанныхНА.НалоговыйПериод + ?(ДанныеДокумента.СпециальныйДокумент2013года, 0, 1) >= ГодПереходаНаОКТМО);
	
	СоответствиеКодовСтран = Новый Соответствие;
	СоответствиеИменСтран = Новый Соответствие;
	СоответствиеДокументовУдостоверяющиеЛичность = Новый Соответствие;
	СоответствиеКодовРегионов = Новый Соответствие;
	СоответствиеКодовДоходов = Новый Соответствие;
	СоответствиеСтавокДоходов = Новый Соответствие;
	СоответствиеКодовВычетов = Новый Соответствие;
	ПрочитатьКлассификаторыВСоответствия(СтруктураДанныхНА.НалоговыйПериод, СоответствиеКодовСтран, СоответствиеИменСтран, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеКодовРегионов, СоответствиеКодовДоходов, СоответствиеКодовВычетов, СоответствиеСтавокДоходов);
	
	Если СтруктураДанныхНА.НалоговыйПериод = 2010 Или (СтруктураДанныхНА.НалоговыйПериод = 2011 И ДанныеДокумента.СпециальныйДокумент2011года) Тогда
		ТабДокумент = Печать2010(Отказ, СтруктураДанныхНА, ТаблицаДанныхСотрудников, ТаблицаДанныхОДоходах, ТаблицаДанныхОВычетах, ТаблицаИтоговыхДанных, СоответствиеКодовСтран, СоответствиеИменСтран, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеКодовРегионов, СоответствиеКодовДоходов, СоответствиеКодовВычетов)
	ИначеЕсли СтруктураДанныхНА.НалоговыйПериод + ?(ДанныеДокумента.СпециальныйДокумент2013года, 0, 1) < ГодПереходаНаОКТМО Тогда
		ТабДокумент = ПечатьС2011года(Отказ, СтруктураДанныхНА.НалоговыйПериод, "Форма2НДФЛс2011", СтруктураДанныхНА, ТаблицаДанныхСотрудников, ТаблицаДанныхОДоходах, ТаблицаДанныхОВычетах, ТаблицаИтоговыхДанных, СоответствиеКодовСтран, СоответствиеИменСтран, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеКодовРегионов, СоответствиеКодовДоходов, СоответствиеКодовВычетов, СоответствиеСтавокДоходов)
	Иначе
		ТабДокумент = ПечатьС2011года(Отказ, СтруктураДанныхНА.НалоговыйПериод, "Форма2НДФЛс2014", СтруктураДанныхНА, ТаблицаДанныхСотрудников, ТаблицаДанныхОДоходах, ТаблицаДанныхОВычетах, ТаблицаИтоговыхДанных, СоответствиеКодовСтран, СоответствиеИменСтран, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеКодовРегионов, СоответствиеКодовДоходов, СоответствиеКодовВычетов, СоответствиеСтавокДоходов)
	КонецЕсли;
	
	Если Отказ Тогда
		
		ОбработкаКомментариев.ПоказатьСообщения();
		
		Возврат Неопределено	
		
	КонецЕсли;
	
	Если ОбработкаКомментариев.Сообщения.Строки.Количество() > 0 Тогда
		ОбработкаКомментариев.ПоказатьСообщения();
	КонецЕсли;
	
	Возврат ТабДокумент;

КонецФункции // ПечатьСправки()

#КонецЕсли

