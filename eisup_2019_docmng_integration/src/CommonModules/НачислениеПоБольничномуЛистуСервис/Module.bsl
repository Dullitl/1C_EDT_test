
Процедура ПрочитатьДанныеОЛьготах(Объект) Экспорт
	
	Сотрудник = Объект.Сотрудник;
	
	Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		
		Запрос = Новый Запрос;
		Если Объект.ПериодРегистрации < ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами() Или Объект.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ТравмаНаПроизводстве Тогда
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СотрудникиОрганизаций.Физлицо,
			|	ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.ПустаяСсылка) КАК ВыплатаЗаСчетФедеральногоБюджета
			|ИЗ
			|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
			|ГДЕ
			|	СотрудникиОрганизаций.Ссылка = &Ссылка";
		Иначе
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СотрудникиОрганизаций.Физлицо,
			|	ВЫБОР
			|		КОГДА СотрудникиОрганизаций.Физлицо.ЛьготаПриНачисленииПособий = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПриНачисленииБольничных.Маяк)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.Маяк)
			|		КОГДА СотрудникиОрганизаций.Физлицо.ЛьготаПриНачисленииПособий = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПриНачисленииБольничных.ПодразделенияОсобогоРиска)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.ПодразделенияОсобогоРиска)
			|		КОГДА СотрудникиОрганизаций.Физлицо.ЛьготаПриНачисленииПособий = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПриНачисленииБольничных.Семипалатинск)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.Семипалатинск)
			|		КОГДА СотрудникиОрганизаций.Физлицо.ЛьготаПриНачисленииПособий = ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПриНачисленииБольничных.Чернобыль)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.Чернобыль)
			|		КОГДА ФизическиеЛицаСтажи.НомерСтроки ЕСТЬ НЕ NULL 
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.ЗачетНестраховыхПериодов)
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.ПустаяСсылка)
			|	КОНЕЦ КАК ВыплатаЗаСчетФедеральногоБюджета
			|ИЗ
			|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.Стажи КАК ФизическиеЛицаСтажи
			|		ПО СотрудникиОрганизаций.Физлицо = ФизическиеЛицаСтажи.Ссылка
			|			И (ФизическиеЛицаСтажи.ВидСтажа = ЗНАЧЕНИЕ(Справочник.ВидыСтажа.РасширенныйСтраховойСтажДляБЛ))
			|ГДЕ
			|	СотрудникиОрганизаций.Ссылка = &Ссылка";
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Ссылка", Сотрудник);
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Объект, Выборка);
		
	Иначе
		
		Объект.Физлицо = "";
		Объект.ВыплатаЗаСчетФедеральногоБюджета = "";
		
	КонецЕсли;
	
	Объект.ПрименятьЛьготыПриНачисленииПособия = ЗначениеЗаполнено(Объект.ВыплатаЗаСчетФедеральногоБюджета);

КонецПроцедуры

Процедура ПрочитатьРазмерыПособия(Объект, ДополнительныеДанные, ТолькоДополнительныеДанные, ГоловнаяОрганизация = Неопределено) Экспорт

	НачислениеПоБольничномуЛистуПереопределяемый.ОбновитьРазмерыПособия(Объект, ДополнительныеДанные, ТолькоДополнительныеДанные);
	
	Если ТолькоДополнительныеДанные Тогда
		Возврат
	КонецЕсли;
	
	Если ГоловнаяОрганизация = Неопределено Тогда
		ГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Объект.Организация)
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ВыплатаЗаСчетФедеральногоБюджета) Тогда
		Объект.ПроцентОплатыБезЛьгот = Объект.ПроцентОплаты;
		Объект.ОграничениеПособияБезЛьгот = Объект.ОграничениеПособия;
		Если Объект.ВыплатаЗаСчетФедеральногоБюджета = Перечисления.ВыплатыПособийЗаСчетФедеральногоБюджета.ЗачетНестраховыхПериодов Тогда
			
			ДатаНачалаСобытия 	= Объект.ДатаНачалаСобытия;
			Сотрудник			= Объект.Сотрудник;
			Физлицо				= Объект.Физлицо;
			
			Если Объект.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоБеременностиИРодам Тогда	
				
				Объект.ПроцентОплаты = 100;
				
				ДатаСтажа = ПроведениеРасчетов.ПолучитьНачалоСтажаДляБольничногоЛиста(ДатаНачалаСобытия, Физлицо, ГоловнаяОрганизация, Справочники.ВидыСтажа.РасширенныйСтраховойСтажДляБЛ);
				Если ЗначениеЗаполнено(ДатаСтажа) Тогда
					Лет	= 0; Месяцев = 0; 
					ОбщегоНазначения.РазобратьРазностьДат(ДатаНачалаСобытия, ДатаСтажа, Лет, Месяцев);
					Если Лет * 12 + Месяцев >= 6 Тогда
						Объект.ОграничениеПособия = Перечисления.ВидыОграниченияПособия.ОбщееОграничение
					Иначе
						Объект.ОграничениеПособия = Перечисления.ВидыОграниченияПособия.ОграничениеВРазмереММОТ;
					КонецЕсли;
				Иначе
					Объект.ОграничениеПособия = Перечисления.ВидыОграниченияПособия.ОграничениеВРазмереММОТ;
				КонецЕсли;
				
			ИначеЕсли ДатаНачалаСобытия = '00010101' Тогда
				
				Объект.ПроцентОплаты = 100;
				
			Иначе
				
				Запрос = Новый Запрос;
				ТекстЗапроса = 
				"ВЫБРАТЬ
				|	ВЫБОР
				|		КОГДА  РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &ДатаАктуальности
				|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
				|		ТОГДА РаботникиОрганизацийСрезПоследних.ПричинаИзмененияСостоянияЗавершения
				|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.ПричинаИзмененияСостояния
				|	КОНЕЦ КАК ПричинаИзмененияСостояния
				|ИЗ
				|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаАктуальности, Сотрудник = &Сотрудник) КАК РаботникиОрганизацийСрезПоследних
				|ГДЕ
				|	ВЫБОР
				|		КОГДА  РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &ДатаАктуальности
				|				И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
				|		ТОГДА РаботникиОрганизацийСрезПоследних.ПричинаИзмененияСостоянияЗавершения
				|		ИНАЧЕ РаботникиОрганизацийСрезПоследних.ПричинаИзмененияСостояния
				|	КОНЕЦ <> &Увольнение";
				
				Запрос.Текст = ТекстЗапроса;
				Запрос.УстановитьПараметр("ДатаАктуальности",	ДатаНачалаСобытия);
				Запрос.УстановитьПараметр("Сотрудник",			Сотрудник);
				Запрос.УстановитьПараметр("Увольнение",			Перечисления.ПричиныИзмененияСостояния.Увольнение);
				
				УжеНеРаботает = Запрос.Выполнить().Пустой(); // в рег-ре РаботникиОрганизаций нет других записей, кроме как об увольнении
				
				ДатаСтажа = ПроведениеРасчетов.ПолучитьНачалоСтажаДляБольничногоЛиста(ДатаНачалаСобытия, Физлицо, ГоловнаяОрганизация, Справочники.ВидыСтажа.РасширенныйСтраховойСтажДляБЛ);
				Если ЗначениеЗаполнено(ДатаСтажа) Тогда
					Лет	= 0; Месяцев = 0; 
					ОбщегоНазначения.РазобратьРазностьДат(ДатаНачалаСобытия, ДатаСтажа, Лет, Месяцев);
					Если Лет * 12 + Месяцев >= 6 Тогда
						Объект.ОграничениеПособия = Перечисления.ВидыОграниченияПособия.ОбщееОграничение
					Иначе
						Объект.ОграничениеПособия = Перечисления.ВидыОграниченияПособия.ОграничениеВРазмереММОТ;
					КонецЕсли;
					Объект.ПроцентОплаты = ?(УжеНеРаботает, 60,?(Лет >= 8, 100,?(Лет >= 5, 80, 60)));
					
				Иначе
					
					Объект.ОграничениеПособия = Перечисления.ВидыОграниченияПособия.ОграничениеВРазмереММОТ;
					Объект.ПроцентОплаты = 60;
					
				КонецЕсли;
				
			КонецЕсли;
		Иначе
			Объект.ПроцентОплаты = 100;
			Объект.ОграничениеПособия = Перечисления.ВидыОграниченияПособия.ОбщееОграничение;
		КонецЕсли;
	Иначе  
		Объект.ПроцентОплатыБезЛьгот = 0;
		Объект.ОграничениеПособияБезЛьгот = "";
	КонецЕсли;
	
КонецПроцедуры
