
Процедура ОткрытьИсториюСтатусов(Ссылка) Экспорт
	
	ЗначениеОтбора = Новый Структура("ОбъектСогласования", Ссылка);
	СтруктураПараметров = Новый Структура("Отбор", ЗначениеОтбора);
	ОткрытьФорму("РегистрСведений.абс_ИсторияСтатусовАСБ.ФормаСписка", СтруктураПараметров);

КонецПроцедуры

Процедура ЗаполнитьСписокДоступныхСтатусов(ЭтаФорма, ЭтотОбъект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.абс_Статус) Тогда
		ТекущаяМодицированность = ЭтаФорма.Модифицированность;  
		ЭтотОбъект.абс_Статус = Перечисления.абс_СтатусыАСБ.Активный;
		Если НЕ ТекущаяМодицированность Тогда 
			ЭтаФорма.Модифицированность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТекущийСтатус", ЭтотОбъект.абс_Статус);
	
	МассивСтатусов = РегистрыСведений.абс_ИсторияСтатусовАСБ.ПолучитьСписокДоступныхСтатусов(СтруктураПараметров);
	ЭтаФорма.ЭлементыФормы.абс_Статус.СписокВыбора.ЗагрузитьЗначения(МассивСтатусов);
	
КонецПроцедуры

Процедура УстановитьВидимостьПоСтатусам(ЭтаФорма, ЭтотОбъект) Экспорт
	
	ЭтоУпрФорма = ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма");
	РедактированиеРазрешено = (ЭтотОбъект.абс_Статус = Перечисления.абс_СтатусыАСБ.Корректировка) 
						  		ИЛИ (ЭтотОбъект.абс_Статус = Перечисления.абс_СтатусыАСБ.Подготовка); 
								
	Если ЭтоУпрФорма Тогда 
		Элементы = ЭтаФорма.Элементы;
	Иначе
		Элементы = ЭтаФорма.ЭлементыФормы; 	
	КонецЕсли;
								
	Для Каждого Элемент Из Элементы Цикл
		Если Элемент.Имя    = "абс_Статус" 
			или ТипЗнч(Элемент) = Тип("Надпись") 
			или ТипЗнч(Элемент) = Тип("Панель") 
			или Элемент.Имя    = "ДействияФормы"  
			или Элемент.Имя    = "ОсновныеДействияФормы" 
			или ТипЗнч(Элемент) = Тип("РамкаГруппы") 
			или ТипЗнч(Элемент) = Тип("Рамка") 
			ИЛИ ТипЗнч(элемент) = тип("ДекорацияФормы") 	
			ИЛИ ТипЗнч(элемент) = тип("КнопкаКоманднойПанели") 
			ИЛИ ТипЗнч(элемент) = тип("КнопкаФормы")
			ИЛИ ТипЗнч(элемент) = тип("ГруппаФормы")
			ИЛИ ТипЗнч(элемент) = тип("УправляемаяФорма")
			Тогда
			Продолжить;
		КонецЕсли;	      
		
		Если ЭтоУпрФорма Тогда 
			Элемент.ТолькоПросмотр = НЕ РедактированиеРазрешено;	
		Иначе
			Если ТипЗнч(Элемент) = Тип("ПолеВвода") ИЛИ ТипЗнч(Элемент) = Тип("ТабличноеПоле") Тогда	
				Элемент.ТолькоПросмотр = НЕ РедактированиеРазрешено		
			Иначе	
				Элемент.Доступность = РедактированиеРазрешено;
			КонецЕсли;				
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПричинуИзмененияСтатуса(ЭтотОбъект, ПричинаИзмененияСтатуса = Неопределено) Экспорт
	
	ПричинаИзмененияСтатуса = "";
	Если ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		СтатусПоРегистру = РегистрыСведений.абс_ИсторияСтатусовАСБ.ПолучитьТекущийСтатус(ЭтотОбъект.Ссылка);
		Если СтатусПоРегистру <> ЭтотОбъект.абс_Статус И ЗначениеЗаполнено(СтатусПоРегистру) Тогда 
			ВвестиСтроку(ПричинаИзмененияСтатуса, "Введите причину изменения статуса",,Истина);
		КонецЕсли;
	Иначе
		ПричинаИзмененияСтатуса = "Ввод нового элемента";	
	КонецЕсли;
	
	Если ТипЗнч(ЭтотОбъект) <> Тип("ДанныеФормыСтруктура") Тогда 
		ЭтотОбъект.ДополнительныеСвойства.Вставить("ПричинаИзмененияСтатуса", ПричинаИзмененияСтатуса);
	КонецЕсли;
	
КонецПроцедуры


