
// Служебная функция, предназначенная для установки соответствий аналитики бух. и медж. планов счетов
// Используется в УстановитьСоответствие
//
Функция ЗаполнитьСтруктуру(Знач Выборка, СчетБУ, Субконто1 = Неопределено, Субконто2 = Неопределено, Субконто3 = Неопределено)
	Структура = Новый Структура("Счет, Субконто1, Субконто2, Субконто3, СтруктураСчетов");
	Структура.Счет  = Выборка.СчетМеждународный;
	Структура.Субконто1 = Неопределено;
	Структура.Субконто2 = Неопределено;
	Структура.Субконто3 = Неопределено;
	Структура.СтруктураСчетов = Неопределено;

	//Заполняем субконто
	Для Ном = 1 по 3 Цикл
		Если Ном <= Выборка["СчетМеждународный"].ВидыСубконто.Количество() Тогда
			Тип = Выборка.СчетМеждународный.ВидыСубконто[Ном-1].ВидСубконто.ТипЗначения.Типы();
			
			Если НЕ ЗначениеЗаполнено(Выборка["СубконтоМежд"+Ном]) Тогда
				// Заполняем значением из исходной проводки (если совпадают типы)
				Если Тип[0] = ТипЗнч(Субконто1) Тогда
					Структура["Субконто"+Ном] = Субконто1;
				ИначеЕсли Тип[0] = ТипЗнч(Субконто2) Тогда
					Структура["Субконто"+Ном] = Субконто2;
				ИначеЕсли Тип[0] = ТипЗнч(Субконто3) Тогда
					Структура["Субконто"+Ном] = Субконто3;
				КонецЕсли;
			Иначе
				// Заполняем как указано в соответствиях
				Структура["Субконто"+Ном] = Выборка["СубконтоМежд"+Ном];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Структура;
КонецФункции

// Возрващает список пустых значений для заданного плана видов характеристик
//
// Параметры
//  ВидыСубконто  – ПланВидовХарактеристикСсылка – план видов характеристик
//
// Возвращаемое значение:
//   СписокЗначений
//
Функция ПолучитьСписокПустыхСубконто(ВидыСубконто) Экспорт

	сзПустыеСубконто = Новый СписокЗначений;
	
	ВыборкаСубконто = ВидыСубконто.Выбрать();
	
	Пока ВыборкаСубконто.Следующий() Цикл
		сзПустыеСубконто.Добавить(ВыборкаСубконто.ТипЗначения.ПривестиЗначение());
	КонецЦикла;
	
	сзПустыеСубконто.Добавить(Неопределено);
	
	Возврат сзПустыеСубконто; 

КонецФункции // ПолучитьСписокПустыхСубконто()

// Функция возвразает структуру, состоящую из счета и трех субконт международного плана счетов, 
// соответствующих хозрасчетным, переданным как параметры функции
// Например: ПреобразоватьСчетаБУвСчетМСФО(СчетБУ, Субконто1, Субконто2, Субконто3).Счет
Функция ПреобразоватьСчетаБУвСчетМСФОРЖД(СчетБУ, Субконто1 = Неопределено, Субконто2 = Неопределено, Субконто3 = Неопределено, Знач ДатаСреза = Неопределено, ПолучитьВсеСоответствия = Ложь) Экспорт
	
	Структура = Неопределено;
	СтруктураСчетов = Новый Структура();
	
	Если ДатаСреза = Неопределено Тогда
		ДатаСреза = ОбщегоНазначенияЗК.ПолучитьРабочуюДату();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СчетМеждународный КАК СчетМеждународный,
	               |	абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоМежд1 КАК СубконтоМежд1,
	               |	абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоМежд2 КАК СубконтоМежд2,
	               |	абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоМежд3 КАК СубконтоМежд3,
	               |	абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.Реквизит КАК Реквизит,
	               |	абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.Значение КАК Значение
	               |ИЗ
	               |	РегистрСведений.абс_СоответствиеСчетовБУиМСФООСРЖД.СрезПоследних(&ДатаСреза, СчетХозрасчетный В ИЕРАРХИИ (&СчетБУ)) КАК абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних
	               |ГДЕ
	               |	абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.Учитывается
	               |	И абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СчетМеждународный ЕСТЬ НЕ NULL 
	               |	И (абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр1 В (&ПустоеСубконто)
	               |			ИЛИ абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр1 = &Субконто1
	               |			ИЛИ абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр1 = &Субконто2
	               |			ИЛИ абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр1 = &Субконто3)
	               |	И (абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр2 В (&ПустоеСубконто)
	               |			ИЛИ абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр2 = &Субконто1
	               |			ИЛИ абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр2 = &Субконто2
	               |			ИЛИ абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр2 = &Субконто3)
	               |	И (абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр3 В (&ПустоеСубконто)
	               |			ИЛИ абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр3 = &Субконто1
	               |			ИЛИ абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр3 = &Субконто2
	               |			ИЛИ абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СубконтоХозр3 = &Субконто3)";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("СчетБУ", СчетБУ);
	Запрос.УстановитьПараметр("ПустоеСубконто", ПолучитьСписокПустыхСубконто(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные));
	Запрос.УстановитьПараметр("Субконто1", Субконто1);
	Запрос.УстановитьПараметр("Субконто2", Субконто2);
	Запрос.УстановитьПараметр("Субконто3", Субконто3);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		
		СтруктураСчетов.Вставить("Код"+СтрЗаменить(Выборка.СчетМеждународный.Код,".",""), Выборка.СчетМеждународный);
		
		Если НЕ ЗначениеЗаполнено(Выборка.Реквизит) Тогда
			Если Структура = Неопределено Тогда
				Структура = ЗаполнитьСтруктуру(Выборка, СчетБУ, Субконто1, Субконто2, Субконто3);
				Если НЕ ПолучитьВсеСоответствия Тогда
					Возврат Структура;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(Выборка.Реквизит) Тогда // проверяем условие
				НомерСубконто = Число(Сред(Выборка.Реквизит,9,1));
				Рекв = Сред(Выборка.Реквизит,Найти(Выборка.Реквизит, ".")+1);
				Попытка
					Если (НомерСубконто = 1) и ((Субконто1 = Неопределено) или (Субконто1[Рекв] <> Выборка.Значение)) или 
						 (НомерСубконто = 2) и ((Субконто1 = Неопределено) или (Субконто2[Рекв] <> Выборка.Значение)) или 
						 (НомерСубконто = 3) и ((Субконто1 = Неопределено) или (Субконто3[Рекв] <> Выборка.Значение)) Тогда
						Продолжить;
					КонецЕсли
				Исключение
					Сообщить("В правиле соответствия для субконто "+НомерСубконто+" счета "+СчетБУ+" не найден реквизит "+Рекв);
					Продолжить;
				КонецПопытки;
			КонецЕсли;
			Если Структура = Неопределено Тогда
				Структура = ЗаполнитьСтруктуру(Выборка, СчетБУ, Субконто1, Субконто2, Субконто3);
				Если НЕ ПолучитьВсеСоответствия Тогда
					Возврат Структура;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если Структура = Неопределено Тогда
		Структура = Новый Структура("Счет, Субконто1, Субконто2, Субконто3, СтруктураСчетов", Неопределено, Неопределено, Неопределено, Неопределено);
	КонецЕсли;
	
	Структура.Вставить("СтруктураСчетов", СтруктураСчетов);
	
	Возврат Структура;
КонецФункции



/////////////////////////////////////////////////////////////////////////////////
// Функции, формирующие тексты запросов для инициализации данных документов

Функция ТекстЗапросаВидыСубконтоСчетов(ВременнаяТаблица = Неопределено, Индексы = "Счет") Экспорт
	
	Возврат "ВЫБРАТЬ
	        |	абс_МеждународныйОСРЖД.Ссылка КАК Счет,
	        |	ЕСТЬNULL(ВидыСубконто1.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)) КАК ВидСубконто1,
	        |	ЕСТЬNULL(ВидыСубконто2.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)) КАК ВидСубконто2,
	        |	ЕСТЬNULL(ВидыСубконто3.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)) КАК ВидСубконто3
	        |"+?(ВременнаяТаблица=Неопределено, "", "ПОМЕСТИТЬ "+ВременнаяТаблица)+"
	        |ИЗ
	        |	ПланСчетов.абс_МеждународныйОСРЖД КАК абс_МеждународныйОСРЖД
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.абс_МеждународныйОСРЖД.ВидыСубконто КАК ВидыСубконто1
	        |		ПО абс_МеждународныйОСРЖД.Ссылка = ВидыСубконто1.Ссылка
	        |			И (ВидыСубконто1.НомерСтроки = 1)
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.абс_МеждународныйОСРЖД.ВидыСубконто КАК ВидыСубконто2
	        |		ПО абс_МеждународныйОСРЖД.Ссылка = ВидыСубконто2.Ссылка
	        |			И (ВидыСубконто2.НомерСтроки = 2)
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.абс_МеждународныйОСРЖД.ВидыСубконто КАК ВидыСубконто3
	        |		ПО абс_МеждународныйОСРЖД.Ссылка = ВидыСубконто3.Ссылка
	        |			И (ВидыСубконто3.НомерСтроки = 3)
	        |
	        |"+?(Индексы=Неопределено Или ВременнаяТаблица=Неопределено, "", "
	        |ИНДЕКСИРОВАТЬ ПО
	        |	"+Индексы)+"
			|;
			|";
	
КонецФункции

Функция ТекстЗапросаСоответствиеСчетовХозрасчетныйМеждународныйРЖД(ПараметрПериод = Неопределено, ПараметрПустыеСубконто = "ПустыеСубконто", ВременнаяТаблица = Неопределено, Индексы = "СчетХозрасчетный, ВидДвижения") Экспорт
	
	Возврат "ВЫБРАТЬ
	        |	абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СчетХозрасчетный КАК СчетХозрасчетный,
	        |	абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.ВидДвижения КАК ВидДвижения,
	        |	абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних.СчетМеждународный
	        |"+?(ВременнаяТаблица=Неопределено, "", "ПОМЕСТИТЬ "+ВременнаяТаблица)+"
	        |ИЗ
	        |	РегистрСведений.абс_СоответствиеСчетовБУиМСФООСРЖД.СрезПоследних(
	        |			"+?(ПараметрПериод=Неопределено, "", "&"+ПараметрПериод)+",
	        |			СубконтоХозр1 В (&"+ПараметрПустыеСубконто+")
	        |				И СубконтоХозр2 В (&"+ПараметрПустыеСубконто+")
	        |				И СубконтоХозр3 В (&"+ПараметрПустыеСубконто+")) КАК абс_СоответствиеСчетовБУиМСФООСРЖДСрезПоследних
	        |
	        |"+?(Индексы=Неопределено Или ВременнаяТаблица=Неопределено, "", "
	        |ИНДЕКСИРОВАТЬ ПО
	        |	"+Индексы)+"
			|;
			|";
	
КонецФункции
