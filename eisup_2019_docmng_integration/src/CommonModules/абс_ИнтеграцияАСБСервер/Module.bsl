
Процедура ПередЗаписью_ИнтеграцияCognos_РегистрацияИзменений(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда 
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Узел = Справочники.абс_НастройкиОбменаСSQL.ИнтеграцияCognos_ПолучитьУзел();
	
	Если НЕ ЗначениеЗаполнено(Узел) Тогда 
		Возврат;
	КонецЕсли;
	
	Источник.ДополнительныеСвойства.Вставить("абс_ИнтеграцияCognos_Узел", Узел);
	
	Если Источник.ЭтоНовый() Тогда
		Источник.ДополнительныеСвойства.Вставить("абс_ИнтеграцияCognos_РегистрироватьИзменения", Истина);
		Возврат;	
	КонецЕсли;
	
	МассивПроверяемыхРеквизитовИсточника = Новый Массив;
	МассивПроверяемыхРеквизитов = Новый Массив;
	МассивПроверяемыхРеквизитов.Добавить("Наименование");
	МассивПроверяемыхРеквизитов.Добавить("Организация");
	МассивПроверяемыхРеквизитов.Добавить("абс_Организация");
	МассивПроверяемыхРеквизитов.Добавить("Владелец");
	МассивПроверяемыхРеквизитов.Добавить("Родитель");
	МассивПроверяемыхРеквизитов.Добавить("Код");
	МассивПроверяемыхРеквизитов.Добавить("абс_ДатаПо");
	МассивПроверяемыхРеквизитов.Добавить("абс_Используется");
	//МассивПроверяемыхРеквизитов.Добавить("абс_Статус");
	МассивПроверяемыхРеквизитов.Добавить("абс_СтатусОрганизации");
	МассивПроверяемыхРеквизитов.Добавить("НаименованиеПолное");
	МассивПроверяемыхРеквизитов.Добавить("ИНН");
	МассивПроверяемыхРеквизитов.Добавить("КПП");
	МассивПроверяемыхРеквизитов.Добавить("абс_КодИСУЗК");
		
	РеквизитыИсточника = Источник.Метаданные().Реквизиты;
	СтандартныеРеквизитыИсточника = Источник.Метаданные().СтандартныеРеквизиты; 
	
	Для Каждого ИмяРеквизита из МассивПроверяемыхРеквизитов Цикл 	
		Если РеквизитыИсточника.Найти(ИмяРеквизита) <> Неопределено Тогда
			МассивПроверяемыхРеквизитовИсточника.Добавить(ИмяРеквизита);
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого Реквизит из СтандартныеРеквизитыИсточника Цикл 	
		Если МассивПроверяемыхРеквизитов.Найти(Реквизит.Имя) <> Неопределено Тогда
			МассивПроверяемыхРеквизитовИсточника.Добавить(Реквизит.Имя);
		КонецЕсли;	
	КонецЦикла;
	
	Если МассивПроверяемыхРеквизитовИсточника.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка, МассивПроверяемыхРеквизитовИсточника);
	Для Каждого ИмяРеквизита из МассивПроверяемыхРеквизитовИсточника Цикл 	
		Если Источник[ИмяРеквизита] <> ЗначенияРеквизитов[ИмяРеквизита] Тогда 
			Источник.ДополнительныеСвойства.Вставить("абс_ИнтеграцияCognos_РегистрироватьИзменения", Истина);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи_ИнтеграцияCognos_РегистрацияИзменений(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда 
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Если НЕ Источник.ДополнительныеСвойства.Свойство("абс_ИнтеграцияCognos_РегистрироватьИзменения") Тогда
		Возврат;
	КонецЕсли;
		
	Если НЕ Источник.ДополнительныеСвойства.Свойство("абс_ИнтеграцияCognos_Узел") Тогда
		Возврат;
	КонецЕсли;
	
	Узел = Источник.ДополнительныеСвойства.абс_ИнтеграцияCognos_Узел;
	
	Если НЕ ЗначениеЗаполнено(Узел) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.абс_ИнтеграцияCognos_РегистрироватьИзменения Тогда 
		ПланыОбмена.ЗарегистрироватьИзменения(Узел, Источник.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

