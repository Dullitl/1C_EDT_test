

Функция ПолучитьСтруктуруЗапросовПроведения(ТипДокументаОснования) Экспорт 
	
	СтруктураЗапросов = Новый Структура;
	
	ДобавитьОбщиеЗапросы1(СтруктураЗапросов);
	
	Если ТипДокументаОснования = Тип("ДокументСсылка.ПринятиеКУчетуОС") Тогда 
		ДобавитьЗапросыДляПоступление_Принятие(СтруктураЗапросов);
	ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.МодернизацияОС") Тогда 	
		ДобавитьЗапросыДляПоступление_Модернизация(СтруктураЗапросов);
	ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ВводНачальныхОстатковОС") Тогда 	
		ДобавитьЗапросыДляПоступление_Остатки(СтруктураЗапросов);
	КонецЕсли;
	
	ДобавитьОбщиеЗапросы2(СтруктураЗапросов);
	
	Возврат СтруктураЗапросов;
	
КонецФункции 

Процедура ДобавитьЗапросыДляПоступление_Принятие(СтруктураЗапросов) 
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПринятиеКУчетуОС.Дата КАК ДатаПринятияКУчету,
		|	ПринятиеКУчетуОС.ПодразделениеОрганизации КАК Местонахождение,
		|	ПринятиеКУчетуОС.МОЛБУ КАК МОЛ,
		|	ВЫБОР
		|		КОГДА ПринятиеКУчетуОС.КоэффициентУскоренияБУ = 0
		|			ТОГДА 1
		|		ИНАЧЕ ПринятиеКУчетуОС.КоэффициентУскоренияБУ
		|	КОНЕЦ КАК КоэффициентУскорения,
		|	ЕСТЬNULL(Соответствие_СчетУчета.СчетМеждународный, ЗНАЧЕНИЕ(ПланСчетов.Международный.ОСвОрганизации)) КАК СчетУчета,
		|	ЕСТЬNULL(Соответствие_СчетАмортизации.СчетМеждународный, ЗНАЧЕНИЕ(ПланСчетов.Международный.АмортизацияОС_01)) КАК СчетНачисленияАмортизации,
		|	ПринятиеКУчетуОС.НачислятьАмортизациюБУ КАК НачислятьАмортизацию,
		|	ПринятиеКУчетуОС.СпособыОтраженияРасходовПоАмортизации,
		|	ПринятиеКУчетуОС.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования
		|ПОМЕСТИТЬ ДанныеШапкиОснования
		|ИЗ
		|	Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеСчетовХозрасчетныйМСФО КАК Соответствие_СчетУчета
		|		ПО ПринятиеКУчетуОС.СчетУчетаБУ = Соответствие_СчетУчета.СчетХозрасчетный
		|			И (Соответствие_СчетУчета.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийБухгалтерии.Дебет))
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеСчетовХозрасчетныйМСФО КАК Соответствие_СчетАмортизации
		|		ПО ПринятиеКУчетуОС.СчетНачисленияАмортизацииБУ = Соответствие_СчетАмортизации.СчетХозрасчетный
		|			И (Соответствие_СчетАмортизации.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийБухгалтерии.Кредит))
		|ГДЕ
		|	ПринятиеКУчетуОС.Ссылка = &ДокументОснование";
		
	СтруктураЗапросов.Вставить("Поступление_Принятие_ТекстЗапросаДанныеШапкиОснования", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеШапкиОснования.ДатаПринятияКУчету,
		|	ДанныеШапкиОснования.Местонахождение,
		|	ДанныеШапкиОснования.МОЛ,
		|	ДанныеШапкиОснования.КоэффициентУскорения,
		|	ДанныеШапкиОснования.СчетУчета,
		|	ДанныеШапкиОснования.СчетНачисленияАмортизации,
		|	ДанныеШапкиОснования.НачислятьАмортизацию,
		|	ДанныеШапкиОснования.СрокПолезногоИспользования,
		|	СоответствиеСчетовХозрасчетныйМСФО.СчетМеждународный КАК СчетЗатрат,
		|	ВЫБОР
		|		КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения)
		|					ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ПодразделениеОрганизации
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат)
		|							ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СтатьяЗатрат
		|						ИНАЧЕ ВЫБОР
		|								КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.НоменклатурныеГруппы)
		|									ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.НоменклатурнаяГруппа
		|								ИНАЧЕ ВЫБОР
		|										КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОбъектыСтроительства)
		|											ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ОбъектСтроительства
		|										ИНАЧЕ ВЫБОР
		|												КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СпособыСтроительства)
		|													ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СпособСтроительства
		|												ИНАЧЕ СпособыОтраженияРасходовПоАмортизацииСпособы.Субконто1
		|											КОНЕЦ
		|									КОНЕЦ
		|							КОНЕЦ
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Субконто1,
		|	ВЫБОР
		|		КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения)
		|					ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ПодразделениеОрганизации
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат)
		|							ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СтатьяЗатрат
		|						ИНАЧЕ ВЫБОР
		|								КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.НоменклатурныеГруппы)
		|									ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.НоменклатурнаяГруппа
		|								ИНАЧЕ ВЫБОР
		|										КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОбъектыСтроительства)
		|											ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ОбъектСтроительства
		|										ИНАЧЕ ВЫБОР
		|												КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СпособыСтроительства)
		|													ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СпособСтроительства
		|												ИНАЧЕ СпособыОтраженияРасходовПоАмортизацииСпособы.Субконто2
		|											КОНЕЦ
		|									КОНЕЦ
		|							КОНЕЦ
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Субконто2,
		|	ВЫБОР
		|		КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения)
		|					ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ПодразделениеОрганизации
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат)
		|							ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СтатьяЗатрат
		|						ИНАЧЕ ВЫБОР
		|								КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.НоменклатурныеГруппы)
		|									ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.НоменклатурнаяГруппа
		|								ИНАЧЕ ВЫБОР
		|										КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОбъектыСтроительства)
		|											ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ОбъектСтроительства
		|										ИНАЧЕ ВЫБОР
		|												КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СпособыСтроительства)
		|													ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СпособСтроительства
		|												ИНАЧЕ СпособыОтраженияРасходовПоАмортизацииСпособы.Субконто3
		|											КОНЕЦ
		|									КОНЕЦ
		|							КОНЕЦ
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Субконто3
		|ПОМЕСТИТЬ ДанныеДляЗаполненияТабличнойЧасти
		|ИЗ
		|	ДанныеШапкиОснования КАК ДанныеШапкиОснования
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияРасходовПоАмортизации.Способы КАК СпособыОтраженияРасходовПоАмортизацииСпособы
		|			ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеСчетовХозрасчетныйМСФО КАК СоответствиеСчетовХозрасчетныйМСФО
		|				ЛЕВОЕ СОЕДИНЕНИЕ ВидыСубконтоСчетов КАК ВидыСубконтоСчетов
		|				ПО СоответствиеСчетовХозрасчетныйМСФО.СчетМеждународный = ВидыСубконтоСчетов.Счет
		|			ПО СпособыОтраженияРасходовПоАмортизацииСпособы.СчетЗатрат = СоответствиеСчетовХозрасчетныйМСФО.СчетХозрасчетный
		|				И (СоответствиеСчетовХозрасчетныйМСФО.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийБухгалтерии.Дебет))
		|		ПО (СпособыОтраженияРасходовПоАмортизацииСпособы.НомерСтроки = 1)
		|			И ДанныеШапкиОснования.СпособыОтраженияРасходовПоАмортизации = СпособыОтраженияРасходовПоАмортизацииСпособы.Ссылка";
		
	СтруктураЗапросов.Вставить("Поступление_Принятие_ТекстЗапросаДанныеДляЗаполненияТабличнойЧасти", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Справочник.ОсновныеСредства) КАК ОсновноеСредство,
		|	ДанныеДляЗаполненияТабличнойЧасти.ДатаПринятияКУчету,
		|	ДанныеДляЗаполненияТабличнойЧасти.Местонахождение,
		|	ДанныеДляЗаполненияТабличнойЧасти.МОЛ,
		|	ДанныеДляЗаполненияТабличнойЧасти.СрокПолезногоИспользования,
		|	ДанныеДляЗаполненияТабличнойЧасти.КоэффициентУскорения,
		|	ХозрасчетныйДвиженияССубконто.Сумма КАК Стоимость,
		|	0 КАК Амортизация,
		|	ДанныеДляЗаполненияТабличнойЧасти.СчетУчета,
		|	ДанныеДляЗаполненияТабличнойЧасти.СчетНачисленияАмортизации,
		|	ДанныеДляЗаполненияТабличнойЧасти.СчетЗатрат,
		|	ДанныеДляЗаполненияТабличнойЧасти.Субконто1,
		|	ДанныеДляЗаполненияТабличнойЧасти.Субконто2,
		|	ДанныеДляЗаполненияТабличнойЧасти.Субконто3,
		|	ЗНАЧЕНИЕ(ПланСчетов.Международный.Служебный) КАК СчетСниженияСтоимости,
		|	ДанныеДляЗаполненияТабличнойЧасти.НачислятьАмортизацию,
		|	Счета_Коррсчет.СчетМеждународный КАК КоррСчет,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК КоррСубконто1,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК КоррСубконто2,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК КоррСубконто3,
		|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Справочник.ОсновныеСредства).ГруппаОС КАК ГруппаОС,
		|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Справочник.ОсновныеСредства).ЦельИспользования КАК ЦельИспользования
		|ПОМЕСТИТЬ ТабличнаяЧастьОсновныеСредства
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&МоментОснования,
		|			&МоментОснования,
		|			Регистратор = &ДокументОснование
		|				И СчетДт В (&СчетаУчетаБУ),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеСчетовХозрасчетныйМСФО КАК Счета_Коррсчет
		|		ПО ХозрасчетныйДвиженияССубконто.СчетКт = Счета_Коррсчет.СчетХозрасчетный
		|			И (Счета_Коррсчет.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийБухгалтерии.Кредит)),
		|	ДанныеДляЗаполненияТабличнойЧасти КАК ДанныеДляЗаполненияТабличнойЧасти";
		
	СтруктураЗапросов.Вставить("Поступление_Принятие_ТекстЗапросаТабличнаяЧастьОсновныеСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"УНИЧТОЖИТЬ ДанныеШапкиОснования";
		
	СтруктураЗапросов.Вставить("Поступление_Принятие_УдалениеВременныхТаблиц", ТекстЗапроса+";");
	
КонецПроцедуры

Процедура ДобавитьЗапросыДляПоступление_Модернизация(СтруктураЗапросов) 
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	МодернизацияОСОС.ОсновноеСредство
		|ПОМЕСТИТЬ ОсновныеСредстваОснования
		|ИЗ
		|	Документ.МодернизацияОС.ОС КАК МодернизацияОСОС
		|ГДЕ
		|	МодернизацияОСОС.Ссылка = &ДокументОснование";
		
	СтруктураЗапросов.Вставить("Поступление_Модернизация_ТекстЗапросаОсновныеСредстваОснования", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПериодыРегистраторыСреза.ОсновноеСредство,
		|	ОсновныеСредстваМеждународныйУчет.Организация,
		|	ОсновныеСредстваМеждународныйУчет.ДатаПринятияКУчету,
		|	ОсновныеСредстваМеждународныйУчет.МестонахождениеОбъекта,
		|	ОсновныеСредстваМеждународныйУчет.МОЛ,
		|	ОсновныеСредстваМеждународныйУчет.Состояние,
		|	ОсновныеСредстваМеждународныйУчет.СчетУчета,
		|	ОсновныеСредстваМеждународныйУчет.СрокПолезногоИспользования,
		|	ОсновныеСредстваМеждународныйУчет.НачислятьАмортизацию,
		|	ОсновныеСредстваМеждународныйУчет.МетодНачисленияАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.СчетНачисленияАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.ПервоначальнаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.СправедливаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.ЛиквидационнаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.СчетЗатрат,
		|	ОсновныеСредстваМеждународныйУчет.Субконто1,
		|	ОсновныеСредстваМеждународныйУчет.Субконто2,
		|	ОсновныеСредстваМеждународныйУчет.Субконто3,
		|	ОсновныеСредстваМеждународныйУчет.ПредполагаемыйОбъемПродукции,
		|	ОсновныеСредстваМеждународныйУчет.СуммаНачисленнойАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.УбытокОтОбесценения,
		|	ОсновныеСредстваМеждународныйУчет.КоэффициентУскорения,
		|	ОсновныеСредстваМеждународныйУчет.СчетСниженияСтоимости
		|ПОМЕСТИТЬ ПараметрыОсновныхСредствОснования
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПериодыСреза.ОсновноеСредство КАК ОсновноеСредство,
		|		ПериодыСреза.Период КАК Период,
		|		МАКСИМУМ(ОсновныеСредстваМеждународныйУчет.Регистратор) КАК Регистратор
		|	ИЗ
		|		(ВЫБРАТЬ
		|			ОсновныеСредстваОснования.ОсновноеСредство КАК ОсновноеСредство,
		|			МАКСИМУМ(ОсновныеСредстваМеждународныйУчет.Период) КАК Период
		|		ИЗ
		|			ОсновныеСредстваОснования КАК ОсновныеСредстваОснования
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|				ПО ОсновныеСредстваОснования.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|					И (ОсновныеСредстваМеждународныйУчет.Период <= &Период)
		|					И (ОсновныеСредстваМеждународныйУчет.Активность)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ОсновныеСредстваОснования.ОсновноеСредство) КАК ПериодыСреза
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|			ПО ПериодыСреза.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|				И ПериодыСреза.Период = ОсновныеСредстваМеждународныйУчет.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПериодыСреза.ОсновноеСредство,
		|		ПериодыСреза.Период) КАК ПериодыРегистраторыСреза
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|		ПО ПериодыРегистраторыСреза.Период = ОсновныеСредстваМеждународныйУчет.Период
		|			И ПериодыРегистраторыСреза.Регистратор = ОсновныеСредстваМеждународныйУчет.Регистратор
		|			И ПериодыРегистраторыСреза.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("Поступление_Модернизация_ТекстЗапросаПараметрыОсновныхСредствОснования", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_ПоступлениеОСМОсновныеСредства.ОсновноеСредство,
		|	абс_ПоступлениеОСМОсновныеСредства.ДатаПринятияКУчету,
		|	абс_ПоступлениеОСМОсновныеСредства.Местонахождение,
		|	абс_ПоступлениеОСМОсновныеСредства.МОЛ,
		|	абс_ПоступлениеОСМОсновныеСредства.КоэффициентУскорения,
		|	ПараметрыОсновныхСредствОснования.СчетУчета,
		|	ПараметрыОсновныхСредствОснования.СчетНачисленияАмортизации,
		|	ЕСТЬNULL(ПараметрыОсновныхСредствОснования.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользования,
		|	абс_ПоступлениеОСМОсновныеСредства.НачислятьАмортизацию,
		|	абс_ПоступлениеОСМОсновныеСредства.СчетЗатрат КАК СчетЗатрат,
		|	абс_ПоступлениеОСМОсновныеСредства.Субконто1 КАК Субконто1,
		|	абс_ПоступлениеОСМОсновныеСредства.Субконто2 КАК Субконто2,
		|	абс_ПоступлениеОСМОсновныеСредства.Субконто3 КАК Субконто3
		|ПОМЕСТИТЬ ДанныеДляЗаполненияТабличнойЧасти
		|ИЗ
		|	Документ.абс_ПоступлениеОСМ.ОсновныеСредства КАК абс_ПоступлениеОСМОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредствОснования КАК ПараметрыОсновныхСредствОснования
		|		ПО абс_ПоступлениеОСМОсновныеСредства.ОсновноеСредство = ПараметрыОсновныхСредствОснования.ОсновноеСредство
		|ГДЕ
		|	абс_ПоступлениеОСМОсновныеСредства.Ссылка = &Ссылка";
		
	СтруктураЗапросов.Вставить("Поступление_Модернизация_ТекстЗапросаДанныеДляЗаполненияТабличнойЧасти", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Справочник.ОсновныеСредства) КАК ОсновноеСредство,
		|	ДанныеДляЗаполненияТабличнойЧасти.ДатаПринятияКУчету,
		|	ДанныеДляЗаполненияТабличнойЧасти.Местонахождение,
		|	ДанныеДляЗаполненияТабличнойЧасти.МОЛ,
		|	ДанныеДляЗаполненияТабличнойЧасти.СрокПолезногоИспользования,
		|	ДанныеДляЗаполненияТабличнойЧасти.КоэффициентУскорения,
		|	ХозрасчетныйДвиженияССубконто.Сумма КАК Стоимость,
		|	0 КАК Амортизация,
		|	ЕСТЬNULL(ДанныеДляЗаполненияТабличнойЧасти.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Международный.ОСвОрганизации)) КАК СчетУчета,
		|	ДанныеДляЗаполненияТабличнойЧасти.СчетНачисленияАмортизации,
		|	ДанныеДляЗаполненияТабличнойЧасти.СчетЗатрат,
		|	ДанныеДляЗаполненияТабличнойЧасти.Субконто1,
		|	ДанныеДляЗаполненияТабличнойЧасти.Субконто2,
		|	ДанныеДляЗаполненияТабличнойЧасти.Субконто3,
		|	ЗНАЧЕНИЕ(ПланСчетов.Международный.Служебный) КАК СчетСниженияСтоимости,
		|	ДанныеДляЗаполненияТабличнойЧасти.НачислятьАмортизацию,
		|	Счета_Коррсчет.СчетМеждународный КАК КоррСчет,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК КоррСубконто1,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК КоррСубконто2,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК КоррСубконто3,
		|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Справочник.ОсновныеСредства).ГруппаОС КАК ГруппаОС,
		|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Справочник.ОсновныеСредства).ЦельИспользования КАК ЦельИспользования
		|ПОМЕСТИТЬ ТабличнаяЧастьОсновныеСредства
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&МоментОснования,
		|			&МоментОснования,
		|			Регистратор = &ДокументОснование
		|				И СчетДт В (&СчетаУчетаБУ),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеСчетовХозрасчетныйМСФО КАК Счета_Коррсчет
		|		ПО ХозрасчетныйДвиженияССубконто.СчетКт = Счета_Коррсчет.СчетХозрасчетный
		|			И (Счета_Коррсчет.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийБухгалтерии.Кредит))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДляЗаполненияТабличнойЧасти КАК ДанныеДляЗаполненияТабличнойЧасти
		|		ПО ХозрасчетныйДвиженияССубконто.СубконтоДт1 = ДанныеДляЗаполненияТабличнойЧасти.ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("Поступление_Модернизация_ТекстЗапросаТабличнаяЧастьОсновныеСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"УНИЧТОЖИТЬ ОсновныеСредстваОснования";
		
	СтруктураЗапросов.Вставить("Поступление_Модернизация_УдалениеВременныхТаблиц", ТекстЗапроса+";");
	
КонецПроцедуры

Процедура ДобавитьЗапросыДляПоступление_Остатки(СтруктураЗапросов);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВводНачальныхОстатковОСОС.ОсновноеСредство,
		|	ВводНачальныхОстатковОСОС.ДатаПринятияКУчетуРегл КАК ДатаПринятияКУчету,
		|	ВводНачальныхОстатковОСОС.Ссылка.ПодразделениеОрганизации КАК Местонахождение,
		|	ВводНачальныхОстатковОСОС.МОЛРегл КАК МОЛ,
		|	ВводНачальныхОстатковОСОС.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования,
		|	ВводНачальныхОстатковОСОС.КоэффициентУскоренияБУ КАК КоэффициентУскорения,
		|	ВводНачальныхОстатковОСОС.ТекущаяСтоимостьБУ КАК Стоимость,
		|	ВводНачальныхОстатковОСОС.НакопленнаяАмортизацияБУ КАК Амортизация,
		|	ЕСТЬNULL(СчетаУчета.СчетМеждународный, ЗНАЧЕНИЕ(ПланСчетов.Международный.ОСвОрганизации)) КАК СчетУчета,
		|	ЕСТЬNULL(СчетаАмортизации.СчетМеждународный, ЗНАЧЕНИЕ(ПланСчетов.Международный.АмортизацияОС_01)) КАК СчетНачисленияАмортизации,
		|	СчетаЗатрат.СчетМеждународный КАК СчетЗатрат,
		|	ВЫБОР
		|		КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения)
		|					ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ПодразделениеОрганизации
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат)
		|							ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СтатьяЗатрат
		|						ИНАЧЕ ВЫБОР
		|								КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.НоменклатурныеГруппы)
		|									ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.НоменклатурнаяГруппа
		|								ИНАЧЕ ВЫБОР
		|										КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОбъектыСтроительства)
		|											ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ОбъектСтроительства
		|										ИНАЧЕ ВЫБОР
		|												КОГДА ВидыСубконтоСчетов.ВидСубконто1 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СпособыСтроительства)
		|													ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СпособСтроительства
		|												ИНАЧЕ СпособыОтраженияРасходовПоАмортизацииСпособы.Субконто1
		|											КОНЕЦ
		|									КОНЕЦ
		|							КОНЕЦ
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Субконто1,
		|	ВЫБОР
		|		КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения)
		|					ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ПодразделениеОрганизации
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат)
		|							ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СтатьяЗатрат
		|						ИНАЧЕ ВЫБОР
		|								КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.НоменклатурныеГруппы)
		|									ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.НоменклатурнаяГруппа
		|								ИНАЧЕ ВЫБОР
		|										КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОбъектыСтроительства)
		|											ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ОбъектСтроительства
		|										ИНАЧЕ ВЫБОР
		|												КОГДА ВидыСубконтоСчетов.ВидСубконто2 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СпособыСтроительства)
		|													ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СпособСтроительства
		|												ИНАЧЕ СпособыОтраженияРасходовПоАмортизацииСпособы.Субконто2
		|											КОНЕЦ
		|									КОНЕЦ
		|							КОНЕЦ
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Субконто2,
		|	ВЫБОР
		|		КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения)
		|					ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ПодразделениеОрганизации
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат)
		|							ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СтатьяЗатрат
		|						ИНАЧЕ ВЫБОР
		|								КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.НоменклатурныеГруппы)
		|									ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.НоменклатурнаяГруппа
		|								ИНАЧЕ ВЫБОР
		|										КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОбъектыСтроительства)
		|											ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.ОбъектСтроительства
		|										ИНАЧЕ ВЫБОР
		|												КОГДА ВидыСубконтоСчетов.ВидСубконто3 = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СпособыСтроительства)
		|													ТОГДА СпособыОтраженияРасходовПоАмортизацииСпособы.СпособСтроительства
		|												ИНАЧЕ СпособыОтраженияРасходовПоАмортизацииСпособы.Субконто3
		|											КОНЕЦ
		|									КОНЕЦ
		|							КОНЕЦ
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Субконто3,
		|	ЗНАЧЕНИЕ(ПланСчетов.Международный.Служебный) КАК СчетСниженияСтоимости,
		|	ВводНачальныхОстатковОСОС.НачислятьАмортизациюБУ КАК НачислятьАмортизацию,
		|	КоррСчета.СчетМеждународный КАК КоррСчет,
		|	ВводНачальныхОстатковОСОС.СубконтоБУ КАК КоррСубконто1,
		|	НЕОПРЕДЕЛЕНО КАК КоррСубконто2,
		|	НЕОПРЕДЕЛЕНО КАК КоррСубконто3,
		|	ВводНачальныхОстатковОСОС.ОсновноеСредство.ГруппаОС КАК ГруппаОС,
		|	ВводНачальныхОстатковОСОС.ОсновноеСредство.ЦельИспользования КАК ЦельИспользования
		|ПОМЕСТИТЬ ТабличнаяЧастьОсновныеСредства
		|ИЗ
		|	Документ.ВводНачальныхОстатковОС.ОС КАК ВводНачальныхОстатковОСОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеСчетовХозрасчетныйМСФО КАК СчетаУчета
		|		ПО ВводНачальныхОстатковОСОС.СчетУчетаБУ = СчетаУчета.СчетХозрасчетный
		|			И (СчетаУчета.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийБухгалтерии.Дебет))
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеСчетовХозрасчетныйМСФО КАК СчетаАмортизации
		|		ПО (СчетаАмортизации.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийБухгалтерии.Кредит))
		|			И ВводНачальныхОстатковОСОС.СчетАмортизацииБУ = СчетаАмортизации.СчетХозрасчетный
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеСчетовХозрасчетныйМСФО КАК КоррСчета
		|		ПО (КоррСчета.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийБухгалтерии.Кредит))
		|			И ВводНачальныхОстатковОСОС.СчетБУ = КоррСчета.СчетХозрасчетный
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияРасходовПоАмортизации.Способы КАК СпособыОтраженияРасходовПоАмортизацииСпособы
		|			ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеСчетовХозрасчетныйМСФО КАК СчетаЗатрат
		|				ЛЕВОЕ СОЕДИНЕНИЕ ВидыСубконтоСчетов КАК ВидыСубконтоСчетов
		|				ПО СчетаЗатрат.СчетМеждународный = ВидыСубконтоСчетов.Счет
		|			ПО СпособыОтраженияРасходовПоАмортизацииСпособы.СчетЗатрат = СчетаЗатрат.СчетХозрасчетный
		|				И (СчетаЗатрат.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийБухгалтерии.Дебет))
		|		ПО ВводНачальныхОстатковОСОС.СпособОтраженияРасходовПоАмортизацииБУ = СпособыОтраженияРасходовПоАмортизацииСпособы.Ссылка
		|			И (СпособыОтраженияРасходовПоАмортизацииСпособы.НомерСтроки = 1)
		|ГДЕ
		|	ВводНачальныхОстатковОСОС.Ссылка = &ДокументОснование";
		
	СтруктураЗапросов.Вставить("Поступление_Остатки_ТекстЗапросаТабличнаяЧастьОсновныеСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"УНИЧТОЖИТЬ ТабличнаяЧастьОсновныеСредства";
		
	СтруктураЗапросов.Вставить("Поступление_Остатки_УдалениеВременныхТаблиц", ТекстЗапроса+";");
	
КонецПроцедуры

Процедура ДобавитьОбщиеЗапросы1(СтруктураЗапросов)

	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Международный.Ссылка КАК Счет,
		|	ЕСТЬNULL(ВидыСубконто1.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто1,
		|	ЕСТЬNULL(ВидыСубконто2.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто2,
		|	ЕСТЬNULL(ВидыСубконто3.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто3
		|ПОМЕСТИТЬ ВидыСубконтоСчетов
		|ИЗ
		|	ПланСчетов.Международный КАК Международный
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто1
		|		ПО Международный.Ссылка = ВидыСубконто1.Ссылка
		|			И (ВидыСубконто1.НомерСтроки = 1)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто2
		|		ПО Международный.Ссылка = ВидыСубконто2.Ссылка
		|			И (ВидыСубконто2.НомерСтроки = 2)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто3
		|		ПО Международный.Ссылка = ВидыСубконто3.Ссылка
		|			И (ВидыСубконто3.НомерСтроки = 3)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет";
		
	СтруктураЗапросов.Вставить("абс_МеждународныйУчет_ТекстЗапросаВидыСубконтоСчетов", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СоответствиеСчетовБУиМСФОСрезПоследних.СчетХозрасчетный КАК СчетХозрасчетный,
		|	СоответствиеСчетовБУиМСФОСрезПоследних.ВидДвижения КАК ВидДвижения,
		|	СоответствиеСчетовБУиМСФОСрезПоследних.СчетМеждународный
		|ПОМЕСТИТЬ СоответствиеСчетовХозрасчетныйМСФО
		|ИЗ
		|	РегистрСведений.СоответствиеСчетовБУиМСФО.СрезПоследних(
		|			&Период,
		|			СубконтоХозр1 В (&ПустыеСубконто)
		|				И СубконтоХозр2 В (&ПустыеСубконто)
		|				И СубконтоХозр3 В (&ПустыеСубконто)) КАК СоответствиеСчетовБУиМСФОСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СчетХозрасчетный,
		|	ВидДвижения";
		
	СтруктураЗапросов.Вставить("абс_МеждународныйУчет_ТекстЗапросаСоответствиеСчетовХозрасчетный", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТаблицаСроков.ГруппаОС КАК Перечисление.ГруппыОС) КАК ГруппаОС,
		|	ВЫРАЗИТЬ(ТаблицаСроков.ЦельИспользования КАК Справочник.ЦелиИспользования) КАК ЦельИспользования,
		|	ВЫРАЗИТЬ(ТаблицаСроков.СрокИспользования КАК ЧИСЛО(4, 0)) КАК СрокИспользования
		|ПОМЕСТИТЬ СрокиПолезногоИспользования
		|ИЗ
		|	&ТаблицаСроков КАК ТаблицаСроков
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ГруппаОС,
		|	ЦельИспользования";
		
	СтруктураЗапросов.Вставить("абс_МеждународныйУчет_ТекстЗапросаСрокиПолезногоИспользования", ТекстЗапроса+";");
	
	
КонецПроцедуры

Процедура ДобавитьОбщиеЗапросы2(СтруктураЗапросов)

	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_СтоимостьОСМеждународныйОстатки.ОсновноеСредство КАК ОсновноеСредство,
		|	абс_СтоимостьОСМеждународныйОстатки.СтоимостьОстаток КАК Стоимость,
		|	абс_СтоимостьОСМеждународныйОстатки.АмортизацияОстаток КАК Амортизация
		|ПОМЕСТИТЬ СтоимостьОсновныхСредствДоДокумента
		|ИЗ
		|	РегистрНакопления.абс_СтоимостьОСМеждународный.Остатки(
		|			&МоментДокумента,
		|			ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ТабличнаяЧастьОсновныеСредства.ОсновноеСредство
		|					ИЗ
		|						ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства)
		|				И Организация = &Организация) КАК абс_СтоимостьОСМеждународныйОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("Поступление_ТекстЗапросаСтоимостьОсновныхСредствДоДокумента", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПериодыРегистраторыСреза.Период,
		|	ПериодыРегистраторыСреза.Регистратор,
		|	ПериодыРегистраторыСреза.ОсновноеСредство КАК ОсновноеСредство,
		|	ОсновныеСредстваМеждународныйУчет.Организация,
		|	ОсновныеСредстваМеждународныйУчет.ДатаПринятияКУчету,
		|	ОсновныеСредстваМеждународныйУчет.МестонахождениеОбъекта,
		|	ОсновныеСредстваМеждународныйУчет.МОЛ,
		|	ОсновныеСредстваМеждународныйУчет.Состояние,
		|	ОсновныеСредстваМеждународныйУчет.СчетУчета,
		|	ОсновныеСредстваМеждународныйУчет.СрокПолезногоИспользования,
		|	ОсновныеСредстваМеждународныйУчет.НачислятьАмортизацию,
		|	ОсновныеСредстваМеждународныйУчет.МетодНачисленияАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.СчетНачисленияАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.ПервоначальнаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.СправедливаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.ЛиквидационнаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.СчетЗатрат,
		|	ОсновныеСредстваМеждународныйУчет.Субконто1,
		|	ОсновныеСредстваМеждународныйУчет.Субконто2,
		|	ОсновныеСредстваМеждународныйУчет.Субконто3,
		|	ОсновныеСредстваМеждународныйУчет.ПредполагаемыйОбъемПродукции,
		|	ОсновныеСредстваМеждународныйУчет.СуммаНачисленнойАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.УбытокОтОбесценения,
		|	ОсновныеСредстваМеждународныйУчет.КоэффициентУскорения,
		|	ОсновныеСредстваМеждународныйУчет.СчетСниженияСтоимости
		|ПОМЕСТИТЬ ПараметрыОсновныхСредств
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПериодыСреза.ОсновноеСредство КАК ОсновноеСредство,
		|		ПериодыСреза.Период КАК Период,
		|		МАКСИМУМ(ОсновныеСредстваМеждународныйУчет.Регистратор) КАК Регистратор
		|	ИЗ
		|		(ВЫБРАТЬ
		|			ТабличнаяЧастьОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
		|			МАКСИМУМ(ОсновныеСредстваМеждународныйУчет.Период) КАК Период
		|		ИЗ
		|			ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|				ПО ТабличнаяЧастьОсновныеСредства.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|					И (ОсновныеСредстваМеждународныйУчет.Период <= &Период)
		|					И (ОсновныеСредстваМеждународныйУчет.Активность)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ТабличнаяЧастьОсновныеСредства.ОсновноеСредство) КАК ПериодыСреза
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|			ПО ПериодыСреза.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|				И ПериодыСреза.Период = ОсновныеСредстваМеждународныйУчет.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПериодыСреза.ОсновноеСредство,
		|		ПериодыСреза.Период) КАК ПериодыРегистраторыСреза
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|		ПО ПериодыРегистраторыСреза.Период = ОсновныеСредстваМеждународныйУчет.Период
		|			И ПериодыРегистраторыСреза.Регистратор = ОсновныеСредстваМеждународныйУчет.Регистратор
		|			И ПериодыРегистраторыСреза.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("абс_ВнеоборотныеАктивы_ТекстЗапросаОсновныеСредстваМСФОПоТаблицеОС", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.ОсновноеСредство КАК ОсновноеСредство,
		|	ВложенныйЗапрос.Стоимость + ЕСТЬNULL(СтоимостьОсновныхСредствДоДокумента.Стоимость, 0) КАК Сумма,
		|	ВложенныйЗапрос.Амортизация + ЕСТЬNULL(СтоимостьОсновныхСредствДоДокумента.Амортизация, 0) КАК Амортизация
		|ПОМЕСТИТЬ НоваяСтоимостьОсновныхСредств
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТабличнаяЧастьОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
		|		СУММА(ТабличнаяЧастьОсновныеСредства.Стоимость) КАК Стоимость,
		|		СУММА(ТабличнаяЧастьОсновныеСредства.Амортизация) КАК Амортизация
		|	ИЗ
		|		ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ТабличнаяЧастьОсновныеСредства.ОсновноеСредство) КАК ВложенныйЗапрос
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОсновныхСредствДоДокумента КАК СтоимостьОсновныхСредствДоДокумента
		|		ПО ВложенныйЗапрос.ОсновноеСредство = СтоимостьОсновныхСредствДоДокумента.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("Поступление_ТекстЗапросаНоваяСтоимостьОсновныхСредств", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТабличнаяЧастьОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.ДатаПринятияКУчету) КАК ДатаПринятияКУчету,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.Местонахождение) КАК Местонахождение,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.МОЛ) КАК МОЛ,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.СрокПолезногоИспользования) КАК СрокПолезногоИспользования,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.КоэффициентУскорения) КАК КоэффициентУскорения,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.СчетУчета) КАК СчетУчета,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.СчетНачисленияАмортизации) КАК СчетНачисленияАмортизации,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.СчетЗатрат) КАК СчетЗатрат,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.Субконто1) КАК Субконто1,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.Субконто2) КАК Субконто2,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.Субконто3) КАК Субконто3,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.СчетСниженияСтоимости) КАК СчетСниженияСтоимости,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.НачислятьАмортизацию) КАК НачислятьАмортизацию,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.ГруппаОС) КАК ГруппаОС,
		|	МАКСИМУМ(ТабличнаяЧастьОсновныеСредства.ЦельИспользования) КАК ЦельИспользования
		|ПОМЕСТИТЬ ИзменениеПараметровОсновныхСредств
		|ИЗ
		|	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|
		|СГРУППИРОВАТЬ ПО
		|	ТабличнаяЧастьОсновныеСредства.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("Поступление_ТекстЗапросаИзменениеПараметровОсновныхСредств", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Организация,
		|	ТабличнаяЧастьОсновныеСредства.ОсновноеСредство,
		|	ТабличнаяЧастьОсновныеСредства.Стоимость,
		|	ТабличнаяЧастьОсновныеСредства.Амортизация
		|ИЗ
		|	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|ГДЕ
		|	(ТабличнаяЧастьОсновныеСредства.Стоимость <> 0
		|			ИЛИ ТабличнаяЧастьОсновныеСредства.Амортизация <> 0)";
		
	СтруктураЗапросов.Вставить("Поступление_ТекстЗапросаТаблица_абс_СтоимостьОСМеждународный", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	ИзменениеПараметровОсновныхСредств.ОсновноеСредство,
		|	&Организация,
		|	ИзменениеПараметровОсновныхСредств.ДатаПринятияКУчету,
		|	ИзменениеПараметровОсновныхСредств.Местонахождение,
		|	ИзменениеПараметровОсновныхСредств.МОЛ,
		|	ЕСТЬNULL(ПараметрыОсновныхСредств.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВведеноВЭксплуатацию)) КАК Состояние,
		|	ИзменениеПараметровОсновныхСредств.СчетУчета,
		|	ЕСТЬNULL(СрокиПолезногоИспользования.СрокИспользования, ИзменениеПараметровОсновныхСредств.СрокПолезногоИспользования) КАК СрокПолезногоИспользования,
		|	ИзменениеПараметровОсновныхСредств.НачислятьАмортизацию,
		|	ЕСТЬNULL(ПараметрыОсновныхСредств.МетодНачисленияАмортизации, ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизацииОСМеждународный.Линейный)) КАК МетодНачисленияАмортизации,
		|	ИзменениеПараметровОсновныхСредств.СчетНачисленияАмортизации,
		|	ЕСТЬNULL(ПараметрыОсновныхСредств.ПервоначальнаяСтоимость, ЕСТЬNULL(НоваяСтоимостьОсновныхСредств.Сумма, 0)) КАК ПервоначальнаяСтоимость,
		|	ЕСТЬNULL(НоваяСтоимостьОсновныхСредств.Сумма, 0) КАК СправедливаяСтоимость,
		|	0 КАК ЛиквидационнаяСтоимость,
		|	ИзменениеПараметровОсновныхСредств.СчетЗатрат,
		|	ИзменениеПараметровОсновныхСредств.Субконто1,
		|	ИзменениеПараметровОсновныхСредств.Субконто2,
		|	ИзменениеПараметровОсновныхСредств.Субконто3,
		|	ЕСТЬNULL(ПараметрыОсновныхСредств.ПредполагаемыйОбъемПродукции, 0) КАК ПредполагаемыйОбъемПродукции,
		|	ЕСТЬNULL(НоваяСтоимостьОсновныхСредств.Амортизация, 0) КАК СуммаНачисленнойАмортизации,
		|	ЕСТЬNULL(ПараметрыОсновныхСредств.УбытокОтОбесценения, 0) КАК УбытокОтОбесценения,
		|	ИзменениеПараметровОсновныхСредств.КоэффициентУскорения,
		|	ИзменениеПараметровОсновныхСредств.СчетСниженияСтоимости
		|ИЗ
		|	ИзменениеПараметровОсновныхСредств КАК ИзменениеПараметровОсновныхСредств
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
		|		ПО ИзменениеПараметровОсновныхСредств.ОсновноеСредство = ПараметрыОсновныхСредств.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ НоваяСтоимостьОсновныхСредств КАК НоваяСтоимостьОсновныхСредств
		|		ПО ИзменениеПараметровОсновныхСредств.ОсновноеСредство = НоваяСтоимостьОсновныхСредств.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ СрокиПолезногоИспользования КАК СрокиПолезногоИспользования
		|		ПО ИзменениеПараметровОсновныхСредств.ГруппаОС = СрокиПолезногоИспользования.ГруппаОС
		|			И ИзменениеПараметровОсновныхСредств.ЦельИспользования = СрокиПолезногоИспользования.ЦельИспользования";
		
	СтруктураЗапросов.Вставить("Поступление_ТекстЗапросаТаблица_ОсновныеСредстваМеждународныйУчет", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ТабличнаяЧастьОсновныеСредства.СчетУчета КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства) КАК ВидСубконтоДт1,
		|	ТабличнаяЧастьОсновныеСредства.ОсновноеСредство КАК СубконтоДт1,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка) КАК ВидСубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка) КАК ВидСубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ТабличнаяЧастьОсновныеСредства.КоррСчет КАК СчетКт,
		|	ВидыСубконтоСчетов.ВидСубконто1 КАК ВидСубконтоКт1,
		|	ТабличнаяЧастьОсновныеСредства.КоррСубконто1 КАК СубконтоКт1,
		|	ВидыСубконтоСчетов.ВидСубконто2 КАК ВидСубконтоКт2,
		|	ТабличнаяЧастьОсновныеСредства.КоррСубконто2 КАК СубконтоКт2,
		|	ВидыСубконтоСчетов.ВидСубконто3 КАК ВидСубконтоКт3,
		|	ТабличнаяЧастьОсновныеСредства.КоррСубконто3 КАК СубконтоКт3,
		|	ТабличнаяЧастьОсновныеСредства.Стоимость КАК Сумма,
		|	&ДокументОснование КАК ПервичныйДокумент
		|ИЗ
		|	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыСубконтоСчетов КАК ВидыСубконтоСчетов
		|		ПО ТабличнаяЧастьОсновныеСредства.КоррСчет = ВидыСубконтоСчетов.Счет
		|ГДЕ
		|	ТабличнаяЧастьОсновныеСредства.Стоимость <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ТабличнаяЧастьОсновныеСредства.КоррСчет,
		|	ВидыСубконтоСчетов.ВидСубконто1,
		|	ТабличнаяЧастьОсновныеСредства.КоррСубконто1,
		|	ВидыСубконтоСчетов.ВидСубконто2,
		|	ТабличнаяЧастьОсновныеСредства.КоррСубконто2,
		|	ВидыСубконтоСчетов.ВидСубконто3,
		|	ТабличнаяЧастьОсновныеСредства.КоррСубконто3,
		|	ТабличнаяЧастьОсновныеСредства.СчетНачисленияАмортизации,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства),
		|	ТабличнаяЧастьОсновныеСредства.ОсновноеСредство,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка),
		|	НЕОПРЕДЕЛЕНО,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка),
		|	НЕОПРЕДЕЛЕНО,
		|	ТабличнаяЧастьОсновныеСредства.Амортизация,
		|	&ДокументОснование
		|ИЗ
		|	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыСубконтоСчетов КАК ВидыСубконтоСчетов
		|		ПО ТабличнаяЧастьОсновныеСредства.КоррСчет = ВидыСубконтоСчетов.Счет
		|ГДЕ
		|	ТабличнаяЧастьОсновныеСредства.Амортизация <> 0";
		
	СтруктураЗапросов.Вставить("Поступление_ТекстЗапросаТаблица_Международный", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"УНИЧТОЖИТЬ СтоимостьОсновныхСредствДоДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПараметрыОсновныхСредств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НоваяСтоимостьОсновныхСредств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ИзменениеПараметровОсновныхСредств";
		
	СтруктураЗапросов.Вставить("Поступление_УдалениеВременныхТаблиц", ТекстЗапроса+";");
	
КонецПроцедуры



