
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ttk_ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр kwr_СоответствиеАналитикБУиУУ
	Движения.kwr_СоответствиеАналитикБУиУУ.Записывать = Истина;
	Движения.kwr_СоответствиеАналитикБУиУУ.Очистить();
	Для Каждого ТекСтрокаСоответствиеАналитикБУиУУ Из СоответствиеАналитикБУиУУ Цикл
		
		Движение 							= Движения.kwr_СоответствиеАналитикБУиУУ.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ТекСтрокаСоответствиеАналитикБУиУУ); 
		Движение.Период 					= Дата;
		
	КонецЦикла;
	
	// регистр kwr_ЗаменяемыеКлючиАналитик
	Движения.kwr_ЗаменяемыеКлючиАналитик.Записывать = Истина;
	Движения.kwr_ЗаменяемыеКлючиАналитик.Очистить();
	Для Каждого ТекСтрокаЗаменяемыеКлючиАналитик Из ЗаменяемыеКлючиАналитик Цикл
		
		Движение 							= Движения.kwr_ЗаменяемыеКлючиАналитик.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ТекСтрокаЗаменяемыеКлючиАналитик); 
		Движение.Период 					= Дата;
		
	КонецЦикла;
	
	Движения.ttk_СоответствиеУслугПоКатегориям.Записывать = Истина;
	Движения.kwr_ЗаменяемыеКлючиАналитик.Очистить();
	Для Каждого ТекСтрокаСоответствиеУслугПоКатегориям Из СоответствиеУслугПоКатегориям Цикл
		
		Движение		= Движения.ttk_СоответствиеУслугПоКатегориям.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ТекСтрокаСоответствиеУслугПоКатегориям); 
		Движение.Период = Дата;
		
	КонецЦикла;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Для Каждого СтрокаСоответствиеАналитикБУиУУ Из СоответствиеАналитикБУиУУ Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаСоответствиеАналитикБУиУУ.СубконтоХозр1)
				И Не ЗначениеЗаполнено(СтрокаСоответствиеАналитикБУиУУ.СубконтоХозр2)
				И Не ЗначениеЗаполнено(СтрокаСоответствиеАналитикБУиУУ.СубконтоХозр3) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнена одна из колонок ""Субконто хозр."" в строке "
				+ СтрокаСоответствиеАналитикБУиУУ.НомерСтроки 
				+ " списка ""Соответствие аналитик БУ и УУ""";
			Сообщение.Сообщить();
			Отказ = Истина;
			
		КонецЕсли;		
		
	КонецЦикла;
	
	Для Каждого СтрокаСоответствиеУслугПоКатегориям Из СоответствиеУслугПоКатегориям Цикл
		Если Не ЗначениеЗаполнено(СтрокаСоответствиеУслугПоКатегориям.Номенклатура) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнена колонка ""Номенклатура"" в строке "
				+ СтрокаСоответствиеУслугПоКатегориям.НомерСтроки 
				+ " списка ""Соответствие услуг по категориям""";
			Сообщение.Сообщить();
			Отказ = Истина;
		ИначеЕсли НЕ СтрокаСоответствиеУслугПоКатегориям.Номенклатура.Предопределенный Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Неверно заполнена колонка ""Номенклатура"" в строке "
				+ СтрокаСоответствиеУслугПоКатегориям.НомерСтроки 
				+ " списка ""Соответствие услуг по категориям"". Указана не утвержденная услуга.";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаСоответствиеУслугПоКатегориям.ЦФО) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнена колонка ""ЦФО"" в строке "
				+ СтрокаСоответствиеУслугПоКатегориям.НомерСтроки 
				+ " списка ""Соответствие услуг по категориям""";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаСоответствиеУслугПоКатегориям.НоменклатурнаяГруппа) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнена колонка ""Номенклатурная Группа"" в строке "
				+ СтрокаСоответствиеУслугПоКатегориям.НомерСтроки 
				+ " списка ""Соответствие услуг по категориям""";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Для Каждого СтрокаСоответствиеАналитикБУиУУ Из СоответствиеАналитикБУиУУ Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаСоответствиеАналитикБУиУУ.СубконтоХозр1) Тогда
			СтрокаСоответствиеАналитикБУиУУ.СубконтоХозр1 = Неопределено;	
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаСоответствиеАналитикБУиУУ.СубконтоХозр2) Тогда
			СтрокаСоответствиеАналитикБУиУУ.СубконтоХозр2 = Неопределено;	
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаСоответствиеАналитикБУиУУ.СубконтоХозр3) Тогда
			СтрокаСоответствиеАналитикБУиУУ.СубконтоХозр3 = Неопределено;	
		КонецЕсли;		
		
	КонецЦикла;
	
КонецПроцедуры
