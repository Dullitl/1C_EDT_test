Перем СтруктураПодключения;

Процедура ЗаполнитьТаблицуВыгрузки() Экспорт
	
	ТаблицаВыгрузки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Документ КАК Реализация,
	|	СУММА(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.СуммаВзаиморасчетовПриход) КАК СуммаОплат,
	|	СУММА(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.СуммаВзаиморасчетовРасход) КАК СуммаСчетов
	|ПОМЕСТИТЬ ВТ_СуммаОплат
	|ИЗ
	|	РегистрНакопления.РасчетыПоРеализацииВУсловныхЕдиницахОрганизации.Обороты(, , Регистратор, Организация = &Организация) КАК РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты
	|ГДЕ
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|	И РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Документ.Дата <= &ДатаКонца
	|	И РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Документ.Дата >= &ДатаНачала
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Документ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Регистратор,
	|	СУММА(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.СуммаВзаиморасчетовРасход),
	|	СУММА(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.СуммаВзаиморасчетовРасход)
	|ИЗ
	|	РегистрНакопления.РасчетыПоРеализацииВУсловныхЕдиницахОрганизации.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, Организация = &Организация) КАК РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты
	|ГДЕ
	|	(НЕ РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Документ ССЫЛКА Документ.РеализацияТоваровУслуг)
	|	И РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СуммаОплат.Реализация КАК ДокументОтгрузки,
	|	СУММА(ЕСТЬNULL(ВТ_СуммаОплат.СуммаСчетов, 0)) КАК СуммаОтгрузки,
	|	СУММА(ЕСТЬNULL(ВТ_СуммаОплат.СуммаОплат, 0)) КАК СуммаОплаты,
	|	ВЫБОР
	|		КОГДА ВТ_СуммаОплат.Реализация ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ВТ_СуммаОплат.Реализация КАК Документ.РеализацияТоваровУслуг).ДоговорКонтрагента
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДОговорыКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ВТ_СуммаОплат.Реализация ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ВТ_СуммаОплат.Реализация КАК Документ.РеализацияТоваровУслуг).ДоговорКонтрагента.ВалютаВзаиморасчетов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|	КОНЕЦ КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.абс_ТипыОплатыК2.Оплата) КАК ТипОплаты
	|ПОМЕСТИТЬ ВТ_ИтогОплаты
	|ИЗ
	|	ВТ_СуммаОплат КАК ВТ_СуммаОплат
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СуммаОплат.Реализация,
	|	ВЫБОР
	|		КОГДА ВТ_СуммаОплат.Реализация ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ВТ_СуммаОплат.Реализация КАК Документ.РеализацияТоваровУслуг).ДоговорКонтрагента
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДОговорыКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТ_СуммаОплат.Реализация ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ВТ_СуммаОплат.Реализация КАК Документ.РеализацияТоваровУслуг).ДоговорКонтрагента.ВалютаВзаиморасчетов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Документ.Реализациятоваровуслуг.пустаяссылка) КАК ДокументОтгрузки,
	|	СУММА(0) КАК СуммаОтгрузки,
	|	СУММА(РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.СуммаВзаиморасчетовОстаток) КАК СуммаОплаты,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.абс_ТипыОплатыК2.Аванс) КАК ТипОплаты
	|ПОМЕСТИТЬ ВТ_СуммаАвансов
	|ИЗ
	|	РегистрНакопления.РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Остатки(
	|			&ДатаКонца,
	|			Организация = &Организация
	|				И (НЕ документ ССЫЛКА Документ.РеализацияТоваровУслуг)) КАК РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки
	|ГДЕ
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.СуммаВзаиморасчетовОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИтогОплаты.ДоговорКонтрагента,
	|	ВТ_ИтогОплаты.ДоговорКонтрагента.абс_ВстречныйДоговор
	|ПОМЕСТИТЬ ВТ_ВстречныеДоговоры
	|ИЗ
	|	ВТ_ИтогОплаты КАК ВТ_ИтогОплаты
	|ГДЕ
	|	(НЕ ВТ_ИтогОплаты.ДоговорКонтрагента.абс_ВстречныйДоговор = ЗНАЧЕНИЕ(Справочник.договорыКонтрагентов.пустаяссылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ИтогОплаты.ДоговорКонтрагента,
	|	ВТ_ИтогОплаты.ДоговорКонтрагента.абс_ВстречныйДоговор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_СуммаАвансов.ДоговорКонтрагента,
	|	ВТ_СуммаАвансов.ДоговорКонтрагента.абс_ВстречныйДоговор
	|ИЗ
	|	ВТ_СуммаАвансов КАК ВТ_СуммаАвансов
	|ГДЕ
	|	(НЕ ВТ_СуммаАвансов.ДоговорКонтрагента.абс_ВстречныйДоговор = ЗНАЧЕНИЕ(Справочник.договорыКонтрагентов.пустаяссылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СуммаАвансов.ДоговорКонтрагента,
	|	ВТ_СуммаАвансов.ДоговорКонтрагента.абс_ВстречныйДоговор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.СуммаВзаиморасчетовОстаток
	|ПОМЕСТИТЬ ВТ_ЗадолженностьПоВстречнымДоговорам
	|ИЗ
	|	РегистрНакопления.РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Остатки(
	|			&ДатаКонца,
	|			Организация = &Организация
	|				И ДоговорКонтрагента В
	|					(ВЫБРАТЬ
	|						ВТ_ВстречныеДоговоры.ДоговорКонтрагентаабс_ВстречныйДоговор КАК ДоговорКонтрагента
	|					ИЗ
	|						ВТ_ВстречныеДоговоры КАК ВТ_ВстречныеДоговоры)) КАК РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки
	|ГДЕ
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.СуммаВзаиморасчетовОстаток < 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Документ.Реализациятоваровуслуг.пустаяссылка) КАК ДокументОтгрузки,
	|	СУММА(0) КАК СуммаОтгрузки,
	|	СУММА(-ВТ_ЗадолженностьПоВстречнымДоговорам.СуммаВзаиморасчетовОстаток) КАК СуммаОплаты,
	|	ВТ_ВстречныеДоговоры.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_ЗадолженностьПоВстречнымДоговорам.ДоговорКонтрагентаВалютаВзаиморасчетов КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.абс_ТипыОплатыК2.ВстречнаяЗадолженность) КАК ТипОплаты
	|ПОМЕСТИТЬ ВТ_ИтогВстречнаяЗадолженность
	|ИЗ
	|	ВТ_ВстречныеДоговоры КАК ВТ_ВстречныеДоговоры
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ЗадолженностьПоВстречнымДоговорам КАК ВТ_ЗадолженностьПоВстречнымДоговорам
	|		ПО ВТ_ВстречныеДоговоры.ДоговорКонтрагентаабс_ВстречныйДоговор = ВТ_ЗадолженностьПоВстречнымДоговорам.ДоговорКонтрагента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ЗадолженностьПоВстречнымДоговорам.ДоговорКонтрагентаВалютаВзаиморасчетов,
	|	ВТ_ВстречныеДоговоры.ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИтогОплаты.ДокументОтгрузки,
	|	ВТ_ИтогОплаты.СуммаОтгрузки,
	|	ВТ_ИтогОплаты.СуммаОплаты,
	|	ВТ_ИтогОплаты.ДоговорКонтрагента,
	|	ВТ_ИтогОплаты.Валюта,
	|	ВТ_ИтогОплаты.ТипОплаты
	|ПОМЕСТИТЬ ВТ_ДанныеИтог
	|ИЗ
	|	ВТ_ИтогОплаты КАК ВТ_ИтогОплаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_СуммаАвансов.ДокументОтгрузки,
	|	ВТ_СуммаАвансов.СуммаОтгрузки,
	|	ВТ_СуммаАвансов.СуммаОплаты,
	|	ВТ_СуммаАвансов.ДоговорКонтрагента,
	|	ВТ_СуммаАвансов.Валюта,
	|	ВТ_СуммаАвансов.ТипОплаты
	|ИЗ
	|	ВТ_СуммаАвансов КАК ВТ_СуммаАвансов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ИтогВстречнаяЗадолженность.ДокументОтгрузки,
	|	ВТ_ИтогВстречнаяЗадолженность.СуммаОтгрузки,
	|	ВТ_ИтогВстречнаяЗадолженность.СуммаОплаты,
	|	ВТ_ИтогВстречнаяЗадолженность.ДоговорКонтрагента,
	|	ВТ_ИтогВстречнаяЗадолженность.Валюта,
	|	ВТ_ИтогВстречнаяЗадолженность.ТипОплаты
	|ИЗ
	|	ВТ_ИтогВстречнаяЗадолженность КАК ВТ_ИтогВстречнаяЗадолженность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеИтог.ДокументОтгрузки,
	|	ВТ_ДанныеИтог.СуммаОтгрузки,
	|	ВТ_ДанныеИтог.СуммаОплаты,
	|	ВТ_ДанныеИтог.ДоговорКонтрагента,
	|	ВТ_ДанныеИтог.Валюта,
	|	ВТ_ДанныеИтог.ТипОплаты,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеИтог.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ВТ_ДанныеИтог.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).абс_СчетНаОплатуПокупателю.FACTUREEXTERNALID
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК FACTURENUM,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеИтог.Валюта.Наименование = ""Руб.""
	|			ТОГДА ""RUB""
	|		ИНАЧЕ ВТ_ДанныеИтог.Валюта.Наименование
	|	КОНЕЦ КАК CURRENCYCODE,
	|	ВТ_ДанныеИтог.СуммаОтгрузки КАК SUMMA,
	|	ВТ_ДанныеИтог.СуммаОплаты КАК SUMMAOPLAT,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеИтог.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.абс_ТипыОплатыК2.Оплата)
	|			ТОГДА 1
	|		КОГДА ВТ_ДанныеИтог.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.абс_ТипыОплатыК2.Аванс)
	|			ТОГДА 2
	|		КОГДА ВТ_ДанныеИтог.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.абс_ТипыОплатыК2.ВстречнаяЗадолженность)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК TYPE_OPL,
	|	ВТ_ДанныеИтог.ДоговорКонтрагента.Номер КАК DOGID
	|ИЗ
	|	ВТ_ДанныеИтог КАК ВТ_ДанныеИтог";
	
	Запрос.УстановитьПараметр("ДатаНачала", НачПериода);
	Запрос.УстановитьПараметр("ДатаКонца", КонецДня(КонПериода));
	Запрос.УстановитьПараметр("Организация",Организация);
	
	ТаблицаВыгрузки.Загрузить(Запрос.Выполнить().Выгрузить());	
	
КонецПроцедуры

Процедура ВыгрузитьВSQL() Экспорт
	
	// Подготовим данные для выгрузки, запишем документ, а затем выгрузим
	Если Не РольДоступна("ПолныеПрава") Тогда
		Возврат;
	Конецесли;
	
	Префикс = "";
	Если ЗначениеЗаполнено(Организация) Тогда
		Префикс = Организация.Префикс;
	КонецЕсли;
	
	Если Префикс = "КТТ" Тогда
		Префикс = "KTT";
	КонецЕсли;  
	
	DATESESSION  	= Дата;
	SESSIONID 		= Префикс+Формат(DATESESSION,"ДФ=yyMMddччммсс");
	
	ТабВыгрузки = ТаблицаВыгрузки.Выгрузить();
	
	// АБС ИЗМЕНЕНО 20120803 Фролов
	// Не используем схему с выделением авансов, распределяем все на реализацию
	//ТабВыгрузки.Свернуть("FACTURENUM,CURRENCYCODE,DOGID,TYPE_OPL", "SUMMA,SUMMAOPLAT");
	ТабВыгрузки.Свернуть("FACTURENUM,CURRENCYCODE", "SUMMA,SUMMAOPLAT");
	
	ДанныеВыгрузка.Очистить();
	
	Для Каждого Стр Из ТабВыгрузки Цикл
		
		// АБС ИЗМЕНЕНО 20120803 Фролов
		// Не используем схему с выделением авансов, распределяем все на реализацию		
		
		//Если Не ЗначениеЗаполнено(Стр.FACTURENUM) И Стр.TYPE_OPL = 1 Тогда
		//	Продолжить;
		//КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Стр.FACTURENUM) Тогда
			Продолжить;
		КонецЕсли;		
		
		СтрВыгрузка = ДанныеВыгрузка.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрВыгрузка, Стр);
			
	КонецЦикла;
	
	Записать();
	
	абс_СерверныеФункции.ВыполнитьВыгрузкуК2ВSQL(Ссылка);
	
	Выгружен = Истина;
	
	Записать();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация")
	
КонецПроцедуры

ТекПользователь = глЗначениеПеременной("глТекущийПользователь");




