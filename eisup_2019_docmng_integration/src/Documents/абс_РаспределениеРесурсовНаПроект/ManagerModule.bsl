
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция РаспределитьПоНатуральнымПоказателям(Организация, Период, МассивОшибок = Неопределено) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	абс_РесурсыПоПроектамБазаСрезПоследних.Проект,
	                      |	абс_РесурсыПоПроектамБазаСрезПоследних.Ресурс,
	                      |	абс_РесурсыПоПроектамБазаСрезПоследних.Значение КАК Коэффициент
	                      |ПОМЕСТИТЬ РесурсыПоПроектамДетально
	                      |ИЗ
	                      |	РегистрСведений.абс_РесурсыПоПроектамБаза.СрезПоследних(&Период, Проект.абс_Организация = &Организация) КАК абс_РесурсыПоПроектамБазаСрезПоследних
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СУММА(ВТ_РесурсыПоПроектам.Коэффициент) КАК Коэффициент,
	                      |	ВТ_РесурсыПоПроектам.Ресурс
	                      |ПОМЕСТИТЬ СуммаКэфициентовПоРесурсу
	                      |ИЗ
	                      |	РесурсыПоПроектамДетально КАК ВТ_РесурсыПоПроектам
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВТ_РесурсыПоПроектам.Ресурс
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	РесурсыПоПроектамДетально.Проект,
	                      |	РесурсыПоПроектамДетально.Ресурс,
	                      |	РесурсыПоПроектамДетально.Коэффициент КАК КоэффициентЗначение,
	                      |	СуммаКэфициентовПоРесурсу.Коэффициент КАК СуммаКоэффициентов,
	                      |	ВЫБОР
	                      |		КОГДА РесурсыПоПроектамДетально.Коэффициент = 0
	                      |			ТОГДА 0
	                      |		ИНАЧЕ ВЫРАЗИТЬ(РесурсыПоПроектамДетально.Коэффициент / СуммаКэфициентовПоРесурсу.Коэффициент КАК ЧИСЛО(15, 6))
	                      |	КОНЕЦ КАК Коэффициент
	                      |ПОМЕСТИТЬ КоэффициентаПоРесурсам
	                      |ИЗ
	                      |	РесурсыПоПроектамДетально КАК РесурсыПоПроектамДетально
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СуммаКэфициентовПоРесурсу КАК СуммаКэфициентовПоРесурсу
	                      |		ПО РесурсыПоПроектамДетально.Ресурс = СуммаКэфициентовПоРесурсу.Ресурс
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	абс_ЗатратыПоПроектамОбороты.ЦФО,
	                      |	абс_ЗатратыПоПроектамОбороты.СтатьяОборотов,
	                      |	абс_ЗатратыПоПроектамОбороты.КВ,
	                      |	абс_ЗатратыПоПроектамОбороты.ТЭО,
	                      |	абс_ЗатратыПоПроектамОбороты.ЦФУ,
	                      |	абс_ЗатратыПоПроектамОбороты.ДоговорКонтрагента,
	                      |	абс_ЗатратыПоПроектамОбороты.РесурсПроекта,
	                      |	абс_ЗатратыПоПроектамОбороты.СуммаОборот КАК Сумма,
	                      |	абс_ЗатратыПоПроектамОбороты.МетодРаспределения,
	                      |	абс_ЗатратыПоПроектамОбороты.Регистратор
	                      |ПОМЕСТИТЬ ЗатратыПоПроектамДетально
	                      |ИЗ
	                      |	РегистрНакопления.абс_ЗатратыПоПроектам.Обороты(
	                      |			&НачалоПериода,
	                      |			&КонецПериода,
	                      |			Регистратор,
	                      |			Организация = &Организация
	                      |				И МетодРаспределения = &Метод) КАК абс_ЗатратыПоПроектамОбороты
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_ЗатратыПоПроектам.РесурсПроекта,
	                      |	СУММА(ВТ_ЗатратыПоПроектам.Сумма) КАК Сумма
	                      |ПОМЕСТИТЬ СуммаЗатратыПоРесурсам
	                      |ИЗ
	                      |	ЗатратыПоПроектамДетально КАК ВТ_ЗатратыПоПроектам
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВТ_ЗатратыПоПроектам.РесурсПроекта
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЗатратыПоПроектамДетально.ЦФО,
	                      |	ЗатратыПоПроектамДетально.СтатьяОборотов,
	                      |	ЗатратыПоПроектамДетально.КВ,
	                      |	ЗатратыПоПроектамДетально.ЦФУ,
	                      |	ЗатратыПоПроектамДетально.ДоговорКонтрагента,
	                      |	ЗатратыПоПроектамДетально.РесурсПроекта КАК РесурсПроекта,
	                      |	КоэффициентаПоРесурсам.Проект КАК Проект,
	                      |	СуммаЗатратыПоРесурсам.Сумма КАК СуммаПоРесурсам,
	                      |	КоэффициентаПоРесурсам.Коэффициент КАК Коэффициент,
	                      |	ЗатратыПоПроектамДетально.Сумма КАК Сумма,
	                      |	ЗатратыПоПроектамДетально.МетодРаспределения,
	                      |	ЗатратыПоПроектамДетально.Регистратор,
	                      |	КоэффициентаПоРесурсам.Проект.абс_ТЭО КАК ТЭО
	                      |ИЗ
	                      |	ЗатратыПоПроектамДетально КАК ЗатратыПоПроектамДетально
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентаПоРесурсам КАК КоэффициентаПоРесурсам
	                      |		ПО (КоэффициентаПоРесурсам.Ресурс = ЗатратыПоПроектамДетально.РесурсПроекта)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ СуммаЗатратыПоРесурсам КАК СуммаЗатратыПоРесурсам
	                      |		ПО (СуммаЗатратыПоРесурсам.РесурсПроекта = ЗатратыПоПроектамДетально.РесурсПроекта)
	                      |ИТОГИ
	                      |	МАКСИМУМ(СуммаПоРесурсам),
	                      |	МАКСИМУМ(Коэффициент)
	                      |ПО
	                      |	РесурсПроекта,
	                      |	Проект");
						  
	Запрос.УстановитьПараметр("КонецПериода", Новый Граница(КонецМесяца(Период)));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("Организация", Организация);						  
	Запрос.УстановитьПараметр("Период", Новый Граница(КонецМесяца(Период)));
	Запрос.УстановитьПараметр("Метод", Перечисления.абс_МетодыРаспределенияРесурсовПоПроектам.ПоНатуральнымПоказателям);
	
	Результат = Запрос.Выполнить();
	ТЗ = ПолучитьТЗПоРезультатуЗапроса(Результат);
	ВыборкаПоРесурсам = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоРесурсам.Следующий() Цикл 
		
		ВыборкаПоПроектам = ВыборкаПоРесурсам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоПроектам.Следующий() Цикл 
			
			Если ЗначениеЗаполнено(ВыборкаПоПроектам.Проект) Тогда 
				СуммаПоПроекту = ВыборкаПоПроектам.СуммаПоРесурсам * ВыборкаПоПроектам.Коэффициент;
			Иначе
				ЗаписатьОшибку(МассивОшибок, "Отсутствуют коэффициенты для распределения затрат по ресурсу: " + Строка(ВыборкаПоПроектам.РесурсПроекта));
				Продолжить;
			КонецЕсли;
			
			й = 1;
			ОсталосьРаспределить = СуммаПоПроекту;
			Выборка = ВыборкаПоПроектам.Выбрать();
			
			Пока Выборка.Следующий() Цикл 
				
				НоваяСтрока = ТЗ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				
				Если й <> Выборка.Количество() Тогда 
					НоваяСтрока.Сумма = ?(ЗначениеЗаполнено(ВыборкаПоПроектам.СуммаПоРесурсам), 
											(Выборка.Сумма / ВыборкаПоПроектам.СуммаПоРесурсам) * СуммаПоПроекту, 0);
				Иначе
					НоваяСтрока.Сумма = ОсталосьРаспределить;	
				КонецЕсли;
				
				й = й + 1;
				ОсталосьРаспределить = ОсталосьРаспределить - НоваяСтрока.Сумма;
				
			КонецЦикла;	
		КонецЦикла;
	КонецЦикла;	
	
	Возврат ТЗ;
	
КонецФункции

Функция РаспределитьПоОборотамБюджета(Организация, Период, ПоВыручке = Истина, МассивОшибок = Неопределено) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	абс_ЗатратыПоПроектамОбороты.ЦФО,
	                      |	абс_ЗатратыПоПроектамОбороты.СтатьяОборотов,
	                      |	абс_ЗатратыПоПроектамОбороты.КВ,
	                      |	абс_ЗатратыПоПроектамОбороты.ТЭО,
	                      |	абс_ЗатратыПоПроектамОбороты.ЦФУ,
	                      |	абс_ЗатратыПоПроектамОбороты.ДоговорКонтрагента,
	                      |	абс_ЗатратыПоПроектамОбороты.СуммаОборот КАК Сумма,
	                      |	абс_ЗатратыПоПроектамОбороты.МетодРаспределения,
	                      |	абс_ЗатратыПоПроектамОбороты.Регистратор
	                      |ПОМЕСТИТЬ ЗатратыПоПроектамДетально
	                      |ИЗ
	                      |	РегистрНакопления.абс_ЗатратыПоПроектам.Обороты(
	                      |			&НачалоПериода,
	                      |			&КонецПериода,
	                      |			Регистратор,
	                      |			МетодРаспределения = &Метод
	                      |				И Организация = &Организация) КАК абс_ЗатратыПоПроектамОбороты
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЗатратыПоПроектамДетально.ДоговорКонтрагента,
	                      |	СУММА(ЗатратыПоПроектамДетально.Сумма) КАК Сумма
	                      |ПОМЕСТИТЬ ЗатратыПоДоговору
	                      |ИЗ
	                      |	ЗатратыПоПроектамДетально КАК ЗатратыПоПроектамДетально
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ЗатратыПоПроектамДетально.ДоговорКонтрагента
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЗатратыПоПроектамДетально.ДоговорКонтрагента,
	                      |	ДоговорыКонтрагентовабс_Проекты.Проект
	                      |ПОМЕСТИТЬ ДоговораПоПроектам
	                      |ИЗ
	                      |	ЗатратыПоПроектамДетально КАК ЗатратыПоПроектамДетально
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов.абс_Проекты КАК ДоговорыКонтрагентовабс_Проекты
	                      |		ПО ЗатратыПоПроектамДетально.ДоговорКонтрагента = ДоговорыКонтрагентовабс_Проекты.Ссылка
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ЗатратыПоПроектамДетально.ДоговорКонтрагента,
	                      |	ДоговорыКонтрагентовабс_Проекты.Проект
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ДоговораПоПроектам.ДоговорКонтрагента
	                      |ПОМЕСТИТЬ ДоговораПоПроектамБезПроектов
	                      |ИЗ
	                      |	ДоговораПоПроектам КАК ДоговораПоПроектам
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ДоговораПоПроектам.ДоговорКонтрагента
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ОборотыБюджетовОбороты.Проект,
	                      |	СУММА(ОборотыБюджетовОбороты.СуммаСценарияОборот) КАК Выручка,
	                      |	ОборотыБюджетовОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента
	                      |ПОМЕСТИТЬ ОборотыБюджета
	                      |ИЗ
	                      |	РегистрНакопления.ОборотыБюджетов.Обороты(
	                      |			&НачалоПериода,
	                      |			&КонецПериода,
	                      |			Регистратор,
	                      |			Организация = &Организация
	                      |				И СтатьяОборотов.абс_ДоходнаяСтатья = &Доходная
	                      |				И Проект <> ЗНАЧЕНИЕ(Справочник.Проекты.Распределять)
	                      |				И Проект <> ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
	                      |				И Сценарий В (&МассивСценариев)) КАК ОборотыБюджетовОбороты
	                      |ГДЕ
	                      |	НЕ ОборотыБюджетовОбороты.Регистратор ССЫЛКА Документ.абс_РаспределениеРесурсовНаПроект
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ОборотыБюджетовОбороты.Проект,
	                      |	ОборотыБюджетовОбороты.ДоговорКонтрагента
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	СУММА(ВыручкаПоДоговорам.Выручка) КАК Выручка
	                      |ПОМЕСТИТЬ ВыручкаИтого
	                      |ИЗ
	                      |	ОборотыБюджета КАК ВыручкаПоДоговорам
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	СУММА(ОборотыБюджета.Выручка) КАК Выручка
	                      |ПОМЕСТИТЬ ВыручкаПоДоговорамИтого
	                      |ИЗ
	                      |	ОборотыБюджета КАК ОборотыБюджета
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоговораПоПроектам КАК ДоговораПоПроектам
	                      |		ПО ОборотыБюджета.Проект = ДоговораПоПроектам.Проект
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	СУММА(ВыручкаПоДоговорам.Выручка) КАК Выручка,
	                      |	ВыручкаПоДоговорам.Проект КАК Проект
	                      |ПОМЕСТИТЬ ВыручкаПоПроектам
	                      |ИЗ
	                      |	ОборотыБюджета КАК ВыручкаПоДоговорам
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВыручкаПоДоговорам.Проект
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ОборотыБюджета.Проект,
	                      |	СУММА(ОборотыБюджета.Выручка) КАК Выручка
	                      |ПОМЕСТИТЬ ОборотыБюджетаПоДоговорамПоПроектам
	                      |ИЗ
	                      |	ОборотыБюджета КАК ОборотыБюджета
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоговораПоПроектам КАК ДоговораПоПроектам
	                      |		ПО ОборотыБюджета.Проект = ДоговораПоПроектам.Проект
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ОборотыБюджета.Проект
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ВЫБОР
	                      |		КОГДА ВыручкаИтого.Выручка = 0
	                      |			ТОГДА 0
	                      |		ИНАЧЕ ВЫРАЗИТЬ(ВыручкаПоПроектам.Выручка / ВыручкаИтого.Выручка КАК ЧИСЛО(15, 6))
	                      |	КОНЕЦ КАК Коэффициент,
	                      |	ВыручкаПоПроектам.Проект
	                      |ПОМЕСТИТЬ Коэффициенты
	                      |ИЗ
	                      |	ВыручкаПоПроектам КАК ВыручкаПоПроектам,
	                      |	ВыручкаИтого КАК ВыручкаИтого
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ВЫБОР
	                      |		КОГДА ВыручкаПоДоговорамИтого.Выручка = 0
	                      |			ТОГДА 0
	                      |		ИНАЧЕ ВЫРАЗИТЬ(ОборотыБюджетаПоДоговорамПоПроектам.Выручка / ВыручкаПоДоговорамИтого.Выручка КАК ЧИСЛО(15, 2))
	                      |	КОНЕЦ КАК Коэффициент,
	                      |	ОборотыБюджетаПоДоговорамПоПроектам.Проект
	                      |ПОМЕСТИТЬ Коэффициенты2
	                      |ИЗ
	                      |	ОборотыБюджетаПоДоговорамПоПроектам КАК ОборотыБюджетаПоДоговорамПоПроектам,
	                      |	ВыручкаПоДоговорамИтого КАК ВыручкаПоДоговорамИтого
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ЗатратыПоПроектамДетально.ДоговорКонтрагента КАК ДоговорКонтрагента,
	                      |	Коэффициенты.Проект КАК Проект,
	                      |	ЗатратыПоПроектамДетально.Сумма КАК Сумма,
	                      |	Коэффициенты.Коэффициент КАК Коэффициент,
	                      |	ЗатратыПоПроектамДетально.ЦФО,
	                      |	ЗатратыПоПроектамДетально.СтатьяОборотов,
	                      |	ЗатратыПоПроектамДетально.КВ,
	                      |	ЗатратыПоПроектамДетально.ЦФУ,
	                      |	ЗатратыПоПроектамДетально.МетодРаспределения,
	                      |	ЗатратыПоПроектамДетально.Регистратор,
	                      |	Коэффициенты.Проект.абс_ТЭО КАК ТЭО,
	                      |	ЗатратыПоДоговору.Сумма КАК ЗатратыПоДоговору
	                      |ПОМЕСТИТЬ ВТ_Распределение
	                      |ИЗ
	                      |	ЗатратыПоПроектамДетально КАК ЗатратыПоПроектамДетально
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ДоговораПоПроектам КАК ДоговораПоПроектам
	                      |		ПО ЗатратыПоПроектамДетально.ДоговорКонтрагента = ДоговораПоПроектам.ДоговорКонтрагента
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗатратыПоДоговору КАК ЗатратыПоДоговору
	                      |		ПО ЗатратыПоПроектамДетально.ДоговорКонтрагента = ЗатратыПоДоговору.ДоговорКонтрагента,
	                      |	Коэффициенты КАК Коэффициенты
	                      |ГДЕ
	                      |	ДоговораПоПроектам.ДоговорКонтрагента ЕСТЬ NULL 
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЗатратыПоПроектамДетально.ЦФО,
	                      |	ЗатратыПоПроектамДетально.СтатьяОборотов,
	                      |	ЗатратыПоПроектамДетально.КВ,
	                      |	ЗатратыПоПроектамДетально.ТЭО,
	                      |	ЗатратыПоПроектамДетально.ЦФУ,
	                      |	ЗатратыПоПроектамДетально.ДоговорКонтрагента,
	                      |	ЗатратыПоПроектамДетально.Сумма,
	                      |	ЗатратыПоПроектамДетально.МетодРаспределения,
	                      |	ЗатратыПоПроектамДетально.Регистратор,
	                      |	Коэффициенты2.Коэффициент,
	                      |	Коэффициенты2.Проект,
	                      |	ЗатратыПоДоговору.Сумма КАК ЗатратыПоДоговору
	                      |ПОМЕСТИТЬ ВТ_Распределение2
	                      |ИЗ
	                      |	ЗатратыПоПроектамДетально КАК ЗатратыПоПроектамДетально
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗатратыПоДоговору КАК ЗатратыПоДоговору
	                      |		ПО ЗатратыПоПроектамДетально.ДоговорКонтрагента = ЗатратыПоДоговору.ДоговорКонтрагента,
	                      |	Коэффициенты2 КАК Коэффициенты2
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Распределение.ДоговорКонтрагента КАК ДоговорКонтрагента,
	                      |	ВТ_Распределение.Проект КАК Проект,
	                      |	ВТ_Распределение.Сумма КАК Сумма,
	                      |	ВТ_Распределение.Коэффициент КАК Коэффициент,
	                      |	ВТ_Распределение.ЦФО,
	                      |	ВТ_Распределение.СтатьяОборотов,
	                      |	ВТ_Распределение.КВ,
	                      |	ВТ_Распределение.ЦФУ,
	                      |	ВТ_Распределение.МетодРаспределения,
	                      |	ВТ_Распределение.Регистратор,
	                      |	ВТ_Распределение.ТЭО,
	                      |	1 КАК ВидРаспределения,
	                      |	ВТ_Распределение.ЗатратыПоДоговору КАК ЗатратыПоДоговору
	                      |ИЗ
	                      |	ВТ_Распределение КАК ВТ_Распределение
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	ЕСТЬNULL(ВТ_Распределение2.ДоговорКонтрагента, ДоговораПоПроектамБезПроектов.ДоговорКонтрагента),
	                      |	ВТ_Распределение2.Проект,
	                      |	ВТ_Распределение2.Сумма,
	                      |	ВТ_Распределение2.Коэффициент,
	                      |	ВТ_Распределение2.ЦФО,
	                      |	ВТ_Распределение2.СтатьяОборотов,
	                      |	ВТ_Распределение2.КВ,
	                      |	ВТ_Распределение2.ЦФУ,
	                      |	ВТ_Распределение2.МетодРаспределения,
	                      |	ВТ_Распределение2.Регистратор,
	                      |	ВТ_Распределение2.ТЭО,
	                      |	2,
	                      |	ВТ_Распределение2.ЗатратыПоДоговору
	                      |ИЗ
	                      |	ДоговораПоПроектамБезПроектов КАК ДоговораПоПроектамБезПроектов
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Распределение2 КАК ВТ_Распределение2
	                      |		ПО ДоговораПоПроектамБезПроектов.ДоговорКонтрагента = ВТ_Распределение2.ДоговорКонтрагента
	                      |ИТОГИ
	                      |	МАКСИМУМ(Сумма),
	                      |	МАКСИМУМ(Коэффициент),
	                      |	МАКСИМУМ(ЗатратыПоДоговору)
	                      |ПО
	                      |	ВидРаспределения,
	                      |	ДоговорКонтрагента");
						  
	Запрос.УстановитьПараметр("КонецПериода", 	Новый Граница(КонецМесяца(Период)));
	Запрос.УстановитьПараметр("НачалоПериода", 	НачалоМесяца(Период));
	Запрос.УстановитьПараметр("Организация", 	Организация);						  
	Запрос.УстановитьПараметр("Период", 		Новый Граница(КонецМесяца(Период)));
	Запрос.УстановитьПараметр("МассивСценариев",ПолучитьМассивСценариевФакт());
	Запрос.УстановитьПараметр("Доходная", 		ПоВыручке);
	
	Если ПоВыручке Тогда 
		Запрос.УстановитьПараметр("Метод", Перечисления.абс_МетодыРаспределенияРесурсовПоПроектам.ПоОбъемуВыручки);	
	Иначе
		Запрос.УстановитьПараметр("Метод", Перечисления.абс_МетодыРаспределенияРесурсовПоПроектам.ПоПрямымЗатратам);
	КонецЕсли;

	
	Результат = Запрос.Выполнить();
	ТЗ = ПолучитьТЗПоРезультатуЗапроса(Результат);	
	ВыборкаПоРесурсам = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоРесурсам.Следующий() Цикл 
		
		ВыборкаПоПроектам = ВыборкаПоРесурсам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоПроектам.Следующий() Цикл 
			
			Если НЕ ЗначениеЗаполнено(ВыборкаПоПроектам.Коэффициент) Тогда 
				Если ПоВыручке Тогда 
					ЗаписатьОшибку(МассивОшибок, "Отсутствует выручка для распределения затрат по договору " + Строка(ВыборкаПоПроектам.ДоговорКонтрагента));
					
				Иначе
					ЗаписатьОшибку(МассивОшибок, "Отсутствуют прямые затраты по проекту для распределения затрат по договору " + Строка(ВыборкаПоПроектам.ДоговорКонтрагента));
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			й = 1;
			ОсталосьРаспределить = ВыборкаПоПроектам.ЗатратыПоДоговору;
			Выборка = ВыборкаПоПроектам.Выбрать();
			Пока Выборка.Следующий() Цикл 
				
				НоваяСтрока = ТЗ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);	
				
				Если й <> Выборка.Количество() Тогда 
					НоваяСтрока.Сумма = НоваяСтрока.Сумма * НоваяСтрока.Коэффициент;;
				Иначе
					НоваяСтрока.Сумма = ОсталосьРаспределить;	
				КонецЕсли;
				
				й = й + 1;
				ОсталосьРаспределить = ОсталосьРаспределить - НоваяСтрока.Сумма;
			КонецЦикла;	
		КонецЦикла;
	КонецЦикла;	
	
	Возврат ТЗ;
	
КонецФункции

Функция ПолучитьДанныеДляПроведенияОборотыБюджетов(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.ЦФО,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.СтатьяОборотов,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.КВ КАК КВ,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.ТЭО КАК ТЭО,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.ЦФУ КАК ЦФУ,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.ДоговорКонтрагента,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Проект,
	                      |	СУММА(абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Сумма) КАК Сумма,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Ссылка КАК Ссылка,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Регистратор КАК ДокументРегистратор
	                      |ПОМЕСТИТЬ ВТ_ТаблицаРаспределения
	                      |ИЗ
	                      |	Документ.абс_РаспределениеРесурсовНаПроект.РаспределениеПоПроектам КАК абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам
	                      |ГДЕ
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Ссылка = &Ссылка
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.ЦФО,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.ТЭО,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.ЦФУ,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.СтатьяОборотов,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.КВ,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.ДоговорКонтрагента,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Проект,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Ссылка,
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Регистратор
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Регистратор КАК ДокументРегистратор
	                      |ПОМЕСТИТЬ РегистраторыДляСторно
	                      |ИЗ
	                      |	Документ.абс_РаспределениеРесурсовНаПроект.РаспределениеПоПроектам КАК абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам
	                      |ГДЕ
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Ссылка = &Ссылка
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	абс_РаспределениеРесурсовНаПроектРаспределениеПоПроектам.Регистратор
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	РегистраторыДляСторно.ДокументРегистратор,
	                      |	МАКСИМУМ(ОборотыБюджетовОбороты.Сценарий) КАК Сценарий
	                      |ПОМЕСТИТЬ СценарииПоРегистратору
	                      |ИЗ
	                      |	РегистраторыДляСторно КАК РегистраторыДляСторно
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОборотыБюджетов.Обороты(, , Регистратор, ) КАК ОборотыБюджетовОбороты
	                      |		ПО РегистраторыДляСторно.ДокументРегистратор = ОборотыБюджетовОбороты.Регистратор
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	РегистраторыДляСторно.ДокументРегистратор
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_ТаблицаРаспределения.ЦФО,
	                      |	ВТ_ТаблицаРаспределения.СтатьяОборотов,
	                      |	ВТ_ТаблицаРаспределения.КВ КАК абс_КВ,
	                      |	ВТ_ТаблицаРаспределения.ТЭО КАК абс_ТЭО,
	                      |	ВТ_ТаблицаРаспределения.ЦФУ КАК абс_ЦФУ,
	                      |	ВТ_ТаблицаРаспределения.ДоговорКонтрагента,
	                      |	ВТ_ТаблицаРаспределения.Проект,
	                      |	СУММА(ВТ_ТаблицаРаспределения.Сумма) КАК СуммаСценария,
	                      |	ВТ_ТаблицаРаспределения.ДоговорКонтрагента.Владелец КАК Контрагент,
	                      |	ВЫБОР
	                      |		КОГДА СценарииПоРегистратору.Сценарий ЕСТЬ НЕ NULL 
	                      |			ТОГДА СценарииПоРегистратору.Сценарий
	                      |		ИНАЧЕ &Сценарий
	                      |	КОНЕЦ,
	                      |	ИСТИНА КАК Активность,
	                      |	ВТ_ТаблицаРаспределения.ДоговорКонтрагента.Владелец.абс_ТипыКонтрагентов КАК абс_ТипКонтрагента,
	                      |	СУММА(ВТ_ТаблицаРаспределения.Сумма) КАК ВалютнаяСумма,
	                      |	СУММА(ВТ_ТаблицаРаспределения.Сумма) КАК СуммаУпр,
	                      |	&Валюта,
	                      |	NULL КАК Номенклатура,
	                      |	NULL КАК абс_ТипСети,
	                      |	NULL КАК абс_ТипРасхода,
	                      |	ВТ_ТаблицаРаспределения.Ссылка.Организация КАК Организация,
	                      |	NULL КАК абс_СчетБУ,
	                      |	NULL КАК КоличествоОборот,
	                      |	&Период
	                      |ИЗ
	                      |	ВТ_ТаблицаРаспределения КАК ВТ_ТаблицаРаспределения
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ СценарииПоРегистратору КАК СценарииПоРегистратору
	                      |		ПО ВТ_ТаблицаРаспределения.ДокументРегистратор = СценарииПоРегистратору.ДокументРегистратор
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВТ_ТаблицаРаспределения.ЦФО,
	                      |	ВТ_ТаблицаРаспределения.ТЭО,
	                      |	ВТ_ТаблицаРаспределения.ЦФУ,
	                      |	ВТ_ТаблицаРаспределения.СтатьяОборотов,
	                      |	ВТ_ТаблицаРаспределения.КВ,
	                      |	ВТ_ТаблицаРаспределения.ДоговорКонтрагента,
	                      |	ВТ_ТаблицаРаспределения.Проект,
	                      |	ВТ_ТаблицаРаспределения.ДоговорКонтрагента.Владелец,
	                      |	ВТ_ТаблицаРаспределения.ДоговорКонтрагента.Владелец.абс_ТипыКонтрагентов,
	                      |	ВТ_ТаблицаРаспределения.Ссылка.Организация,
	                      |	ВЫБОР
	                      |		КОГДА СценарииПоРегистратору.Сценарий ЕСТЬ НЕ NULL 
	                      |			ТОГДА СценарииПоРегистратору.Сценарий
	                      |		ИНАЧЕ &Сценарий
	                      |	КОНЕЦ
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	ОборотыБюджетовОбороты.ЦФО,
	                      |	ОборотыБюджетовОбороты.СтатьяОборотов,
	                      |	ОборотыБюджетовОбороты.абс_КВ,
	                      |	ОборотыБюджетовОбороты.абс_ТЭО,
	                      |	ОборотыБюджетовОбороты.абс_ЦФУ,
	                      |	ОборотыБюджетовОбороты.ДоговорКонтрагента,
	                      |	ОборотыБюджетовОбороты.Проект,
	                      |	-ОборотыБюджетовОбороты.СуммаСценарияОборот,
	                      |	ОборотыБюджетовОбороты.Контрагент,
	                      |	ОборотыБюджетовОбороты.Сценарий,
	                      |	ИСТИНА,
	                      |	ОборотыБюджетовОбороты.абс_ТипКонтрагента,
	                      |	-ОборотыБюджетовОбороты.ВалютнаяСуммаОборот,
	                      |	-ОборотыБюджетовОбороты.СуммаУпрОборот,
	                      |	ОборотыБюджетовОбороты.Валюта,
	                      |	ОборотыБюджетовОбороты.Номенклатура,
	                      |	ОборотыБюджетовОбороты.абс_ТипСети,
	                      |	ОборотыБюджетовОбороты.абс_ТипРасхода,
	                      |	ОборотыБюджетовОбороты.Организация,
	                      |	ОборотыБюджетовОбороты.абс_СчетБУ,
	                      |	-ОборотыБюджетовОбороты.КоличествоОборот,
	                      |	&Период
	                      |ИЗ
	                      |	РегистрНакопления.ОборотыБюджетов.Обороты(
	                      |			,
	                      |			,
	                      |			Регистратор,
	                      |			(Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
	                      |				ИЛИ Проект = ЗНАЧЕНИЕ(Справочник.Проекты.Распределять))
	                      |				И Сценарий.абс_Факт) КАК ОборотыБюджетовОбороты
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыДляСторно КАК РегистраторыДляСторно
	                      |		ПО ОборотыБюджетовОбороты.Регистратор = РегистраторыДляСторно.ДокументРегистратор");
						  
	Запрос.УстановитьПараметр("Ссылка", 	ДокументСсылка);
	Запрос.УстановитьПараметр("Период", 	КонецМесяца(ДокументСсылка.Дата));
	Запрос.УстановитьПараметр("Сценарий", 	глЗначениеПеременной("абс_СценарийДляФакта"));
	Запрос.УстановитьПараметр("Валюта", 	глЗначениеПеременной("ВалютаРегламентированногоУчета"));
	
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПолучитьМассивСценариевФакт() Экспорт
	
	СписокСценариевФакт = Новый Массив;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	СценарииПланирования.Ссылка
	                      |ИЗ
	                      |	Справочник.СценарииПланирования КАК СценарииПланирования
	                      |ГДЕ
	                      |	СценарииПланирования.абс_Факт");
						  
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 	
		СписокСценариевФакт.Добавить(Выборка.Ссылка);		
	КонецЦикла;
		
	Возврат СписокСценариевФакт;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ


Функция ПолучитьТЗПоРезультатуЗапроса(Результат)
	
	ТЗ = Новый ТаблицаЗначений;
	Для Каждого колонки из Результат.Колонки Цикл 
		ТЗ.Колонки.Добавить(колонки.Имя, колонки.ТипЗначения);
	КонецЦикла;
	
	Возврат ТЗ;
		
КонецФункции

Процедура ЗаписатьОшибку(МассивОшибок, ОписаниеОшибки)
	
	Если МассивОшибок = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(МассивОшибок) <> Тип("Массив") Тогда 
		Возврат;
	КонецЕсли;
	
	МассивОшибок.Добавить(ОписаниеОшибки);
	
КонецПроцедуры


