
Функция ПолучитьСтруктуруЗапросовПроведения() Экспорт

	СтруктураЗапросов = Новый Структура;
	СтруктураЗапросов.Вставить("абс_МеждународныйУчетОСРЖД_ТекстЗапросаВидыСубконтоСчетов", абс_МеждународныйУчетОСРЖД.ТекстЗапросаВидыСубконтоСчетов("ВидыСубконтоСчетов"));
	СтруктураЗапросов.Вставить("абс_МеждународныйУчетОСРЖД_ТекстЗапросаСоответствиеСчетовХозрасчетныйРЖД", абс_МеждународныйУчетОСРЖД.ТекстЗапросаСоответствиеСчетовХозрасчетныйМеждународныйОСРЖД("Период",, "СоответствиеСчетовХозрасчетныйРЖД"));
	СтруктураЗапросов.Вставить("ТекстЗапросаСоответствиеСчетовХозрасчетныйМеждународныйОСРЖД", абс_МеждународныйУчетОСРЖД.ТекстЗапросаСоответствиеСчетовХозрасчетныйМеждународныйОСРЖД("Период",, "СоответствиеСчетовХозрасчетныйРЖД"));
	
	ДобавитьЗапросыДляРазукрупнения(СтруктураЗапросов);
	
	Возврат СтруктураЗапросов;

КонецФункции

Процедура ДобавитьЗапросыДляРазукрупнения(СтруктураЗапросов)

	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПериодыРегистраторыСреза.Период,
		|	ПериодыРегистраторыСреза.Регистратор,
		|	ПериодыРегистраторыСреза.ОсновноеСредство,
		|	абс_ОсновныеСредстваРЖД.Организация,
		|	абс_ОсновныеСредстваРЖД.ДатаПринятияКУчету,
		|	абс_ОсновныеСредстваРЖД.МестонахождениеОбъекта,
		|	абс_ОсновныеСредстваРЖД.МОЛ,
		|	абс_ОсновныеСредстваРЖД.Состояние,
		|	абс_ОсновныеСредстваРЖД.СчетУчета,
		|	абс_ОсновныеСредстваРЖД.СрокПолезногоИспользования,
		|	абс_ОсновныеСредстваРЖД.НачислятьАмортизацию,
		|	абс_ОсновныеСредстваРЖД.МетодНачисленияАмортизации,
		|	абс_ОсновныеСредстваРЖД.СчетНачисленияАмортизации,
		|	абс_ОсновныеСредстваРЖД.ПервоначальнаяСтоимость,
		|	абс_ОсновныеСредстваРЖД.СправедливаяСтоимость,
		|	абс_ОсновныеСредстваРЖД.ЛиквидационнаяСтоимость,
		|	абс_ОсновныеСредстваРЖД.СчетЗатрат,
		|	абс_ОсновныеСредстваРЖД.Субконто1,
		|	абс_ОсновныеСредстваРЖД.Субконто2,
		|	абс_ОсновныеСредстваРЖД.Субконто3,
		|	абс_ОсновныеСредстваРЖД.ПредполагаемыйОбъемПродукции,
		|	абс_ОсновныеСредстваРЖД.СуммаНачисленнойАмортизации,
		|	абс_ОсновныеСредстваРЖД.УбытокОтОбесценения,
		|	абс_ОсновныеСредстваРЖД.КоэффициентУскорения,
		|	абс_ОсновныеСредстваРЖД.СчетСниженияСтоимости
		|ПОМЕСТИТЬ ПараметрыОсновногоСредства
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПериодыСреза.ОсновноеСредство КАК ОсновноеСредство,
		|		ПериодыСреза.Период КАК Период,
		|		МАКСИМУМ(абс_ОсновныеСредстваРЖД.Регистратор) КАК Регистратор
		|	ИЗ
		|		(ВЫБРАТЬ
		|			абс_ОсновныеСредстваРЖД.ОсновноеСредство КАК ОсновноеСредство,
		|			МАКСИМУМ(абс_ОсновныеСредстваРЖД.Период) КАК Период
		|		ИЗ
		|			РегистрСведений.абс_ОсновныеСредстваРЖД КАК абс_ОсновныеСредстваРЖД
		|		ГДЕ
		|			абс_ОсновныеСредстваРЖД.ОсновноеСредство = &ОсновноеСредство
		|			И абс_ОсновныеСредстваРЖД.Период <= &Период
		|			И абс_ОсновныеСредстваРЖД.Активность
		|		
		|		СГРУППИРОВАТЬ ПО
		|			абс_ОсновныеСредстваРЖД.ОсновноеСредство) КАК ПериодыСреза
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_ОсновныеСредстваРЖД КАК абс_ОсновныеСредстваРЖД
		|			ПО ПериодыСреза.ОсновноеСредство = абс_ОсновныеСредстваРЖД.ОсновноеСредство
		|				И ПериодыСреза.Период = абс_ОсновныеСредстваРЖД.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПериодыСреза.ОсновноеСредство,
		|		ПериодыСреза.Период) КАК ПериодыРегистраторыСреза
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_ОсновныеСредстваРЖД КАК абс_ОсновныеСредстваРЖД
		|		ПО ПериодыРегистраторыСреза.Период = абс_ОсновныеСредстваРЖД.Период
		|			И ПериодыРегистраторыСреза.Регистратор = абс_ОсновныеСредстваРЖД.Регистратор
		|			И ПериодыРегистраторыСреза.ОсновноеСредство = абс_ОсновныеСредстваРЖД.ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("абс_ВнеоборотныеАктивы_ТекстЗапроса_ОсновныеСредстваРЖДПоОдномуОС", ТекстЗапроса+";");
	
    ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_РазукрупнениеОСРЖД.Ссылка,
		|	абс_РазукрупнениеОСРЖД.Номер,
		|	абс_РазукрупнениеОСРЖД.Дата,
		|	абс_РазукрупнениеОСРЖД.Организация,
		|	абс_РазукрупнениеОСРЖД.ДокументОснование,
		|	абс_РазукрупнениеОСРЖД.ОсновноеСредство
		|ПОМЕСТИТЬ Шапкадокумента
		|ИЗ
		|	Документ.абс_РазукрупнениеОСРЖД КАК абс_РазукрупнениеОСРЖД
		|ГДЕ
		|	абс_РазукрупнениеОСРЖД.Ссылка = &Ссылка";
		
	СтруктураЗапросов.Вставить("Разукрупнение_ТекстЗапросаШапкаДокумента", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.НомерСтроки,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.ОсновноеСредство,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.Коэффициент,
		|	ПараметрыОсновногоСредства.СчетУчета,
		|	ПараметрыОсновногоСредства.СчетНачисленияАмортизации,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.СчетЗатрат,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.Субконто1,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.Субконто2,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.Субконто3,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.СчетСниженияСтоимости,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.Местонахождение,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.МОЛ,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.КоэффициентУскорения,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.НачислятьАмортизацию,
		|	ПараметрыОсновногоСредства.СрокПолезногоИспользования,
		|	ПараметрыОсновногоСредства.ДатаПринятияКУчету,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.ОсновноеСредство.КодПоОКОФ КАК ОКОФ,
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.ОсновноеСредство.абс_ГруппаОСРЖД КАК ГруппаОСРЖД
		|ПОМЕСТИТЬ ТабличнаяЧастьОсновныеСредства
		|ИЗ
		|	Документ.абс_РазукрупнениеОСРЖД.ОсновныеСредства КАК абс_РазукрупнениеОСРЖДОсновныеСредства,
		|	ПараметрыОсновногоСредства КАК ПараметрыОсновногоСредства
		|ГДЕ
		|	абс_РазукрупнениеОСРЖДОсновныеСредства.Ссылка = &Ссылка";
		
	СтруктураЗапросов.Вставить("Разукрупнение_ТекстЗапросаТабличнаяЧастьОсновныеСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_СтоимостьОСРЖДОстатки.ОсновноеСредство,
		|	абс_СтоимостьОСРЖДОстатки.СтоимостьОстаток КАК Стоимость,
		|	абс_СтоимостьОСРЖДОстатки.АмортизацияОстаток КАК Амортизация
		|ПОМЕСТИТЬ СтоимостьСтарогоОсновногоСредства
		|ИЗ
		|	РегистрНакопления.абс_СтоимостьОСРЖД.Остатки(
		|			&МоментДокумента,
		|			ОсновноеСредство = &ОсновноеСредство
		|				И Организация = &Организация) КАК абс_СтоимостьОСРЖДОстатки";
		
	СтруктураЗапросов.Вставить("Разукрупнение_ТекстЗапросаСтоимостьСтарогоОсновногоСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТабличнаяЧастьОсновныеСредства.НомерСтроки КАК НомерСтроки,
		|	ТабличнаяЧастьОсновныеСредства.ОсновноеСредство,
		|	ВЫРАЗИТЬ(СтоимостьСтарогоОсновногоСредства.Стоимость * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)) КАК Стоимость,
		|	ВЫРАЗИТЬ(СтоимостьСтарогоОсновногоСредства.Амортизация * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)) КАК Амортизация
		|ПОМЕСТИТЬ РаспределениеСтоимости
		|ИЗ
		|	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства,
		|	СтоимостьСтарогоОсновногоСредства КАК СтоимостьСтарогоОсновногоСредства
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки";
		
	СтруктураЗапросов.Вставить("Разукрупнение_ТекстЗапросаРаспределениеСтоимости", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтоимостьСтарогоОсновногоСредства.Стоимость - ЕСТЬNULL(ВложенныйЗапрос.Стоимость, 0) КАК Стоимость,
		|	СтоимостьСтарогоОсновногоСредства.Амортизация - ЕСТЬNULL(ВложенныйЗапрос.Амортизация, 0) КАК Амортизация
		|ПОМЕСТИТЬ НераспределеннаяСтоимость
		|ИЗ
		|	СтоимостьСтарогоОсновногоСредства КАК СтоимостьСтарогоОсновногоСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СУММА(РаспределениеСтоимости.Стоимость) КАК Стоимость,
		|			СУММА(РаспределениеСтоимости.Амортизация) КАК Амортизация
		|		ИЗ
		|			РаспределениеСтоимости КАК РаспределениеСтоимости) КАК ВложенныйЗапрос
		|		ПО (ИСТИНА)";
		
	СтруктураЗапросов.Вставить("Разукрупнение_ТекстЗапросаНераспределеннаяСтоимость", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	РаспределениеСтоимости.НомерСтроки КАК НомерСтроки,
		|	РаспределениеСтоимости.ОсновноеСредство,
		|	РаспределениеСтоимости.Стоимость + ЕСТЬNULL(НераспределеннаяСтоимость.Стоимость, 0) КАК Стоимость,
		|	РаспределениеСтоимости.Амортизация + ЕСТЬNULL(НераспределеннаяСтоимость.Амортизация, 0) КАК Амортизация
		|ПОМЕСТИТЬ СтоимостьНовыхОсновныхСредств
		|ИЗ
		|	РаспределениеСтоимости КАК РаспределениеСтоимости
		|		ЛЕВОЕ СОЕДИНЕНИЕ НераспределеннаяСтоимость КАК НераспределеннаяСтоимость
		|		ПО (РаспределениеСтоимости.НомерСтроки = 1)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки";
		
	СтруктураЗапросов.Вставить("Разукрупнение_ТекстЗапросаСтоимостьНовыхОсновныхСредств", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	ПараметрыОсновногоСредства.ОсновноеСредство,
		|	ПараметрыОсновногоСредства.Организация,
		|	ПараметрыОсновногоСредства.ДатаПринятияКУчету,
		|	ПараметрыОсновногоСредства.МестонахождениеОбъекта,
		|	ПараметрыОсновногоСредства.МОЛ,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК Состояние,
		|	ПараметрыОсновногоСредства.СчетУчета,
		|	ПараметрыОсновногоСредства.СрокПолезногоИспользования,
		|	ЛОЖЬ КАК НачислятьАмортизацию,
		|	ПараметрыОсновногоСредства.МетодНачисленияАмортизации,
		|	ПараметрыОсновногоСредства.СчетНачисленияАмортизации,
		|	ПараметрыОсновногоСредства.ПервоначальнаяСтоимость,
		|	0 КАК СправедливаяСтоимость,
		|	ПараметрыОсновногоСредства.ЛиквидационнаяСтоимость,
		|	ПараметрыОсновногоСредства.СчетЗатрат,
		|	ПараметрыОсновногоСредства.Субконто1,
		|	ПараметрыОсновногоСредства.Субконто2,
		|	ПараметрыОсновногоСредства.Субконто3,
		|	ПараметрыОсновногоСредства.ПредполагаемыйОбъемПродукции,
		|	0 КАК СуммаНачисленнойАмортизации,
		|	ПараметрыОсновногоСредства.УбытокОтОбесценения,
		|	ПараметрыОсновногоСредства.КоэффициентУскорения,
		|	ПараметрыОсновногоСредства.СчетСниженияСтоимости
		|ИЗ
		|	ПараметрыОсновногоСредства КАК ПараметрыОсновногоСредства
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	ТабличнаяЧастьОсновныеСредства.ОсновноеСредство,
		|	&Организация,
		|	ТабличнаяЧастьОсновныеСредства.ДатаПринятияКУчету,
		|	ТабличнаяЧастьОсновныеСредства.Местонахождение,
		|	ТабличнаяЧастьОсновныеСредства.МОЛ,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВведеноВЭксплуатацию),
		|	ТабличнаяЧастьОсновныеСредства.СчетУчета,
		|	ВЫБОР
		|		КОГДА ТабличнаяЧастьОсновныеСредства.СрокПолезногоИспользования = 0
		|			ТОГДА ПараметрыОсновногоСредства.СрокПолезногоИспользования
		|		ИНАЧЕ ТабличнаяЧастьОсновныеСредства.СрокПолезногоИспользования
		|	КОНЕЦ,
		|	ИСТИНА,
		|	ПараметрыОсновногоСредства.МетодНачисленияАмортизации,
		|	ТабличнаяЧастьОсновныеСредства.СчетНачисленияАмортизации,
		|	ВЫРАЗИТЬ(ПараметрыОсновногоСредства.ПервоначальнаяСтоимость * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ПараметрыОсновногоСредства.СправедливаяСтоимость * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ПараметрыОсновногоСредства.ЛиквидационнаяСтоимость * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)),
		|	ТабличнаяЧастьОсновныеСредства.СчетЗатрат,
		|	ТабличнаяЧастьОсновныеСредства.Субконто1,
		|	ТабличнаяЧастьОсновныеСредства.Субконто2,
		|	ТабличнаяЧастьОсновныеСредства.Субконто3,
		|	ПараметрыОсновногоСредства.ПредполагаемыйОбъемПродукции,
		|	СтоимостьНовыхОсновныхСредств.Амортизация,
		|	ВЫРАЗИТЬ(ПараметрыОсновногоСредства.УбытокОтОбесценения * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)),
		|	ВЫБОР
		|		КОГДА ТабличнаяЧастьОсновныеСредства.КоэффициентУскорения = 0
		|			ТОГДА 1
		|		ИНАЧЕ ТабличнаяЧастьОсновныеСредства.КоэффициентУскорения
		|	КОНЕЦ,
		|	ТабличнаяЧастьОсновныеСредства.СчетСниженияСтоимости
		|ИЗ
		|	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНовыхОсновныхСредств КАК СтоимостьНовыхОсновныхСредств
		|		ПО ТабличнаяЧастьОсновныеСредства.НомерСтроки = СтоимостьНовыхОсновныхСредств.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СоответствиеОКОФГруппОСРЖД КАК абс_СоответствиеОКОФГруппОСРЖД
		|		ПО ТабличнаяЧастьОсновныеСредства.ОКОФ = абс_СоответствиеОКОФГруппОСРЖД.ОКОФ
		|			И ТабличнаяЧастьОсновныеСредства.ГруппаОСРЖД = абс_СоответствиеОКОФГруппОСРЖД.ГруппаОСРЖД,
		|	ПараметрыОсновногоСредства КАК ПараметрыОсновногоСредства";
		
	СтруктураЗапросов.Вставить("Разукрупнение_ТекстЗапросаТаблица_абс_ОсновныеСредстваРЖД", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	СтоимостьСтарогоОсновногоСредства.ОсновноеСредство КАК ОсновноеСредство,
		|	-СтоимостьСтарогоОсновногоСредства.Стоимость КАК Стоимость,
		|	-СтоимостьСтарогоОсновногоСредства.Амортизация КАК Амортизация
		|ИЗ
		|	СтоимостьСтарогоОсновногоСредства КАК СтоимостьСтарогоОсновногоСредства
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	&Организация,
		|	СтоимостьНовыхОсновныхСредств.ОсновноеСредство,
		|	СтоимостьНовыхОсновныхСредств.Стоимость,
		|	СтоимостьНовыхОсновныхСредств.Амортизация
		|ИЗ
		|	СтоимостьНовыхОсновныхСредств КАК СтоимостьНовыхОсновныхСредств";
		
	СтруктураЗапросов.Вставить("Разукрупнение_ТекстЗапросаТаблица_абс_СтоимостьОСРЖД", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ПараметрыОсновногоСредства.СчетУчета КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства) КАК ВидСубконтоДт1,
		|	СтоимостьСтарогоОсновногоСредства.ОсновноеСредство КАК СубконтоДт1,
		|	ЗНАЧЕНИЕ(ПланСчетов.абс_МеждународныйОСРЖД.Служебный) КАК СчетКт,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка) КАК ВидСубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	-СтоимостьСтарогоОсновногоСредства.Стоимость КАК Сумма,
		|	""Разукрупнение ОС"" КАК Содержание,
		|	Шапкадокумента.ДокументОснование КАК ПервичныйДокумент
		|ИЗ
		|	СтоимостьСтарогоОсновногоСредства КАК СтоимостьСтарогоОсновногоСредства,
		|	ПараметрыОсновногоСредства КАК ПараметрыОсновногоСредства,
		|	Шапкадокумента КАК Шапкадокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.абс_МеждународныйОСРЖД.Служебный),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	НЕОПРЕДЕЛЕНО,
		|	ПараметрыОсновногоСредства.СчетНачисленияАмортизации,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|	СтоимостьСтарогоОсновногоСредства.ОсновноеСредство,
		|	-СтоимостьСтарогоОсновногоСредства.Амортизация,
		|	""Разукрупнение ОС"",
		|	Шапкадокумента.ДокументОснование
		|ИЗ
		|	ПараметрыОсновногоСредства КАК ПараметрыОсновногоСредства,
		|	Шапкадокумента КАК Шапкадокумента,
		|	СтоимостьСтарогоОсновногоСредства КАК СтоимостьСтарогоОсновногоСредства
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ТабличнаяЧастьОсновныеСредства.СчетУчета,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|	ТабличнаяЧастьОсновныеСредства.ОсновноеСредство,
		|	ЗНАЧЕНИЕ(ПланСчетов.абс_МеждународныйОСРЖД.Служебный),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	НЕОПРЕДЕЛЕНО,
		|	СтоимостьНовыхОсновныхСредств.Стоимость,
		|	""Разукрупнение ОС"",
		|	Шапкадокумента.ДокументОснование
		|ИЗ
		|	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНовыхОсновныхСредств КАК СтоимостьНовыхОсновныхСредств
		|		ПО ТабличнаяЧастьОсновныеСредства.НомерСтроки = СтоимостьНовыхОсновныхСредств.НомерСтроки,
		|	Шапкадокумента КАК Шапкадокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.абс_МеждународныйОСРЖД.Служебный),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
		|	НЕОПРЕДЕЛЕНО,
		|	ТабличнаяЧастьОсновныеСредства.СчетНачисленияАмортизации,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|	СтоимостьНовыхОсновныхСредств.ОсновноеСредство,
		|	СтоимостьНовыхОсновныхСредств.Амортизация,
		|	""Разукрупнение ОС"",
		|	Шапкадокумента.ДокументОснование
		|ИЗ
		|	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНовыхОсновныхСредств КАК СтоимостьНовыхОсновныхСредств
		|		ПО ТабличнаяЧастьОсновныеСредства.НомерСтроки = СтоимостьНовыхОсновныхСредств.НомерСтроки,
		|	Шапкадокумента КАК Шапкадокумента";
		
	СтруктураЗапросов.Вставить("Разукрупнение_ТекстЗапросаТаблица_абс_МеждународныйОСРЖД", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"УНИЧТОЖИТЬ ПараметрыОсновногоСредства";
		
	СтруктураЗапросов.Вставить("Разукрупнение_УдалениеВременныхТаблиц", ТекстЗапроса+";");
	
КонецПроцедуры














/////////////////////////////////////////////////////////////////////////////////
// Программный интерфейс проведения документов

Процедура ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Ссылка.Дата);
	Запрос.УстановитьПараметр("МоментДокумента", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация", Ссылка.Организация);
	Запрос.УстановитьПараметр("ОсновноеСредство", Ссылка.ОсновноеСредство);
	Запрос.УстановитьПараметр("ДокументОснование", Ссылка.ДокументОснование);
	
	ТекстыЗапросов = Новый СписокЗначений;
	
	ТекстыЗапросов.Добавить(ТекстЗапросаТабличнаяЧастьОсновныеСредства(), "");
	ТекстыЗапросов.Добавить(абс_ВнеоборотныеАктивы.ТекстЗапроса_ОсновныеСредстваРЖДПоОдномуОС("ОсновноеСредство", "Период", "ПараметрыОсновногоСредства"), "");
	ТекстыЗапросов.Добавить(ТекстЗапросаСтоимостьСтарогоОсновногоСредства(), "");
	ТекстыЗапросов.Добавить(ТекстЗапросаИзменениеСтоимостиОсновныхСредств(), "");
	
	ТекстыЗапросов.Добавить(ТекстЗапросаТаблица_абс_ОсновныеСредстваРЖД(), "Таблица_абс_ОсновныеСредстваРЖД");
	ТекстыЗапросов.Добавить(ТекстЗапросаТаблица_абс_СтоимостьОСРЖД(), "Таблица_абс_СтоимостьОСРЖД");
	ТекстыЗапросов.Добавить(ТекстЗапросаТаблица_абс_МеждународныйОСРЖД(), "Таблица_абс_МеждународныйОСРЖД");
	
	абс_ПроведениеДокументов.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапросов, ДополнительныеСвойства.ТаблицыДляДвижений);
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////
// Функции, возвращающие тексты запросов для инициализации данных документа

Функция ТекстЗапросаТабличнаяЧастьОсновныеСредства()
	
	Возврат "ВЫБРАТЬ
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.Коэффициент,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.СчетУчета,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.СчетНачисленияАмортизации,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.СчетЗатрат,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.Субконто1,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.Субконто2,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.Субконто3,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.СчетСниженияСтоимости,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.Местонахождение,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.МОЛ,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.КоэффициентУскорения,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.НачислятьАмортизацию,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.СрокПолезногоИспользования,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.ДатаПринятияКУчету,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.ОсновноеСредство.КодПоОКОФ КАК ОКОФ,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.ОсновноеСредство.абс_ГруппаОСРЖД КАК ГруппаОСРЖД,
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.МетодНачисленияАмортизации
	        |ПОМЕСТИТЬ ТабличнаяЧастьОсновныеСредства
	        |ИЗ
	        |	Документ.абс_РазукрупнениеОСРЖД.ОсновныеСредства КАК абс_РазукрупнениеОСРЖДОсновныеСредства
	        |ГДЕ
	        |	абс_РазукрупнениеОСРЖДОсновныеСредства.Ссылка = &Ссылка
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ОсновноеСредство
			|;
			|";
	
КонецФункции

Функция ТекстЗапросаСтоимостьСтарогоОсновногоСредства()
	
	Возврат "ВЫБРАТЬ
	        |	абс_СтоимостьОСРЖДОстатки.ОсновноеСредство,
	        |	абс_СтоимостьОСРЖДОстатки.СтоимостьОстаток КАК Стоимость,
	        |	абс_СтоимостьОСРЖДОстатки.АмортизацияОстаток КАК Амортизация
	        |ПОМЕСТИТЬ СтоимостьСтарогоОсновногоСредства
	        |ИЗ
	        |	РегистрНакопления.абс_СтоимостьОСРЖД.Остатки(
	        |			&МоментДокумента,
	        |			ОсновноеСредство = &ОсновноеСредство
	        |				И Организация = &Организация) КАК абс_СтоимостьОСРЖДОстатки
			|;
			|";
	
КонецФункции

Функция ТекстЗапросаИзменениеСтоимостиОсновныхСредств()
	
	Возврат "ВЫБРАТЬ
	        |	ВложенныйЗапрос.ОсновноеСредство,
	        |	СУММА(ВложенныйЗапрос.Стоимость) КАК Стоимость,
	        |	СУММА(ВложенныйЗапрос.Амортизация) КАК Амортизация
	        |ПОМЕСТИТЬ ИзменениеСтоимостиОсновныхСредств
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		СтоимостьСтарогоОсновногоСредства.ОсновноеСредство КАК ОсновноеСредство,
	        |		-СтоимостьСтарогоОсновногоСредства.Стоимость КАК Стоимость,
	        |		-СтоимостьСтарогоОсновногоСредства.Амортизация КАК Амортизация
	        |	ИЗ
	        |		СтоимостьСтарогоОсновногоСредства КАК СтоимостьСтарогоОсновногоСредства
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		ТабличнаяЧастьОсновныеСредства.ОсновноеСредство,
	        |		ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьСтарогоОсновногоСредства.Стоимость, 0) * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)),
	        |		ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьСтарогоОсновногоСредства.Амортизация, 0) * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2))
	        |	ИЗ
	        |		ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
	        |			ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьСтарогоОсновногоСредства КАК СтоимостьСтарогоОсновногоСредства
	        |			ПО (ИСТИНА)) КАК ВложенныйЗапрос
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВложенныйЗапрос.ОсновноеСредство
			|;
			|";
	
КонецФункции



Функция ТекстЗапросаТаблица_абс_ОсновныеСредстваРЖД()
	
	Возврат "ВЫБРАТЬ
	        |	&Период,
	        |	ТабличнаяЧастьОсновныеСредства.ОсновноеСредство,
	        |	&Организация,
	        |	ТабличнаяЧастьОсновныеСредства.ДатаПринятияКУчету,
	        |	ТабличнаяЧастьОсновныеСредства.Местонахождение КАК МестонахождениеОбъекта,
	        |	ТабличнаяЧастьОсновныеСредства.МОЛ,
	        |	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВведеноВЭксплуатацию) КАК Состояние,
	        |	ТабличнаяЧастьОсновныеСредства.СчетУчета,
	        |	ТабличнаяЧастьОсновныеСредства.СрокПолезногоИспользования,
	        |	ТабличнаяЧастьОсновныеСредства.НачислятьАмортизацию,
	        |	ТабличнаяЧастьОсновныеСредства.МетодНачисленияАмортизации,
	        |	ТабличнаяЧастьОсновныеСредства.СчетНачисленияАмортизации,
	        |	ВЫРАЗИТЬ(ЕСТЬNULL(ПараметрыОсновногоСредства.ПервоначальнаяСтоимость, ЕСТЬNULL(СтоимостьСтарогоОсновногоСредства.Стоимость, 0)) * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)) КАК ПервоначальнаяСтоимость,
	        |	ВЫРАЗИТЬ(ЕСТЬNULL(ПараметрыОсновногоСредства.СправедливаяСтоимость, 0) * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)) КАК СправедливаяСтоимость,
	        |	ВЫРАЗИТЬ(ЕСТЬNULL(ПараметрыОсновногоСредства.ЛиквидационнаяСтоимость, 0) * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)) КАК ЛиквидационнаяСтоимость,
	        |	ТабличнаяЧастьОсновныеСредства.СчетЗатрат,
	        |	ТабличнаяЧастьОсновныеСредства.Субконто1,
	        |	ТабличнаяЧастьОсновныеСредства.Субконто2,
	        |	ТабличнаяЧастьОсновныеСредства.Субконто3,
	        |	ЕСТЬNULL(ПараметрыОсновногоСредства.ПредполагаемыйОбъемПродукции, 0) КАК ПредполагаемыйОбъемПродукции,
	        |	ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьСтарогоОсновногоСредства.Амортизация, 0) * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)) КАК СуммаНачисленнойАмортизации,
	        |	ВЫРАЗИТЬ(ЕСТЬNULL(ПараметрыОсновногоСредства.УбытокОтОбесценения, 0) * ТабличнаяЧастьОсновныеСредства.Коэффициент КАК ЧИСЛО(15, 2)) КАК УбытокОтОбесценения,
	        |	ТабличнаяЧастьОсновныеСредства.КоэффициентУскорения,
	        |	ТабличнаяЧастьОсновныеСредства.СчетСниженияСтоимости
	        |ИЗ
	        |	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновногоСредства КАК ПараметрыОсновногоСредства
	        |		ПО (ИСТИНА),
	        |	СтоимостьСтарогоОсновногоСредства КАК СтоимостьСтарогоОсновногоСредства
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	&Период,
	        |	ПараметрыОсновногоСредства.ОсновноеСредство,
	        |	ПараметрыОсновногоСредства.Организация,
	        |	ПараметрыОсновногоСредства.ДатаПринятияКУчету,
	        |	ПараметрыОсновногоСредства.МестонахождениеОбъекта,
	        |	ПараметрыОсновногоСредства.МОЛ,
	        |	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета),
	        |	ПараметрыОсновногоСредства.СчетУчета,
	        |	ПараметрыОсновногоСредства.СрокПолезногоИспользования,
	        |	ЛОЖЬ,
	        |	ПараметрыОсновногоСредства.МетодНачисленияАмортизации,
	        |	ПараметрыОсновногоСредства.СчетНачисленияАмортизации,
	        |	ПараметрыОсновногоСредства.ПервоначальнаяСтоимость,
	        |	ПараметрыОсновногоСредства.СправедливаяСтоимость,
	        |	ПараметрыОсновногоСредства.ЛиквидационнаяСтоимость,
	        |	ПараметрыОсновногоСредства.СчетЗатрат,
	        |	ПараметрыОсновногоСредства.Субконто1,
	        |	ПараметрыОсновногоСредства.Субконто2,
	        |	ПараметрыОсновногоСредства.Субконто3,
	        |	ПараметрыОсновногоСредства.ПредполагаемыйОбъемПродукции,
	        |	ПараметрыОсновногоСредства.СуммаНачисленнойАмортизации,
	        |	ПараметрыОсновногоСредства.УбытокОтОбесценения,
	        |	ПараметрыОсновногоСредства.КоэффициентУскорения,
	        |	ПараметрыОсновногоСредства.СчетСниженияСтоимости
	        |ИЗ
	        |	ПараметрыОсновногоСредства КАК ПараметрыОсновногоСредства
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
	        |		ПО ПараметрыОсновногоСредства.ОсновноеСредство = ТабличнаяЧастьОсновныеСредства.ОсновноеСредство
	        |ГДЕ
	        |	ТабличнаяЧастьОсновныеСредства.ОсновноеСредство ЕСТЬ NULL
			|;
			|";
	
КонецФункции

Функция ТекстЗапросаТаблица_абс_СтоимостьОСРЖД()
	
	Возврат "ВЫБРАТЬ
	        |	&Период,
	        |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	        |	&Организация,
	        |	ИзменениеСтоимостиОсновныхСредств.ОсновноеСредство,
	        |	ИзменениеСтоимостиОсновныхСредств.Стоимость,
	        |	ИзменениеСтоимостиОсновныхСредств.Амортизация
	        |ИЗ
	        |	ИзменениеСтоимостиОсновныхСредств КАК ИзменениеСтоимостиОсновныхСредств
			|;
			|";
	
КонецФункции

Функция ТекстЗапросаТаблица_абс_МеждународныйОСРЖД()
	
	Возврат "ВЫБРАТЬ
	        |	&Период,
	        |	&Организация,
	        |	ЕСТЬNULL(ТабличнаяЧастьОсновныеСредства.СчетУчета, ПараметрыОсновногоСредства.СчетУчета) КАК СчетДт,
	        |	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства) КАК ВидСубконтоДт1,
	        |	ИзменениеСтоимостиОсновныхСредств.ОсновноеСредство КАК СубконтоДт1,
	        |	ЗНАЧЕНИЕ(ПланСчетов.абс_МеждународныйОСРЖД.Служебный) КАК СчетКт,
	        |	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка) КАК ВидСубконтоКт1,
	        |	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	        |	ИзменениеСтоимостиОсновныхСредств.Стоимость КАК Сумма,
	        |	&ДокументОснование КАК ПервичныйДокумент
	        |ИЗ
	        |	ИзменениеСтоимостиОсновныхСредств КАК ИзменениеСтоимостиОсновныхСредств
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
	        |		ПО ИзменениеСтоимостиОсновныхСредств.ОсновноеСредство = ТабличнаяЧастьОсновныеСредства.ОсновноеСредство
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновногоСредства КАК ПараметрыОсновногоСредства
	        |		ПО ИзменениеСтоимостиОсновныхСредств.ОсновноеСредство = ПараметрыОсновногоСредства.ОсновноеСредство
	        |ГДЕ
	        |	ИзменениеСтоимостиОсновныхСредств.Стоимость <> 0
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	&Период,
	        |	&Организация,
	        |	ЗНАЧЕНИЕ(ПланСчетов.абс_МеждународныйОСРЖД.Служебный),
	        |	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка),
	        |	НЕОПРЕДЕЛЕНО,
	        |	ЕСТЬNULL(ТабличнаяЧастьОсновныеСредства.СчетНачисленияАмортизации, ПараметрыОсновногоСредства.СчетНачисленияАмортизации),
	        |	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	        |	ИзменениеСтоимостиОсновныхСредств.ОсновноеСредство,
	        |	ИзменениеСтоимостиОсновныхСредств.Амортизация,
	        |	&ДокументОснование
	        |ИЗ
	        |	ИзменениеСтоимостиОсновныхСредств КАК ИзменениеСтоимостиОсновныхСредств
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
	        |		ПО ИзменениеСтоимостиОсновныхСредств.ОсновноеСредство = ТабличнаяЧастьОсновныеСредства.ОсновноеСредство
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновногоСредства КАК ПараметрыОсновногоСредства
	        |		ПО ИзменениеСтоимостиОсновныхСредств.ОсновноеСредство = ПараметрыОсновногоСредства.ОсновноеСредство
	        |ГДЕ
	        |	ИзменениеСтоимостиОсновныхСредств.Амортизация <> 0
			|;
			|";
	
КонецФункции

