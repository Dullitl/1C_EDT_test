
&НаКлиенте
Функция ПеременнаяСодержитСвойство(Переменная, ИмяСвойства)
	
	// Инициализируем структуру для теста с ключом (значение переменной "ИмяСвойства") и значением NULL
	СтруктураПроверка = Новый Структура;
	СтруктураПроверка.Вставить(ИмяСвойства, NULL);
	
	// Заполняем созданную структуру из переданного значения переменной
	ЗаполнитьЗначенияСвойств(СтруктураПроверка, Переменная);
	
	// Если значение для свойства структуры осталось NULL, то искомое свойство не найдено, и наоборот.
	Если СтруктураПроверка[ИмяСвойства] = NULL Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьТолькоПросмотрВсемЭлементам(мТолькоПросмотр = Истина)

	Для Каждого Элемент Из Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ДекорацияФормы") Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			Элемент.ТолькоПросмотр = мТолькоПросмотр;
		Исключение
			Элемент.Доступность = Не мТолькоПросмотр;						
		КонецПопытки;
	КонецЦикла;
	Элементы.ФормаДокументttk_УстановкаСоответствияНоменклатурыБюджетнымАналитикамкмдПерейтиСоответствиеБиллинговыхУслуг.Доступность = Истина;
	Элементы.ФормаДокументttk_УстановкаСоответствияНоменклатурыБюджетнымАналитикамкмдПерейтиСоответствиеБюджетныхАналитик.Доступность = Истина;
    Элементы.Ответственный.ТолькоПросмотр = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоответствиеБюджетныхАналитик(Организация)
	
	СписокСоответствия = Документы.ttk_УстановкаСоответствияНоменклатурыБюджетнымАналитикам.ЗаполнитьСоответствиеБюджетныхАналитик(Организация);
	Объект.СоответствиеБюджетныхАналитик.Загрузить(СписокСоответствия);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоответствиеБиллинговыхУслуг(Организация)
	
	СписокСоответствия = Документы.ttk_УстановкаСоответствияНоменклатурыБюджетнымАналитикам.ЗаполнитьСоответствиеБиллинговыхУслуг(Организация);
	Объект.СоответствиеБиллинговыхУслуг.Загрузить(СписокСоответствия);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличнуюЧастьДокумента(ТЧ)
	
	ТабличныеЧасти = Новый Соответствие;
	ТабличныеЧасти.Вставить("СоответствиеБюджетныхАналитик", "Соответствие бюджетных аналитик");
	ТабличныеЧасти.Вставить("СоответствиеБиллинговыхУслуг", "Соответствие биллинговых услуг");
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Текст = "Для заполнения табличной части документа необходимо выбрать организацию.";
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Текст;
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Объект[ТЧ].Количество() > 0 Тогда
		Ответ = Вопрос("Перед заполнением табличная часть документа '" + Строка(ТабличныеЧасти.Получить(ТЧ)) + "' будет очищена. Продолжить?", РежимДиалогаВопрос.ОКОтмена);
		Если Ответ = КодВозвратаДиалога.Отмена Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ТЧ = "СоответствиеБюджетныхАналитик" Тогда	
		ЗаполнитьСоответствиеБюджетныхАналитик(Объект.Организация);
	ИначеЕсли ТЧ = "СоответствиеБиллинговыхУслуг" Тогда
		ЗаполнитьСоответствиеБиллинговыхУслуг(Объект.Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыНаСервере()
	
	ИсторияСогласования.Параметры.УстановитьЗначениеПараметра("Дт", ТекущаяДата());
	ИсторияСогласования.Параметры.УстановитьЗначениеПараметра("СсылкаНаДокумент", Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимость()
	
	мРолиПользователя = абс_БизнесПроцессы.ПолучитьСписокДоступныхРолейПользователя(глЗначениеПеременной("глТекущийПользователь"));	
	
	УстановитьТолькоПросмотрВсемЭлементам();
	Если мСтатус = ПредопределенноеЗначение("Перечисление.абс_СтатусыПервичныхДокументов.Подготовка") Тогда
		УстановитьТолькоПросмотрВсемЭлементам(Ложь);
	ИначеЕсли мСтатус = ПредопределенноеЗначение("Перечисление.абс_СтатусыПервичныхДокументов.СогласованиеОФК") Тогда
		Если (Не мРолиПользователя.Найти(Справочники.РолиИсполнителей.СогласованиеДЭФ) = Неопределено Или
				Не мРолиПользователя.Найти(Справочники.РолиИсполнителей.СуперПользователь) = Неопределено) Тогда
			УстановитьТолькоПросмотрВсемЭлементам(Ложь);
		КонецЕсли;
	Иначе			
		УстановитьТолькоПросмотрВсемЭлементам(Ложь);
	КонецЕсли;		
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьСтатусыДокументаНаСервере()

	Документы.ttk_УстановкаСоответствияНоменклатурыБюджетнымАналитикам.ЗаполнитьСписокДоступныхСтатусов(Элементы.мСтатус.СписокВыбора, Объект.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьСтатусыДокументаНаСервере();
	мСтатус = Объект.Статус;
	ТекущаяОрганизация = Объект.Организация;
	ОбновитьВидимость();
	Если ЭтоНовый Тогда
		кмдЗаполнить(Элементы.ФормакмдЗаполнить);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоНовый = (Параметры.Ключ.Пустая() И Параметры.ЗначениеКопирования.Пустая());	
	УстановитьПараметрыНаСервере();	
	вСтатус = Объект.Статус;	
	
КонецПроцедуры

&НаКлиенте
Процедура мСтатусПриИзменении(Элемент)
	
	Объект.Статус = мСтатус;	
	ЗаполнитьСтатусыДокументаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьВидимость();
	УстановитьПараметрыНаСервере();
	Элементы.ИсторияСогласования.Обновить();
	ЗаполнитьСтатусыДокументаНаСервере();
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если вСтатус <> Объект.Статус Тогда
		ПричинаИзмененияСтатуса = "";
		Если ВвестиСтроку(ПричинаИзмененияСтатуса, "Укажите причину изменения статуса.", , Истина) Тогда
			Объект.ПричинаИзмененияСтатуса = ПричинаИзмененияСтатуса;
		Иначе 
			мСтатус = вСтатус;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура кмдЗаполнитьСоответствиеБюджетныхАналитик(Команда)
	
	ЗаполнитьТабличнуюЧастьДокумента("СоответствиеБюджетныхАналитик");
	
КонецПроцедуры

&НаКлиенте
Процедура кмдЗаполнитьСоответствиеБиллинговыхУслуг(Команда)
	
	ЗаполнитьТабличнуюЧастьДокумента("СоответствиеБиллинговыхУслуг");
	
КонецПроцедуры

&НаКлиенте
Процедура кмдЗаполнить(Команда)
	
	ЗаполнитьТабличнуюЧастьДокумента("СоответствиеБюджетныхАналитик");
	ЗаполнитьТабличнуюЧастьДокумента("СоответствиеБиллинговыхУслуг");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ТекущаяОрганизация <> Объект.Организация Тогда
		ЗаполнитьТабличнуюЧастьДокумента("СоответствиеБюджетныхАналитик");
		ЗаполнитьТабличнуюЧастьДокумента("СоответствиеБиллинговыхУслуг");		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущаяОрганизация = Объект.Организация;
	
КонецПроцедуры
