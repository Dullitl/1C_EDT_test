Перем мВалютаРегламентированногоУчета Экспорт;
Перем мИмяВалютыРегламентированногоУчета Экспорт;
Перем мПрежнееОтражатьВУправленческомУчете;
Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

#Если Клиент Тогда

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "ИсторияПогашения" Тогда
		
		ТабДокумент = Новый ТабличныйДокумент;
		Макет = ПолучитьМакет("ИсторияПогашенияЗайма");
		СекцияШапка = Макет.ПолучитьОбласть("Шапка");
		СекцияМесяц = Макет.ПолучитьОбласть("Месяц");
		СекцияИтогоПогашено = Макет.ПолучитьОбласть("ИтогоПогашено");
		СекцияПодвал = Макет.ПолучитьОбласть("Подвал");
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
	    Запрос.УстановитьПараметр("ФизЛицо",ФизЛицо);
	    Запрос.УстановитьПараметр("Организация",Организация);
		Запрос.УстановитьПараметр("Рубли",Константы.ВалютаРегламентированногоУчета.Получить());
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорЗаймаСРаботником.Дата,
		|	ДоговорЗаймаСРаботником.Номер,
		|	ДоговорЗаймаСРаботником.ФизЛицо,
		|	ДоговорЗаймаСРаботником.ПроцентЗаПользованиеЗаймом,
		|	ВЫБОР
		|		КОГДА ДоговорЗаймаСРаботником.ВалютаДокумента = &Рубли
		|			ТОГДА ""руб.""
		|		ИНАЧЕ ДоговорЗаймаСРаботником.ВалютаДокумента.Наименование
		|	КОНЕЦ КАК НаименованиеВалюты,
		|	ДоговорЗаймаСРаботником.СуммаЗайма,
		|	ДоговорЗаймаСРаботником.СрокПогашения,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ДоговорЗаймаСРаботником.ФизЛицо.Наименование) КАК Работник
		|ИЗ
		|	Документ.ДоговорЗаймаСРаботником КАК ДоговорЗаймаСРаботником
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(, ФизЛицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ДоговорЗаймаСРаботником.Ссылка = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		СекцияШапка.Параметры.Заполнить(Выборка);
		
		ПериодПогашения = "";
		Если ПорядокПогашенияЗайма = Перечисления.ПорядокПогашенияЗаймаПроцентов.Ежемесячно Тогда
			УниверсальныеМеханизмы.Просклонять(глЗначениеПеременной("глКомпонентаСклоненияФИО"), НРег(Формат(НачалоПогашения,"ДФ='ММММ гггг'")), 2, , ПериодПогашения);
		КонецЕсли;
		СекцияШапка.Параметры.ОписаниеНачалаПогашения = НРег(ПорядокПогашенияЗайма) + ?(ПорядокПогашенияЗайма = Перечисления.ПорядокПогашенияЗаймаПроцентов.Ежемесячно," с " + ПериодПогашения + " г.","");
		
		ВалютаУчета = ?(ОтражатьВУправленческомУчете,Константы.ВалютаУправленческогоУчета.Получить(),Константы.ВалютаРегламентированногоУчета.Получить());
		СекцияШапка.Параметры.ИмяВалютыУчета = ВалютаУчета.НаименованиеПолное;
		ТабДокумент.Вывести(СекцияШапка);

		Если ОтражатьВУправленческомУчете Тогда
			ИмяРегистраУчета = "ПогашениеЗаймовРаботниками";
			ТекстНачислений = 
			"ВЫБРАТЬ
			|	СУММА(УправленческиеНачисления.Результат) КАК Результат,
			|	УправленческиеНачисления.ПериодРегистрации КАК ПериодРегистрации
			|ИЗ
			|	РегистрРасчета.УправленческиеНачисления КАК УправленческиеНачисления
			|ГДЕ
			|	УправленческиеНачисления.ФизЛицо = &ФизЛицо
			|
			|СГРУППИРОВАТЬ ПО
			|	УправленческиеНачисления.ПериодРегистрации";
		Иначе
			ИмяРегистраУчета = "ПогашениеЗаймовРаботникамиОрганизаций";
			ТекстНачислений = 
			"ВЫБРАТЬ
			|	СУММА(РезультатыНачислений.Результат) КАК Результат,
			|	РезультатыНачислений.ПериодРегистрации КАК ПериодРегистрации
			|ИЗ
			|	(ВЫБРАТЬ
			|		ДополнительныеНачисленияРаботниковОрганизаций.Результат КАК Результат,
			|		НАЧАЛОПЕРИОДА(ДополнительныеНачисленияРаботниковОрганизаций.ПериодРегистрации, МЕСЯЦ) КАК ПериодРегистрации
			|	ИЗ
			|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисленияРаботниковОрганизаций
			|	ГДЕ
			|		ДополнительныеНачисленияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
			|		И ДополнительныеНачисленияРаботниковОрганизаций.ОбособленноеПодразделение = &Организация
			|		И (НЕ ДополнительныеНачисленияРаботниковОрганизаций.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ОсновныеНачисленияРаботниковОрганизаций.Результат,
			|		ОсновныеНачисленияРаботниковОрганизаций.ПериодРегистрации
			|	ИЗ
			|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисленияРаботниковОрганизаций
			|	ГДЕ
			|		ОсновныеНачисленияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
			|		И ОсновныеНачисленияРаботниковОрганизаций.ОбособленноеПодразделение = &Организация
			|		И (НЕ ОсновныеНачисленияРаботниковОрганизаций.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме)) КАК РезультатыНачислений
			|
			|СГРУППИРОВАТЬ ПО
			|	РезультатыНачислений.ПериодРегистрации";
		КонецЕсли;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПогашениеЗаймов.Период КАК Период,
		|	ПогашениеЗаймов.ПроцентыПриход КАК ПроцентыПриход,
		|	ПогашениеЗаймов.ПроцентыКонечныйОстаток КАК ПроцентыКонечныйОстаток,
		|	ПогашениеЗаймов.ПроцентыРасход КАК ПроцентыРасход,
		|	ПогашениеЗаймов.ОсновнойДолгРасход КАК ОсновнойДолгРасход,
		|	ПогашениеЗаймов.ОсновнойДолгКонечныйОстаток КАК ОсновнойДолгКонечныйОстаток,
		|	ПогашениеЗаймов.ПроцентыКонечныйОстаток + ПогашениеЗаймов.ОсновнойДолгКонечныйОстаток КАК ОстатокЗайма,
		|	НачисленныеСуммы.Результат КАК Зарплата
		|ИЗ
		|	РегистрНакопления." + ИмяРегистраУчета + ".ОстаткиИОбороты(
		|		,
		|		,
		|		Месяц,
		|		,
		|		ДоговорЗайма = &Ссылка
		|		    И ФизЛицо = &ФизЛицо) КАК ПогашениеЗаймов
		|		ЛЕВОЕ СОЕДИНЕНИЕ (" + ТекстНачислений + ") КАК НачисленныеСуммы
		|		ПО ПогашениеЗаймов.Период = НачисленныеСуммы.ПериодРегистрации
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период
		|ИТОГИ
		|	СУММА(ПроцентыРасход),
		|	СУММА(ОсновнойДолгРасход)
		|ПО
		|	ОБЩИЕ";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ОбщийИтог Тогда
				СекцияИтогоПогашено.Параметры.Заполнить(Выборка);
			Иначе
				СекцияМесяц.Параметры.Заполнить(Выборка);
				СекцияПодвал.Параметры.Заполнить(Выборка);
				ТабДокумент.Вывести(СекцияМесяц);
			КонецЕсли;
		КонецЦикла;
		СекцияИтогоПогашено.Параметры.ДатаОкончания = Формат(ДобавитьМесяц(НачалоПогашения, СрокПогашения),"ДЛФ=DD");
		ТабДокумент.Вывести(СекцияИтогоПогашено);
		ТабДокумент.Вывести(СекцияПодвал);
		Возврат УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ttk_ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект,"Отчет о погашении займа"));
		
	ИначеЕсли ИмяМакета = "Договор"  Тогда
		
		ТабДокумент = Новый ТабличныйДокумент;
		Макет = ПолучитьМакет("Договор");
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("СтруктурнаяЕдиница",Организация);
	    Запрос.УстановитьПараметр("Дата",Дата);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтветственныеЛицаСрезПоследних.ФизическоеЛицо.Наименование КАК ФизическоеЛицоНаименование,
		|	ОтветственныеЛицаСрезПоследних.Должность.Наименование КАК Должность,
		|	ОтветственныеЛицаСрезПоследних.ФизическоеЛицо,
		|	ФизическиеЛица.Комментарий
		|ПОМЕСТИТЬ ВТДанныеОбОтветственномЛице
		|ИЗ
		|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
		|			&Дата,
		|			СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|				И ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель)) КАК ОтветственныеЛицаСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ПО ОтветственныеЛицаСрезПоследних.ФизическоеЛицо = ФизическиеЛица.Ссылка";
		Запрос.Выполнить();
		
		Запрос.УстановитьПараметр("Рубли",Константы.ВалютаРегламентированногоУчета.Получить());
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
	    Запрос.УстановитьПараметр("ФизЛицо",ФизЛицо);

		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорЗаймаСРаботником.Дата,
		|	ДоговорЗаймаСРаботником.Номер,
		|	ВЫРАЗИТЬ(ДоговорЗаймаСРаботником.Организация.НаименованиеПолное КАК СТРОКА(300)) КАК НазваниеОрганизации,
		|	ДоговорЗаймаСРаботником.ФизЛицо,
		|	ДоговорЗаймаСРаботником.ПроцентЗаПользованиеЗаймом,
		|	ВЫБОР
		|		КОГДА ДоговорЗаймаСРаботником.ВалютаДокумента = &Рубли
		|			ТОГДА ""руб.""
		|		ИНАЧЕ ДоговорЗаймаСРаботником.ВалютаДокумента.Наименование
		|	КОНЕЦ КАК НаименованиеВалюты,
		|	ДоговорЗаймаСРаботником.СуммаЗайма,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ДоговорЗаймаСРаботником.ФизЛицо.Наименование) КАК Работник,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид.Наименование КАК ДокументВид,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер,
		|	КонтактнаяИнформация.Представление КАК АдресОрганизации,
		|	ВЫБОР
		|		КОГДА ДоговорЗаймаСРаботником.ФизЛицо.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическихЛиц.Женский)
		|			ТОГДА ""ая""
		|		ИНАЧЕ ""ый""
		|	КОНЕЦ КАК Окончание,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность КАК ДолжностьРуководителя,
		|	ЕСТЬNULL(ФИОФизЛиц.Фамилия + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОФизЛиц.Имя, 1, 1) <> """"
		|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛиц.Имя, 1, 1) + "".""
		|			ИНАЧЕ """"
		|		КОНЕЦ + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОФизЛиц.Отчество, 1, 1) <> """"
		|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛиц.Отчество, 1, 1) + "".""
		|			ИНАЧЕ """"
		|		КОНЕЦ, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицоНаименование) КАК ФИОРуководителя
		|ИЗ
		|	Документ.ДоговорЗаймаСРаботником КАК ДоговорЗаймаСРаботником
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ФизЛицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(&Дата, ФизЛицо = &ФизЛицо) КАК ПаспортныеДанныеФизЛицСрезПоследних
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|		ПО ДоговорЗаймаСРаботником.Организация = КонтактнаяИнформация.Объект
		|			И (КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
		|			И (КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбОтветственномЛице КАК ОтветственныеЛицаОрганизацийСрезПоследних
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
		|				&Дата,
		|				ФизЛицо В
		|					(ВЫБРАТЬ
		|						ДанныеОбОтветственномЛице.ФизическоеЛицо
		|					ИЗ
		|						ВТДанныеОбОтветственномЛице КАК ДанныеОбОтветственномЛице)) КАК ФИОФизЛиц
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ДоговорЗаймаСРаботником.Ссылка = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		Макет.Параметры.Заполнить(Выборка);
		Макет.Параметры.Дата = Формат(Дата,"ДЛФ=DD");
		Макет.Параметры.ОкончаниеПогашения = Формат(ДобавитьМесяц(НачалоПогашения, СрокПогашения),"ДЛФ=DD");
		Макет.Параметры.РабочаяДата = ОбщегоНазначенияЗК.ПолучитьРабочуюДату();
		Макет.Параметры.ТекстПорядка = НРег(ПорядокПогашенияЗайма);
		Макет.Параметры.НазваниеОрганизации = СокрЛП(Макет.Параметры.НазваниеОрганизации);
		ТабДокумент.Вывести(Макет);
		
		Возврат УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ttk_ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект,"Договор займа"));
		
	КонецЕсли;

КонецФункции // Печать

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("ИсторияПогашения,Договор","Отчет о погашении займа", "Договор займа");

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  Отказ 					- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеШапки(Отказ, Заголовок)

	// ФизЛицо
	Если НЕ ЗначениеЗаполнено(ФизЛицо) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан сотрудник!", Отказ, Заголовок);
	КонецЕсли;

	Если СрокПогашения = 0 Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан срок погашения займа!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НачалоПогашения) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указано начало погашения займа!", Отказ, Заголовок);
		
	ИначеЕсли НачалоПогашения < НачалоМесяца(Дата) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Начало погашения займа не может предшествовать месяцу выдачи займа!", Отказ, Заголовок);
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указана валюта займа!", Отказ, Заголовок);
		
	ИначеЕсли КурсДокумента = 0 Тогда 
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан курс валюты займа!", Отказ, Заголовок);
		
	КонецЕсли;
	
    ПроверитьЗаполнениеШапкиРегл(Отказ, Заголовок)
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  Отказ 					- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеШапкиРегл(Отказ, Заголовок)

	//  Организация
	Если ОтражатьВБухгалтерскомУчете и НЕ ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указана организация!", Отказ, Заголовок);
	КонецЕсли;
	
	// ставка НДФЛ
	Если НачислятьМатериальнуюВыгоду и НЕ ЗначениеЗаполнено(СтавкаНалогообложенияРезидента) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указана ставка налогообложения НДФЛ материальной выгоды по заемным средствам!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ОтражатьВБухгалтерскомУчете и ПроцентЗаПользованиеЗаймом > 0 и НЕ ЗначениеЗаполнено(СчетУчетаПроцентовПоЗайму) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан бухгалтерский счет учета процентов по займу!", Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Процедура формирования движения регистров
//
Процедура ДвиженияРегистров()
	
	ДвиженияРегистровУпр();
	ДвиженияРегистровРегл();
	
КонецПроцедуры // ДвиженияРегистров()

Процедура ДвиженияРегистровУпр()
	
	Если ОтражатьВУправленческомУчете Тогда
		
		Движение = Движения.ПогашениеЗаймовРаботниками.Добавить();

		// Свойства
		Движение.Период			= Дата;
		Движение.ВидДвижения	= ВидДвиженияНакопления.Приход;
		
		// Измерения
		Движение.ФизЛицо		= ФизЛицо;
		Движение.ДоговорЗайма	= Ссылка;
		
		ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
		СтруктураКурса = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаУправленческогоУчета,Дата);
		
		// Ресурсы
		Движение.ОсновнойДолг	= МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаЗайма,ВалютаДокумента,ВалютаУправленческогоУчета,
																		КурсДокумента,СтруктураКурса.Курс,
																		КратностьДокумента,СтруктураКурса.Кратность);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДвиженияРегистровРегл()
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		
		Движение = Движения.ПогашениеЗаймовРаботникамиОрганизаций.Добавить();

		// Свойства
		Движение.Период			= Дата;
		Движение.ВидДвижения	= ВидДвиженияНакопления.Приход;
		
		// Измерения
		Движение.ФизЛицо		= ФизЛицо;
		Движение.Организация	= Организация;
		Движение.ДоговорЗайма	= Ссылка;
		
		// Ресурсы
		Движение.ОсновнойДолг	= МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаЗайма,ВалютаДокумента,мВалютаРегламентированногоУчета,
																		КурсДокумента,1,
																		КратностьДокумента,1);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// удалим движения
	УдалитьДвижения();
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ttk_ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	//Надо позвать проверку заполнения реквизитов шапки
	ПроверитьЗаполнениеШапки(Отказ, Заголовок);
	
	// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
	Если НЕ Отказ Тогда
		
		ДвиженияРегистров()

	КонецЕсли;
	
	ОбработкаКомментариев.ПоказатьСообщения();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Установим прежнее состояние ОтражатьВУправленческомУчете
	мПрежнееОтражатьВУправленческомУчете = Ссылка.ОтражатьВУправленческомУчете;
	
	мУдалятьДвижения = Не ЭтоНовый();
	
КонецПроцедуры

// удаление движений - документ не удаляет движения автоматически
Процедура УдалитьДвижения()

	Если НЕ мУдалятьДвижения Тогда
		Возврат;
	КонецЕсли;
	
	Если мПрежнееОтражатьВУправленческомУчете Тогда
		Движения.ПогашениеЗаймовРаботниками.Очистить();
		Движения.ПогашениеЗаймовРаботниками.Записать();
	Иначе
		Движения.ПогашениеЗаймовРаботникамиОрганизаций.Очистить();
		Движения.ПогашениеЗаймовРаботникамиОрганизаций.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	// удалим движения
	УдалитьДвижения();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИНИЦИАЛИЗАЦИЯ ОБЪЕКТА 

Запрос = Новый Запрос("ВЫБРАТЬ
                      |	Константы.ВалютаРегламентированногоУчета.Ссылка КАК Ссылка,
                      |	Константы.ВалютаРегламентированногоУчета.Наименование КАК Наименование
                      |ИЗ
                      |	Константы КАК Константы");

Выборка = Запрос.Выполнить().Выбрать();
Выборка.Следующий();
мИмяВалютыРегламентированногоУчета = Выборка.Наименование;
мВалютаРегламентированногоУчета = Выборка.Ссылка;
