
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДополнительныеПараметры = Неопределено;
	ПарметрыСписка = Список.Параметры.Элементы;
	Если Параметры.Свойство("ДополнительныеПараметры", ДополнительныеПараметры) Тогда 
		Для каждого Элем из ДополнительныеПараметры Цикл
			ПараметрОтбора = ПарметрыСписка.Найти(Элем.Ключ);
			Если Не ПараметрОтбора = Неопределено Тогда
				Список.Параметры.УстановитьЗначениеПараметра(Элем.Ключ, Элем.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДополнительныйОтбор = Неопределено;
	Если Параметры.Свойство("ДополнительныйОтбор", ДополнительныйОтбор) Тогда 
		ОтборСписка = Список.Отбор.Элементы;
		ОтборСписка.Очистить();
		Для каждого Элем из ДополнительныйОтбор Цикл
			ЭлементОтбора					= ОтборСписка.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных(Элем.Ключ);
			ЭлементОтбора.ВидСравнения		= ?(ТипЗнч(Элем.Значение) = Тип("Массив"), ВидСравненияКомпоновкиДанных.ВСписке, ВидСравненияКомпоновкиДанных.Равно);
			ЭлементОтбора.ПравоеЗначение	= Элем.Значение;
			ЭлементОтбора.Использование		= Истина;		
			ЭлементОтбора.РежимОтображения	= РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;		
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

