
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ВидОперации") Тогда
		
		ОтборВидОперации = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		
		ОтборВидОперации.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
		ОтборВидОперации.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("ВидОперации");
		ОтборВидОперации.ПравоеЗначение = Параметры.ВидОперации;
		ОтборВидОперации.Использование	= Истина;
		
		ОтборВидОперации.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура УдалитьЭлементОтбораСписка(Список, ИмяПоля)

	ЭлементыДляУдаления = Новый Массив;
	
	ЭлементыОтбора = Список.Отбор.Элементы;
	ПолеКомпоновки = Новый ПолеКомпоновкиДанных(ИмяПоля);
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
			И ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновки Тогда
			ЭлементыДляУдаления.Добавить(ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлементОтбораДляУдаления Из ЭлементыДляУдаления Цикл
		ЭлементыОтбора.Удалить(ЭлементОтбораДляУдаления);
	КонецЦикла;

КонецПроцедуры // УстановитьУдалитьОтбор()      

&НаКлиенте
Процедура ИзменитьЭлементОтбораСписка(Список, ИмяПоля, ПравоеЗначение = Неопределено, Установить = Ложь, ВидСравнения = Неопределено) 
	
	УдалитьЭлементОтбораСписка(Список, ИмяПоля);
	
	Если Установить Тогда
		УстановитьЭлементОтбораСписка(Список, ИмяПоля, ПравоеЗначение, ВидСравнения);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура УстановитьЭлементОтбораСписка(Список, ИмяПоля, ПравоеЗначение, ВидСравнения = Неопределено, Представление = "") Экспорт
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ИмяПоля);	
	ЭлементОтбора.ВидСравнения   = ?(ВидСравнения = Неопределено, ВидСравненияКомпоновкиДанных.Равно, ВидСравнения);
	ЭлементОтбора.Использование  = Истина;
	ЭлементОтбора.ПравоеЗначение = ПравоеЗначение;
	ЭлементОтбора.Представление  = Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура ЦФО1ПриИзменении(Элемент)
	ИзменитьЭлементОтбораСписка(
		Список, 
		"ЦФО", 
		ЦФО, 
		ЗначениеЗаполнено(ЦФО));
КонецПроцедуры

&НаКлиенте
Процедура ПериодОтраженияПриИзменении(Элемент)
	ИзменитьЭлементОтбораСписка(
		Список, 
		"ПериодНачисления", 
		НачалоМесяца(ПериодОтражения), 
		ЗначениеЗаполнено(ПериодОтражения));
КонецПроцедуры

 