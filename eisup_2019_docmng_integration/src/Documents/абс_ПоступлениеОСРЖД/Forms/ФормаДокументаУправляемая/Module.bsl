
/////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ЗначенияЗаполнения.Количество()>0 Тогда
		ЗаполнитьЗначенияСвойств(Объект, Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	
КонецПроцедуры



/////////////////////////////////////////////////////////////////////////////////
// Обработчики событий элементов формы

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	ДокументОснованиеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДокументОснованиеПриИзмененииНаСервере();
	
	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	Док = РеквизитФормыВЗначение("Объект");
	Док.Заполнить(Объект.ДокументОснование);
	ЗначениеВРеквизитФормы(Док, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеСредстваОсновноеСредствоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОсновныеСредства.ТекущиеДанные;
	
	СтрокиОсновногоСредства = Объект.ОсновныеСредства.НайтиСтроки(Новый Структура("ОсновноеСредство", ТекущиеДанные.ОсновноеСредство));
	Если СтрокиОсновногоСредства.Количество()>1 Тогда
		Для Каждого СтрокаОС Из СтрокиОсновногоСредства Цикл
			Если СтрокаОС.НомерСтроки<>ТекущиеДанные.НомерСтроки Тогда
				ЗаполнитьЗначенияСвойств(ТекущиеДанные, СтрокаОС);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ПараметрыОС = ПолучитьПараметрыОсновногоСредства(ТекущиеДанные.ОсновноеСредство);
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПараметрыОС);
	КонецЕсли;
	
	ТекущиеДанные.Стоимость = 0;
	ТекущиеДанные.СуммаНачисленнойАмортизации = 0;
	ТекущиеДанные.СправедливаяСтоимость = 0;
	ТекущиеДанные.ЛиквидационнаяСтоимость = 0;
	ТекущиеДанные.УбытокОтОбесценения = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеСредстваПараметрыПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОсновныеСредства.ТекущиеДанные;
	
	ОсновноеСредство = ТекущиеДанные.ОсновноеСредство;
	Если Не ЗначениеЗаполнено(ОсновноеСредство) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКолонки = Сред(Элемент.Имя, 17);
	ЗначениеКолонки = ТекущиеДанные[ИмяКолонки];
	
	Для Каждого СтрокаОС Из Объект.ОсновныеСредства Цикл
		Если СтрокаОС.ОсновноеСредство=ОсновноеСредство Тогда
			СтрокаОС[ИмяКолонки] = ЗначениеКолонки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// Обработчики команд формы

&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	ДокументОснованиеПриИзмененииНаСервере();
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// Прочие процедуры и функции

&НаСервереБезКонтекста
Функция ПолучитьПараметрыОсновногоСредства(ОсновноеСредство)
	
	Возврат РегистрыСведений.абс_ОсновныеСредстваРЖД.ПолучитьПоследнее( , Новый Структура("ОсновноеСредство", ОсновноеСредство));
	
КонецФункции



