Перем мУдалятьДвижения;

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОтражатьВБухгалтерскомУчете Тогда
		ОтражатьВБухгалтерскомУчете = Истина;
	КонецЕсли;
	Если НЕ ОтражатьВНалоговомУчете Тогда
		ОтражатьВНалоговомУчете = Истина;
	КонецЕсли;
	Если НЕ ОтражатьВУправленческомУчете Тогда
		ОтражатьВУправленческомУчете = Истина;
	КонецЕсли;

	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью()

Процедура ОбработкаПроведения(Отказ,РежимПроведения)

	Если мУдалятьДвижения Тогда
		ttk_ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	Заголовок = ttk_ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	ТабОС = Комплектация.ВыгрузитьКолонку("ОсновноеСредство");
	
	// Проверим правильность заполнения документа
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);
	//ПроверитьЗаполнениеТабЧасти(РежимПроведения, ТабОС, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	Если НЕ Отказ Тогда
		ДвиженияПоРегистрам(ТабОС, СтруктураШапкиДокумента, Заголовок, Отказ);
	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()

// Процедура проверяет корректность заполнения реквизитов шапки документа
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	// Укажем, что надо проверить:
	СтруктураРеквизитовШапки = Новый Структура;
	СтруктураРеквизитовШапки.Вставить("Организация");
	СтруктураРеквизитовШапки.Вставить("ОсновноеСредство");
	СтруктураРеквизитовШапки.Вставить("СобытиеПринятияКУчету");
	СтруктураРеквизитовШапки.Вставить("СобытиеВводВЭксплуатацию");
	СтруктураРеквизитовШапки.Вставить("СобытиеСписание");
	
	Если НЕ ЗначениеЗаполнено(ГлавноеОсновноеСредство) Тогда
		ttk_ОбщегоНазначения.СообщитьОбОшибке("Не указано главное основное средство!", Отказ, Заголовок);
	КонецЕсли;
					
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураРеквизитовШапки, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Процедура формирования движений по регистрам.
//
Процедура ДвиженияПоРегистрам(ТабОС, СтруктураШапкиДокумента, Заголовок, Отказ)
	
	ТабАмортизации = Новый ТаблицаЗначений;
	ТабАмортизации.Колонки.Добавить("ОсновноеСредство",       Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
	ТабАмортизации.Колонки.Добавить("АмортизацияЗаМесяцБУ",   Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла( 15, 2)));
	ТабАмортизации.Колонки.Добавить("АмортизацияЗаМесяцНУ",   Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла( 15, 2)));
	
	//ДвижениеПоРегиструОСМеждународныйУчет( ТабОС, СтруктураШапкиДокумента);
	ФормированиеПроводокМеждународный( ТабОС, СтруктураШапкиДокумента);
	
КонецПроцедуры // ДвиженияПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ДВИЖЕНИЙ ДОКУМЕНТА ПО РЕГИСТРАМ

Процедура ДвижениеПоРегиструОСМеждународныйУчет(ТабОС,ТабСтоимостьОС,ТабАмортизация)
	
	НаборДвижений = РегистрыСведений.ОсновныеСредстваМеждународныйУчет.СоздатьНаборЗаписей();
	НаборДвижений.Отбор.Регистратор.Установить(Ссылка);
	Движение = НаборДвижений.Добавить();
	Движение.Регистратор = Ссылка;
	Движение.ОсновноеСредство = ОсновноеСредство;
	Движение.Период      = Дата;
	Движение.Организация = Организация;
	Движение.ДатаПринятияКУчету = Дата;
	Движение.Состояние   = Перечисления.ВидыСостоянийОС.ВведеноВЭксплуатацию;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.МОЛ,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СчетУчета,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СрокПолезногоИспользования,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.НачислятьАмортизацию,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СчетНачисленияАмортизации,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СчетЗатрат,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.Субконто1,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.Субконто2,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.Субконто3,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.ПредполагаемыйОбъемПродукции,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СуммаНачисленнойАмортизации,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.УбытокОтОбесценения,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.КоэффициентУскорения,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СчетСниженияСтоимости,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.МетодНачисленияАмортизации,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.МестонахождениеОбъекта
	|ИЗ
	|	РегистрСведений.ОсновныеСредстваМеждународныйУчет.СрезПоследних(
	|			&НаДату,
	|			ОсновноеСредство = &ОсновноеСредство
	|				И Организация = &Организация) КАК ОсновныеСредстваМеждународныйУчетСрезПоследних";
	
	Запрос.УстановитьПараметр("НаДату", Дата);
	Запрос.УстановитьПараметр("ОсновноеСредство", ГлавноеОсновноеСредство);
	Запрос.УстановитьПараметр("Организация", Организация);

	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение.МОЛ                             = ВыборкаДетальныеЗаписи.МОЛ;
		Движение.СчетУчета                       = ВыборкаДетальныеЗаписи.СчетУчета;
		Движение.СрокПолезногоИспользования      = ВыборкаДетальныеЗаписи.СрокПолезногоИспользования;
		Движение.НачислятьАмортизацию            = ВыборкаДетальныеЗаписи.НачислятьАмортизацию;
		Движение.МетодНачисленияАмортизации      = ВыборкаДетальныеЗаписи.МетодНачисленияАмортизации;
		Движение.СчетНачисленияАмортизации       = ВыборкаДетальныеЗаписи.СчетНачисленияАмортизации;
		Движение.СчетЗатрат                      = ВыборкаДетальныеЗаписи.СчетЗатрат;
		Движение.Субконто1                       = ВыборкаДетальныеЗаписи.Субконто1;
		Движение.Субконто2                       = ВыборкаДетальныеЗаписи.Субконто2;
		Движение.Субконто3                       = ВыборкаДетальныеЗаписи.Субконто3;
		Движение.ПредполагаемыйОбъемПродукции    = ВыборкаДетальныеЗаписи.ПредполагаемыйОбъемПродукции; 
		Движение.УбытокОтОбесценения             = ВыборкаДетальныеЗаписи.УбытокОтОбесценения;
		Движение.КоэффициентУскорения            = ВыборкаДетальныеЗаписи.КоэффициентУскорения;
		Движение.СчетСниженияСтоимости           = ВыборкаДетальныеЗаписи.СчетСниженияСтоимости;
		Движение.МестонахождениеОбъекта			 = ВыборкаДетальныеЗаписи.МестонахождениеОбъекта;
	КонецЦикла;


	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ОсновныеСредстваМеждународныйУчетСрезПоследних.ПервоначальнаяСтоимость) КАК ПервоначальнаяСтоимость,
	|	СУММА(ОсновныеСредстваМеждународныйУчетСрезПоследних.СуммаНачисленнойАмортизации) КАК СуммаНачисленнойАмортизации,
	|	СУММА(ОсновныеСредстваМеждународныйУчетСрезПоследних.СправедливаяСтоимость) КАК СправедливаяСтоимость
	|ИЗ
	|	РегистрСведений.ОсновныеСредстваМеждународныйУчет.СрезПоследних(
	|			&НаДату,
	|			ОсновноеСредство В (&СписокОС)
	|				И Организация = &Организация) КАК ОсновныеСредстваМеждународныйУчетСрезПоследних";
	
	Запрос.УстановитьПараметр("НаДату", Дата);
	Запрос.УстановитьПараметр("СписокОС", ТабОС);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение.ПервоначальнаяСтоимость         = ВыборкаДетальныеЗаписи.ПервоначальнаяСтоимость; 
		Движение.СправедливаяСтоимость           = ВыборкаДетальныеЗаписи.СправедливаяСтоимость;
		Движение.СуммаНачисленнойАмортизации     = ВыборкаДетальныеЗаписи.СуммаНачисленнойАмортизации;
	КонецЦикла;

	НаборДвижений.Записать();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

// Процедура формирование проводок.
//
Процедура ФормированиеПроводокМеждународный( ТабОС, СтруктураШапкиДокумента)
	
	Операция = Движения.Международный;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МеждународныйОстатки.Субконто1,
	|	МеждународныйОстатки.СуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Период,
	|			Счет = &Счет,
	|			,
	|			Организация = &Организация
	|				И Субконто1 В (&ОсновныеСредства)) КАК МеждународныйОстатки";
	Запрос.УстановитьПараметр("Период", 			Новый Граница(Дата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Счет", 				ПланыСчетов.Международный.ОСвОрганизации);
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("ОсновныеСредства", 	ТабОС);
	ОстаткиПоОС = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаОС Из ОстаткиПоОС Цикл
		
		Проводка = Операция.Добавить();
		Проводка.Период      = Дата;
		Проводка.Организация = Организация;
		Проводка.Содержание  = "Комплектация ОС";
		
		Проводка.Сумма       = СтрокаОС.СуммаОстаток;
		
		Проводка.СчетДт = ПланыСчетов.Международный.ОСвОрганизации;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства", ОсновноеСредство);
		
		Проводка.СчетКт = ПланыСчетов.Международный.ОСвОрганизации;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", СтрокаОС.Субконто1);
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МеждународныйОстатки.Субконто1,
	|	-МеждународныйОстатки.СуммаОстаток КАК Амортизация
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Период,
	|			Счет = &Счет,
	|			,
	|			Организация = &Организация
	|				И Субконто1 В (&ОсновныеСредства)) КАК МеждународныйОстатки";
	Запрос.УстановитьПараметр("Период", 			Новый Граница(Дата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Счет", 				ПланыСчетов.Международный.АмортизацияОС_01);
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("ОсновныеСредства", 	ТабОС);
	ОстаткиПоОСАм = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаОС Из ОстаткиПоОСАм Цикл
		
		Проводка = Операция.Добавить();
		Проводка.Период      = Дата;
		Проводка.Организация = Организация;
		Проводка.Содержание  = "Комплектация ОС";
		
		Проводка.Сумма       = СтрокаОС.Амортизация;
		
		Проводка.СчетДт = ПланыСчетов.Международный.АмортизацияОС_01;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства", СтрокаОС.Субконто1);
		
		Проводка.СчетКт = ПланыСчетов.Международный.АмортизацияОС_01;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ОсновноеСредство);
		
	КонецЦикла;
	
	ДвижениеПоРегиструОСМеждународныйУчет(ТабОС,ОстаткиПоОС,ОстаткиПоОСАм);
	
КонецПроцедуры // ФормированиеПроводокБух

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если мУдалятьДвижения Тогда
		ttk_ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ГлавноеОсновноеСредство = Неопределено;
КонецПроцедуры
