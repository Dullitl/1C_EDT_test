//////////////////////////////////////////////////////////////////////////////////
// Обработчики событий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения)=Тип("ДокументСсылка.ПередачаОС") Тогда
		СтандартнаяОбработка = Ложь;
		ЗаполнитьВыбытиеОСМПоПередачеОС(ДанныеЗаполнения);
	ИначеЕсли  ТипЗнч(ДанныеЗаполнения)=Тип("ДокументСсылка.ПодготовкаКПередачеОС") Тогда 
		СтандартнаяОбработка = Ложь;
		ЗаполнитьВыбытиеОСМПоПодготовкеКПередачеОС(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения)=Тип("ДокументСсылка.СписаниеОС") Тогда
		СтандартнаяОбработка = Ложь;
		ЗаполнитьВыбытиеОСМПоСписаниюОС(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения)=Тип("ДокументСсылка.абс_ЧастичнаяЛиквидацияОС") Тогда
		СтандартнаяОбработка = Ложь;
		ЗаполнитьВыбытиеОСМПоЧастичнойЛиквидацииОС(ДанныеЗаполнения);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
	Если Не ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Номер) Тогда
		УстановитьНовыйНомер(Организация.Префикс);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Не ДополнительныеСвойства.ЭтоНовый Тогда
		Для Каждого НаборЗаписей Из Движения Цикл
			НаборЗаписей.Очистить();
			НаборЗаписей.Записывать = Истина;
		КонецЦикла;
	КонецЕсли;
	
	НачалоПериода = НачалоМесяца(Дата);
	КонецПериода = КонецМесяца(Дата);
	
	ТипДокументаОснования = ТипЗнч(ДокументОснование);
	СтруктураЗапросов = Документы.абс_ВыбытиеОСМ.ПолучитьСтруктуруЗапросовПроведения(ТипДокументаОснования);
	
	СтруктураПараметровСтрока = ПолучитьМакет("СтруктураПараметров").ПолучитьТекст();
	СтруктураПараметров = ЗначениеИзСтрокиВнутр(СтруктураПараметровСтрока);
	
	ТаблицаСроков = Новый ТаблицаЗначений;
	ТаблицаСроков.Колонки.Добавить("ГруппаОС", Новый ОписаниеТипов("ПеречислениеСсылка.ГруппыОС"));
	ТаблицаСроков.Колонки.Добавить("ЦельИспользования", Новый ОписаниеТипов("СправочникСсылка.ЦелиИспользования"));
	ТаблицаСроков.Колонки.Добавить("СрокИспользования", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4, 0)));
	
	Для Сч = 1 По 12 Цикл
		СтрокаТаблицы = ТаблицаСроков.Добавить();
		СтрокаТаблицы.ГруппаОС = СтруктураПараметров["ГруппаУчета"+Сч];
		СтрокаТаблицы.ЦельИспользования = СтруктураПараметров["Цель"+Сч];
		СтрокаТаблицы.СрокИспользования = СтруктураПараметров["СПИ"+Сч];
	КонецЦикла;
	
	ПустыеСубконто = ПолучитьПустыеСубконто();
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Период", НачалоПериода);
	Запрос.УстановитьПараметр("ТаблицаСроков", ТаблицаСроков);
	Запрос.УстановитьПараметр("ПустыеСубконто", ПустыеСубконто);
	
	ТекстыЗапросов = Новый СписокЗначений;
	ТекстыЗапросов.Добавить(СтруктураЗапросов.абс_МеждународныйУчет_ТекстЗапросаВидыСубконтоСчетов, "");
	ТекстыЗапросов.Добавить(СтруктураЗапросов.абс_МеждународныйУчет_ТекстЗапросаСоответствиеСчетовХозрасчетный, "");
	ТекстыЗапросов.Добавить(СтруктураЗапросов.абс_МеждународныйУчет_ТекстЗапросаСрокиПолезногоИспользования, "");
	
	Запрос.Текст = "";
	Для Каждого ЭлементСписка Из ТекстыЗапросов Цикл
		Запрос.Текст = Запрос.Текст+ЭлементСписка.Значение;
	КонецЦикла;
	
	Запрос.ВыполнитьПакет();
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("МоментДокументаДляРН", Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("МоментДокумента", МоментВремени());
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ТекстыЗапросов = Новый СписокЗначений;
	
	/////////////////////////////////////////////////////////////////////////
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("МоментОснования", ДокументОснование.МоментВремени());
	
	Если ТипДокументаОснования=Тип("ДокументСсылка.абс_ЧастичнаяЛиквидацияОС") Тогда
		
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Ликвидация_ТекстЗапросаТабличнаяЧастьОсновныеСредства , "");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Ликвидация_ТекстЗапросаСтоимостьОсновныхСредств , "");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Ликвидация_ТекстЗапросаСтоимостьОсновныхСредствБУ , "");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Ликвидация_ТекстЗапросаСписаниеБУ , "");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Ликвидация_ТекстЗапросаДанныеДляФормированияДвижений , "");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.абс_ВнеоборотныеАктивы_ТекстЗапросаОсновныеСредстваМСФОПоТаблицеОС , "");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Ликвидация_ТекстЗапросаТаблица_абс_СтоимостьОСМеждународный , "Таблица_абс_СтоимостьОСМеждународный");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Ликвидация_ТекстЗапросаТаблица_ОсновныеСредстваМеждународныйУчет , "Таблица_ОсновныеСредстваМеждународныйУчет");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Ликвидация_ТекстЗапросаТаблица_Международный , "Таблица_Международный");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Ликвидация_УдалениеВременныхТаблиц, "");
		
	Иначе 
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СписаниеОС") И ДокументОснование.ВидОперации=Перечисления.абс_ВидыОперацийСписанияОС.ПеремещениеМеждуФилиалами Тогда
			ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Передача_ТекстЗапросаТабличнаяЧастьОсновныеСредства , "");
		Иначе
			ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Списание_ТекстЗапросаТабличнаяЧастьОсновныеСредства , "");
		КонецЕсли;
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Списание_ТекстЗапросаСтоимостьОсновныхСредств , "");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.абс_ВнеоборотныеАктивы_ТекстЗапросаОсновныеСредстваМСФОПоТаблицеОС , "");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Списание_ТекстЗапросаТаблица_абс_СтоимостьОСМеждународный , "Таблица_абс_СтоимостьОСМеждународный");
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Списание_ТекстЗапросаТаблица_ОсновныеСредстваМеждународныйУчет , "Таблица_ОсновныеСредстваМеждународныйУчет");
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.СписаниеОС") И ДокументОснование.ВидОперации=Перечисления.абс_ВидыОперацийСписанияОС.ПеремещениеМеждуФилиалами Тогда
			ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Передача_ТекстЗапросаТаблица_Международный , "Таблица_Международный");
		Иначе
			ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Списание_ТекстЗапросаТаблица_Международный , "Таблица_Международный");
		КонецЕсли;
		ТекстыЗапросов.Добавить(СтруктураЗапросов.Выбытие_Списание_УдалениеВременныхТаблиц, "");
		
	КонецЕсли;
	
	/////////////////////////////////////////////////////////////////////////
	
	
	Запрос.Текст = "";
	Для Каждого ТекстЗапроса Из ТекстыЗапросов Цикл
		Запрос.Текст = Запрос.Текст+ТекстЗапроса.Значение;
	КонецЦикла;
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДанных = Новый Структура;
	
	Для Каждого ТекстЗапроса Из ТекстыЗапросов Цикл
		
		ИмяТаблицы = ТекстЗапроса.Представление;
		
		Если Не ПустаяСтрока(ИмяТаблицы) Тогда
			Индекс = ТекстыЗапросов.Индекс(ТекстЗапроса);
			ТаблицыДанных.Вставить(ИмяТаблицы, Результат[Индекс].Выгрузить());
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицыДанных.Свойство("Таблица_ОсновныеСредстваМеждународныйУчет") Тогда
		
		ТаблицаРегистра = ТаблицыДанных.Таблица_ОсновныеСредстваМеждународныйУчет;
		Если ТаблицаРегистра.Количество()>0 Тогда
			
			НаборЗаписей = Движения.ОсновныеСредстваМеждународныйУчет;
			НаборЗаписей.Загрузить(ТаблицаРегистра);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТаблицыДанных.Свойство("Таблица_абс_СтоимостьОСМеждународный") Тогда
		
		ТаблицаРегистра = ТаблицыДанных.Таблица_абс_СтоимостьОСМеждународный;
		Если ТаблицаРегистра.Количество()>0 Тогда
			
			НаборЗаписей = Движения.абс_СтоимостьОСМеждународный;
			НаборЗаписей.Загрузить(ТаблицаРегистра);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТаблицыДанных.Свойство("Таблица_Международный") Тогда
		
		ТаблицаРегистра = ТаблицыДанных.Таблица_Международный;
		Если ТаблицаРегистра.Количество()>0 Тогда
			
			НаборЗаписей = Движения.Международный;
			НаборЗаписей.Загрузить(ТаблицаРегистра);
			
		КонецЕсли;
		
	КонецЕсли;
	
		
КонецПроцедуры


//////////////////////////////////////////////////////////////////////////
// Обработка заполнения

Процедура ЗаполнитьВыбытиеОСМПоЧастичнойЛиквидацииОС(ДанныеЗаполнения)
	
	ДокОснование = ДанныеЗаполнения.ПолучитьОбъект();
	ЗаполнитьШапкуИОчиститьТЧ(ДокОснование);
	
	
	Для Каждого СтрокаОС Из ДокОснование.ОС Цикл
		
		СтрокаТЧ = ОсновныеСредства.Добавить();
		СтрокаТЧ.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
		СтрокаТЧ.ЧастичнаяЛиквидация = Истина;
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ЗаполнитьВыбытиеОСМПоСписаниюОС(ДанныеЗаполнения)
	
	ДокОснование = ДанныеЗаполнения.ПолучитьОбъект();
	ЗаполнитьШапкуИОчиститьТЧ(ДокОснование);
	
	Для Каждого СтрокаОС Из ДокОснование.ОС Цикл
		
		СтрокаТЧ = ОсновныеСредства.Добавить();
		СтрокаТЧ.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ЗаполнитьВыбытиеОСМПоПередачеОС(ДанныеЗаполнения)
	
	ДокОснование = ДанныеЗаполнения.ПолучитьОбъект();
	ЗаполнитьШапкуИОчиститьТЧ(ДокОснование);
		
	Для Каждого СтрокаОС Из ДокОснование.ОС Цикл
		
		СтрокаТЧ = ОсновныеСредства.Добавить();
		СтрокаТЧ.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ЗаполнитьВыбытиеОСМПоПодготовкеКПередачеОС(ДанныеЗаполнения)
	
	ДокОснование = ДанныеЗаполнения.ПолучитьОбъект();
	ЗаполнитьШапкуИОчиститьТЧ(ДокОснование);
	
	Для Каждого СтрокаОС Из ДокОснование.ОС Цикл
		
		СтрокаТЧ = ОсновныеСредства.Добавить();
		СтрокаТЧ.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ЗаполнитьШапкуИОчиститьТЧ(ДокОснование)
	
	Дата = ДокОснование.Дата;
	Организация = ДокОснование.Организация;
	ДокументОснование = ДокОснование.Ссылка;
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
	Если Номер = "" Тогда
		УстановитьНовыйНомер(Организация.Префикс);
	КонецЕсли;
	
	ОсновныеСредства.Очистить();
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////
// Прочие процедуры и функции

Функция ПолучитьПустыеСубконто()

	ПустыеСубконто = Новый СписокЗначений;
	ВыборкаСубконто = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Выбрать();
	Пока ВыборкаСубконто.Следующий() Цикл
		ПустыеСубконто.Добавить(ВыборкаСубконто.ТипЗначения.ПривестиЗначение());
	КонецЦикла;
	ПустыеСубконто.Добавить(Неопределено);

	Возврат ПустыеСубконто;

КонецФункции // ПолучитьПустыеСубконто()

