

Функция ПолучитьСтруктуруЗапросовПроведения(ТипДокументаОснования) Экспорт 
	
	СтруктураЗапросов = Новый Структура;
	
	ДобавитьОбщиеЗапросы(СтруктураЗапросов);
	
	Если ТипДокументаОснования = Тип("ДокументСсылка.абс_ЧастичнаяЛиквидацияОС") Тогда 	
		ДобавитьЗапросыДляВыбытие_Ликвидация(СтруктураЗапросов);
	Иначе
		ДобавитьЗапросыДляВыбытие_Остальные(СтруктураЗапросов);
	КонецЕсли;
	
	Возврат СтруктураЗапросов;
	
КонецФункции

Процедура ДобавитьЗапросыДляВыбытие_Ликвидация(СтруктураЗапросов)

	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_ВыбытиеОСМОсновныеСредства.ОсновноеСредство,
		|	абс_ВыбытиеОСМОсновныеСредства.НачислятьАмортизацию
		|ПОМЕСТИТЬ ТабличнаяЧастьОсновныеСредства
		|ИЗ
		|	Документ.абс_ВыбытиеОСМ.ОсновныеСредства КАК абс_ВыбытиеОСМОсновныеСредства
		|ГДЕ
		|	абс_ВыбытиеОСМОсновныеСредства.Ссылка = &Ссылка";
		
	СтруктураЗапросов.Вставить("Выбытие_Ликвидация_ТекстЗапросаТабличнаяЧастьОсновныеСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_СтоимостьОСМеждународныйОстатки.ОсновноеСредство,
		|	абс_СтоимостьОСМеждународныйОстатки.СтоимостьОстаток КАК Стоимость,
		|	абс_СтоимостьОСМеждународныйОстатки.АмортизацияОстаток КАК Амортизация
		|ПОМЕСТИТЬ СтоимостьОсновныхСредств
		|ИЗ
		|	РегистрНакопления.абс_СтоимостьОСМеждународный.Остатки(
		|			&МоментДокумента,
		|			ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ТабличнаяЧастьОсновныеСредства.ОсновноеСредство
		|					ИЗ
		|						ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства)
		|				И Организация = &Организация) КАК абс_СтоимостьОСМеждународныйОстатки";
		
	СтруктураЗапросов.Вставить("Выбытие_Ликвидация_ТекстЗапросаСтоимостьОсновныхСредств", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтоимостьОСБухгалтерскийУчетОстатки.ОсновноеСредство КАК ОсновноеСредство,
		|	СтоимостьОСБухгалтерскийУчетОстатки.СтоимостьОстаток КАК Стоимость,
		|	СтоимостьОСБухгалтерскийУчетОстатки.АмортизацияОстаток КАК Амортизация
		|ПОМЕСТИТЬ СтоимостьОсновныхСредствБУ
		|ИЗ
		|	РегистрНакопления.СтоимостьОСБухгалтерскийУчет.Остатки(
		|			&МоментОснования,
		|			Организация = &Организация
		|				И ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ТабличнаяЧастьОсновныеСредства.ОсновноеСредство
		|					ИЗ
		|						ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства)) КАК СтоимостьОСБухгалтерскийУчетОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("Выбытие_Ликвидация_ТекстЗапросаСтоимостьОсновныхСредствБУ", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
		|	СУММА(СтоимостьОС.Стоимость) КАК Стоимость,
		|	СУММА(СтоимостьОС.Амортизация) КАК Амортизация
		|ПОМЕСТИТЬ СписаниеБУ
		|ИЗ
		|	РегистрНакопления.СтоимостьОСБухгалтерскийУчет КАК СтоимостьОС
		|ГДЕ
		|	СтоимостьОС.Период = &Период
		|	И СтоимостьОС.Регистратор = &ДокументОснование
		|	И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И СтоимостьОС.Активность
		|
		|СГРУППИРОВАТЬ ПО
		|	СтоимостьОС.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("Выбытие_Ликвидация_ТекстЗапросаСписаниеБУ", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтоимостьОсновныхСредств.ОсновноеСредство,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(СтоимостьОсновныхСредствБУ.Стоимость, 0) = 0
		|			ТОГДА СтоимостьОсновныхСредств.Стоимость
		|		ИНАЧЕ СтоимостьОсновныхСредств.Стоимость * ЕСТЬNULL(СписаниеБУ.Стоимость, 0) / СтоимостьОсновныхСредствБУ.Стоимость
		|	КОНЕЦ КАК Стоимость,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(СтоимостьОсновныхСредствБУ.Стоимость, 0) = 0
		|			ТОГДА СтоимостьОсновныхСредств.Амортизация
		|		ИНАЧЕ СтоимостьОсновныхСредств.Стоимость * ЕСТЬNULL(СписаниеБУ.Амортизация, 0) / СтоимостьОсновныхСредствБУ.Стоимость
		|	КОНЕЦ КАК Амортизация
		|ПОМЕСТИТЬ ДанныеДляФормированияДвижений
		|ИЗ
		|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОсновныхСредствБУ КАК СтоимостьОсновныхСредствБУ
		|		ПО СтоимостьОсновныхСредств.ОсновноеСредство = СтоимостьОсновныхСредствБУ.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ СписаниеБУ КАК СписаниеБУ
		|		ПО СтоимостьОсновныхСредств.ОсновноеСредство = СписаниеБУ.ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("Выбытие_Ликвидация_ТекстЗапросаДанныеДляФормированияДвижений", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПериодыРегистраторыСреза.Период,
		|	ПериодыРегистраторыСреза.Регистратор,
		|	ПериодыРегистраторыСреза.ОсновноеСредство КАК ОсновноеСредство,
		|	ОсновныеСредстваМеждународныйУчет.Организация,
		|	ОсновныеСредстваМеждународныйУчет.ДатаПринятияКУчету,
		|	ОсновныеСредстваМеждународныйУчет.МестонахождениеОбъекта,
		|	ОсновныеСредстваМеждународныйУчет.МОЛ,
		|	ОсновныеСредстваМеждународныйУчет.Состояние,
		|	ОсновныеСредстваМеждународныйУчет.СчетУчета,
		|	ОсновныеСредстваМеждународныйУчет.СрокПолезногоИспользования,
		|	ОсновныеСредстваМеждународныйУчет.НачислятьАмортизацию,
		|	ОсновныеСредстваМеждународныйУчет.МетодНачисленияАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.СчетНачисленияАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.ПервоначальнаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.СправедливаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.ЛиквидационнаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.СчетЗатрат,
		|	ОсновныеСредстваМеждународныйУчет.Субконто1,
		|	ОсновныеСредстваМеждународныйУчет.Субконто2,
		|	ОсновныеСредстваМеждународныйУчет.Субконто3,
		|	ОсновныеСредстваМеждународныйУчет.ПредполагаемыйОбъемПродукции,
		|	ОсновныеСредстваМеждународныйУчет.СуммаНачисленнойАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.УбытокОтОбесценения,
		|	ОсновныеСредстваМеждународныйУчет.КоэффициентУскорения,
		|	ОсновныеСредстваМеждународныйУчет.СчетСниженияСтоимости
		|ПОМЕСТИТЬ ПараметрыОсновныхСредств
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПериодыСреза.ОсновноеСредство КАК ОсновноеСредство,
		|		ПериодыСреза.Период КАК Период,
		|		МАКСИМУМ(ОсновныеСредстваМеждународныйУчет.Регистратор) КАК Регистратор
		|	ИЗ
		|		(ВЫБРАТЬ
		|			ТабличнаяЧастьОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
		|			МАКСИМУМ(ОсновныеСредстваМеждународныйУчет.Период) КАК Период
		|		ИЗ
		|			ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|				ПО ТабличнаяЧастьОсновныеСредства.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|					И (ОсновныеСредстваМеждународныйУчет.Период <= &Период)
		|					И (ОсновныеСредстваМеждународныйУчет.Активность)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ТабличнаяЧастьОсновныеСредства.ОсновноеСредство) КАК ПериодыСреза
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|			ПО ПериодыСреза.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|				И ПериодыСреза.Период = ОсновныеСредстваМеждународныйУчет.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПериодыСреза.ОсновноеСредство,
		|		ПериодыСреза.Период) КАК ПериодыРегистраторыСреза
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|		ПО ПериодыРегистраторыСреза.Период = ОсновныеСредстваМеждународныйУчет.Период
		|			И ПериодыРегистраторыСреза.Регистратор = ОсновныеСредстваМеждународныйУчет.Регистратор
		|			И ПериодыРегистраторыСреза.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("абс_ВнеоборотныеАктивы_ТекстЗапросаОсновныеСредстваМСФОПоТаблицеОС", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Организация,
		|	ДанныеДляФормированияДвижений.ОсновноеСредство,
		|	ДанныеДляФормированияДвижений.Стоимость,
		|	ДанныеДляФормированияДвижений.Амортизация
		|ИЗ
		|	ДанныеДляФормированияДвижений КАК ДанныеДляФормированияДвижений";
		
	СтруктураЗапросов.Вставить("Выбытие_Ликвидация_ТекстЗапросаТаблица_абс_СтоимостьОСМеждународный", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	ПараметрыОсновныхСредств.ОсновноеСредство,
		|	&организация,
		|	ПараметрыОсновныхСредств.ДатаПринятияКУчету,
		|	ПараметрыОсновныхСредств.МестонахождениеОбъекта,
		|	ПараметрыОсновныхСредств.МОЛ,
		|	ПараметрыОсновныхСредств.Состояние КАК Состояние,
		|	ПараметрыОсновныхСредств.СчетУчета,
		|	ПараметрыОсновныхСредств.СрокПолезногоИспользования,
		|	ПараметрыОсновныхСредств.НачислятьАмортизацию КАК НачислятьАмортизацию,
		|	ПараметрыОсновныхСредств.МетодНачисленияАмортизации,
		|	ПараметрыОсновныхСредств.СчетНачисленияАмортизации,
		|	ПараметрыОсновныхСредств.ПервоначальнаяСтоимость,
		|	ВЫБОР
		|		КОГДА ПараметрыОсновныхСредств.СправедливаяСтоимость > ЕСТЬNULL(ДанныеДляФормированияДвижений.Стоимость, 0)
		|			ТОГДА ПараметрыОсновныхСредств.СправедливаяСтоимость - ЕСТЬNULL(ДанныеДляФормированияДвижений.Стоимость, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СправедливаяСтоимость,
		|	ПараметрыОсновныхСредств.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
		|	ПараметрыОсновныхСредств.СчетЗатрат,
		|	ПараметрыОсновныхСредств.Субконто1,
		|	ПараметрыОсновныхСредств.Субконто2,
		|	ПараметрыОсновныхСредств.Субконто3,
		|	ПараметрыОсновныхСредств.ПредполагаемыйОбъемПродукции,
		|	ЕСТЬNULL(СтоимостьОсновныхСредств.Амортизация, 0) - ЕСТЬNULL(ДанныеДляФормированияДвижений.Амортизация, 0) КАК СуммаНачисленнойАмортизации,
		|	ПараметрыОсновныхСредств.УбытокОтОбесценения,
		|	ПараметрыОсновныхСредств.КоэффициентУскорения,
		|	ПараметрыОсновныхСредств.СчетСниженияСтоимости
		|ИЗ
		|	ДанныеДляФормированияДвижений КАК ДанныеДляФормированияДвижений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
		|		ПО ДанныеДляФормированияДвижений.ОсновноеСредство = ПараметрыОсновныхСредств.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
		|		ПО ДанныеДляФормированияДвижений.ОсновноеСредство = СтоимостьОсновныхСредств.ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("Выбытие_Ликвидация_ТекстЗапросаТаблица_ОсновныеСредстваМеждународныйУчет", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Международный.ВыбытиеОС) КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства) КАК ВидСубконтоДт1,
		|	ДанныеДляФормированияДвижений.ОсновноеСредство КАК СубконтоДт1,
		|	ПараметрыОсновныхСредств.СчетУчета КАК СчетКт,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства) КАК ВидСубконтоКт1,
		|	ДанныеДляФормированияДвижений.ОсновноеСредство КАК СубконтоКт1,
		|	ДанныеДляФормированияДвижений.Стоимость КАК Сумма,
		|	&ДокументОснование КАК ПервичныйДокумент
		|ИЗ
		|	ДанныеДляФормированияДвижений КАК ДанныеДляФормированияДвижений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
		|		ПО ДанныеДляФормированияДвижений.ОсновноеСредство = ПараметрыОсновныхСредств.ОсновноеСредство
		|ГДЕ
		|	ДанныеДляФормированияДвижений.Стоимость <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ПараметрыОсновныхСредств.СчетНачисленияАмортизации,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства),
		|	ДанныеДляФормированияДвижений.ОсновноеСредство,
		|	ЗНАЧЕНИЕ(ПланСчетов.Международный.ВыбытиеОС),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства),
		|	ДанныеДляФормированияДвижений.ОсновноеСредство,
		|	ДанныеДляФормированияДвижений.Амортизация,
		|	&ДокументОснование
		|ИЗ
		|	ДанныеДляФормированияДвижений КАК ДанныеДляФормированияДвижений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
		|		ПО ДанныеДляФормированияДвижений.ОсновноеСредство = ПараметрыОсновныхСредств.ОсновноеСредство
		|ГДЕ
		|	ДанныеДляФормированияДвижений.Амортизация <> 0";
		
	СтруктураЗапросов.Вставить("Выбытие_Ликвидация_ТекстЗапросаТаблица_Международный", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"УНИЧТОЖИТЬ ТабличнаяЧастьОсновныеСредства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПараметрыОсновныхСредств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СтоимостьОсновныхСредств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СтоимостьОсновныхСредствБУ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СписаниеБУ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДанныеДляФормированияДвижений";
		
	СтруктураЗапросов.Вставить("Выбытие_Ликвидация_УдалениеВременныхТаблиц", ТекстЗапроса+";");
		
КонецПроцедуры

Процедура ДобавитьЗапросыДляВыбытие_Остальные(СтруктураЗапросов)

	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_ВыбытиеОСМОсновныеСредства.ОсновноеСредство,
		|	абс_ВыбытиеОСМОсновныеСредства.НачислятьАмортизацию,
		|	ЕСТЬNULL(СоответствиеСчетовХозрасчетныйМСФО.СчетМеждународный, ЗНАЧЕНИЕ(ПланСчетов.Международный.ВыбытиеОС)) КАК КоррСчет,
		|	ЕСТЬNULL(абс_ВыбытиеОСМОсновныеСредства.Ссылка.ДокументОснование.СубконтоБУ, абс_ВыбытиеОСМОсновныеСредства.ОсновноеСредство) КАК КоррСубконто1
		|ПОМЕСТИТЬ ТабличнаяЧастьОсновныеСредства
		|ИЗ
		|	Документ.абс_ВыбытиеОСМ.ОсновныеСредства КАК абс_ВыбытиеОСМОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеСчетовХозрасчетныйМСФО КАК СоответствиеСчетовХозрасчетныйМСФО
		|		ПО абс_ВыбытиеОСМОсновныеСредства.Ссылка.ДокументОснование.СчетСписанияБУ = СоответствиеСчетовХозрасчетныйМСФО.СчетХозрасчетный
		|			И (СоответствиеСчетовХозрасчетныйМСФО.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийБухгалтерии.Дебет))
		|ГДЕ
		|	абс_ВыбытиеОСМОсновныеСредства.Ссылка = &Ссылка";
		
	СтруктураЗапросов.Вставить("Выбытие_Передача_ТекстЗапросаТабличнаяЧастьОсновныеСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_ВыбытиеОСМОсновныеСредства.ОсновноеСредство,
		|	абс_ВыбытиеОСМОсновныеСредства.НачислятьАмортизацию
		|ПОМЕСТИТЬ ТабличнаяЧастьОсновныеСредства
		|ИЗ
		|	Документ.абс_ВыбытиеОСМ.ОсновныеСредства КАК абс_ВыбытиеОСМОсновныеСредства
		|ГДЕ
		|	абс_ВыбытиеОСМОсновныеСредства.Ссылка = &Ссылка";
		
	СтруктураЗапросов.Вставить("Выбытие_Списание_ТекстЗапросаТабличнаяЧастьОсновныеСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_СтоимостьОСМеждународныйОстатки.ОсновноеСредство,
		|	абс_СтоимостьОСМеждународныйОстатки.СтоимостьОстаток КАК Стоимость,
		|	абс_СтоимостьОСМеждународныйОстатки.АмортизацияОстаток КАК Амортизация
		|ПОМЕСТИТЬ СтоимостьОсновныхСредств
		|ИЗ
		|	РегистрНакопления.абс_СтоимостьОСМеждународный.Остатки(
		|			&МоментДокумента,
		|			ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ТабличнаяЧастьОсновныеСредства.ОсновноеСредство
		|					ИЗ
		|						ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства)
		|				И Организация = &Организация) КАК абс_СтоимостьОСМеждународныйОстатки";
		
	СтруктураЗапросов.Вставить("Выбытие_Списание_ТекстЗапросаСтоимостьОсновныхСредств", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПериодыРегистраторыСреза.Период,
		|	ПериодыРегистраторыСреза.Регистратор,
		|	ПериодыРегистраторыСреза.ОсновноеСредство КАК ОсновноеСредство,
		|	ОсновныеСредстваМеждународныйУчет.Организация,
		|	ОсновныеСредстваМеждународныйУчет.ДатаПринятияКУчету,
		|	ОсновныеСредстваМеждународныйУчет.МестонахождениеОбъекта,
		|	ОсновныеСредстваМеждународныйУчет.МОЛ,
		|	ОсновныеСредстваМеждународныйУчет.Состояние,
		|	ОсновныеСредстваМеждународныйУчет.СчетУчета,
		|	ОсновныеСредстваМеждународныйУчет.СрокПолезногоИспользования,
		|	ОсновныеСредстваМеждународныйУчет.НачислятьАмортизацию,
		|	ОсновныеСредстваМеждународныйУчет.МетодНачисленияАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.СчетНачисленияАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.ПервоначальнаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.СправедливаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.ЛиквидационнаяСтоимость,
		|	ОсновныеСредстваМеждународныйУчет.СчетЗатрат,
		|	ОсновныеСредстваМеждународныйУчет.Субконто1,
		|	ОсновныеСредстваМеждународныйУчет.Субконто2,
		|	ОсновныеСредстваМеждународныйУчет.Субконто3,
		|	ОсновныеСредстваМеждународныйУчет.ПредполагаемыйОбъемПродукции,
		|	ОсновныеСредстваМеждународныйУчет.СуммаНачисленнойАмортизации,
		|	ОсновныеСредстваМеждународныйУчет.УбытокОтОбесценения,
		|	ОсновныеСредстваМеждународныйУчет.КоэффициентУскорения,
		|	ОсновныеСредстваМеждународныйУчет.СчетСниженияСтоимости
		|ПОМЕСТИТЬ ПараметрыОсновныхСредств
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПериодыСреза.ОсновноеСредство КАК ОсновноеСредство,
		|		ПериодыСреза.Период КАК Период,
		|		МАКСИМУМ(ОсновныеСредстваМеждународныйУчет.Регистратор) КАК Регистратор
		|	ИЗ
		|		(ВЫБРАТЬ
		|			ТабличнаяЧастьОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
		|			МАКСИМУМ(ОсновныеСредстваМеждународныйУчет.Период) КАК Период
		|		ИЗ
		|			ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|				ПО ТабличнаяЧастьОсновныеСредства.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|					И (ОсновныеСредстваМеждународныйУчет.Период <= &Период)
		|					И (ОсновныеСредстваМеждународныйУчет.Активность)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ТабличнаяЧастьОсновныеСредства.ОсновноеСредство) КАК ПериодыСреза
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|			ПО ПериодыСреза.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|				И ПериодыСреза.Период = ОсновныеСредстваМеждународныйУчет.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПериодыСреза.ОсновноеСредство,
		|		ПериодыСреза.Период) КАК ПериодыРегистраторыСреза
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
		|		ПО ПериодыРегистраторыСреза.Период = ОсновныеСредстваМеждународныйУчет.Период
		|			И ПериодыРегистраторыСреза.Регистратор = ОсновныеСредстваМеждународныйУчет.Регистратор
		|			И ПериодыРегистраторыСреза.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("абс_ВнеоборотныеАктивы_ТекстЗапросаОсновныеСредстваМСФОПоТаблицеОС", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Организация,
		|	СтоимостьОсновныхСредств.ОсновноеСредство,
		|	СтоимостьОсновныхСредств.Стоимость,
		|	СтоимостьОсновныхСредств.Амортизация
		|ИЗ
		|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств";
		
	СтруктураЗапросов.Вставить("Выбытие_Списание_ТекстЗапросаТаблица_абс_СтоимостьОСМеждународный", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	ПараметрыОсновныхСредств.ОсновноеСредство,
		|	&организация,
		|	ПараметрыОсновныхСредств.ДатаПринятияКУчету,
		|	ПараметрыОсновныхСредств.МестонахождениеОбъекта,
		|	ПараметрыОсновныхСредств.МОЛ,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК Состояние,
		|	ПараметрыОсновныхСредств.СчетУчета,
		|	ПараметрыОсновныхСредств.СрокПолезногоИспользования,
		|	ЛОЖЬ КАК НачислятьАмортизацию,
		|	ПараметрыОсновныхСредств.МетодНачисленияАмортизации,
		|	ПараметрыОсновныхСредств.СчетНачисленияАмортизации,
		|	ПараметрыОсновныхСредств.ПервоначальнаяСтоимость,
		|	0 КАК СправедливаяСтоимость,
		|	0 КАК ЛиквидационнаяСтоимость,
		|	ПараметрыОсновныхСредств.СчетЗатрат,
		|	ПараметрыОсновныхСредств.Субконто1,
		|	ПараметрыОсновныхСредств.Субконто2,
		|	ПараметрыОсновныхСредств.Субконто3,
		|	ПараметрыОсновныхСредств.ПредполагаемыйОбъемПродукции,
		|	0 КАК СуммаНачисленнойАмортизации,
		|	ПараметрыОсновныхСредств.УбытокОтОбесценения,
		|	ПараметрыОсновныхСредств.КоэффициентУскорения,
		|	ПараметрыОсновныхСредств.СчетСниженияСтоимости
		|ИЗ
		|	ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств";
		
	СтруктураЗапросов.Вставить("Выбытие_Списание_ТекстЗапросаТаблица_ОсновныеСредстваМеждународныйУчет", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ТабличнаяЧастьОсновныеСредства.КоррСчет КАК СчетДт,
		|	ВидыСубконтоСчетов.ВидСубконто1 КАК ВидСубконтоДт1,
		|	ТабличнаяЧастьОсновныеСредства.КоррСубконто1 КАК СубконтоДт1,
		|	ПараметрыОсновныхСредств.СчетУчета КАК СчетКт,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства) КАК ВидСубконтоКт1,
		|	СтоимостьОсновныхСредств.ОсновноеСредство КАК СубконтоКт1,
		|	СтоимостьОсновныхСредств.Стоимость КАК Сумма,
		|	&ДокументОснование КАК ПервичныйДокумент
		|ИЗ
		|	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
		|		ПО ТабличнаяЧастьОсновныеСредства.ОсновноеСредство = СтоимостьОсновныхСредств.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
		|		ПО ТабличнаяЧастьОсновныеСредства.ОсновноеСредство = ПараметрыОсновныхСредств.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыСубконтоСчетов КАК ВидыСубконтоСчетов
		|		ПО ТабличнаяЧастьОсновныеСредства.КоррСчет = ВидыСубконтоСчетов.Счет
		|ГДЕ
		|	СтоимостьОсновныхСредств.Стоимость <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ПараметрыОсновныхСредств.СчетНачисленияАмортизации,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства),
		|	СтоимостьОсновныхСредств.ОсновноеСредство,
		|	ТабличнаяЧастьОсновныеСредства.КоррСчет,
		|	ВидыСубконтоСчетов.ВидСубконто1,
		|	ТабличнаяЧастьОсновныеСредства.КоррСубконто1,
		|	СтоимостьОсновныхСредств.Амортизация,
		|	&ДокументОснование
		|ИЗ
		|	ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
		|		ПО ТабличнаяЧастьОсновныеСредства.ОсновноеСредство = СтоимостьОсновныхСредств.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
		|		ПО ТабличнаяЧастьОсновныеСредства.ОсновноеСредство = ПараметрыОсновныхСредств.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыСубконтоСчетов КАК ВидыСубконтоСчетов
		|		ПО ТабличнаяЧастьОсновныеСредства.КоррСчет = ВидыСубконтоСчетов.Счет
		|ГДЕ
		|	СтоимостьОсновныхСредств.Амортизация <> 0";
		
	СтруктураЗапросов.Вставить("Выбытие_Передача_ТекстЗапросаТаблица_Международный", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Международный.ВыбытиеОС) КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства) КАК ВидСубконтоДт1,
		|	СтоимостьОсновныхСредств.ОсновноеСредство КАК СубконтоДт1,
		|	ПараметрыОсновныхСредств.СчетУчета КАК СчетКт,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства) КАК ВидСубконтоКт1,
		|	СтоимостьОсновныхСредств.ОсновноеСредство КАК СубконтоКт1,
		|	СтоимостьОсновныхСредств.Стоимость КАК Сумма,
		|	&ДокументОснование КАК ПервичныйДокумент
		|ИЗ
		|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
		|		ПО СтоимостьОсновныхСредств.ОсновноеСредство = ПараметрыОсновныхСредств.ОсновноеСредство
		|ГДЕ
		|	СтоимостьОсновныхСредств.Стоимость <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ПараметрыОсновныхСредств.СчетНачисленияАмортизации,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства),
		|	СтоимостьОсновныхСредств.ОсновноеСредство,
		|	ЗНАЧЕНИЕ(ПланСчетов.Международный.ВыбытиеОС),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства),
		|	СтоимостьОсновныхСредств.ОсновноеСредство,
		|	СтоимостьОсновныхСредств.Амортизация,
		|	&ДокументОснование
		|ИЗ
		|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
		|		ПО СтоимостьОсновныхСредств.ОсновноеСредство = ПараметрыОсновныхСредств.ОсновноеСредство
		|ГДЕ
		|	СтоимостьОсновныхСредств.Амортизация <> 0";
		
	СтруктураЗапросов.Вставить("Выбытие_Списание_ТекстЗапросаТаблица_Международный", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"УНИЧТОЖИТЬ ТабличнаяЧастьОсновныеСредства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПараметрыОсновныхСредств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СтоимостьОсновныхСредств";
		
	СтруктураЗапросов.Вставить("Выбытие_Списание_УдалениеВременныхТаблиц", ТекстЗапроса+";");
	
	
КонецПроцедуры

Процедура ДобавитьОбщиеЗапросы(СтруктураЗапросов)

	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Международный.Ссылка КАК Счет,
		|	ЕСТЬNULL(ВидыСубконто1.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто1,
		|	ЕСТЬNULL(ВидыСубконто2.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто2,
		|	ЕСТЬNULL(ВидыСубконто3.ВидСубконто, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконто3
		|ПОМЕСТИТЬ ВидыСубконтоСчетов
		|ИЗ
		|	ПланСчетов.Международный КАК Международный
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто1
		|		ПО Международный.Ссылка = ВидыСубконто1.Ссылка
		|			И (ВидыСубконто1.НомерСтроки = 1)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто2
		|		ПО Международный.Ссылка = ВидыСубконто2.Ссылка
		|			И (ВидыСубконто2.НомерСтроки = 2)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто3
		|		ПО Международный.Ссылка = ВидыСубконто3.Ссылка
		|			И (ВидыСубконто3.НомерСтроки = 3)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет";
		
	СтруктураЗапросов.Вставить("абс_МеждународныйУчет_ТекстЗапросаВидыСубконтоСчетов", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СоответствиеСчетовБУиМСФОСрезПоследних.СчетХозрасчетный КАК СчетХозрасчетный,
		|	СоответствиеСчетовБУиМСФОСрезПоследних.ВидДвижения КАК ВидДвижения,
		|	СоответствиеСчетовБУиМСФОСрезПоследних.СчетМеждународный
		|ПОМЕСТИТЬ СоответствиеСчетовХозрасчетныйМСФО
		|ИЗ
		|	РегистрСведений.СоответствиеСчетовБУиМСФО.СрезПоследних(
		|			&Период,
		|			СубконтоХозр1 В (&ПустыеСубконто)
		|				И СубконтоХозр2 В (&ПустыеСубконто)
		|				И СубконтоХозр3 В (&ПустыеСубконто)) КАК СоответствиеСчетовБУиМСФОСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СчетХозрасчетный,
		|	ВидДвижения";
		
	СтруктураЗапросов.Вставить("абс_МеждународныйУчет_ТекстЗапросаСоответствиеСчетовХозрасчетный", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТаблицаСроков.ГруппаОС КАК Перечисление.ГруппыОС) КАК ГруппаОС,
		|	ВЫРАЗИТЬ(ТаблицаСроков.ЦельИспользования КАК Справочник.ЦелиИспользования) КАК ЦельИспользования,
		|	ВЫРАЗИТЬ(ТаблицаСроков.СрокИспользования КАК ЧИСЛО(4, 0)) КАК СрокИспользования
		|ПОМЕСТИТЬ СрокиПолезногоИспользования
		|ИЗ
		|	&ТаблицаСроков КАК ТаблицаСроков
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ГруппаОС,
		|	ЦельИспользования";
		
	СтруктураЗапросов.Вставить("абс_МеждународныйУчет_ТекстЗапросаСрокиПолезногоИспользования", ТекстЗапроса+";");

КонецПроцедуры

