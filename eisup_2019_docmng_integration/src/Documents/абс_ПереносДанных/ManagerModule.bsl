
Функция ПолучитьДокументПоОперацииНаправлению(ДЗО, Операция, НаправлениеПереноса, МассивСчетов = Неопределено) Экспорт
	
	Если Операция <> Справочники.абс_ВидыОперацийПереносаДанных.ПоСчетам Тогда 
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	абс_ВыполненныеПереносы.Регистратор КАК Документ
		                      |ИЗ
		                      |	РегистрСведений.абс_ВыполненныеПереносы КАК абс_ВыполненныеПереносы
		                      |ГДЕ
		                      |	абс_ВыполненныеПереносы.ДЗО = &ДЗО
		                      |	И абс_ВыполненныеПереносы.Операция = &Операция
		                      |	И абс_ВыполненныеПереносы.НаправлениеПереноса = &НаправлениеПереноса");
		Запрос.УстановитьПараметр("ДЗО", ДЗО);
		Запрос.УстановитьПараметр("Операция", Операция);
		Запрос.УстановитьПараметр("НаправлениеПереноса", НаправлениеПереноса);
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат ПустаяСсылка();
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			Возврат Выборка.Документ;
		КонецЕсли;
	Иначе
		
		Если МассивСчетов = Неопределено Тогда 
			МассивСчетов = Новый Массив;
		КонецЕсли;
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	абс_ПереносДанныхСчетаБУНУ.Ссылка КАК Документ
		                      |ИЗ
		                      |	Документ.абс_ПереносДанных.СчетаБУНУ КАК абс_ПереносДанныхСчетаБУНУ
		                      |ГДЕ
		                      |	абс_ПереносДанныхСчетаБУНУ.Ссылка.ДЗО = &ДЗО
		                      |	И абс_ПереносДанныхСчетаБУНУ.Ссылка.Операция = &Операция
		                      |	И абс_ПереносДанныхСчетаБУНУ.Ссылка.НаправлениеПереноса = &НаправлениеПереноса
		                      |	И абс_ПереносДанныхСчетаБУНУ.Счет В(&МассивСчетов)
		                      |	И абс_ПереносДанныхСчетаБУНУ.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.абс_СтатусыПереносДанных.ВыполненПеренос)
		                      |
		                      |СГРУППИРОВАТЬ ПО
		                      |	абс_ПереносДанныхСчетаБУНУ.Ссылка");
		Запрос.УстановитьПараметр("ДЗО", 				 ДЗО);
		Запрос.УстановитьПараметр("Операция", 			 Операция);
		Запрос.УстановитьПараметр("НаправлениеПереноса", НаправлениеПереноса);
		Запрос.УстановитьПараметр("МассивСчетов", 		 МассивСчетов);
	
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат ПустаяСсылка();
		Иначе
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл 
				Если МассивыИдентичны(МассивСчетов, Выборка.Документ.СчетаБУНУ.ВыгрузитьКолонку("Счет")) Тогда 
					Возврат Выборка.Документ;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Возврат ПустаяСсылка();
		
	КонецЕсли
	
КонецФункции

Функция МассивыИдентичны(Массив1, Массив2)
	
	Если Массив1.Количество() <> Массив2.Количество() Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого элемент из Массив1 Цикл 
		Если Массив2.Найти(элемент) = Неопределено Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции
