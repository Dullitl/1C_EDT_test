////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мГоловнаяОрганизация;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАСЧЕТА

// Получаем таблицу сотрудников для сбора данных и расчета
//
// Возвращаемое значение:
//   ТаблицаЗначений - перечень сотрудников, или Неопределено, если по всем
//						поля таблицы: Сотрудник и Физлицо
//
Функция ПолучитьОграничениеНаСотрудников(Отказ)

	МассивПодразделений = Новый Массив;
	
	ТекстЗапроса = "";
	
	Если Не ЗначениеЗаполнено(Ответственный) и Не ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
		// нет условий по подразделению	
	ИначеЕсли Не ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
		//применяем условие на подразделение по ответственному
		Если ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "РасчетЗарплатыОрганизацииПоОтветственным") Тогда
			//расчет зарплаты по ответственным включен, получим список подразделений
			ТекстЗапроса = 
			"ВЫБРАТЬ 
			|	ПодразделенияОрганизаций.Ссылка
			|ИЗ
			|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
			|ГДЕ
			|	ПодразделенияОрганизаций.Ссылка В ИЕРАРХИИ
			|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|				Расчетчики.ПодразделениеОрганизации
			|			ИЗ
			|				РегистрСведений.РасчетчикиЗарплатыОрганизаций.СрезПоследних(&парамПериодРегистрации, ) КАК Расчетчики
			|			ГДЕ
			|				Расчетчики.Пользователь = &парамОтветственный)
			|	И ПодразделенияОрганизаций.Владелец = &парамОрганизация";
	
		КонецЕсли;
		
	Иначе // отбор по подразделению заданному в форме документа	
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Ссылка В ИЕРАРХИИ(&парамПодразделениеОрганизации)
		|	И ПодразделенияОрганизаций.Владелец = &парамОрганизация";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("парамОрганизация",				Организация);
		Запрос.УстановитьПараметр("парамПодразделениеОрганизации",	ПодразделениеОрганизации);
		Запрос.УстановитьПараметр("парамПериодРегистрации",			ПериодРегистрации);
		Запрос.УстановитьПараметр("парамОтветственный",				Ответственный);
		
		Запрос.Текст = ТекстЗапроса;
		МассивПодразделений = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		Если МассивПодразделений.Количество() = 0 Тогда
			//список может быть пустым когда ответственному не назначены подразделения для расчета
			ОбщегоНазначения.ВывестиИнформациюОбОшибке("Для ответственного не задан список подразделений для расчета!", Отказ, "Расчет страховых взносов");
		КонецЕсли;
	
	КонецЕсли;
	
	Если Отказ Или МассивПодразделений.Количество() = 0 Тогда
		//ошибка или нет отбора по подразделениям, возвращаем пустой список
		Возврат Неопределено; 	
	КонецЕсли;	
	
	//необходимо собрать список сотрудников, которые работали по состоянию на конец месяца
	//в подразделении из списка МассивПодразделений
	
	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("парамМассивПодразделений",	МассивПодразделений);
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация",	мГоловнаяОрганизация);
	Запрос.УстановитьПараметр("парамОрганизация",			Организация);
	Запрос.УстановитьПараметр("НачалоПериода",				ПериодРегистрации);
	Запрос.УстановитьПараметр("КонецПериода",				КонецМесяца(ПериодРегистрации));
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// ВТ_Физлица - список физлиц, которые когда-либо работали в выбранном
	// обособленном подразделении и подразделении организации
	
	// ВТ_ФизлицаДоговорники - список физлиц, которые когда-либо работали в выбранном
	// обособленном подразделении и подразделении организации
	// и чистых договорников
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РаботникиОрганизаций.Сотрудник.Физлицо КАК Физлицо
	|ПОМЕСТИТЬ ВТ_Физлица
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|ГДЕ
	|	РаботникиОрганизаций.Организация = &парамГоловнаяОрганизация
	|	И РаботникиОрганизаций.Период <= &КонецПериода
	|	И РаботникиОрганизаций.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|	И ВЫБОР
	|			КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|					И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизаций.ОбособленноеПодразделениеЗавершения
	|			ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделение
	|		КОНЕЦ = &парамОрганизация
	|	И ВЫБОР
	|			КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|					И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизаций.ПодразделениеОрганизацииЗавершения
	|			ИНАЧЕ РаботникиОрганизаций.ПодразделениеОрганизации
	|		КОНЕЦ В (&парамМассивПодразделений)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Договорники.Физлицо КАК Физлицо
	|ПОМЕСТИТЬ ВТ_ФизлицаДоговорники
	|ИЗ
	|	Справочник.СотрудникиОрганизаций КАК Договорники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ПО Договорники.Физлицо = СотрудникиОрганизаций.Физлицо
	|			И ((НЕ СотрудникиОрганизаций.ВидДоговора В (ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизлицами.Авторский), ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизлицами.Подряда))))
	|			И (СотрудникиОрганизаций.Организация = &парамГоловнаяОрганизация)
	|ГДЕ
	|	Договорники.Организация = &парамГоловнаяОрганизация
	|	И Договорники.ВидДоговора В (ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизлицами.Авторский), ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизлицами.Подряда))
	|	И СотрудникиОрганизаций.Ссылка ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Физлица.Физлицо
	|ИЗ
	|	ВТ_Физлица КАК Физлица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.Ссылка КАК Сотрудник,
	|	СотрудникиОрганизаций.Физлицо КАК Физлицо
	|ИЗ
	|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РаботникиОрганизаций.Сотрудник.Физлицо КАК Физлицо
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ДатыПоследнихДвижений.Период КАК Период,
	|				ДатыПоследнихДвижений.Физлицо КАК Физлицо,
	|				КОЛИЧЕСТВО(РаботникиОрганизаций.Период) КАК КолВоДвижений,
	|				ДатыПоследнихДвижений.ДатаНачала КАК ДатаНачала
	|			ИЗ
	|				(ВЫБРАТЬ
	|					РаботникиОрганизаций.Сотрудник.Физлицо КАК Физлицо,
	|					МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период,
	|					МАКСИМУМ(Договора.ДатаНачала) КАК ДатаНачала
	|				ИЗ
	|					РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|						ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДоговорНаВыполнениеРаботСФизЛицом КАК Договора
	|						ПО (Договора.ДатаНачала <= &КонецПериода)
	|							И (Договора.ДатаОкончания >= &НачалоПериода)
	|							И (Договора.Проведен)
	|							И (Договора.Организация = &парамОрганизация)
	|							И (Договора.ФизЛицо = РаботникиОрганизаций.Сотрудник.Физлицо)
	|				ГДЕ
	|					РаботникиОрганизаций.Организация = &парамГоловнаяОрганизация
	|					И РаботникиОрганизаций.Период <= &КонецПериода
	|					И РаботникиОрганизаций.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|					И ВЫБОР
	|							КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|									И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|								ТОГДА РаботникиОрганизаций.ОбособленноеПодразделениеЗавершения
	|							ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделение
	|						КОНЕЦ = &парамОрганизация
	|					И РаботникиОрганизаций.Сотрудник.Физлицо В
	|							(ВЫБРАТЬ
	|								ВТ_Физлица.Физлицо
	|							ИЗ
	|								ВТ_Физлица)
	|				
	|				СГРУППИРОВАТЬ ПО
	|					РаботникиОрганизаций.Сотрудник.Физлицо) КАК ДатыПоследнихДвижений
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|					ПО ДатыПоследнихДвижений.Период = РаботникиОрганизаций.Период
	|						И ДатыПоследнихДвижений.Физлицо = РаботникиОрганизаций.Сотрудник.Физлицо
	|			ГДЕ
	|				РаботникиОрганизаций.Организация = &парамГоловнаяОрганизация
	|				И РаботникиОрганизаций.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|				И ВЫБОР
	|						КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|								И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|							ТОГДА РаботникиОрганизаций.ОбособленноеПодразделениеЗавершения
	|						ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделение
	|					КОНЕЦ = &парамОрганизация
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ДатыПоследнихДвижений.Физлицо,
	|				ДатыПоследнихДвижений.Период,
	|				ДатыПоследнихДвижений.ДатаНачала) КАК ПоследниеДаты
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|				ПО ПоследниеДаты.Физлицо = РаботникиОрганизаций.Сотрудник.Физлицо
	|					И ПоследниеДаты.Период = РаботникиОрганизаций.Период
	|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДоговорНаВыполнениеРаботСФизЛицом КАК ДоговорНаВыполнениеРаботСФизЛицом
	|				ПО (ДоговорНаВыполнениеРаботСФизЛицом.Организация = &парамОрганизация)
	|					И (ДоговорНаВыполнениеРаботСФизЛицом.Проведен)
	|					И ПоследниеДаты.Физлицо = ДоговорНаВыполнениеРаботСФизЛицом.ФизЛицо
	|					И ПоследниеДаты.ДатаНачала = ДоговорНаВыполнениеРаботСФизЛицом.ДатаНачала
	|		ГДЕ
	|			РаботникиОрганизаций.Организация = &парамГоловнаяОрганизация
	|			И РаботникиОрганизаций.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|			И ВЫБОР
	|					КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|							И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА РаботникиОрганизаций.ОбособленноеПодразделениеЗавершения
	|					ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделение
	|				КОНЕЦ = &парамОрганизация
	|			И (ПоследниеДаты.КолВоДвижений = 2
	|						И РаботникиОрганизаций.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|					ИЛИ ПоследниеДаты.КолВоДвижений = 1)
	|			И ВЫБОР
	|					КОГДА РаботникиОрганизаций.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|							И РаботникиОрганизаций.Период <= &НачалоПериода
	|							И (НЕ ДоговорНаВыполнениеРаботСФизЛицом.ФизЛицо ЕСТЬ NULL )
	|						ТОГДА ЛОЖЬ
	|					КОГДА РаботникиОрганизаций.ПричинаИзмененияСостоянияЗавершения = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|							И РаботникиОрганизаций.ПериодЗавершения <= &НачалоПериода
	|							И (НЕ ДоговорНаВыполнениеРаботСФизЛицом.ФизЛицо ЕСТЬ NULL )
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|			И ВЫБОР
	|					КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|							И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА РаботникиОрганизаций.ПодразделениеОрганизацииЗавершения
	|					ИНАЧЕ РаботникиОрганизаций.ПодразделениеОрганизации
	|				КОНЕЦ В (&парамМассивПодразделений)
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			ДатыПоследнихДоговоров.Физлицо
	|		ИЗ
	|			(ВЫБРАТЬ
	|				Договора.ФизЛицо КАК Физлицо,
	|				Договора.Организация КАК Организация,
	|				МАКСИМУМ(Договора.ДатаНачала) КАК ДатаНачала,
	|				МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период
	|			ИЗ
	|				Документ.ДоговорНаВыполнениеРаботСФизЛицом КАК Договора
	|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|					ПО Договора.ФизЛицо = РаботникиОрганизаций.Сотрудник.Физлицо
	|						И (РаботникиОрганизаций.Организация = &парамГоловнаяОрганизация)
	|						И (ВЫБОР
	|							КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|									И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|								ТОГДА РаботникиОрганизаций.ОбособленноеПодразделениеЗавершения
	|							ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделение
	|						КОНЕЦ = &парамОрганизация)
	|						И (РаботникиОрганизаций.Период <= &КонецПериода)
	|						И (РаботникиОрганизаций.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство))
	|			ГДЕ
	|				Договора.ДатаНачала <= &КонецПериода
	|				И Договора.ДатаОкончания >= &НачалоПериода
	|				И Договора.Проведен
	|				И Договора.Организация = &парамОрганизация
	|				И Договора.ФизЛицо В
	|						(ВЫБРАТЬ
	|							ВТ_ФизлицаДоговорники.Физлицо
	|						ИЗ
	|							ВТ_ФизлицаДоговорники)
	|			
	|			СГРУППИРОВАТЬ ПО
	|				Договора.ФизЛицо,
	|				Договора.Организация) КАК ДатыПоследнихДоговоров
	|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДоговорНаВыполнениеРаботСФизЛицом КАК ДоговорНаВыполнениеРаботСФизЛицом
	|				ПО ДатыПоследнихДоговоров.Физлицо = ДоговорНаВыполнениеРаботСФизЛицом.ФизЛицо
	|					И ДатыПоследнихДоговоров.Организация = ДоговорНаВыполнениеРаботСФизЛицом.Организация
	|					И ДатыПоследнихДоговоров.ДатаНачала = ДоговорНаВыполнениеРаботСФизЛицом.ДатаНачала
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|				ПО ДатыПоследнихДоговоров.Физлицо = РаботникиОрганизаций.Сотрудник.Физлицо
	|					И ДатыПоследнихДоговоров.Период = РаботникиОрганизаций.Период
	|					И (РаботникиОрганизаций.Организация = &парамГоловнаяОрганизация)
	|					И (ВЫБОР
	|						КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|								И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|							ТОГДА РаботникиОрганизаций.ОбособленноеПодразделениеЗавершения
	|						ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделение
	|					КОНЕЦ = &парамОрганизация)
	|					И (РаботникиОрганизаций.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство))
	|		ГДЕ
	|			ВЫБОР
	|					КОГДА РаботникиОрганизаций.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|							И РаботникиОрганизаций.Период <= &НачалоПериода
	|						ТОГДА ИСТИНА
	|					КОГДА РаботникиОрганизаций.ПричинаИзмененияСостоянияЗавершения = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|							И РаботникиОрганизаций.ПериодЗавершения <= &НачалоПериода
	|						ТОГДА ИСТИНА
	|					КОГДА РаботникиОрганизаций.ПричинаИзмененияСостоянияЗавершения ЕСТЬ NULL 
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ДатыПоследнихДоговоров.Физлицо
	|		
	|		ИМЕЮЩИЕ
	|			МАКСИМУМ(ДоговорНаВыполнениеРаботСФизЛицом.ПодразделениеОрганизации) В (&парамМассивПодразделений)) КАК СписокФизлиц
	|		ПО СотрудникиОрганизаций.Физлицо = СписокФизлиц.Физлицо
	|ГДЕ
	|	СотрудникиОрганизаций.Организация = &парамГоловнаяОрганизация";
	
	Запрос.Текст = ТекстЗапроса;
	
    Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьОграничениеНаСотрудников()

// Предназначена для получения выборки из запроса с данными расчета 
// По текущему регистратору получает 
//    	данные о начисленной зарплате в разрезе кодов дохода 
// 		данные об инвалидности физлиц помесячно
//		присоединяет данные о скидках к доходам
//
// Параметры:
//  нет
// Возвращаемое значение:
//  Выборка результата запроса
//
Функция ПолучитьДанныеРасчета(ГоловнаяОрганизация, ОрганичениеНаСотрудников, ДанныеОДоходах, Отказ)
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// расчет вызван независимо от получения данных о доходах,
	// получим список сотрудников для расчета 
	Если ОрганичениеНаСотрудников = Неопределено Тогда
		ОрганичениеНаСотрудников = ПолучитьОграничениеНаСотрудников(Отказ);	
		Если Отказ Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("парамКонецМесяцаПериодаРегистрации", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("парамОбособленноеПодразделение", Организация);
	Запрос.УстановитьПараметр("парамНачалоГода", НачалоГода(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамКонецГода", КонецГода(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамГодРасчета", Год(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамМесяцРасчета", Месяц(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамПредыдущийМесяц", Месяц(ПериодРегистрации) - 1);
	Запрос.УстановитьПараметр("парамПериодРегистрации", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	ПредельнаяСумма = РегистрыСведений.ПредельнаяВеличинаБазыСтраховыхВзносов.ПолучитьПоследнее(КонецМесяца(ПериодРегистрации)).Размер;
	Запрос.УстановитьПараметр("ПредельнаяСумма", ?(ЗначениеЗаполнено(ПредельнаяСумма),ПредельнаяСумма,0));
	Ставка = РегистрыСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.ПолучитьПоследнее(КонецМесяца(ПериодРегистрации), Новый Структура("Организация", Организация)).Ставка;
	Запрос.УстановитьПараметр("СтавкаВзносаФСС_НС_ПЗ", Ставка / 100);
	
	// ---------------------------------------------------------------------------
	// тексты запросов
	//

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОДоходах.ФизЛицо,
	|	МЕСЯЦ(СведенияОДоходах.Период) КАК Месяц,
	|	СведенияОДоходах.ОблагаетсяЕНВД,
	|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СведенияОДоходах.ВидДохода,
	|	СведенияОДоходах.Результат - СведенияОДоходах.Скидка КАК ОблагаемыйДоход
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	&ДанныеОДоходах КАК СведенияОДоходах";
	Запрос.УстановитьПараметр("ДанныеОДоходах", ДанныеОДоходах);
	Запрос.Выполнить();
	
	// ФизлицаДляРасчета
	//
	Если ОрганичениеНаСотрудников = Неопределено Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОДоходах.ФизЛицо КАК ФизЛицо
		|ПОМЕСТИТЬ ВТСписокСотрудников
		|ИЗ
		|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СведенияОДоходах
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, МЕСЯЦ) = &парамПериодРегистрации
		|	И СведенияОДоходах.ОбособленноеПодразделение = &парамОбособленноеПодразделение
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОДоходах.ФизЛицо
		|ИЗ
		|	ВТДанныеДокумента КАК СведенияОДоходах
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
		Запрос.Выполнить();
		
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
        |   ТаблицаСотрудников.ФизЛицо КАК ФизЛицо
        |ПОМЕСТИТЬ ВТСписокСотрудников
        |ИЗ
        |   &парамТаблицаСотрудников КАК ТаблицаСотрудников
        |
        |ИНДЕКСИРОВАТЬ ПО
        |   ФизЛицо";
		Запрос.УстановитьПараметр("парамТаблицаСотрудников", ОрганичениеНаСотрудников);
		Запрос.Выполнить();
	КонецЕсли;
	
	// Периоды
	// Таблица список периодов
	// Поля:
	//		Период
	// Описание:
	//  список периодов - первые даты месяцев с начала года по "текущий" для запроса период
	//
	
	// конец месяца налогового периода
	КонецМесяца = КонецМесяца(НачалоГода(ПериодРегистрации));
	// первый месяц
	ПериодыТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период ПОМЕСТИТЬ ВТПериоды";
	// прибавим остальные месяцы
	Для Сч = 2 По Месяц(ПериодРегистрации) Цикл
		КонецМесяца = КонецМесяца(КонецМесяца+1);
    	ПериодыТекст = ПериодыТекст +" ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	Запрос.Текст = ПериодыТекст;
	Запрос.Выполнить();

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчет.ВидТарифаСтраховыхВзносов, ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОбщийНалоговыйРежим)) КАК ВидТарифаСтраховыхВзносов
	|ПОМЕСТИТЬ ВТИспользуемыеТарифы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериоды КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаНалоговыйУчет КАК УчетнаяПолитикаНалоговыйУчет
	|			ПО Периоды.Период >= УчетнаяПолитикаНалоговыйУчет.Период
	|				И (УчетнаяПолитикаНалоговыйУчет.Организация = &парамГоловнаяОрганизация)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.Период) КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаНалоговыйУчет КАК УчетнаяПолитикаНалоговыйУчет
	|		ПО Периоды.ПериодРегистра = УчетнаяПолитикаНалоговыйУчет.Период
	|			И (УчетнаяПолитикаНалоговыйУчет.Организация = &парамГоловнаяОрганизация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТарифСтраховыхВзносовСрезПоследних.ВидТарифа КАК ВидТарифа,
	|	ТарифСтраховыхВзносовСрезПоследних.ПФР,
	|	ТарифСтраховыхВзносовСрезПоследних.ПФРНакопительная,
	|	ТарифСтраховыхВзносовСрезПоследних.ФСС,
	|	ТарифСтраховыхВзносовСрезПоследних.ФФОМС,
	|	ТарифСтраховыхВзносовСрезПоследних.ТФОМС
	|ПОМЕСТИТЬ ВТТарифы
	|ИЗ
	|	РегистрСведений.ТарифСтраховыхВзносов.СрезПоследних(&парамКонецГода, ) КАК ТарифСтраховыхВзносовСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидТарифа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	ГражданствоФизЛиц.ФизЛицо КАК Физлицо,
	|	ЕСТЬNULL(ГражданствоФизЛиц.НеИмеетПравоНаПенсию, ЛОЖЬ) КАК НеИмеетПравоНаПенсию,
	|	ЕСТЬNULL(ГражданствоФизЛиц.НеИмеетПравоНаМедицинскоеСтрахование, ЛОЖЬ) КАК НеИмеетПравоНаМедицинскоеСтрахование
	|ПОМЕСТИТЬ ВТДанныеОПравеНаПенсию
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		ГражданствоФизЛиц.ФизЛицо КАК Физлицо,
	|		МАКСИМУМ(ГражданствоФизЛиц.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериоды КАК Периоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|			ПО Периоды.Период >= ГражданствоФизЛиц.Период
	|	ГДЕ
	|		ГражданствоФизЛиц.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТСписокСотрудников КАК СписокСотрудников)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ГражданствоФизЛиц.ФизЛицо,
	|		Периоды.Период) КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО Периоды.ПериодРегистра = ГражданствоФизЛиц.Период
	|			И Периоды.Физлицо = ГражданствоФизЛиц.ФизЛицо
	|			И (ГражданствоФизЛиц.НеИмеетПравоНаПенсию
	|				ИЛИ ГражданствоФизЛиц.НеИмеетПравоНаМедицинскоеСтрахование)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо,
	|	СведенияОбИнвалидностиФизлиц.Период КАК Период,
	|	СведенияОбИнвалидностиФизлиц.Инвалидность
	|ПОМЕСТИТЬ ВТ_СведенияОбИнвалидности
	|ИЗ
	|	РегистрСведений.СведенияОбИнвалидностиФизлиц КАК СведенияОбИнвалидностиФизлиц
	|ГДЕ
	|	СведенияОбИнвалидностиФизлиц.Физлицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокСотрудников КАК СписокСотрудников)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо
	|ПОМЕСТИТЬ ВТДанныеОбИнвалидности
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо,
	|		МАКСИМУМ(СведенияОбИнвалидностиФизлиц.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериоды КАК Периоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СведенияОбИнвалидности КАК СведенияОбИнвалидностиФизлиц
	|			ПО Периоды.Период >= СведенияОбИнвалидностиФизлиц.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СведенияОбИнвалидностиФизлиц.Физлицо,
	|		Периоды.Период) КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СведенияОбИнвалидности КАК СведенияОбИнвалидностиФизлиц
	|		ПО (СведенияОбИнвалидностиФизлиц.Период = Периоды.ПериодРегистра)
	|			И Периоды.Физлицо = СведенияОбИнвалидностиФизлиц.Физлицо
	|			И (СведенияОбИнвалидностиФизлиц.Инвалидность)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|	МЕСЯЦ(СведенияОДоходах.Период) КАК Месяц,
	|	СведенияОДоходах.ОблагаетсяЕНВД,
	|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СведенияОДоходах.ВидДохода.ВходитВБазуПФР КАК ВходитВБазуПФР,
	|	СведенияОДоходах.ВидДохода.ВходитВБазуФСС КАК ВходитВБазуФСС,
	|	СведенияОДоходах.ВидДохода.ВходитВБазуФОМС КАК ВходитВБазуФОМС,
	|	СведенияОДоходах.РезультатОборот - СведенияОДоходах.СкидкаОборот КАК ОблагаемыйДоход
	|ПОМЕСТИТЬ ВТДоходыПоФизлицамМесяцам
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах.Обороты(
	|			&парамНачалоГода,
	|			&парамКонецМесяцаПериодаРегистрации,
	|			Месяц,
	|			Организация = &парамГоловнаяОрганизация
	|				И Физлицо В
	|					(ВЫБРАТЬ
	|						СписокСотрудников.ФизЛицо
	|					ИЗ
	|						ВТСписокСотрудников КАК СписокСотрудников)) КАК СведенияОДоходах
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|	МЕСЯЦ(СтраховыеВзносыСведенияОДоходах.Период),
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуПФР,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФСС,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФОМС,
	|	-СтраховыеВзносыСведенияОДоходах.Результат + СтраховыеВзносыСведенияОДоходах.Скидка
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СтраховыеВзносыСведенияОДоходах
	|ГДЕ
	|	СтраховыеВзносыСведенияОДоходах.Регистратор = &Регистратор
	|	И СтраховыеВзносыСведенияОДоходах.Организация = &парамГоловнаяОрганизация
	|	И СтраховыеВзносыСведенияОДоходах.ФизЛицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокСотрудников КАК СписокСотрудников)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|	СтраховыеВзносыСведенияОДоходах.Месяц,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуПФР,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФСС,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФОМС,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаемыйДоход
	|ИЗ
	|	ВТДанныеДокумента КАК СтраховыеВзносыСведенияОДоходах
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходах.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ ВТФизлицаБезОблагаемойБазы
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК СведенияОДоходах
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходах.ФизЛицо
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходах.ВходитВБазуПФР
	|					ИЛИ СведенияОДоходах.ВходитВБазуФОМС
	|				ТОГДА СведенияОДоходах.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РанееИсчисленныеВзносы.ФизЛицо КАК ФизЛицо,
	|	МЕСЯЦ(РанееИсчисленныеВзносы.Период) КАК Месяц,
	|	РанееИсчисленныеВзносы.ФССОборот,
	|	РанееИсчисленныеВзносы.ФФОМСОборот,
	|	РанееИсчисленныеВзносы.ТФОМСОборот,
	|	РанееИсчисленныеВзносы.ПФРНакопительнаяОборот,
	|	РанееИсчисленныеВзносы.ПФРСтраховаяОборот,
	|	РанееИсчисленныеВзносы.ПФРНакопительнаяЕНВДОборот,
	|	РанееИсчисленныеВзносы.ПФРСтраховаяЕНВДОборот,
	|	РанееИсчисленныеВзносы.ПФРПоДополнительномуТарифуОборот
	|ПОМЕСТИТЬ ВТРанееИсчисленныеВзносыПоМесяцам
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыИсчисленные.Обороты(
	|			&парамНачалоГода,
	|			&парамКонецМесяцаПериодаРегистрации,
	|			Месяц,
	|			Организация = &парамГоловнаяОрганизация
	|				И (НЕ ФизЛицо В
	|						(ВЫБРАТЬ
	|							СписокСотрудников.ФизЛицо
	|						ИЗ
	|							ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|				И ФизЛицо В
	|					(ВЫБРАТЬ
	|						СписокСотрудников.ФизЛицо
	|					ИЗ
	|						ВТСписокСотрудников КАК СписокСотрудников)) КАК РанееИсчисленныеВзносы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыИсчисленные.ФизЛицо,
	|	МЕСЯЦ(СтраховыеВзносыИсчисленные.Период),
	|	-СтраховыеВзносыИсчисленные.ФСС,
	|	-СтраховыеВзносыИсчисленные.ФФОМС,
	|	-СтраховыеВзносыИсчисленные.ТФОМС,
	|	-СтраховыеВзносыИсчисленные.ПФРНакопительная,
	|	-СтраховыеВзносыИсчисленные.ПФРСтраховая,
	|	-СтраховыеВзносыИсчисленные.ПФРНакопительнаяЕНВД,
	|	-СтраховыеВзносыИсчисленные.ПФРСтраховаяЕНВД,
	|	-СтраховыеВзносыИсчисленные.ПФРПоДополнительномуТарифу
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыИсчисленные КАК СтраховыеВзносыИсчисленные
	|ГДЕ
	|	СтраховыеВзносыИсчисленные.Регистратор = &Регистратор
	|	И СтраховыеВзносыИсчисленные.Организация = &парамГоловнаяОрганизация
	|	И (НЕ СтраховыеВзносыИсчисленные.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|	И СтраховыеВзносыИсчисленные.ФизЛицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокСотрудников КАК СписокСотрудников)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РанееИсчисленныеВзносы.ФизЛицо КАК ФизЛицо,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ФССОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ФССОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ФФОМСОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ФФОМСОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ТФОМСОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ТФОМСОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ФССОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ФССОборотИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ФФОМСОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ФФОМСОборотИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ТФОМСОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ТФОМСОборотИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРНакопительнаяОборот + РанееИсчисленныеВзносы.ПФРНакопительнаяЕНВДОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРНакопительнаяОборотИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРСтраховаяОборот + РанееИсчисленныеВзносы.ПФРСтраховаяЕНВДОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРСтраховаяОборотИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРНакопительнаяОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРНакопительнаяОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРСтраховаяОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРСтраховаяОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРНакопительнаяЕНВДОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРНакопительнаяЕНВДОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРСтраховаяЕНВДОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРСтраховаяЕНВДОборот,
	|	СУММА(РанееИсчисленныеВзносы.ПФРПоДополнительномуТарифуОборот) КАК ПФРПоДополнительномуТарифуОборот
	|ПОМЕСТИТЬ ВТРанееИсчисленныеВзносы
	|ИЗ
	|	ВТРанееИсчисленныеВзносыПоМесяцам КАК РанееИсчисленныеВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбИнвалидности КАК Инвалид
	|		ПО РанееИсчисленныеВзносы.ФизЛицо = Инвалид.Физлицо
	|			И РанееИсчисленныеВзносы.Месяц = Инвалид.Месяц
	|
	|СГРУППИРОВАТЬ ПО
	|	РанееИсчисленныеВзносы.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Основная.ФизЛицо КАК ФизЛицо,
	|	Основная.Месяц КАК Месяц,
	|	СУММА(ВЫБОР
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФР,
	|	СУММА(ВЫБОР
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФСС,
	|	СУММА(ВЫБОР
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаМедицинскоеСтрахование))
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМС,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|					И Основная.ОблагаетсяЕНВД
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРИнвалидаЕНВД,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|				ТОГДА 0
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРПоОсновномуТарифу,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРЕНВД,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФССИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|				ТОГДА 0
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФССПоОсновномуТарифу,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФССЕНВД,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМСИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|				ТОГДА 0
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаМедицинскоеСтрахование))
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМСПоОсновномуТарифу,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМСЕНВД
	|ПОМЕСТИТЬ ВТДоходыЗастрахованныхЛиц
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК Основная
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОПравеНаПенсию КАК Иностр
	|		ПО Основная.ФизЛицо = Иностр.Физлицо
	|			И Основная.Месяц = Иностр.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбИнвалидности КАК Инвалид
	|		ПО Основная.ФизЛицо = Инвалид.Физлицо
	|			И Основная.Месяц = Инвалид.Месяц
	|ГДЕ
	|	(НЕ Основная.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|
	|СГРУППИРОВАТЬ ПО
	|	Основная.ФизЛицо,
	|	Основная.Месяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	ДоходыЗастрахованныхЛиц.ФизЛицо КАК ФизЛицо,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаПФР) КАК БазаПФР,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаФСС) КАК БазаФСС,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаФОМС) КАК БазаФОМС
	|ПОМЕСТИТЬ ВТНакопленнаяОблагаемаяБаза
	|ИЗ
	|	ВТПериоды КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыЗастрахованныхЛиц КАК ДоходыЗастрахованныхЛиц
	|		ПО (ДоходыЗастрахованныхЛиц.Месяц МЕЖДУ 1 И МЕСЯЦ(Периоды.Период))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыЗастрахованныхЛиц.ФизЛицо,
	|	МЕСЯЦ(Периоды.Период)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыЗастрахованныхЛиц.ФизЛицо КАК ФизЛицо,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаПФРИнвалида) КАК БазаПФРИнвалидаВсего,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаПФРИнвалидаЕНВД) КАК БазаПФРИнвалидаЕНВД
	|ПОМЕСТИТЬ ВТБазаПФРПоИнвалидам
	|ИЗ
	|	ВТДоходыЗастрахованныхЛиц КАК ДоходыЗастрахованныхЛиц
	|ГДЕ
	|	ДоходыЗастрахованныхЛиц.Месяц = &парамМесяцРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыЗастрахованныхЛиц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	База.Месяц КАК Месяц,
	|	База.ФизЛицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА База.БазаПФР <= &ПредельнаяСумма
	|			ТОГДА 1
	|		КОГДА База.БазаПФР = ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0)
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0) >= &ПредельнаяСумма
	|			ТОГДА 0
	|		ИНАЧЕ (&ПредельнаяСумма - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0)) / (База.БазаПФР - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0))
	|	КОНЕЦ КАК КоэффициентОбложенияПФР,
	|	ВЫБОР
	|		КОГДА База.БазаФСС <= &ПредельнаяСумма
	|			ТОГДА 1
	|		КОГДА База.БазаФСС = ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0)
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0) >= &ПредельнаяСумма
	|			ТОГДА 0
	|		ИНАЧЕ (&ПредельнаяСумма - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0)) / (База.БазаФСС - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0))
	|	КОНЕЦ КАК КоэффициентОбложенияФСС,
	|	ВЫБОР
	|		КОГДА База.БазаФОМС <= &ПредельнаяСумма
	|			ТОГДА 1
	|		КОГДА База.БазаФОМС = ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0)
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0) >= &ПредельнаяСумма
	|			ТОГДА 0
	|		ИНАЧЕ (&ПредельнаяСумма - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0)) / (База.БазаФОМС - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0))
	|	КОНЕЦ КАК КоэффициентОбложенияФОМС
	|ПОМЕСТИТЬ ВТКоэффициентыДостиженияПредельнойСуммы
	|ИЗ
	|	ВТНакопленнаяОблагаемаяБаза КАК База
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНакопленнаяОблагаемаяБаза КАК НакопленоПоПрошлыйМесяц
	|		ПО (База.Месяц - 1 = НакопленоПоПрошлыйМесяц.Месяц)
	|			И База.ФизЛицо = НакопленоПоПрошлыйМесяц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыЗастрахованныхЛиц.ФизЛицо КАК ФизЛицо,
	|	ДоходыЗастрахованныхЛиц.Месяц КАК Месяц,
	|	ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаПФРИнвалида
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФРИнвалида = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)
	|	КОНЕЦ КАК БазаПФРИнвалида,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФРЕНВД = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)
	|		ИНАЧЕ ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)) * ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаПФРЕНВД) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК БазаПФРПоОсновномуТарифу,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаПФРЕНВД
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФРЕНВД = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0) - (ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)) * ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаПФРЕНВД) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК БазаПФРЕНВД,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФССИнвалида
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФССИнвалида = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)
	|	КОНЕЦ КАК БазаФССИнвалида,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФССЕНВД = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)
	|		ИНАЧЕ ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)) * ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаФССЕНВД) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК БазаФССПоОсновномуТарифу,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФССЕНВД
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФССЕНВД = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0) - (ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)) * ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаФССЕНВД) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК БазаФССЕНВД,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФОМСИнвалида
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМСИнвалида = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)
	|	КОНЕЦ КАК БазаФОМСИнвалида,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМСЕНВД = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)
	|		ИНАЧЕ ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)) * ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаФОМСЕНВД) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК БазаФОМСПоОсновномуТарифу,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФОМСЕНВД
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМСЕНВД = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0) - (ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)) * ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаФОМСЕНВД) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК БазаФОМСЕНВД
	|ПОМЕСТИТЬ ВТОграниченнаяОблагаемаяБаза
	|ИЗ
	|	ВТДоходыЗастрахованныхЛиц КАК ДоходыЗастрахованныхЛиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициентыДостиженияПредельнойСуммы КАК Коэффициенты
	|		ПО ДоходыЗастрахованныхЛиц.ФизЛицо = Коэффициенты.ФизЛицо
	|			И ДоходыЗастрахованныхЛиц.Месяц = Коэффициенты.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНакопленнаяОблагаемаяБаза КАК НакопленнаяОблагаемаяБазаПоПредыдущийМесяц
	|		ПО ДоходыЗастрахованныхЛиц.ФизЛицо = НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.ФизЛицо
	|			И (ДоходыЗастрахованныхЛиц.Месяц - 1 = НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.Месяц)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользуемыеТарифы КАК ИспользуемыеТарифы
	|		ПО ДоходыЗастрахованныхЛиц.Месяц = ИспользуемыеТарифы.Месяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо КАК ФизЛицо,
	|	ОграниченнаяОблагаемаяБаза.ВидТарифаСтраховыхВзносов,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРПоОсновномуТарифу) * ОсновнойТариф.ПФРНакопительная / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРНакопительная,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРПоОсновномуТарифу) * ОсновнойТариф.ПФР / 100 КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРПоОсновномуТарифу) * (ОсновнойТариф.ПФР - ОсновнойТариф.ПФРНакопительная) / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРСтраховая,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФССПоОсновномуТарифу) * ОсновнойТариф.ФСС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФСС,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСПоОсновномуТарифу) * ОсновнойТариф.ФФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФФОМС,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСПоОсновномуТарифу) * ОсновнойТариф.ТФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатТФОМС
	|ПОМЕСТИТЬ ВТИсчисленоПоОсновномуТарифу
	|ИЗ
	|	ВТОграниченнаяОблагаемаяБаза КАК ОграниченнаяОблагаемаяБаза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТарифы КАК ОсновнойТариф
	|		ПО ОграниченнаяОблагаемаяБаза.ВидТарифаСтраховыхВзносов = ОсновнойТариф.ВидТарифа
	|
	|СГРУППИРОВАТЬ ПО
	|	ОсновнойТариф.ПФРНакопительная,
	|	ОсновнойТариф.ПФР,
	|	ОсновнойТариф.ФСС,
	|	ОсновнойТариф.ФФОМС,
	|	ОсновнойТариф.ТФОМС,
	|	ОграниченнаяОблагаемаяБаза.ВидТарифаСтраховыхВзносов,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРИнвалида) * ТарифИнвалида.ПФРНакопительная / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРНакопительнаяИнвалида,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРИнвалида) * ТарифИнвалида.ПФР / 100 КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРИнвалида) * (ТарифИнвалида.ПФР - ТарифИнвалида.ПФРНакопительная) / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРСтраховаяИнвалида,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФССИнвалида) * ТарифИнвалида.ФСС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФССИнвалида,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСИнвалида) * ТарифИнвалида.ФФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФФОМСИнвалида,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСИнвалида) * ТарифИнвалида.ТФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатТФОМСИнвалида,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРЕНВД) * ТарифЕНВД.ПФРНакопительная / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРНакопительнаяЕНВД,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРЕНВД) * ТарифЕНВД.ПФР / 100 КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРЕНВД) * (ТарифЕНВД.ПФР - ТарифЕНВД.ПФРНакопительная) / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРСтраховаяЕНВД
	|ПОМЕСТИТЬ ВТИсчисленоПоЛьготномуТарифу
	|ИЗ
	|	ВТОграниченнаяОблагаемаяБаза КАК ОграниченнаяОблагаемаяБаза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТарифы КАК ТарифЕНВД
	|		ПО (ТарифЕНВД.ВидТарифа = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ЕНВД))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТарифы КАК ТарифИнвалида
	|		ПО (ТарифИнвалида.ВидТарифа = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТарифИнвалида.ПФРНакопительная,
	|	ТарифИнвалида.ПФР,
	|	ТарифИнвалида.ФСС,
	|	ТарифИнвалида.ФФОМС,
	|	ТарифИнвалида.ТФОМС,
	|	ТарифЕНВД.ПФРНакопительная,
	|	ТарифЕНВД.ПФР,
	|	ТарифЕНВД.ФСС,
	|	ТарифЕНВД.ФФОМС,
	|	ТарифЕНВД.ТФОМС,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СУММА(ДоходыФизлиц.ОблагаемыйДоход) * 0.14 КАК ЧИСЛО(15, 2)) КАК РезультатПоДополнительномуТарифу,
	|	ДоходыФизлиц.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ ВТВзносыПоДополнительномуТарифу
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК ДоходыФизлиц
	|ГДЕ
	|	ДоходыФизлиц.ОблагаетсяПоДополнительномуТарифу
	|	И ДоходыФизлиц.ВходитВБазуПФР
	|	И (НЕ ДоходыФизлиц.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыФизлиц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начислено.ФизЛицо КАК ФизЛицо,
	|	СУММА(Начислено.ОблагаемаяБаза) КАК ОблагаемаяБаза,
	|	СУММА(Начислено.ОблагаемаяБазаИнвалида) КАК ОблагаемаяБазаИнвалида,
	|	ВЫРАЗИТЬ(СУММА(Начислено.ОблагаемаяБаза) * &СтавкаВзносаФСС_НС_ПЗ КАК ЧИСЛО(15, 2)) КАК Взносов,
	|	ВЫРАЗИТЬ(СУММА(Начислено.ОблагаемаяБазаИнвалида) * &СтавкаВзносаФСС_НС_ПЗ * 0.6 КАК ЧИСЛО(15, 2)) КАК ВзносовПоИнвалидам
	|ПОМЕСТИТЬ ВТБазаВзносовФСС_НС_ПЗ
	|ИЗ
	|	(ВЫБРАТЬ
	|		БазовыеНачисленияФССНС.ФизЛицо КАК ФизЛицо,
	|		ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|					И ОсновнойТариф.ВидТарифаСтраховыхВзносов <> ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов)
	|				ТОГДА БазовыеНачисленияФССНС.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ОблагаемаяБаза,
	|		ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					ИЛИ ОсновнойТариф.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов)
	|				ТОГДА БазовыеНачисленияФССНС.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ОблагаемаяБазаИнвалида
	|	ИЗ
	|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК БазовыеНачисленияФССНС
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбИнвалидности КАК Инвалид
	|			ПО БазовыеНачисленияФССНС.ФизЛицо = Инвалид.Физлицо
	|				И (Инвалид.Месяц = &парамМесяцРасчета)
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользуемыеТарифы КАК ОсновнойТариф
	|			ПО (ОсновнойТариф.Месяц = &парамМесяцРасчета)
	|	ГДЕ
	|		БазовыеНачисленияФССНС.ПериодРегистрации = &парамПериодРегистрации
	|		И &парамДопУсловие
	|		И БазовыеНачисленияФССНС.ОбособленноеПодразделение = &парамОбособленноеПодразделение
	|		И (НЕ БазовыеНачисленияФССНС.ФизЛицо В
	|					(ВЫБРАТЬ
	|						СписокСотрудников.ФизЛицо
	|					ИЗ
	|						ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|		И БазовыеНачисленияФССНС.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТСписокСотрудников КАК СписокСотрудников)
	|		И БазовыеНачисленияФССНС.ВидРасчета.КодДоходаФСС_НС = ЗНАЧЕНИЕ(Перечисление.ДоходыФСС_НС.Облагается)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БазовыеНачисленияФССНС.ФизЛицо,
	|		ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|					И ОсновнойТариф.ВидТарифаСтраховыхВзносов <> ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов)
	|				ТОГДА БазовыеНачисленияФССНС.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					ИЛИ ОсновнойТариф.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов)
	|				ТОГДА БазовыеНачисленияФССНС.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК БазовыеНачисленияФССНС
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбИнвалидности КАК Инвалид
	|			ПО БазовыеНачисленияФССНС.ФизЛицо = Инвалид.Физлицо
	|				И (Инвалид.Месяц = &парамМесяцРасчета)
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользуемыеТарифы КАК ОсновнойТариф
	|			ПО (ОсновнойТариф.Месяц = &парамМесяцРасчета)
	|	ГДЕ
	|		НАЧАЛОПЕРИОДА(БазовыеНачисленияФССНС.ПериодРегистрации, МЕСЯЦ) = &парамПериодРегистрации
	|		И &парамДопУсловие
	|		И БазовыеНачисленияФССНС.ВидРасчета.КодДоходаФСС_НС = ЗНАЧЕНИЕ(Перечисление.ДоходыФСС_НС.Облагается)
	|		И (НЕ БазовыеНачисленияФССНС.ФизЛицо В
	|					(ВЫБРАТЬ
	|						СписокСотрудников.ФизЛицо
	|					ИЗ
	|						ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|		И БазовыеНачисленияФССНС.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТСписокСотрудников КАК СписокСотрудников)
	|		И БазовыеНачисленияФССНС.ОбособленноеПодразделение = &парамОбособленноеПодразделение) КАК Начислено
	|
	|СГРУППИРОВАТЬ ПО
	|	Начислено.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(БазаВзносовФСС_НС_ПЗ.ОблагаемаяБаза) КАК МаксимумБазы,
	|	МАКСИМУМ(БазаВзносовФСС_НС_ПЗ.ОблагаемаяБазаИнвалида) КАК МаксимумБазыИнвалида
	|ПОМЕСТИТЬ ВТМаксимумыОблагаемойБазыПоНС_ПЗ
	|ИЗ
	|	ВТБазаВзносовФСС_НС_ПЗ КАК БазаВзносовФСС_НС_ПЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(БазаВзносовФСС_НС_ПЗ.ФизЛицо) КАК ФизЛицо,
	|	МАКСИМУМ(БазаВзносовФСС_НС_ПЗИнвалида.ФизЛицо) КАК ФизЛицоИнвалид
	|ПОМЕСТИТЬ ВТФизлицаОкругления
	|ИЗ
	|	ВТМаксимумыОблагаемойБазыПоНС_ПЗ КАК Максимумы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВзносовФСС_НС_ПЗ КАК БазаВзносовФСС_НС_ПЗ
	|		ПО (Максимумы.МаксимумБазы > 0)
	|			И Максимумы.МаксимумБазы = БазаВзносовФСС_НС_ПЗ.ОблагаемаяБаза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВзносовФСС_НС_ПЗ КАК БазаВзносовФСС_НС_ПЗИнвалида
	|		ПО (Максимумы.МаксимумБазыИнвалида > 0)
	|			И Максимумы.МаксимумБазыИнвалида = БазаВзносовФСС_НС_ПЗИнвалида.ОблагаемаяБазаИнвалида
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СУММА(БазаВзносовФСС_НС_ПЗ.ОблагаемаяБаза) * &СтавкаВзносаФСС_НС_ПЗ КАК ЧИСЛО(15, 2)) КАК РассчитаноВзносов,
	|	ВЫРАЗИТЬ(СУММА(БазаВзносовФСС_НС_ПЗ.ОблагаемаяБазаИнвалида) * &СтавкаВзносаФСС_НС_ПЗ * 0.6 КАК ЧИСЛО(15, 2)) КАК РассчитаноВзносовПоИнвалидам,
	|	СУММА(ВЫБОР
	|			КОГДА БазаВзносовФСС_НС_ПЗ.ФизЛицо = ФизлицоОкругления.ФизЛицо
	|				ТОГДА 0
	|			ИНАЧЕ БазаВзносовФСС_НС_ПЗ.Взносов
	|		КОНЕЦ) КАК ПросуммированоВзносов,
	|	СУММА(ВЫБОР
	|			КОГДА БазаВзносовФСС_НС_ПЗ.ФизЛицо = ФизлицоОкругления.ФизЛицоИнвалид
	|				ТОГДА 0
	|			ИНАЧЕ БазаВзносовФСС_НС_ПЗ.ВзносовПоИнвалидам
	|		КОНЕЦ) КАК ПросуммированоВзносовПоИнвалидам
	|ПОМЕСТИТЬ ВТИтогоВзносовФСС_НС_ПЗ
	|ИЗ
	|	ВТБазаВзносовФСС_НС_ПЗ КАК БазаВзносовФСС_НС_ПЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФизлицаОкругления КАК ФизлицоОкругления
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БазаВзносовФСС_НС_ПЗ.ФизЛицо КАК ФизЛицо,
	|	БазаВзносовФСС_НС_ПЗ.Взносов + БазаВзносовФСС_НС_ПЗ.ВзносовПоИнвалидам КАК Взносов
	|ПОМЕСТИТЬ ВТВзносыФС_НС_ПЗ
	|ИЗ
	|	ВТБазаВзносовФСС_НС_ПЗ КАК БазаВзносовФСС_НС_ПЗ
	|ГДЕ
	|	(НЕ БазаВзносовФСС_НС_ПЗ.ФизЛицо В
	|				(ВЫБРАТЬ
	|					ФизлицоОкругления.ФизЛицо
	|				ИЗ
	|					ВТФизлицаОкругления КАК ФизлицоОкругления
	|				ГДЕ
	|					ФизлицоОкругления.ФизЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|		
	|				ОБЪЕДИНИТЬ ВСЕ
	|		
	|				ВЫБРАТЬ
	|					ФизлицоОкругления.ФизЛицоИнвалид
	|				ИЗ
	|					ВТФизлицаОкругления КАК ФизлицоОкругления
	|				ГДЕ
	|					ФизлицоОкругления.ФизЛицоИнвалид <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)))
	|	И БазаВзносовФСС_НС_ПЗ.Взносов + БазаВзносовФСС_НС_ПЗ.ВзносовПоИнвалидам <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ФизлицоОкругления.ФизЛицо,
	|	ИтогоВзносовФСС_НС_ПЗ.РассчитаноВзносов - ИтогоВзносовФСС_НС_ПЗ.ПросуммированоВзносов
	|ИЗ
	|	ВТФизлицаОкругления КАК ФизлицоОкругления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогоВзносовФСС_НС_ПЗ КАК ИтогоВзносовФСС_НС_ПЗ
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ИтогоВзносовФСС_НС_ПЗ.РассчитаноВзносов <> ИтогоВзносовФСС_НС_ПЗ.ПросуммированоВзносов
	|	И ФизлицоОкругления.ФизЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ФизлицоОкругления.ФизЛицоИнвалид,
	|	ИтогоВзносовФСС_НС_ПЗ.РассчитаноВзносовПоИнвалидам - ИтогоВзносовФСС_НС_ПЗ.ПросуммированоВзносовПоИнвалидам
	|ИЗ
	|	ВТФизлицаОкругления КАК ФизлицоОкругления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогоВзносовФСС_НС_ПЗ КАК ИтогоВзносовФСС_НС_ПЗ
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ИтогоВзносовФСС_НС_ПЗ.РассчитаноВзносовПоИнвалидам <> ИтогоВзносовФСС_НС_ПЗ.ПросуммированоВзносовПоИнвалидам
	|	И ФизлицоОкругления.ФизЛицоИнвалид <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыИсчисленныеОбороты.ФизЛицо,
	|	-СтраховыеВзносыИсчисленныеОбороты.ФССНесчастныеСлучаиОборот
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыИсчисленные.Обороты(
	|			&парамПериодРегистрации,
	|			&парамКонецМесяцаПериодаРегистрации,
	|			,
	|			(НЕ ФизЛицо В
	|						(ВЫБРАТЬ
	|							СписокСотрудников.ФизЛицо
	|						ИЗ
	|							ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|				И ФизЛицо В
	|					(ВЫБРАТЬ
	|						СписокСотрудников.ФизЛицо
	|					ИЗ
	|						ВТСписокСотрудников КАК СписокСотрудников)
	|				И ОбособленноеПодразделение = &парамОбособленноеПодразделение) КАК СтраховыеВзносыИсчисленныеОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыИсчисленные.ФизЛицо,
	|	СтраховыеВзносыИсчисленные.ФССНесчастныеСлучаи
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыИсчисленные КАК СтраховыеВзносыИсчисленные
	|ГДЕ
	|	СтраховыеВзносыИсчисленные.Регистратор = &Регистратор
	|	И (НЕ СтраховыеВзносыИсчисленные.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|	И СтраховыеВзносыИсчисленные.ФизЛицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокСотрудников КАК СписокСотрудников)
	|	И СтраховыеВзносыИсчисленные.ОбособленноеПодразделение = &парамОбособленноеПодразделение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсегоИсчислено.ФизЛицо КАК ФизЛицо,
	|	ВсегоИсчислено.Порядок,
	|	СУММА(ВсегоИсчислено.РезультатПФРНакопительная) КАК РезультатПФРНакопительная,
	|	СУММА(ВсегоИсчислено.РезультатПФРСтраховая) КАК РезультатПФРСтраховая,
	|	СУММА(ВсегоИсчислено.РезультатПФРНакопительнаяЕНВД) КАК РезультатПФРНакопительнаяЕНВД,
	|	СУММА(ВсегоИсчислено.РезультатПФРСтраховаяЕНВД) КАК РезультатПФРСтраховаяЕНВД,
	|	СУММА(ВсегоИсчислено.РезультатФСС) КАК РезультатФСС,
	|	СУММА(ВсегоИсчислено.РезультатФФОМС) КАК РезультатФФОМС,
	|	СУММА(ВсегоИсчислено.РезультатТФОМС) КАК РезультатТФОМС,
	|	СУММА(ВсегоИсчислено.РезультатПФРНакопительнаяИнвалида) КАК РезультатПФРНакопительнаяИнвалида,
	|	СУММА(ВсегоИсчислено.РезультатПФРСтраховаяИнвалида) КАК РезультатПФРСтраховаяИнвалида,
	|	СУММА(ВсегоИсчислено.РезультатПоДополнительномуТарифу) КАК РезультатПоДополнительномуТарифу,
	|	СУММА(ВсегоИсчислено.РезультатФССНесчастныеСлучаи) КАК РезультатФССНесчастныеСлучаи
	|ПОМЕСТИТЬ ВТСуммыВзносовНарастающимИтогом
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИсчисленоПоОсновномуТарифу.ФизЛицо КАК ФизЛицо,
	|		ИсчисленоПоОсновномуТарифу.ФизЛицо.Наименование КАК Порядок,
	|		ИсчисленоПоОсновномуТарифу.РезультатПФРНакопительная КАК РезультатПФРНакопительная,
	|		ИсчисленоПоОсновномуТарифу.РезультатПФРСтраховая КАК РезультатПФРСтраховая,
	|		ИсчисленоПоОсновномуТарифу.РезультатФСС КАК РезультатФСС,
	|		ИсчисленоПоОсновномуТарифу.РезультатФФОМС КАК РезультатФФОМС,
	|		ИсчисленоПоОсновномуТарифу.РезультатТФОМС КАК РезультатТФОМС,
	|		0 КАК РезультатПФРНакопительнаяЕНВД,
	|		0 КАК РезультатПФРСтраховаяЕНВД,
	|		0 КАК РезультатПФРНакопительнаяИнвалида,
	|		0 КАК РезультатПФРСтраховаяИнвалида,
	|		0 КАК РезультатПоДополнительномуТарифу,
	|		0 КАК РезультатФССНесчастныеСлучаи
	|	ИЗ
	|		ВТИсчисленоПоОсновномуТарифу КАК ИсчисленоПоОсновномуТарифу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИсчисленоПоЛьготномуТарифу.ФизЛицо,
	|		ИсчисленоПоЛьготномуТарифу.ФизЛицо.Наименование,
	|		0,
	|		0,
	|		ИсчисленоПоЛьготномуТарифу.РезультатФССИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатФФОМСИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатТФОМСИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатПФРНакопительнаяЕНВД,
	|		ИсчисленоПоЛьготномуТарифу.РезультатПФРСтраховаяЕНВД,
	|		ИсчисленоПоЛьготномуТарифу.РезультатПФРНакопительнаяИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатПФРСтраховаяИнвалида,
	|		0,
	|		0
	|	ИЗ
	|		ВТИсчисленоПоЛьготномуТарифу КАК ИсчисленоПоЛьготномуТарифу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВзносыПоДополнительномуТарифу.ФизЛицо,
	|		ВзносыПоДополнительномуТарифу.ФизЛицо.Наименование,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ВзносыПоДополнительномуТарифу.РезультатПоДополнительномуТарифу,
	|		0
	|	ИЗ
	|		ВТВзносыПоДополнительномуТарифу КАК ВзносыПоДополнительномуТарифу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВзносыФС_НС_ПЗ.ФизЛицо,
	|		ВзносыФС_НС_ПЗ.ФизЛицо.Наименование,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ВзносыФС_НС_ПЗ.Взносов
	|	ИЗ
	|		ВТВзносыФС_НС_ПЗ КАК ВзносыФС_НС_ПЗ) КАК ВсегоИсчислено
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсегоИсчислено.ФизЛицо,
	|	ВсегоИсчислено.Порядок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыНарастающимИтогом.ФизЛицо КАК ФизЛицо,
	|	СуммыНарастающимИтогом.Порядок КАК Порядок,
	|	СуммыНарастающимИтогом.РезультатФСС - ЕСТЬNULL(ИсчисленныеВзносы.ФССОборот, 0) - ЕСТЬNULL(ИсчисленныеВзносы.ФССОборотИнвалида, 0) КАК ФСС,
	|	СуммыНарастающимИтогом.РезультатФФОМС - ЕСТЬNULL(ИсчисленныеВзносы.ФФОМСОборот, 0) - ЕСТЬNULL(ИсчисленныеВзносы.ФФОМСОборотИнвалида, 0) КАК ФФОМС,
	|	СуммыНарастающимИтогом.РезультатТФОМС - ЕСТЬNULL(ИсчисленныеВзносы.ТФОМСОборот, 0) - ЕСТЬNULL(ИсчисленныеВзносы.ТФОМСОборотИнвалида, 0) КАК ТФОМС,
	|	СуммыНарастающимИтогом.РезультатПФРНакопительная - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборот, 0) + ВЫБОР
	|		КОГДА МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего <> 0
	|				И МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД <> 0
	|			ТОГДА ВЫРАЗИТЬ((СуммыНарастающимИтогом.РезультатПФРНакопительнаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборотИнвалида, 0)) * (МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего - МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД) / МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыНарастающимИтогом.РезультатПФРНакопительнаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборотИнвалида, 0)
	|	КОНЕЦ КАК ПФРНакопительная,
	|	СуммыНарастающимИтогом.РезультатПФРСтраховая - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборот, 0) + ВЫБОР
	|		КОГДА МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего <> 0
	|				И МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД <> 0
	|			ТОГДА ВЫРАЗИТЬ((СуммыНарастающимИтогом.РезультатПФРСтраховаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборотИнвалида, 0)) * (МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего - МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД) / МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыНарастающимИтогом.РезультатПФРСтраховаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборотИнвалида, 0)
	|	КОНЕЦ КАК ПФРСтраховая,
	|	СуммыНарастающимИтогом.РезультатПФРНакопительнаяЕНВД - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяЕНВДОборот, 0) + ВЫБОР
	|		КОГДА МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего <> 0
	|				И МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД <> 0
	|			ТОГДА СуммыНарастающимИтогом.РезультатПФРНакопительнаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборотИнвалида, 0) - (ВЫРАЗИТЬ((СуммыНарастающимИтогом.РезультатПФРНакопительнаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборотИнвалида, 0)) * (МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего - МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД) / МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего КАК ЧИСЛО(15, 2)))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРНакопительнаяЕНВД,
	|	СуммыНарастающимИтогом.РезультатПФРСтраховаяЕНВД - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяЕНВДОборот, 0) + ВЫБОР
	|		КОГДА МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего <> 0
	|				И МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД <> 0
	|			ТОГДА СуммыНарастающимИтогом.РезультатПФРСтраховаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборотИнвалида, 0) - (ВЫРАЗИТЬ((СуммыНарастающимИтогом.РезультатПФРСтраховаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборотИнвалида, 0)) * (МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего - МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД) / МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего КАК ЧИСЛО(15, 2)))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРСтраховаяЕНВД,
	|	СуммыНарастающимИтогом.РезультатПоДополнительномуТарифу - ЕСТЬNULL(ИсчисленныеВзносы.ПФРПоДополнительномуТарифуОборот, 0) КАК ПоДополнительномуТарифу,
	|	СуммыНарастающимИтогом.РезультатФССНесчастныеСлучаи КАК ФССНесчастныеСлучаи
	|ПОМЕСТИТЬ ВТВзносыЗаМесяц
	|ИЗ
	|	ВТСуммыВзносовНарастающимИтогом КАК СуммыНарастающимИтогом
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРанееИсчисленныеВзносы КАК ИсчисленныеВзносы
	|		ПО СуммыНарастающимИтогом.ФизЛицо = ИсчисленныеВзносы.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаПФРПоИнвалидам КАК МесячнаяБазаПФРПоИнвалидам
	|		ПО СуммыНарастающимИтогом.ФизЛицо = МесячнаяБазаПФРПоИнвалидам.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Взносы.ФизЛицо,
	|	Взносы.ФСС,
	|	Взносы.ФФОМС,
	|	Взносы.ТФОМС,
	|	Взносы.ПФРНакопительная,
	|	Взносы.ПФРСтраховая,
	|	Взносы.ПФРНакопительнаяЕНВД,
	|	Взносы.ПФРСтраховаяЕНВД,
	|	Взносы.ПоДополнительномуТарифу КАК ПФРПоДополнительномуТарифу,
	|	Взносы.ФССНесчастныеСлучаи
	|ИЗ
	|	ВТВзносыЗаМесяц КАК Взносы
	|ГДЕ
	|	(Взносы.ФСС <> 0
	|			ИЛИ Взносы.ФФОМС <> 0
	|			ИЛИ Взносы.ТФОМС <> 0
	|			ИЛИ Взносы.ПФРНакопительная <> 0
	|			ИЛИ Взносы.ПФРСтраховая <> 0
	|			ИЛИ Взносы.ПФРНакопительнаяЕНВД <> 0
	|			ИЛИ Взносы.ПФРСтраховаяЕНВД <> 0
	|			ИЛИ Взносы.ПоДополнительномуТарифу <> 0
	|			ИЛИ Взносы.ФССНесчастныеСлучаи <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Взносы.Порядок";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"И &парамДопУсловие",РасчетСтраховыхВзносовПереопределяемый.ПолучитьДополнительноеУсловиеЗапросаБазаФСС_НС());
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрСведений.УчетнаяПолитикаНалоговыйУчет", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ИмяУчетнойПолитики());
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции  // ПолучитьДанныеРасчета

// расчет документа
//
Процедура РассчитатьВзносы(ВыборкаПоШапкеДокумента, ОграничениеНаСотрудников = Неопределено)
	
	НаборЗаписейОДоходах = РегистрыНакопления.СтраховыеВзносыСведенияОДоходах.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	Отказ = Ложь;
	РасчетСтраховыхВзносовПереопределяемый.СформироватьДвиженияПоДоходам(ЭтотОбъект, ВыборкаПоШапкеДокумента, "", Отказ, НаборЗаписейОДоходах);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьДвиженияПоСкидкам(ВыборкаПоШапкеДокумента, НаборЗаписейОДоходах);
	
	ИсчисленныеСтраховыеВзносы.Загрузить(ПолучитьДанныеРасчета(ВыборкаПоШапкеДокумента.Организация, ОграничениеНаСотрудников, НаборЗаписейОДоходах, Отказ));	
	
КонецПроцедуры // РассчитатьВзносы()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// заполнение доходов и расчет документа
Процедура ЗаполнитьИРассчитать(Скидки = Истина, Взносы = Истина) Экспорт

	Если Не Скидки И Не Взносы Тогда  // делать ничего не надо
		Возврат
	КонецЕсли;
	
	Отказ = Ложь;
	
	мГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);
	ОписаниеДокумента = Новый Структура;
	ОписаниеДокумента.Вставить("Организация",				мГоловнаяОрганизация);
	ОписаниеДокумента.Вставить("ОрганизацияПредставление",	Строка(мГоловнаяОрганизация));
	ОписаниеДокумента.Вставить("ОбособленноеПодразделение",	Организация);
	ОписаниеДокумента.Вставить("Ссылка",					Ссылка);
	ОписаниеДокумента.Вставить("Год",						Год(ПериодРегистрации));
	ОписаниеДокумента.Вставить("Период",					ПериодРегистрации);
	ОписаниеДокумента.Вставить("ПериодРегистрации",			ПериодРегистрации);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",мГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ПериодРегистрации",ПериодРегистрации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаНалоговыйУчетСрезПоследних.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеУказанВидТарифа
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&ПериодРегистрации, Организация = &ГоловнаяОрганизация) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних";
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрСведений.УчетнаяПолитикаНалоговыйУчет", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ИмяУчетнойПолитики());
	Выборка = Запрос.Выполнить().Выбрать();
	НеУказанВидТарифа = Ложь;
	Если Выборка.Следующий() Тогда
		НеУказанВидТарифа = Выборка.НеУказанВидТарифа
	КонецЕсли;
	ОписаниеДокумента.Вставить("НеУказанВидТарифа", НеУказанВидТарифа);
	
	ПроверитьЗаполнениеШапки(ОписаниеДокумента, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Взносы Тогда // выберем сотрудников для последующей обработки
		ОграничениеНаСотрудников = ПолучитьОграничениеНаСотрудников(Отказ);	
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Скидки И Взносы Тогда // надо полностью перезаполнить данные о доходах
		РасчетСтраховыхВзносовПереопределяемый.Автозаполнение(ЭтотОбъект, ОписаниеДокумента, ОграничениеНаСотрудников, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Скидки и Не Взносы Тогда // только пересчет скидок к доходам
		РасчетСкидок(ОписаниеДокумента);
	КонецЕсли;
	
	Если Взносы Тогда // полное заполнение или пересчет взносов
		РассчитатьВзносы(ОписаниеДокумента, ОграничениеНаСотрудников);
	КонецЕсли;

КонецПроцедуры

Процедура РасчетСкидок(ВыборкаПоШапкеДокумента) Экспорт
	
	Если НеоблагаемыеСуммыДоходов.Количество() = 0 Тогда  // считать оказалось нечего
		Возврат
	КонецЕсли;
	
	Отказ = Ложь;
	ДанныеОДоходах = РегистрыНакопления.СтраховыеВзносыСведенияОДоходах.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	РасчетСтраховыхВзносовПереопределяемый.СформироватьДвиженияПоДоходам(ЭтотОбъект, ВыборкаПоШапкеДокумента, "", Отказ, ДанныеОДоходах);
	Если Отказ Тогда
		Возврат	
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("парамРегистратор",					ВыборкаПоШапкеДокумента.Ссылка);
	Запрос.УстановитьПараметр("парамКонецМесяцаПериодаРегистрации",	КонецМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации));
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация",			ВыборкаПоШапкеДокумента.Организация);
	Запрос.УстановитьПараметр("парамНачалоГода",					НачалоГода(ВыборкаПоШапкеДокумента.ПериодРегистрации));
	Запрос.УстановитьПараметр("парамФизлица",						НеоблагаемыеСуммыДоходов.ВыгрузитьКолонку("ФизЛицо"));
 	Запрос.УстановитьПараметр("парамДанныеОДоходах",						ДанныеОДоходах);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|	СведенияОДоходах.ВидДохода КАК ВидДохода,
	|	СведенияОДоходах.Результат
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	&парамДанныеОДоходах КАК СведенияОДоходах
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидДохода,
	|	ФизЛицо";
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь) КАК ВидДохода,
	|	ДоходыПоМесяцам.ФизЛицо,
	|	СУММА(ДоходыПоМесяцам.Результат) КАК РезультатЗаГод,
	|	СУММА(ДоходыПоМесяцам.Скидка) КАК ПредоставленнаяСкидкаЗаГод
	|ИЗ
	|	(ВЫБРАТЬ
	|		СведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|		СведенияОДоходах.РезультатОборот КАК Результат,
	|		СведенияОДоходах.СкидкаОборот КАК Скидка
	|	ИЗ
	|		РегистрНакопления.СтраховыеВзносыСведенияОДоходах.Обороты(
	|				&парамНачалоГода,
	|				&парамКонецМесяцаПериодаРегистрации,
	|				Месяц,
	|				Организация = &парамГоловнаяОрганизация
	|					И ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь)
	|					И Физлицо В (&парамФизлица)) КАК СведенияОДоходах
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|		-СтраховыеВзносыСведенияОДоходах.Результат,
	|		-СтраховыеВзносыСведенияОДоходах.Скидка
	|	ИЗ
	|		РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СтраховыеВзносыСведенияОДоходах
	|	ГДЕ
	|		СтраховыеВзносыСведенияОДоходах.Регистратор = &парамРегистратор
	|		И СтраховыеВзносыСведенияОДоходах.ФизЛицо В(&парамФизлица)
	|		И СтраховыеВзносыСведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|		СтраховыеВзносыСведенияОДоходах.Результат,
	|		0
	|	ИЗ
	|		ВТДанныеДокумента КАК СтраховыеВзносыСведенияОДоходах
	|	ГДЕ
	|		СтраховыеВзносыСведенияОДоходах.ФизЛицо В(&парамФизлица)
	|		И СтраховыеВзносыСведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь)) КАК ДоходыПоМесяцам
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыПоМесяцам.ФизЛицо";
	
	СтруктураПоиска = Новый Структура("Физлицо, ВидДохода");
	ПравоНаСкидку = РегистрыСведений.СтраховыеВзносыСкидкиКДоходам.ПолучитьПоследнее(НачалоГода(ВыборкаПоШапкеДокумента.ПериодРегистрации), Новый Структура("ВидДохода", Справочники.ДоходыПоСтраховымВзносам.Матпомощь)).ГодоваяСкидка;
	Если Не ЗначениеЗаполнено(ПравоНаСкидку) Тогда
		ПравоНаСкидку = 0
	КонецЕсли;
	
	МодифицируемаяТаблица = НеоблагаемыеСуммыДоходов.Выгрузить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// Распределим скидку между строками о доходах
		ЗаполнитьЗначенияСвойств(СтруктураПоиска,Выборка); // Физлицо
		
		НайденныеСтроки = МодифицируемаяТаблица.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		// В общем случае предоставляемая скидка есть
		НайденныеСтроки[0].Скидка = Мин(Выборка.РезультатЗаГод, ПравоНаСкидку) - Выборка.ПредоставленнаяСкидкаЗаГод;
		Если НайденныеСтроки.Количество() > 1 Тогда
			Для Сч = 1  По НайденныеСтроки.Количество() - 1 Цикл
				НайденныеСтроки[Сч].Скидка = 0
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла; 
	
	НеоблагаемыеСуммыДоходов.Загрузить(МодифицируемаяТаблица);

КонецПроцедуры // РасчетСкидок()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

/// Формирует запрос по шапке документа
//
// Параметры: 
//	нет
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоШапке()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации" , ПериодРегистрации);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетСтраховыхВзносов.Ссылка,
	|	РасчетСтраховыхВзносов.Дата,
	|	РасчетСтраховыхВзносов.Организация КАК ОбособленноеПодразделение,
	|	РасчетСтраховыхВзносов.Ответственный,
	|	РасчетСтраховыхВзносов.ПериодРегистрации КАК ПериодРегистрации,
	|	РасчетСтраховыхВзносов.ПериодРегистрации КАК Период,
	|	ГОД(РасчетСтраховыхВзносов.ПериодРегистрации) КАК Год,
	|	ВЫБОР
	|		КОГДА РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА РасчетСтраховыхВзносов.Организация
	|		ИНАЧЕ РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(ВЫБОР
	|			КОГДА РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА РасчетСтраховыхВзносов.Организация
	|			ИНАЧЕ РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация
	|		КОНЕЦ) КАК ОрганизацияПредставление,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаНалоговыйУчетСрезПоследних.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ПустаяСсылка) 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеУказанВидТарифа
	|ИЗ
	|	Документ.РасчетСтраховыхВзносов КАК РасчетСтраховыхВзносов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&ПериодРегистрации, ) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних
	|		ПО (ВЫБОР
	|				КОГДА РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|					ТОГДА РасчетСтраховыхВзносов.Организация
	|				ИНАЧЕ РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация
	|			КОНЕЦ = УчетнаяПолитикаНалоговыйУчетСрезПоследних.Организация)
	|ГДЕ
	|	РасчетСтраховыхВзносов.Ссылка = &ДокументСсылка";
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрСведений.УчетнаяПолитикаНалоговыйУчет", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ИмяУчетнойПолитики());

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок = "")

	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ОбособленноеПодразделение) Тогда
		СтрокаСообщения = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не указана организация!");
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаСообщения, Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Период) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан период регистрации!", Отказ, Заголовок);
	ИначеЕсли ВыборкаПоШапкеДокумента.ПериодРегистрации < ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами() Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Уплата страховых взносов вводится с 1 января 2010 года!", Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ОбособленноеПодразделение) и ПустаяСтрока(Заголовок) Тогда

		Если ВыборкаПоШапкеДокумента.НеУказанВидТарифа Тогда
			
			Расшифровки = Новый Массив;
			// расшифровка сообщений представляет собой массив структур "Представление+Расшифровка"
			ОткрытьСведенияОСтавках = Новый Массив;
			ОткрытьСведенияОСтавках.Добавить("ПроцедурыУправленияПерсоналом.ОткрытьФормуНастройкаПараметровУчета");
			ОткрытьСведенияОСтавках.Добавить("НалоговыйУчет");
			ОткрытьСведенияОСтавках.Добавить(Новый Структура("Организация, Год", ВыборкаПоШапкеДокумента.Организация, ВыборкаПоШапкеДокумента.Год));
			Расшифровки.Добавить(Новый Структура("Представление,Расшифровка", Формат(ВыборкаПоШапкеДокумента.Год, "ЧЦ=4; ЧГ="), ОткрытьСведенияОСтавках));
	
			СтрокаСообщения = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации") + " " + ВыборкаПоШапкеДокумента.ОрганизацияПредставление;
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(
			"Для " + СтрокаСообщения + " на %% год не указан вид тарифа страховых взносов в ПФР, ФОМС и ФСС!",
					Отказ, Заголовок, Перечисления.ВидыСообщений.Ошибка, Расшифровки);
					
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Выполняет проверку правильности заполнения строки табличной части налогов
Процедура ПроверитьЗаполнениеСтрокиВзносов(ВыборкаПоВзносам, Отказ, Заголовок = "")
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоВзносам.НомерСтроки) +
	""" табл. части Исчисленные страховые взносы: ";
	
	Если Не ЗначениеЗаполнено(ВыборкаПоВзносам.ФизЛицо) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке  + "Не указан сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиВзносов()

Процедура ПроверитьЗаполнениеСтрокиСкидок(Выборка, Отказ, Заголовок = "")
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(Выборка.НомерСтроки) +
	""" табл. части Необлагаемые суммы: ";
	
	Если Не ЗначениеЗаполнено(Выборка.ФизЛицо) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке  + "Не указан сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиВзносов()

// Добавляет запись в движения по регистру учета налогов
Процедура ДобавитьСтрокуВДвиженияПоВзносам(ВыборкаПоВзносам, ВыборкаПоШапкеДокумента)
	
	СтрокаНабора = Движения.СтраховыеВзносыИсчисленные.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаНабора,ВыборкаПоШапкеДокумента);
	ЗаполнитьЗначенияСвойств(СтрокаНабора,ВыборкаПоВзносам);
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоВзносам()

Процедура СформироватьДвиженияПоСкидкам(ВыборкаПоШапкеДокумента, ДанныеОДоходах = Неопределено)
	
	Если ДанныеОДоходах = Неопределено Тогда
		Отказ = Ложь;
		ДанныеОДоходах = РегистрыНакопления.СтраховыеВзносыСведенияОДоходах.СоздатьНаборЗаписей().ВыгрузитьКолонки();
		РасчетСтраховыхВзносовПереопределяемый.СформироватьДвиженияПоДоходам(ЭтотОбъект, ВыборкаПоШапкеДокумента, "", Отказ, ДанныеОДоходах);
		Если Отказ Тогда
			Возврат	
		КонецЕсли;
		НаборЗаписейОДоходах = Движения.СтраховыеВзносыСведенияОДоходах;
	Иначе
		НаборЗаписейОДоходах = ДанныеОДоходах;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Регистратор",	ВыборкаПоШапкеДокумента.Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации",ВыборкаПоШапкеДокумента.ПериодРегистрации);
	Запрос.УстановитьПараметр("НачалоПериода",	НачалоМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода",	КонецМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации));
	Запрос.УстановитьПараметр("ОбособленноеПодразделение",	ВыборкаПоШапкеДокумента.ОбособленноеПодразделение);
	Запрос.УстановитьПараметр("ТаблицаДоходов",	ДанныеОДоходах);
	Запрос.УстановитьПараметр("НеоблагаемыеСуммы", НеоблагаемыеСуммыДоходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СтраховыеВзносыСведенияОДоходах.Результат,
	|	СтраховыеВзносыСведенияОДоходах.ВидРасчета
	|ПОМЕСТИТЬ ВТДанныеДоходовДокумента
	|ИЗ
	|	&ТаблицаДоходов КАК СтраховыеВзносыСведенияОДоходах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетИсчисленный.ФизЛицо КАК ФизЛицо,
	|	РасчетИсчисленный.Скидка,
	|	РасчетИсчисленный.ВидДохода КАК ВидДохода
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	&НеоблагаемыеСуммы КАК РасчетИсчисленный
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	ВидДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
	|	ДанныеДокумента.ВидДохода КАК ВидДохода,
	|	СведенияОДоходах.Результат КАК Результат,
	|	СведенияОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СведенияОДоходах.ВидРасчета КАК ВидРасчета,
	|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу КАК ОблагаетсяПоДополнительномуТарифу
	|ПОМЕСТИТЬ ВТСведенияОДоходах
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СведенияОДоходах
	|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
	|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, МЕСЯЦ) = &ПериодРегистрации
	|	И СведенияОДоходах.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|	И СведенияОДоходах.Регистратор <> &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.ФизЛицо,
	|	ДанныеДокумента.ВидДохода,
	|	СведенияОДоходах.Результат,
	|	СведенияОДоходах.ОблагаетсяЕНВД,
	|	СведенияОДоходах.ВидРасчета,
	|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДоходовДокумента КАК СведенияОДоходах
	|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
	|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	ВидДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(СведенияОДоходах.Результат) КАК Результат,
	|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|	СведенияОДоходах.ВидДохода КАК ВидДохода,
	|	СведенияОДоходах.ОблагаетсяЕНВД,
	|	СведенияОДоходах.ВидРасчета,
	|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
	|ПОМЕСТИТЬ ВТДоходы
	|ИЗ
	|	ВТСведенияОДоходах КАК СведенияОДоходах
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходах.ФизЛицо,
	|	СведенияОДоходах.ВидДохода,
	|	СведенияОДоходах.ОблагаетсяЕНВД,
	|	СведенияОДоходах.ВидРасчета,
	|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	ВидДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
	|	ДанныеДокумента.ВидДохода КАК ВидДохода,
	|	ДанныеДокумента.Скидка,
	|	СведенияОДоходах.ОблагаетсяЕНВД,
	|	ЕСТЬNULL(СведенияОДоходах.Результат, 0) КАК Результат,
	|	СведенияОДоходах.ВидРасчета,
	|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходы КАК СведенияОДоходах
	|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
	|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизЛицо,
	|	ВидДохода";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТаблицаРаспределения = Новый ТаблицаЗначений;
	ТаблицаРаспределения.Колонки.Добавить("ФизЛицо");
	ТаблицаРаспределения.Колонки.Добавить("ВидДохода");
	ТаблицаРаспределения.Колонки.Добавить("ОблагаетсяЕНВД");
	ТаблицаРаспределения.Колонки.Добавить("ОблагаетсяПоДополнительномуТарифу");
	ТаблицаРаспределения.Колонки.Добавить("ВидРасчета");
	ТаблицаРаспределения.Колонки.Добавить("Результат");
	
	Пока Выборка.СледующийПоЗначениюПоля("ФизЛицо") Цикл
		
		Пока Выборка.СледующийПоЗначениюПоля("ВидДохода") Цикл
			
			СкидкаКРаспределению = Выборка.Скидка;
			
			ТаблицаРаспределения.Очистить();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаРаспределения.Добавить(),Выборка);
			КонецЦикла;
			
			МассивКоэффициентов = ТаблицаРаспределения.ВыгрузитьКолонку("Результат");
			МассивСуммКНачислению = ОбщегоНазначенияЗК.РаспределитьПропорционально(СкидкаКРаспределению,МассивКоэффициентов);
			
			Если МассивСуммКНачислению = Неопределено Тогда
				
				СтрокаДвижения = НаборЗаписейОДоходах.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДвижения,ВыборкаПоШапкеДокумента);  // Период, Организация, ОбособленноеПодразделение
				ЗаполнитьЗначенияСвойств(СтрокаДвижения,Выборка,,"Результат"); // ФизЛицо,ВидДохода,ОблагаетсяЕНВД,ВидРасчета, ОблагаетсяПоДополнительномуТарифу
				// ресурсы
				СтрокаДвижения.Скидка = СкидкаКРаспределению;
				
			Иначе
				
				Для каждого СтрокаТЗ Из ТаблицаРаспределения Цикл
					
					СтрокаДвижения = НаборЗаписейОДоходах.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаДвижения,ВыборкаПоШапкеДокумента);  // Период, Организация, ОбособленноеПодразделение
					ЗаполнитьЗначенияСвойств(СтрокаДвижения,СтрокаТЗ,,"Результат"); // ФизЛицо,ВидДохода,ОблагаетсяЕНВД,ВидРасчета,ОблагаетсяПоДополнительномуТарифу
					// ресурсы
					СтрокаДвижения.Скидка = МассивСуммКНачислению[ТаблицаРаспределения.Индекс(СтрокаТЗ)];
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ttk_ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
	
	Для каждого СтрокаТЧ Из ИсчисленныеСтраховыеВзносы Цикл
		ПроверитьЗаполнениеСтрокиВзносов(СтрокаТЧ, Отказ);
		Если Не Отказ Тогда
			ДобавитьСтрокуВДвиженияПоВзносам(СтрокаТЧ, ВыборкаПоШапкеДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаТЧ Из НеоблагаемыеСуммыДоходов Цикл
		ПроверитьЗаполнениеСтрокиСкидок(СтрокаТЧ, Отказ, Заголовок);
	КонецЦикла; 
	
	Если Не Отказ Тогда
		РасчетСтраховыхВзносовПереопределяемый.ДополнительныеДействияОбработкиПроведения(ЭтотОбъект, ВыборкаПоШапкеДокумента, Заголовок, Отказ);
		СформироватьДвиженияПоСкидкам(ВыборкаПоШапкеДокумента);	
	КонецЕсли;
	
	Если Отказ Тогда
		ОбработкаКомментариев.ПоказатьСообщения();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(ИсчисленныеСтраховыеВзносы);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ, "Физлицо");
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	ttk_ОбщегоНазначения.ДобавитьПрефиксУзла(Префикс);
	
КонецПроцедуры // ПриУстановкеНовогоНомера()

