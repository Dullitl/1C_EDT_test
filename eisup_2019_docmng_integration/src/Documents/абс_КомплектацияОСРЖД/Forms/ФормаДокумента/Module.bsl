
/////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ЗначенияЗаполнения.Количество()>0 Тогда
		ЗаполнитьЗначенияСвойств(Объект, Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	
	УстановитьОформлениеГлавногоОС();
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////
// Обработчики событий элементов формы

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	ДокументОснованиеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДокументОснованиеПриИзмененииНаСервере();
	
	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	Док = РеквизитФормыВЗначение("Объект");
	Док.Заполнить(Объект.ДокументОснование);
	ЗначениеВРеквизитФормы(Док, "Объект");
	УстановитьОформлениеГлавногоОС();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	ДокументОснованиеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеСредстваПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ОсновныеСредства.ТекущиеДанные;
	Если ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ГлавноеОсновноеСредство=ТекущиеДанные.ОсновноеСредство Тогда
		Объект.ГлавноеОсновноеСредство = ПредопределенноеЗначение("Справочник.ОсновныеСредства.ПустаяСсылка");
		УстановитьОформлениеГлавногоОС();
	КонецЕсли;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// Обработчики команд формы

&НаКлиенте
Процедура УстановитьГлавным(Команда)
	
	ТекущиеДанные = Элементы.ОсновныеСредства.ТекущиеДанные;
	Если ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ГлавноеОсновноеСредство<>ТекущиеДанные.ОсновноеСредство Тогда
		
		Если Вопрос("Параметры учета будут заменены! Продолжить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да)=КодВозвратаДиалога.Да Тогда
			
			Объект.ГлавноеОсновноеСредство = ТекущиеДанные.ОсновноеСредство;
			УстановитьОформлениеГлавногоОС();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// Служебные процедуры и функции

&НаСервере
Процедура УстановитьОформлениеГлавногоОС()
	
	Если ЗначениеЗаполнено(Объект.ГлавноеОсновноеСредство) Тогда
		ЭтаФорма.УсловноеОформление.Элементы[0].Использование = Истина;
		ЭтаФорма.УсловноеОформление.Элементы[0].Отбор.Элементы[0].ПравоеЗначение = Объект.ГлавноеОсновноеСредство;
	Иначе
		ЭтаФорма.УсловноеОформление.Элементы[0].Использование = Ложь;
	КонецЕсли;
	
КонецПроцедуры

