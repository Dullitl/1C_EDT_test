
Функция ПолучитьСтруктуруЗапросовПроведения() Экспорт 

	СтруктураЗапросов = Новый Структура;
	СтруктураЗапросов.Вставить("абс_МеждународныйУчетОСРЖД_ТекстЗапросаВидыСубконтоСчетов", абс_МеждународныйУчетОСРЖД.ТекстЗапросаВидыСубконтоСчетов("ВидыСубконтоСчетов"));
	СтруктураЗапросов.Вставить("абс_МеждународныйУчетОСРЖД_ТекстЗапросаСоответствиеСчетовХозрасчетныйРЖД", абс_МеждународныйУчетОСРЖД.ТекстЗапросаСоответствиеСчетовХозрасчетныйМеждународныйОСРЖД("Период",, "СоответствиеСчетовХозрасчетныйРЖД"));
	СтруктураЗапросов.Вставить("ТекстЗапросаСоответствиеСчетовХозрасчетныйМеждународныйОСРЖД", абс_МеждународныйУчетОСРЖД.ТекстЗапросаСоответствиеСчетовХозрасчетныйМеждународныйОСРЖД("Период",, "СоответствиеСчетовХозрасчетныйРЖД"));
		
	ДобавитьЗапросыДляКомплектации(СтруктураЗапросов);
	
	Возврат СтруктураЗапросов;

КонецФункции

Процедура ДобавитьЗапросыДляКомплектации(СтруктураЗапросов)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_КомплектацияОСРЖДОсновныеСредства.НомерСтроки,
		|	абс_КомплектацияОСРЖДОсновныеСредства.ОсновноеСредство
		|ПОМЕСТИТЬ ТабличнаяЧастьОсновныеСредства
		|ИЗ
		|	Документ.абс_КомплектацияОСРЖД.ОсновныеСредства КАК абс_КомплектацияОСРЖДОсновныеСредства
		|ГДЕ
		|	абс_КомплектацияОСРЖДОсновныеСредства.Ссылка = &Ссылка";
		
	СтруктураЗапросов.Вставить("Комплектация_ТекстЗапросаТабличнаяЧастьОсновныеСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПериодыРегистраторыСреза.Период,
		|	ПериодыРегистраторыСреза.Регистратор,
		|	ПериодыРегистраторыСреза.ОсновноеСредство,
		|	абс_ОсновныеСредстваРЖД.Организация,
		|	абс_ОсновныеСредстваРЖД.ДатаПринятияКУчету,
		|	абс_ОсновныеСредстваРЖД.МестонахождениеОбъекта,
		|	абс_ОсновныеСредстваРЖД.МОЛ,
		|	абс_ОсновныеСредстваРЖД.Состояние,
		|	абс_ОсновныеСредстваРЖД.СчетУчета,
		|	абс_ОсновныеСредстваРЖД.СрокПолезногоИспользования,
		|	абс_ОсновныеСредстваРЖД.НачислятьАмортизацию,
		|	абс_ОсновныеСредстваРЖД.МетодНачисленияАмортизации,
		|	абс_ОсновныеСредстваРЖД.СчетНачисленияАмортизации,
		|	абс_ОсновныеСредстваРЖД.ПервоначальнаяСтоимость,
		|	абс_ОсновныеСредстваРЖД.СправедливаяСтоимость,
		|	абс_ОсновныеСредстваРЖД.ЛиквидационнаяСтоимость,
		|	абс_ОсновныеСредстваРЖД.СчетЗатрат,
		|	абс_ОсновныеСредстваРЖД.Субконто1,
		|	абс_ОсновныеСредстваРЖД.Субконто2,
		|	абс_ОсновныеСредстваРЖД.Субконто3,
		|	абс_ОсновныеСредстваРЖД.ПредполагаемыйОбъемПродукции,
		|	абс_ОсновныеСредстваРЖД.СуммаНачисленнойАмортизации,
		|	абс_ОсновныеСредстваРЖД.УбытокОтОбесценения,
		|	абс_ОсновныеСредстваРЖД.КоэффициентУскорения,
		|	абс_ОсновныеСредстваРЖД.СчетСниженияСтоимости
		|ПОМЕСТИТЬ ПараметрыОсновныхСредствРЖД
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПериодыСреза.ОсновноеСредство КАК ОсновноеСредство,
		|		ПериодыСреза.Период КАК Период,
		|		МАКСИМУМ(абс_ОсновныеСредстваРЖД.Регистратор) КАК Регистратор
		|	ИЗ
		|		(ВЫБРАТЬ
		|			ТабличнаяЧастьОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
		|			МАКСИМУМ(абс_ОсновныеСредстваРЖД.Период) КАК Период
		|		ИЗ
		|			ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.абс_ОсновныеСредстваРЖД КАК абс_ОсновныеСредстваРЖД
		|				ПО ТабличнаяЧастьОсновныеСредства.ОсновноеСредство = абс_ОсновныеСредстваРЖД.ОсновноеСредство
		|					И (абс_ОсновныеСредстваРЖД.Период <= &Период)
		|					И (абс_ОсновныеСредстваРЖД.Активность)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ТабличнаяЧастьОсновныеСредства.ОсновноеСредство) КАК ПериодыСреза
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_ОсновныеСредстваРЖД КАК абс_ОсновныеСредстваРЖД
		|			ПО ПериодыСреза.ОсновноеСредство = абс_ОсновныеСредстваРЖД.ОсновноеСредство
		|				И ПериодыСреза.Период = абс_ОсновныеСредстваРЖД.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПериодыСреза.ОсновноеСредство,
		|		ПериодыСреза.Период) КАК ПериодыРегистраторыСреза
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_ОсновныеСредстваРЖД КАК абс_ОсновныеСредстваРЖД
		|		ПО ПериодыРегистраторыСреза.Период = абс_ОсновныеСредстваРЖД.Период
		|			И ПериодыРегистраторыСреза.Регистратор = абс_ОсновныеСредстваРЖД.Регистратор
		|			И ПериодыРегистраторыСреза.ОсновноеСредство = абс_ОсновныеСредстваРЖД.ОсновноеСредство";
		
	СтруктураЗапросов.Вставить("абс_ВнеоборотныеАктивы_ТекстЗапроса_ОсновныеСредстваРЖДПоТаблицеОС", ТекстЗапроса+";");
	
    ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_КомплектацияОСРЖД.Ссылка,
		|	абс_КомплектацияОСРЖД.Номер,
		|	абс_КомплектацияОСРЖД.Дата,
		|	абс_КомплектацияОСРЖД.Организация,
		|	абс_КомплектацияОСРЖД.ДокументОснование,
		|	абс_КомплектацияОСРЖД.ОсновноеСредство,
		|	&ГлавноеОсновноеСредство,
		|	ПараметрыОсновныхСредствРЖД.СчетУчета,
		|	ПараметрыОсновныхСредствРЖД.СчетНачисленияАмортизации,
		|	абс_КомплектацияОСРЖД.СчетЗатрат,
		|	абс_КомплектацияОСРЖД.Субконто1,
		|	абс_КомплектацияОСРЖД.Субконто2,
		|	абс_КомплектацияОСРЖД.Субконто3,
		|	абс_КомплектацияОСРЖД.СчетСниженияСтоимости,
		|	абс_КомплектацияОСРЖД.Местонахождение,
		|	абс_КомплектацияОСРЖД.МОЛ,
		|	абс_КомплектацияОСРЖД.КоэффициентУскорения,
		|	ПараметрыОсновныхСредствРЖД.СрокПолезногоИспользования,
		|	абс_КомплектацияОСРЖД.НачислятьАмортизацию,
		|	ПараметрыОсновныхСредствРЖД.ОсновноеСредство.КодПоОКОФ КАК ОКОФ,
		|	ПараметрыОсновныхСредствРЖД.ОсновноеСредство.абс_ГруппаОСРЖД КАК ГруппаОСРЖД
		|ПОМЕСТИТЬ ШапкаДокумента
		|ИЗ
		|	Документ.абс_КомплектацияОСРЖД КАК абс_КомплектацияОСРЖД
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредствРЖД КАК ПараметрыОсновныхСредствРЖД
		|		ПО (ПараметрыОсновныхСредствРЖД.ОсновноеСредство = &ГлавноеОсновноеСредство)
		|ГДЕ
		|	абс_КомплектацияОСРЖД.Ссылка = &Ссылка";
		
	СтруктураЗапросов.Вставить("Комплектация_ТекстЗапросаШапкаДокумента", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	абс_СтоимостьОСРЖДОстатки.ОсновноеСредство,
		|	абс_СтоимостьОСРЖДОстатки.СтоимостьОстаток КАК Стоимость,
		|	абс_СтоимостьОСРЖДОстатки.АмортизацияОстаток КАК Амортизация
		|ПОМЕСТИТЬ СтоимостьОсновныхСредств
		|ИЗ
		|	РегистрНакопления.абс_СтоимостьОСРЖД.Остатки(
		|			&МоментДокументаДляРН,
		|			ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ТабличнаяЧастьОсновныеСредства.ОсновноеСредство
		|					ИЗ
		|						ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства)
		|				И Организация = &Организация) КАК абс_СтоимостьОСРЖДОстатки";
		
	СтруктураЗапросов.Вставить("абс_ВнеоборотныеАктивы_ТекстЗапроса_СтоимостьОСПоТаблицеОС", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СУММА(СтоимостьОсновныхСредств.Стоимость) КАК Стоимость,
		|	СУММА(СтоимостьОсновныхСредств.Амортизация) КАК Амортизация
		|ПОМЕСТИТЬ СтоимостьНовогоОсновногоСредства
		|ИЗ
		|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств";
		
	СтруктураЗапросов.Вставить("Комплектация_ТекстЗапросаСтоимостьНовогоОсновногоСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СУММА(ПараметрыОсновныхСредствРЖД.ПервоначальнаяСтоимость) КАК ПервоначальнаяСтоимость,
		|	СУММА(ПараметрыОсновныхСредствРЖД.СправедливаяСтоимость) КАК СправедливаяСтоимость,
		|	СУММА(ПараметрыОсновныхСредствРЖД.УбытокОтОбесценения) КАК УбытокОтОбесценения,
		|	СУММА(ПараметрыОсновныхСредствРЖД.ЛиквидационнаяСтоимость) КАК ЛиквидационнаяСтоимость
		|ПОМЕСТИТЬ ПервоначальнаяСтоимостьНовогоОсновногоСредства
		|ИЗ
		|	ПараметрыОсновныхСредствРЖД КАК ПараметрыОсновныхСредствРЖД";
		
	СтруктураЗапросов.Вставить("Комплектация_ТекстЗапросаПервоначальнаяСтоимостьНовогоОсновногоСредства", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Организация,
		|	СтоимостьОсновныхСредств.ОсновноеСредство,
		|	СтоимостьОсновныхСредств.Стоимость,
		|	СтоимостьОсновныхСредств.Амортизация
		|ИЗ
		|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	&Организация,
		|	ШапкаДокумента.ОсновноеСредство,
		|	СтоимостьНовогоОсновногоСредства.Стоимость,
		|	СтоимостьНовогоОсновногоСредства.Амортизация
		|ИЗ
		|	ШапкаДокумента КАК ШапкаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНовогоОсновногоСредства КАК СтоимостьНовогоОсновногоСредства
		|		ПО (ИСТИНА)";
		
	СтруктураЗапросов.Вставить("Комплектация_ТекстЗапросаТаблица_абс_СтоимостьОСРЖД", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	ШапкаДокумента.ОсновноеСредство,
		|	&Организация,
		|	ЕСТЬNULL(ПараметрыОсновныхСредствРЖД.ДатаПринятияКУчету, ШапкаДокумента.Дата) КАК ДатаПринятияКУчету,
		|	ШапкаДокумента.Местонахождение КАК МестонахождениеОбъекта,
		|	ШапкаДокумента.МОЛ,
		|	ЕСТЬNULL(ПараметрыОсновныхСредствРЖД.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВведеноВЭксплуатацию)) КАК Состояние,
		|	ШапкаДокумента.СчетУчета,
		|	ВЫБОР
		|		КОГДА ШапкаДокумента.СрокПолезногоИспользования = 0
		|			ТОГДА ЕСТЬNULL(абс_СоответствиеОКОФГруппОСРЖД.СрокПолезногоИспользования, ЕСТЬNULL(ПараметрыОсновныхСредствРЖД.СрокПолезногоИспользования, 0))
		|		ИНАЧЕ ШапкаДокумента.СрокПолезногоИспользования
		|	КОНЕЦ КАК СрокПолезногоИспользования,
		|	ШапкаДокумента.НачислятьАмортизацию,
		|	ЕСТЬNULL(ПараметрыОсновныхСредствРЖД.МетодНачисленияАмортизации, ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизацииОСМеждународный.Линейный)) КАК МетодНачисленияАмортизации,
		|	ШапкаДокумента.СчетНачисленияАмортизации,
		|	ЕСТЬNULL(ПервоначальнаяСтоимостьНовогоОсновногоСредства.ПервоначальнаяСтоимость, ЕСТЬNULL(СтоимостьНовогоОсновногоСредства.Стоимость, 0)) КАК ПервоначальнаяСтоимость,
		|	ЕСТЬNULL(ПервоначальнаяСтоимостьНовогоОсновногоСредства.СправедливаяСтоимость, ЕСТЬNULL(СтоимостьНовогоОсновногоСредства.Стоимость, 0)) КАК СправедливаяСтоимость,
		|	ЕСТЬNULL(ПервоначальнаяСтоимостьНовогоОсновногоСредства.ЛиквидационнаяСтоимость, ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьНовогоОсновногоСредства.Стоимость, 0) * ЕСТЬNULL(абс_СоответствиеОКОФГруппОСРЖД.ПроцентЛиквидационнойСтоимости, 0) / 100 КАК ЧИСЛО(15, 2))) КАК ЛиквидационнаяСтоимость,
		|	ШапкаДокумента.СчетЗатрат,
		|	ШапкаДокумента.Субконто1,
		|	ШапкаДокумента.Субконто2,
		|	ШапкаДокумента.Субконто3,
		|	ПараметрыОсновныхСредствРЖД.ПредполагаемыйОбъемПродукции,
		|	СтоимостьНовогоОсновногоСредства.Амортизация КАК СуммаНачисленнойАмортизации,
		|	ЕСТЬNULL(ПервоначальнаяСтоимостьНовогоОсновногоСредства.УбытокОтОбесценения, 0) КАК УбытокОтОбесценения,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПараметрыОсновныхСредствРЖД.КоэффициентУскорения, 0) = 0
		|			ТОГДА 1
		|		ИНАЧЕ ПараметрыОсновныхСредствРЖД.КоэффициентУскорения
		|	КОНЕЦ КАК Поле1,
		|	ПараметрыОсновныхСредствРЖД.СчетСниженияСтоимости
		|ИЗ
		|	ШапкаДокумента КАК ШапкаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредствРЖД КАК ПараметрыОсновныхСредствРЖД
		|		ПО (ПараметрыОсновныхСредствРЖД.ОсновноеСредство = &ГлавноеОсновноеСредство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.абс_СоответствиеОКОФГруппОСРЖД КАК абс_СоответствиеОКОФГруппОСРЖД
		|		ПО ШапкаДокумента.ОКОФ = абс_СоответствиеОКОФГруппОСРЖД.ОКОФ
		|			И ШапкаДокумента.ГруппаОСРЖД = абс_СоответствиеОКОФГруппОСРЖД.ГруппаОСРЖД
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНовогоОсновногоСредства КАК СтоимостьНовогоОсновногоСредства
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальнаяСтоимостьНовогоОсновногоСредства КАК ПервоначальнаяСтоимостьНовогоОсновногоСредства
		|		ПО (ИСТИНА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	ПараметрыОсновныхСредствРЖД.ОсновноеСредство,
		|	ПараметрыОсновныхСредствРЖД.Организация,
		|	ПараметрыОсновныхСредствРЖД.ДатаПринятияКУчету,
		|	ПараметрыОсновныхСредствРЖД.МестонахождениеОбъекта,
		|	ПараметрыОсновныхСредствРЖД.МОЛ,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета),
		|	ПараметрыОсновныхСредствРЖД.СчетУчета,
		|	ПараметрыОсновныхСредствРЖД.СрокПолезногоИспользования,
		|	ЛОЖЬ,
		|	ПараметрыОсновныхСредствРЖД.МетодНачисленияАмортизации,
		|	ПараметрыОсновныхСредствРЖД.СчетНачисленияАмортизации,
		|	ПараметрыОсновныхСредствРЖД.ПервоначальнаяСтоимость,
		|	0,
		|	0,
		|	ПараметрыОсновныхСредствРЖД.СчетЗатрат,
		|	ПараметрыОсновныхСредствРЖД.Субконто1,
		|	ПараметрыОсновныхСредствРЖД.Субконто2,
		|	ПараметрыОсновныхСредствРЖД.Субконто3,
		|	ПараметрыОсновныхСредствРЖД.ПредполагаемыйОбъемПродукции,
		|	0,
		|	0,
		|	ПараметрыОсновныхСредствРЖД.КоэффициентУскорения,
		|	ПараметрыОсновныхСредствРЖД.СчетСниженияСтоимости
		|ИЗ
		|	ПараметрыОсновныхСредствРЖД КАК ПараметрыОсновныхСредствРЖД
		|		ЛЕВОЕ СОЕДИНЕНИЕ ШапкаДокумента КАК ШапкаДокумента
		|		ПО ПараметрыОсновныхСредствРЖД.ОсновноеСредство = ШапкаДокумента.ОсновноеСредство
		|ГДЕ
		|	ШапкаДокумента.ОсновноеСредство ЕСТЬ NULL ";
		
	СтруктураЗапросов.Вставить("Комплектация_ТекстЗапросаТаблица_абс_ОсновныеСредстваРЖД", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ШапкаДокумента.СчетУчета КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства) КАК ВидСубконтоДт1,
		|	ШапкаДокумента.ОсновноеСредство КАК СубконтоДт1,
		|	ПараметрыОсновныхСредствРЖД.СчетУчета КАК СчетКт,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства) КАК ВидСубконтоКт1,
		|	СтоимостьОсновныхСредств.ОсновноеСредство КАК СубконтоКт1,
		|	СтоимостьОсновныхСредств.Стоимость КАК Сумма,
		|	""Комплектация ОС"" КАК Содержание,
		|	ШапкаДокумента.ДокументОснование КАК ПервичныйДокумент
		|ИЗ
		|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредствРЖД КАК ПараметрыОсновныхСредствРЖД
		|		ПО СтоимостьОсновныхСредств.ОсновноеСредство = ПараметрыОсновныхСредствРЖД.ОсновноеСредство,
		|	ШапкаДокумента КАК ШапкаДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	&Организация,
		|	ПараметрыОсновныхСредствРЖД.СчетНачисленияАмортизации,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|	СтоимостьОсновныхСредств.ОсновноеСредство,
		|	ШапкаДокумента.СчетНачисленияАмортизации,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|	ШапкаДокумента.ОсновноеСредство,
		|	СтоимостьОсновныхСредств.Амортизация,
		|	""Комплектация ОС"",
		|	ШапкаДокумента.ДокументОснование
		|ИЗ
		|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредствРЖД КАК ПараметрыОсновныхСредствРЖД
		|		ПО СтоимостьОсновныхСредств.ОсновноеСредство = ПараметрыОсновныхСредствРЖД.ОсновноеСредство,
		|	ШапкаДокумента КАК ШапкаДокумента";
		
	СтруктураЗапросов.Вставить("Комплектация_ТекстЗапросаТаблица_абс_МеждународныйОСРЖД", ТекстЗапроса+";");
	
	ТекстЗапроса = 
		"УНИЧТОЖИТЬ ТабличнаяЧастьОсновныеСредства";
		
	СтруктураЗапросов.Вставить("Комплектация_УдалениеВременныхТаблиц", ТекстЗапроса+";");

КонецПроцедуры








/////////////////////////////////////////////////////////////////////////////////
// Программный интерфейс проведения документов

Процедура ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Ссылка.Дата);
	Запрос.УстановитьПараметр("Организация", Ссылка.Организация);
	Запрос.УстановитьПараметр("МоментДокумента", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ДокументОснование", Ссылка.ДокументОснование);
	Запрос.УстановитьПараметр("ОсновноеСредство", Ссылка.ОсновноеСредство);
	Запрос.УстановитьПараметр("ГлавноеОсновноеСредство", Ссылка.ГлавноеОсновноеСредство);
	
	ТекстыЗапросов = Новый СписокЗначений;
	ТекстыЗапросов.Добавить(ТекстЗапросаТабличнаяЧастьОсновныеСредства(), "");
	ТекстыЗапросов.Добавить(абс_ВнеоборотныеАктивы.ТекстЗапроса_ОсновныеСредстваРЖДПоТаблицеОС("ТабличнаяЧастьОсновныеСредства", "Период", "ПараметрыОсновныхСредств"), "");
	ТекстыЗапросов.Добавить(ТекстЗапросаСтоимостьОсновныхСредств(), "");
	ТекстыЗапросов.Добавить(ТекстЗапросаСтоимостьНовогоОсновногоСредства(), "");
	ТекстыЗапросов.Добавить(ТекстЗапросаНакопительныеПараметрыНовогоОсновногоСредства(), "");
	
	ТекстыЗапросов.Добавить(ТекстЗапросаТаблица_ОсновныеСредстваМеждународныйУчет(), "Таблица_ОсновныеСредстваМеждународныйУчет");
	ТекстыЗапросов.Добавить(ТекстЗапросаТаблица_СтоимостьОСМеждународный(), "Таблица_СтоимостьОСМеждународный");
	ТекстыЗапросов.Добавить(ТекстЗапросаТаблица_Международный(), "Таблица_Международный");
	
	абс_ПроведениеДокументов.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапросов, ДополнительныеСвойства.ТаблицыДляДвижений);
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////
// Функции, возвращающие тексты запросов для инициализации данных документа

Функция ТекстЗапросаТабличнаяЧастьОсновныеСредства()
	
	Возврат "ВЫБРАТЬ
	        |	абс_КомплектацияОСМОсновныеСредства.ОсновноеСредство
	        |ПОМЕСТИТЬ ТабличнаяЧастьОсновныеСредства
	        |ИЗ
	        |	Документ.абс_КомплектацияОСМ.ОсновныеСредства КАК абс_КомплектацияОСМОсновныеСредства
	        |ГДЕ
	        |	абс_КомплектацияОСМОсновныеСредства.Ссылка = &Ссылка
			|;
			|";
	
КонецФункции

Функция ТекстЗапросаСтоимостьОсновныхСредств()
	
	Возврат "ВЫБРАТЬ
	        |	абс_СтоимостьОСМеждународныйОстатки.ОсновноеСредство,
	        |	абс_СтоимостьОСМеждународныйОстатки.СтоимостьОстаток КАК Стоимость,
	        |	абс_СтоимостьОСМеждународныйОстатки.АмортизацияОстаток КАК Амортизация
	        |ПОМЕСТИТЬ СтоимостьОсновныхСредств
	        |ИЗ
	        |	РегистрНакопления.абс_СтоимостьОСМеждународный.Остатки(
	        |			&МоментДокумента,
	        |			Организация = &Организация
	        |				И ОсновноеСредство В
	        |					(ВЫБРАТЬ
	        |						ТабличнаяЧастьОсновныеСредства.ОсновноеСредство
	        |					ИЗ
	        |						ТабличнаяЧастьОсновныеСредства КАК ТабличнаяЧастьОсновныеСредства)) КАК абс_СтоимостьОСМеждународныйОстатки
			|;
			|";
	
КонецФункции

Функция ТекстЗапросаСтоимостьНовогоОсновногоСредства()
	
	Возврат "ВЫБРАТЬ
	        |	&ОсновноеСредство,
	        |	ЕСТЬNULL(СУММА(СтоимостьОсновныхСредств.Стоимость), 0) КАК Стоимость,
	        |	ЕСТЬNULL(СУММА(СтоимостьОсновныхСредств.Амортизация), 0) КАК Амортизация
	        |ПОМЕСТИТЬ СтоимостьНовогоОсновногоСредства
	        |ИЗ
	        |	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
			|;
			|";
	
КонецФункции

Функция ТекстЗапросаНакопительныеПараметрыНовогоОсновногоСредства()
	
	Возврат "ВЫБРАТЬ
	        |	&ОсновноеСредство,
	        |	СУММА(ПараметрыОсновныхСредств.ПервоначальнаяСтоимость) КАК ПервоначальнаяСтоимость,
	        |	СУММА(ПараметрыОсновныхСредств.СправедливаяСтоимость) КАК СправедливаяСтоимость,
	        |	СУММА(ПараметрыОсновныхСредств.ЛиквидационнаяСтоимость) КАК ЛиквидационнаяСтоимость,
	        |	СУММА(ПараметрыОсновныхСредств.УбытокОтОбесценения) КАК УбытокОтОбесценения
	        |ПОМЕСТИТЬ НакопительныеПраметрыНовогоОсновногоСредства
	        |ИЗ
	        |	ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
			|;
			|";
	
КонецФункции


Функция ТекстЗапросаТаблица_ОсновныеСредстваМеждународныйУчет()
	
	Возврат "ВЫБРАТЬ
	        |	&Период,
	        |	&ОсновноеСредство,
	        |	&Организация,
	        |	ПараметрыОсновныхСредств.ДатаПринятияКУчету,
	        |	ПараметрыОсновныхСредств.МестонахождениеОбъекта,
	        |	ПараметрыОсновныхСредств.МОЛ,
	        |	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВведеноВЭксплуатацию) КАК Состояние,
	        |	ПараметрыОсновныхСредств.СчетУчета,
	        |	ПараметрыОсновныхСредств.СрокПолезногоИспользования,
	        |	ИСТИНА КАК НачислятьАмортизацию,
	        |	ПараметрыОсновныхСредств.МетодНачисленияАмортизации,
	        |	ПараметрыОсновныхСредств.СчетНачисленияАмортизации,
	        |	ЕСТЬNULL(НакопительныеПраметрыНовогоОсновногоСредства.ПервоначальнаяСтоимость, ЕСТЬNULL(СтоимостьНовогоОсновногоСредства.Стоимость, 0)) КАК ПервоначальнаяСтоимость,
	        |	ЕСТЬNULL(НакопительныеПраметрыНовогоОсновногоСредства.СправедливаяСтоимость, ЕСТЬNULL(СтоимостьНовогоОсновногоСредства.Стоимость, 0)) КАК СправедливаяСтоимость,
	        |	ЕСТЬNULL(НакопительныеПраметрыНовогоОсновногоСредства.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимость,
	        |	ПараметрыОсновныхСредств.СчетЗатрат,
	        |	ПараметрыОсновныхСредств.Субконто1,
	        |	ПараметрыОсновныхСредств.Субконто2,
	        |	ПараметрыОсновныхСредств.Субконто3,
	        |	ПараметрыОсновныхСредств.ПредполагаемыйОбъемПродукции,
	        |	СтоимостьНовогоОсновногоСредства.Амортизация КАК СуммаНачисленнойАмортизации,
	        |	ЕСТЬNULL(НакопительныеПраметрыНовогоОсновногоСредства.УбытокОтОбесценения, 0) КАК УбытокОтОбесценения,
	        |	ПараметрыОсновныхСредств.КоэффициентУскорения,
	        |	ПараметрыОсновныхСредств.СчетСниженияСтоимости
	        |ИЗ
	        |	СтоимостьНовогоОсновногоСредства КАК СтоимостьНовогоОсновногоСредства,
	        |	НакопительныеПраметрыНовогоОсновногоСредства КАК НакопительныеПраметрыНовогоОсновногоСредства,
	        |	ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
	        |ГДЕ
	        |	ПараметрыОсновныхСредств.ОсновноеСредство = &ГлавноеОсновноеСредство
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	&Период,
	        |	ПараметрыОсновныхСредств.ОсновноеСредство,
	        |	&Организация,
	        |	ПараметрыОсновныхСредств.ДатаПринятияКУчету,
	        |	ПараметрыОсновныхСредств.МестонахождениеОбъекта,
	        |	ПараметрыОсновныхСредств.МОЛ,
	        |	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета),
	        |	ПараметрыОсновныхСредств.СчетУчета,
	        |	ПараметрыОсновныхСредств.СрокПолезногоИспользования,
	        |	ЛОЖЬ,
	        |	ПараметрыОсновныхСредств.МетодНачисленияАмортизации,
	        |	ПараметрыОсновныхСредств.СчетНачисленияАмортизации,
	        |	ПараметрыОсновныхСредств.ПервоначальнаяСтоимость,
	        |	ПараметрыОсновныхСредств.СправедливаяСтоимость,
	        |	ПараметрыОсновныхСредств.ЛиквидационнаяСтоимость,
	        |	ПараметрыОсновныхСредств.СчетЗатрат,
	        |	ПараметрыОсновныхСредств.Субконто1,
	        |	ПараметрыОсновныхСредств.Субконто2,
	        |	ПараметрыОсновныхСредств.Субконто3,
	        |	ПараметрыОсновныхСредств.ПредполагаемыйОбъемПродукции,
	        |	ПараметрыОсновныхСредств.СуммаНачисленнойАмортизации,
	        |	ПараметрыОсновныхСредств.УбытокОтОбесценения,
	        |	ПараметрыОсновныхСредств.КоэффициентУскорения,
	        |	ПараметрыОсновныхСредств.СчетСниженияСтоимости
	        |ИЗ
	        |	ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
	        |ГДЕ
	        |	ПараметрыОсновныхСредств.ОсновноеСредство <> &ОсновноеСредство
			|;
			|";
	
КонецФункции

Функция ТекстЗапросаТаблица_СтоимостьОСМеждународный()
	
	Возврат "ВЫБРАТЬ
	        |	&Период,
	        |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	        |	&Организация,
	        |	СтоимостьНовогоОсновногоСредства.ОсновноеСредство,
	        |	СтоимостьНовогоОсновногоСредства.Стоимость,
	        |	СтоимостьНовогоОсновногоСредства.Амортизация
	        |ИЗ
	        |	СтоимостьНовогоОсновногоСредства КАК СтоимостьНовогоОсновногоСредства
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	&Период,
	        |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	        |	&Организация,
	        |	СтоимостьОсновныхСредств.ОсновноеСредство,
	        |	СтоимостьОсновныхСредств.Стоимость,
	        |	СтоимостьОсновныхСредств.Амортизация
	        |ИЗ
	        |	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
			|;
			|";
	
КонецФункции

Функция ТекстЗапросаТаблица_Международный()
	
	Возврат "ВЫБРАТЬ
	        |	&Период,
	        |	&Организация,
	        |	ПараметрыНовогоОсновногоСредства.СчетУчета КАК СчетДт,
	        |	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства) КАК ВидСубконтоДт1,
	        |	&ОсновноеСредство КАК СубконтоДт1,
	        |	ПараметрыОсновныхСредств.СчетУчета КАК СчетКт,
	        |	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства) КАК ВидСубконтоКт1,
	        |	СтоимостьОсновныхСредств.ОсновноеСредство КАК СубконтоКт1,
	        |	СтоимостьОсновныхСредств.Стоимость КАК Сумма,
	        |	&ДокументОснование КАК ПервичныйДокумент
	        |ИЗ
	        |	СтоимостьНовогоОсновногоСредства КАК СтоимостьНовогоОсновногоСредства
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыНовогоОсновногоСредства
	        |		ПО (ПараметрыНовогоОсновногоСредства.ОсновноеСредство = &ГлавноеОсновноеСредство)
	        |		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
	        |			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
	        |			ПО СтоимостьОсновныхСредств.ОсновноеСредство = ПараметрыОсновныхСредств.ОсновноеСредство
	        |		ПО (ИСТИНА)
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	&Период,
	        |	&Организация,
	        |	ПараметрыОсновныхСредств.СчетНачисленияАмортизации,
	        |	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства),
	        |	СтоимостьОсновныхСредств.ОсновноеСредство,
	        |	ПараметрыНовогоОсновногоСредства.СчетНачисленияАмортизации,
	        |	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства),
	        |	&ОсновноеСредство,
	        |	СтоимостьОсновныхСредств.Амортизация,
	        |	&ДокументОснование
	        |ИЗ
	        |	СтоимостьНовогоОсновногоСредства КАК СтоимостьНовогоОсновногоСредства
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыНовогоОсновногоСредства
	        |		ПО (ПараметрыНовогоОсновногоСредства.ОсновноеСредство = &ГлавноеОсновноеСредство)
	        |		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
	        |			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыОсновныхСредств КАК ПараметрыОсновныхСредств
	        |			ПО СтоимостьОсновныхСредств.ОсновноеСредство = ПараметрыОсновныхСредств.ОсновноеСредство
	        |		ПО (ИСТИНА)
			|;
			|";
	
КонецФункции
