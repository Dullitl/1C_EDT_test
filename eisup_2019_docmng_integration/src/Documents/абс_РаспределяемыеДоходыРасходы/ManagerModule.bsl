
Функция ВыполнитьПечатьРаспределяемыеДоходыРасходы(ДокументОбъект) Экспорт
	
	
	ТабДок = Новый ТабличныйДокумент;
	//АБС ВСТАВКА №000030071 НАЧАЛО «12 января 2015 г.», Пополитов     		
	НастройкиТабДок(ТабДок,"Печать_РаспределяемыеДоходыРасходы");
	//\\АБС ВСТАВКА №000030071 КОНЕЦ  
	
	Макет = ДокументОбъект.ПолучитьМакет("РаспределяемыеДоходыРасходы");
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Месяц = НРег(Формат(ДокументОбъект.Дата,"ДФ='ММMM yyyy'")) + " г.";
	ТабДок.Вывести(Область);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Доходы.НомерСтроки КАК НомерПП,
		|	Доходы.Филиал КАК НаименованиеФилиала,
		|	Доходы.КодБУ,
		|	Доходы.КодУУ,
		|	Доходы.СуммарныйКоэффициентРаспределения,
		|	Доходы.ОбщаяСуммаКРаспределению,
		|	Доходы.НоменклатурнаяГруппа КАК НаименованиеНоменклатурыСтатьиРасходов
		|ИЗ
		|	Документ.абс_РаспределяемыеДоходыРасходы.Доходы КАК Доходы
		|ГДЕ
		|	Доходы.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	абс_РаспределяемыеДоходыРасходыРасходы.НомерСтроки,
		|	абс_РаспределяемыеДоходыРасходыРасходы.Филиал,
		|	абс_РаспределяемыеДоходыРасходыРасходы.КодБУ,
		|	абс_РаспределяемыеДоходыРасходыРасходы.КодУУ,
		|	абс_РаспределяемыеДоходыРасходыРасходы.СуммарныйКоэффициентРаспределения,
		|	абс_РаспределяемыеДоходыРасходыРасходы.ОбщаяСуммаКРаспределению,
		|	абс_РаспределяемыеДоходыРасходыРасходы.СтатьяЗатрат
		|ИЗ
		|	Документ.абс_РаспределяемыеДоходыРасходы.Расходы КАК абс_РаспределяемыеДоходыРасходыРасходы
		|ГДЕ
		|	абс_РаспределяемыеДоходыРасходыРасходы.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	ОбщаяСуммаКРаспределению = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("Строка");
		ОбщаяСуммаКРаспределению = ОбщаяСуммаКРаспределению + ВыборкаДетальныеЗаписи.ОбщаяСуммаКРаспределению;
		ЗаполнитьЗначенияСвойств(Область.Параметры,ВыборкаДетальныеЗаписи);
		ТабДок.Вывести(Область);
	КонецЦикла;

	Область = Макет.ПолучитьОбласть("Итого");
	Область.Параметры.ОбщаяСуммаКРаспределению = ОбщаяСуммаКРаспределению;
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Подвал");
	Область.Параметры.Исполнитель  = ОбщегоНазначения.ФамилияИнициалыФизЛица(ДокументОбъект.Ответственный.ФизЛицо);
	
	ТабДок.Вывести(Область);
	Возврат ТабДок;
КонецФункции

Функция ВыполнитьПечатьВедомостьПоРаспределяемымДоходам(ДокументОбъект) Экспорт

	ТабДок = Новый ТабличныйДокумент;
	//АБС ВСТАВКА №000030071 НАЧАЛО «12 января 2015 г.», Пополитов     		
	НастройкиТабДок(ТабДок,"Печать_ВедомостьПоРаспределяемымДоходам");
	//\\АБС ВСТАВКА №000030071 КОНЕЦ 
	
	Макет = ДокументОбъект.ПолучитьМакет("ВедомостьПоРаспределяемымДоходам");
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Месяц = НРег(Формат(ДокументОбъект.Дата,"ДФ='ММMM yyyy'")) + " г.";
	ТабДок.Вывести(Область);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Доходы.НомерСтроки КАК НомерПП,
		|	Доходы.Филиал КАК НаименованиеФилиала,
		|	Доходы.КодБУ,
		|	Доходы.КодУУ,
		|	Доходы.СуммарныйКоэффициентРаспределения КАК ЗначениеКоэффициентаРаспределения,
		|	Доходы.ОбщаяСуммаКРаспределению КАК СуммаСучетомНДС,
		|	Доходы.НоменклатурнаяГруппа КАК НаименованиеНоменклатуры
		|ИЗ
		|	Документ.абс_РаспределяемыеДоходыРасходы.Доходы КАК Доходы
		|ГДЕ
		|	Доходы.Ссылка = &Ссылка
		//+ Романова Н.Г. 05.04.2017 #7795274
		|	И Доходы.СчетДт <> &СчетДт";
		//-
		
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	//+ Романова Н.Г. 05.04.2017 #7795274
	Запрос.УстановитьПараметр("СчетДт", ПланыСчетов.Хозрасчетный.Продажи_НДС); // 90.03
	//-
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	ОбщаяСуммаКРаспределению = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("Строка");
		ОбщаяСуммаКРаспределению = ОбщаяСуммаКРаспределению + ВыборкаДетальныеЗаписи.СуммаСучетомНДС;
		ЗаполнитьЗначенияСвойств(Область.Параметры,ВыборкаДетальныеЗаписи);
		ТабДок.Вывести(Область);
	КонецЦикла;

	Область = Макет.ПолучитьОбласть("Итого");
	Область.Параметры.СуммаСучетомНДС = ОбщаяСуммаКРаспределению;
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Подвал");

	Область.Параметры.Руководитель = ОбщегоНазначения.ФамилияИнициалыФизЛица(ДокументОбъект.Руководитель);			
	Область.Параметры.Исполнитель  = ОбщегоНазначения.ФамилияИнициалыФизЛица(ДокументОбъект.Ответственный.ФизЛицо);
	
	ТабДок.Вывести(Область);
	Возврат ТабДок;

КонецФункции

Функция ВыполнитьПечатьВедомостьПоРаспределяемымРасходамПоБлокам(ДокументОбъект) Экспорт

	ТабДок = Новый ТабличныйДокумент;
	//АБС ВСТАВКА №000030071 НАЧАЛО «12 января 2015 г.», Пополитов     		
	НастройкиТабДок(ТабДок,"Печать_ВедомостьПоРаспределяемымРасходамПоБлокам");
	//\\АБС ВСТАВКА №000030071 КОНЕЦ 
	
	Макет = ДокументОбъект.ПолучитьМакет("ВедомостьПоРаспределяемымРасходамПоБлокам");
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Месяц = НРег(Формат(ДокументОбъект.Дата,"ДФ='ММMM yyyy'")) + " г.";
	ТабДок.Вывести(Область);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	абс_РаспределяемыеДоходыРасходыРасходы.НомерСтроки КАК НомерПП,
		|	абс_РаспределяемыеДоходыРасходыРасходы.Филиал КАК НаименованиеФилиала,
		|	абс_РаспределяемыеДоходыРасходыРасходы.КодБУ,
		|	абс_РаспределяемыеДоходыРасходыРасходы.КодУУ,
		|	абс_РаспределяемыеДоходыРасходыРасходы.СуммарныйКоэффициентРаспределения КАК ЗначениеКоэффициентаРаспределения,
		|	абс_РаспределяемыеДоходыРасходыРасходы.ОбщаяСуммаКРаспределению КАК СуммаСучетомНДС,
		|	абс_РаспределяемыеДоходыРасходыРасходы.СтатьяЗатрат
		|ИЗ
		|	Документ.абс_РаспределяемыеДоходыРасходы.Расходы КАК абс_РаспределяемыеДоходыРасходыРасходы
		|ГДЕ
		|	абс_РаспределяемыеДоходыРасходыРасходы.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	ОбщаяСуммаКРаспределению = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("Строка");
		ОбщаяСуммаКРаспределению = ОбщаяСуммаКРаспределению + ВыборкаДетальныеЗаписи.СуммаСучетомНДС;
		ЗаполнитьЗначенияСвойств(Область.Параметры,ВыборкаДетальныеЗаписи);
		ТабДок.Вывести(Область);
	КонецЦикла;

	Область = Макет.ПолучитьОбласть("Итого");
	Область.Параметры.СуммаСучетомНДС = ОбщаяСуммаКРаспределению;
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Подвал");
	Область.Параметры.Руководитель = ОбщегоНазначения.ФамилияИнициалыФизЛица(ДокументОбъект.Руководитель);			
	Область.Параметры.Исполнитель  = ОбщегоНазначения.ФамилияИнициалыФизЛица(ДокументОбъект.Ответственный.ФизЛицо);
	
	ТабДок.Вывести(Область);
	Возврат ТабДок;

КонецФункции

Функция ВыполнитьПечатьСводнаяВедомостьПоРаспределяемымДоходам(ДокументОбъект,Филиал) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	//АБС ВСТАВКА №000030071 НАЧАЛО «12 января 2015 г.», Пополитов     		
	НастройкиТабДок(ТабДок,"Печать_СводнаяВедомостьПоРаспределяемымДоходам");
	//\\АБС ВСТАВКА №000030071 КОНЕЦ 
	
	Макет = Документы.абс_РаспределяемыеДоходыРасходы.ПолучитьМакет("СводнаяВедомостьПоРаспределяемымДоходам");
	Область = Макет.ПолучитьОбласть("Шапка");
	ТабДок.Вывести(Область);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Доходы.НоменклатурнаяГруппа КАК НаименованиеНоменклатурыСтатьиРасходов,
	|	Доходы.Филиал КАК НаименованиеФилиала,
	|	Доходы.КодБУ,
	|	Доходы.КодУУ,
	|	0 КАК СуммаРР,
	|	Доходы.ОбщаяСуммаКРаспределению КАК СуммаРД,
	|	Доходы.Ссылка КАК Документ,
	|	Доходы.Ссылка.ЦФУ КАК ЦФУ,
	|	Доходы.Ссылка.ЦФО КАК ЦФО,
	|	Доходы.СчетДт,
	|	Доходы.СчетКт,
	|	Доходы.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Документ.абс_РаспределяемыеДоходыРасходы.Доходы КАК Доходы
	|ГДЕ
	|	Доходы.Ссылка В(&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Расходы.СтатьяЗатрат,
	|	Расходы.Филиал,
	|	Расходы.КодБУ,
	|	Расходы.КодУУ,
	|	Расходы.ОбщаяСуммаКРаспределению,
	|	0,
	|	Расходы.Ссылка,
	|	Расходы.Ссылка.ЦФУ,
	|	Расходы.Ссылка.ЦФО,
	|	Расходы.СчетДт,
	|	Расходы.СчетКт,
	|	Расходы.СтавкаНДС
	|ИЗ
	|	Документ.абс_РаспределяемыеДоходыРасходы.Расходы КАК Расходы
	|ГДЕ
	|	Расходы.Ссылка В(&Ссылка)";
		
	Запрос.УстановитьПараметр("Филиал",Филиал);
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект);
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	ОбщаяСуммаКРаспределениюД = 0;
	ОбщаяСуммаКРаспределениюР = 0;
	Сч = 1 ;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("Строка");
		ОбщаяСуммаКРаспределениюД = ОбщаяСуммаКРаспределениюД + ВыборкаДетальныеЗаписи.СуммаРД;
		ОбщаяСуммаКРаспределениюР = ОбщаяСуммаКРаспределениюР + ВыборкаДетальныеЗаписи.СуммаРР;
		ЗаполнитьЗначенияСвойств(Область.Параметры,ВыборкаДетальныеЗаписи);
		Область.Параметры.НомерПП = Сч;
		ТабДок.Вывести(Область);
		Сч = Сч + 1;
	КонецЦикла;

	Область = Макет.ПолучитьОбласть("Итого");
	Область.Параметры.СуммаРД = ОбщаяСуммаКРаспределениюД;
	Область.Параметры.СуммаРР = ОбщаяСуммаКРаспределениюР;
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Подвал");
	
	Если ДокументОбъект.Количество() > 0 Тогда	
		Ответственный = ДокументОбъект[0].Ответственный;
		Область.Параметры.Исполнитель  = ОбщегоНазначения.ФамилияИнициалыФизЛица(Ответственный.ФизЛицо);
	КонецЕсли;	
	
	ТабДок.Вывести(Область);
	Возврат ТабДок;

КонецФункции

Функция ВыполнитьПечатьАвизоОПередаваемыхРасходах(ДокументОбъект,ОтборФилиал,НачальникОтделаСводнойОтчетности) Экспорт

	ТабДок = Новый ТабличныйДокумент;
	//АБС ВСТАВКА №000030071 НАЧАЛО «12 января 2015 г.», Пополитов     		
	НастройкиТабДок(ТабДок,"Печать_АвизоОПередаваемыхРасходах");
	//\\АБС ВСТАВКА №000030071 КОНЕЦ 
	
	Макет = Документы.абс_РаспределяемыеДоходыРасходы.ПолучитьМакет("АвизоОПередаваемыхРасходах");
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Филиал            = ОтборФилиал;
	Область.Параметры.ПериодСтрока      = Формат(ДокументОбъект.Дата,"ДЛФ=DD");
	Область.Параметры.ПериодОтражения   = Формат(ДокументОбъект.Дата,"ДФ=dd.MM.yyyy");
	Область.Параметры.СтрокаОбъектСтроительства   =  "";
	Область.Параметры.СтрокаСтатья                =  "";
	Область.Параметры.НомерАвизо                  =  ДокументОбъект.Номер;
	
	ТабДок.Вывести(Область);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствиеСчетовБУиНУСрезПоследних.СчетБУ,
		|	СоответствиеСчетовБУиНУСрезПоследних.СчетНУ,
		|	СоответствиеСчетовБУиНУСрезПоследних.ВидЗатратНУ
		|ПОМЕСТИТЬ втСоответствияСчетовБУ_НУ
		|ИЗ
		|	РегистрСведений.СоответствиеСчетовБУиНУ.СрезПоследних(&НаДату, ) КАК СоответствиеСчетовБУиНУСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	СоответствиеСчетовБУиНУСрезПоследних.СчетБУ,
		|	СоответствиеСчетовБУиНУСрезПоследних.СчетНУ,
		|	СоответствиеСчетовБУиНУСрезПоследних.ВидЗатратНУ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот) КАК СуммаРуб,
		|	ХозрасчетныйОборотыДтКт.СчетДт КАК Дебет,
		|	ХозрасчетныйОборотыДтКт.СчетКт КАК Кредит,
		|	ХозрасчетныйОборотыДтКт.Организация,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3
		|	КОНЕЦ КАК СтатьяЗатрат
		|ПОМЕСТИТЬ втОтраженоВКЦ
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(, , Регистратор, СчетДт В ИЕРАРХИИ (&СчетаРасходов), , СчетКт = &Счет79_02, , Организация = &Организация) КАК ХозрасчетныйОборотыДтКт
		|ГДЕ
		|	ХозрасчетныйОборотыДтКт.Регистратор = &Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОборотыДтКт.СчетДт,
		|	ХозрасчетныйОборотыДтКт.СчетКт,
		|	ХозрасчетныйОборотыДтКт.Организация,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОтраженоВКЦ.СуммаРуб,
		|	втОтраженоВКЦ.Дебет,
		|	втОтраженоВКЦ.Кредит,
		|	втОтраженоВКЦ.Организация,
		|	втОтраженоВКЦ.СтатьяЗатрат,
		|	СтатьиЗатрат.ВидРасходовНУ
		|ПОМЕСТИТЬ втОтраженоВКЦСтатьи
		|ИЗ
		|	втОтраженоВКЦ КАК втОтраженоВКЦ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиЗатрат КАК СтатьиЗатрат
		|		ПО втОтраженоВКЦ.СтатьяЗатрат = СтатьиЗатрат.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОтраженоВКЦСтатьи.СтатьяЗатрат,
		|	-втОтраженоВКЦСтатьи.СуммаРуб КАК СуммаРуб,
		|	втОтраженоВКЦСтатьи.Дебет,
		|	втСоответствияСчетовБУ_НУ.СчетНУ КАК ДебетНУ,
		|	втОтраженоВКЦСтатьи.СуммаРуб КАК СуммаРубФилиал,
		|	втОтраженоВКЦСтатьи.Дебет КАК ДебетФилиал,
		|	втСоответствияСчетовБУ_НУ.СчетНУ КАК ДебетФилиалНУ
		|ИЗ
		|	втОтраженоВКЦСтатьи КАК втОтраженоВКЦСтатьи
		|		ЛЕВОЕ СОЕДИНЕНИЕ втСоответствияСчетовБУ_НУ КАК втСоответствияСчетовБУ_НУ
		|		ПО втОтраженоВКЦСтатьи.Дебет = втСоответствияСчетовБУ_НУ.СчетБУ
		|			И втОтраженоВКЦСтатьи.ВидРасходовНУ = втСоответствияСчетовБУ_НУ.ВидЗатратНУ";
		
	СчетаРасходов = Новый Массив;
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство_);
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы);
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы);
	
	Запрос.УстановитьПараметр("Счет79_02",ПланыСчетов.Хозрасчетный.РасчетыПоТекущимОперациям);
	Запрос.УстановитьПараметр("СчетаРасходов",СчетаРасходов);
	
	Запрос.УстановитьПараметр("Организация",ОтборФилиал);
	Запрос.УстановитьПараметр("Регистратор",ДокументОбъект.ДокументОснованиеПриемник);
	Запрос.УстановитьПараметр("НаДату",ДокументОбъект.Дата);
	
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	ОбщаяСуммаКРаспределению = 0;
	//ОбщаяСуммаКРаспределениюФилиал = 0;
	Сч = 1;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("Строка");
		ОбщаяСуммаКРаспределению       = ОбщаяСуммаКРаспределению + ВыборкаДетальныеЗаписи.СуммаРуб;
		//ОбщаяСуммаКРаспределениюФилиал = ОбщаяСуммаКРаспределениюФилиал + ВыборкаДетальныеЗаписи.СуммаРубФилиал;
		ЗаполнитьЗначенияСвойств(Область.Параметры,ВыборкаДетальныеЗаписи);
		
		Область.Параметры.НомерПП = Сч;
		Сч = Сч + 1;
		ТабДок.Вывести(Область);
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("Итого");
	Область.Параметры.СуммаРуб       = ОбщаяСуммаКРаспределению;
	//	Область.Параметры.СуммаРубФилиал = ОбщаяСуммаКРаспределениюФилиал;
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Подвал");
	
	Руководители = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(ОтборФилиал, ДокументОбъект.Дата,);
	Бухгалтер    = Руководители.ГлавныйБухгалтер;
	Область.Параметры.ГлавныйБухгалтер                 = Бухгалтер;
	Область.Параметры.НачальникОтделаСводнойОтчетности = ОбщегоНазначения.ФамилияИнициалыФизЛица(НачальникОтделаСводнойОтчетности);
	
	
	ТабДок.Вывести(Область);
	Возврат ТабДок;
	
КонецФункции
 
Функция ВыполнитьПечатьАвизоОРаспределяемыхДоходах(ДокументОбъект,ОтборФилиал,НачальникОтделаСводнойОтчетности,НомерДляПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	//АБС ВСТАВКА №000030071 НАЧАЛО «12 января 2015 г.», Пополитов     		
	НастройкиТабДок(ТабДок,"Печать_АвизоОРаспределяемыхДоходах");
	//\\АБС ВСТАВКА №000030071 КОНЕЦ 
	
	Макет = Документы.абс_РаспределяемыеДоходыРасходы.ПолучитьМакет("АвизоОРаспределяемыхДоходах");
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Филиал            = ОтборФилиал;
	
	Если ТипЗнч(ДокументОбъект) = Тип("Массив") Тогда
		НаДату = ДокументОбъект[0].Дата;
	Иначе	
		НаДату = ДокументОбъект.Дата;
	КонецЕсли;
	
	Область.Параметры.ПериодОтражения   = Формат(НаДату,"ДФ=dd.MM.yyyy");
	Область.Параметры.ПериодСтрока      = Формат(НаДату,"ДЛФ=DD");
	
	Область.Параметры.НомерАвизо        = НомерДляПечати;
	
	ТабДок.Вывести(Область);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоответствиеСчетовБУиНУСрезПоследних.СчетБУ,
	|	СоответствиеСчетовБУиНУСрезПоследних.СчетНУ,
	|	СоответствиеСчетовБУиНУСрезПоследних.ВидЗатратНУ
	|ПОМЕСТИТЬ втСоответствияСчетовБУ_НУ
	|ИЗ
	|	РегистрСведений.СоответствиеСчетовБУиНУ.СрезПоследних(&НаДату, ) КАК СоответствиеСчетовБУиНУСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеСчетовБУиНУСрезПоследних.СчетБУ,
	|	СоответствиеСчетовБУиНУСрезПоследних.СчетНУ,
	|	СоответствиеСчетовБУиНУСрезПоследних.ВидЗатратНУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот) КАК СуммаРуб,
	|	ХозрасчетныйОборотыДтКт.Организация,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоКт1 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоКт1
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоКт2 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоКт2
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоКт3 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоКт3
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ХозрасчетныйОборотыДтКт.СчетДт КАК СчетДт,
	|	ХозрасчетныйОборотыДтКт.СчетКт КАК СчетКт
	|ПОМЕСТИТЬ втОтраженоВКЦ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(, , Регистратор, СчетДт = &Счет79_02, , СчетКт В ИЕРАРХИИ (&СчетаДоходов), , Организация = &Организация) КАК ХозрасчетныйОборотыДтКт
	|ГДЕ
	|	ХозрасчетныйОборотыДтКт.Регистратор В(&Регистраторы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОборотыДтКт.Организация,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоКт1 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоКт1
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоКт2 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоКт2
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоКт3 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоКт3
	|	КОНЕЦ,
	|	ХозрасчетныйОборотыДтКт.СчетДт,
	|	ХозрасчетныйОборотыДтКт.СчетКт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот),
	|	ХозрасчетныйОборотыДтКт.Организация,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3
	|	КОНЕЦ,
	|	ХозрасчетныйОборотыДтКт.СчетДт,
	|	ХозрасчетныйОборотыДтКт.СчетКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(, , Регистратор, СчетДт = &Счет90_03, , СчетКт = &Счет79_02, , Организация = &Организация) КАК ХозрасчетныйОборотыДтКт
	|ГДЕ
	|	ХозрасчетныйОборотыДтКт.Регистратор В(&Регистраторы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОборотыДтКт.Организация,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3 ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3
	|	КОНЕЦ,
	|	ХозрасчетныйОборотыДтКт.СчетДт,
	|	ХозрасчетныйОборотыДтКт.СчетКт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОтраженоВКЦ.СуммаРуб,
	|	втОтраженоВКЦ.Организация,
	|	втОтраженоВКЦ.НоменклатурнаяГруппа,
	|	втОтраженоВКЦ.СчетДт,
	|	втОтраженоВКЦ.СчетКт
	|ПОМЕСТИТЬ втОтраженоВКЦСтатьи
	|ИЗ
	|	втОтраженоВКЦ КАК втОтраженоВКЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	-втОтраженоВКЦСтатьи.СуммаРуб КАК СуммаРуб,
	|	втОтраженоВКЦСтатьи.СуммаРуб КАК СуммаРубФилиал,
	|	втОтраженоВКЦСтатьи.НоменклатурнаяГруппа,
	|	втОтраженоВКЦСтатьи.СчетДт КАК Дебет,
	|	втОтраженоВКЦСтатьи.СчетКт КАК Кредит,
	|	втСоответствияСчетовБУ_НУ.СчетНУ КАК ДебетНУ,
	|	втСоответствияСчетовБУ_НУ1.СчетНУ КАК КредитНУ
	|ИЗ
	|	втОтраженоВКЦСтатьи КАК втОтраженоВКЦСтатьи
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСоответствияСчетовБУ_НУ КАК втСоответствияСчетовБУ_НУ
	|		ПО втОтраженоВКЦСтатьи.СчетДт = втСоответствияСчетовБУ_НУ.СчетБУ
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСоответствияСчетовБУ_НУ КАК втСоответствияСчетовБУ_НУ1
	|		ПО втОтраженоВКЦСтатьи.СчетКт = втСоответствияСчетовБУ_НУ1.СчетБУ";
	
	Запрос.УстановитьПараметр("Счет79_02",ПланыСчетов.Хозрасчетный.РасчетыПоТекущимОперациям);
	
	СчетаДоходов = Новый Массив;
	СчетаДоходов.Добавить(ПланыСчетов.Хозрасчетный.Выручка);
	
	Запрос.УстановитьПараметр("Организация",ОтборФилиал);
	Запрос.УстановитьПараметр("СчетаДоходов",СчетаДоходов);
	
	Если ТипЗнч(ДокументОбъект) = Тип("Массив") Тогда
		Запрос.УстановитьПараметр("Регистраторы",ДокументОбъект);	
	Иначе
		МассивДокументов = Новый Массив;
		МассивДокументов.Добавить(ДокументОбъект.ДокументОснованиеПриемник);
		Запрос.УстановитьПараметр("Регистраторы",МассивДокументов);	
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НаДату",НаДату);
	
	Запрос.УстановитьПараметр("Счет90_03",ПланыСчетов.Хозрасчетный.Продажи_НДС);
	//Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	Сч = 1;
	ОбщаяСуммаКРаспределению = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("Строка");
		ОбщаяСуммаКРаспределению = ОбщаяСуммаКРаспределению + ВыборкаДетальныеЗаписи.СуммаРуб;
		ЗаполнитьЗначенияСвойств(Область.Параметры,ВыборкаДетальныеЗаписи);
		
		Область.Параметры.НомерПП = Сч;
		Сч = Сч + 1;

		ТабДок.Вывести(Область);
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("Итого");
	//Область.Параметры.ОбщаяСуммаКРаспределению = ОбщаяСуммаКРаспределению;
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Подвал");
	Руководители = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(ОтборФилиал, НаДату,);
	Бухгалтер    = Руководители.ГлавныйБухгалтер;
	Область.Параметры.ГлавныйБухгалтер                 = Бухгалтер;
	Область.Параметры.НачальникОтделаСводнойОтчетности = ОбщегоНазначения.ФамилияИнициалыФизЛица(НачальникОтделаСводнойОтчетности);
	
	ТабДок.Вывести(Область);
	Возврат ТабДок;

КонецФункции

Функция ВыполнитьПечатьАвизоОРаспределяемыхРасходах(ДокументОбъект,ОтборФилиал,НачальникОтделаСводнойОтчетности,НомерДляПечати) Экспорт

	ТабДок = Новый ТабличныйДокумент;
	//АБС ВСТАВКА №000030071 НАЧАЛО «12 января 2015 г.», Пополитов     		
	НастройкиТабДок(ТабДок,"Печать_АвизоОРаспределяемыхРасходах");
	//\\АБС ВСТАВКА №000030071 КОНЕЦ 
	
	Макет = Документы.абс_РаспределяемыеДоходыРасходы.ПолучитьМакет("АвизоОРаспределяемыхРасходах");
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Филиал            = ОтборФилиал;
	
	Если ТипЗнч(ДокументОбъект) = Тип("Массив") Тогда
		НаДату = ДокументОбъект[0].Дата;
	Иначе	
		НаДату = ДокументОбъект.Дата;
	КонецЕсли;
	
	Область.Параметры.ПериодОтражения   = Формат(НаДату,"ДФ=dd.MM.yyyy");
	Область.Параметры.ПериодСтрока      = Формат(НаДату,"ДЛФ=DD");
	
	Область.Параметры.НомерАвизо        = НомерДляПечати;
	
	ТабДок.Вывести(Область);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствиеСчетовБУиНУСрезПоследних.СчетБУ,
		|	СоответствиеСчетовБУиНУСрезПоследних.СчетНУ,
		|	СоответствиеСчетовБУиНУСрезПоследних.ВидЗатратНУ
		|ПОМЕСТИТЬ втСоответствияСчетовБУ_НУ
		|ИЗ
		|	РегистрСведений.СоответствиеСчетовБУиНУ.СрезПоследних(&НаДату, ) КАК СоответствиеСчетовБУиНУСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	СоответствиеСчетовБУиНУСрезПоследних.СчетБУ,
		|	СоответствиеСчетовБУиНУСрезПоследних.СчетНУ,
		|	СоответствиеСчетовБУиНУСрезПоследних.ВидЗатратНУ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот) КАК СуммаРуб,
		|	ХозрасчетныйОборотыДтКт.СчетДт КАК Дебет,
		|	ХозрасчетныйОборотыДтКт.СчетКт КАК Кредит,
		|	ХозрасчетныйОборотыДтКт.Организация,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3
		|	КОНЕЦ КАК СтатьяЗатрат
		|ПОМЕСТИТЬ втОтраженоВКЦ
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(, , Регистратор, СчетДт В ИЕРАРХИИ (&СчетаРасходов), , СчетКт = &Счет79_02, , Организация = &Организация) КАК ХозрасчетныйОборотыДтКт
		|ГДЕ
		|	ХозрасчетныйОборотыДтКт.Регистратор В(&Регистратор)
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОборотыДтКт.СчетДт,
		|	ХозрасчетныйОборотыДтКт.СчетКт,
		|	ХозрасчетныйОборотыДтКт.Организация,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт2
		|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ХозрасчетныйОборотыДтКт.СубконтоДт3
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОтраженоВКЦ.СуммаРуб,
		|	втОтраженоВКЦ.Дебет,
		|	втОтраженоВКЦ.Кредит,
		|	втОтраженоВКЦ.Организация,
		|	втОтраженоВКЦ.СтатьяЗатрат,
		|	СтатьиЗатрат.ВидРасходовНУ
		|ПОМЕСТИТЬ втОтраженоВКЦСтатьи
		|ИЗ
		|	втОтраженоВКЦ КАК втОтраженоВКЦ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиЗатрат КАК СтатьиЗатрат
		|		ПО втОтраженоВКЦ.СтатьяЗатрат = СтатьиЗатрат.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОтраженоВКЦСтатьи.СтатьяЗатрат,
		|	-втОтраженоВКЦСтатьи.СуммаРуб КАК СуммаРуб,
		|	втОтраженоВКЦСтатьи.Дебет,
		|	втСоответствияСчетовБУ_НУ.СчетНУ КАК ДебетНУ,
		|	втОтраженоВКЦСтатьи.СуммаРуб КАК СуммаРубФилиал,
		|	втОтраженоВКЦСтатьи.Дебет КАК ДебетФилиал,
		|	втСоответствияСчетовБУ_НУ.СчетНУ КАК ДебетФилиалНУ
		|ИЗ
		|	втОтраженоВКЦСтатьи КАК втОтраженоВКЦСтатьи
		|		ЛЕВОЕ СОЕДИНЕНИЕ втСоответствияСчетовБУ_НУ КАК втСоответствияСчетовБУ_НУ
		|		ПО втОтраженоВКЦСтатьи.Дебет = втСоответствияСчетовБУ_НУ.СчетБУ
		|			И втОтраженоВКЦСтатьи.ВидРасходовНУ = втСоответствияСчетовБУ_НУ.ВидЗатратНУ";
		
	СчетаРасходов = Новый Массив;
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство_);
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы);
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы);
	
	Запрос.УстановитьПараметр("Счет79_02",ПланыСчетов.Хозрасчетный.РасчетыПоТекущимОперациям);
	Запрос.УстановитьПараметр("СчетаРасходов",СчетаРасходов);
	
	Запрос.УстановитьПараметр("Организация",ОтборФилиал);
	
	
	Если ТипЗнч(ДокументОбъект) = Тип("Массив") Тогда
		Запрос.УстановитьПараметр("Регистратор",ДокументОбъект);	
	Иначе
		МассивДокументов = Новый Массив;
		МассивДокументов.Добавить(ДокументОбъект.ДокументОснованиеПриемник);
		Запрос.УстановитьПараметр("Регистратор",МассивДокументов);	
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НаДату",НаДату);
	
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	ОбщаяСуммаКРаспределению = 0;
	ОбщаяСуммаКРаспределениюФилиал = 0;
	Сч = 1;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("Строка");
		ОбщаяСуммаКРаспределению       = ОбщаяСуммаКРаспределению + ВыборкаДетальныеЗаписи.СуммаРуб;
		ОбщаяСуммаКРаспределениюФилиал = ОбщаяСуммаКРаспределениюФилиал + ВыборкаДетальныеЗаписи.СуммаРубФилиал;
		ЗаполнитьЗначенияСвойств(Область.Параметры,ВыборкаДетальныеЗаписи);
		
		Область.Параметры.НомерПП = Сч;
		Сч = Сч + 1;
		ТабДок.Вывести(Область);
	КонецЦикла;

	Область = Макет.ПолучитьОбласть("Итого");
	Область.Параметры.СуммаРуб       = ОбщаяСуммаКРаспределению;
	Область.Параметры.СуммаРубФилиал = ОбщаяСуммаКРаспределениюФилиал;
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Подвал");
	Руководители = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(ОтборФилиал, НаДату,);
	Бухгалтер    = Руководители.ГлавныйБухгалтер;
	Область.Параметры.ГлавныйБухгалтер                 = Бухгалтер;
	Область.Параметры.НачальникОтделаСводнойОтчетности = ОбщегоНазначения.ФамилияИнициалыФизЛица(НачальникОтделаСводнойОтчетности);
	
	ТабДок.Вывести(Область);
	Возврат ТабДок;
КонецФункции
              
//АБС ВСТАВКА №000030071 НАЧАЛО «12 января 2015 г.», Пополитов 
Процедура НастройкиТабДок(ТабДок,ИмяПараметровПечати) Экспорт 	
	
	ТабДок.ИмяПараметровПечати = ИмяПараметровПечати;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.ПолеСверху = 0;
	ТабДок.ПолеСлева  = 0;
	ТабДок.ПолеСнизу  = 0;
	ТабДок.ПолеСправа = 0;
	ТабДок.Автомасштаб = Истина;
	
КонецПроцедуры	
//\\АБС ВСТАВКА №000030071 КОНЕЦ






	