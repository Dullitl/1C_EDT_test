Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктПечатныхФорм = Новый Структура;
	СтруктПечатныхФорм.Вставить("РасчетЗадолженностиПени",      "Расчет задолженностей и пеней");
	
	Возврат СтруктПечатныхФорм;
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	Если ЭтоНовый() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Документ можно распечатать только после его записи'"));
		Возврат;
	КонецЕсли;
	
	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "РасчетЗадолженностиПени" Тогда
		
		ТабДокумент = ПечатьРасчетЗадолженностиПени();
		
	ИначеЕсли ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда
		
		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
	Иначе
		Возврат;
	КонецЕсли;
	
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ttk_ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);
	
КонецПроцедуры // Печать

Функция ПечатьРасчетЗадолженностиПени()
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РасчетПеней_РасчетЗадолженностиПени";
	Макет = ПолучитьМакет("РасчетЗадолженностиПени");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.ТекстЗаголовка = "Расчет задолженностей и пеней на " + Формат(Дата, "ДФ=dd.MM.yy");
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	СуммаЗадолженностиИтого = 0;
	СуммаПениИтого = 0;
	СуммаВсегоИтого = 0;
	
	Для каждого СтрокаТабЧасти Из Пени Цикл
		
		Если СтрокаТабЧасти.НомерСтроки = 1 Тогда 
			ОбластьМакета.Параметры.Контрагент = Контрагент;
			ОбластьМакета.Параметры.ДоговорКонтрагента = ДоговорКонтрагента;
		Иначе
			ОбластьМакета.Параметры.Контрагент = "";
			ОбластьМакета.Параметры.ДоговорКонтрагента = "";
		КонецЕсли;
		
		ОбластьМакета.Параметры.ПредставлениеДокументаРасчетов = СокрЛП(СтрокаТабЧасти.ДокументРасчетов.Номер) 
		+ " от " + Формат(СтрокаТабЧасти.ДокументРасчетов.Дата, "ДФ=dd.MM.yy");
		ОбластьМакета.Параметры.ДокументРасчетов = СтрокаТабЧасти.ДокументРасчетов;
		ОбластьМакета.Параметры.СуммаЗадолженности = СтрокаТабЧасти.СуммаЗадолженности;
		ОбластьМакета.Параметры.СуммаЗадолженностиБезНДС = СтрокаТабЧасти.СуммаЗадолженности/118*100;
		ДатаДоставки = ?(ТипЗнч(СтрокаТабЧасти.ДокументРасчетов) = Тип("ДокументСсылка.РеализацияТоваровУслуг") И ЗначениеЗаполнено(СтрокаТабЧасти.ДокументРасчетов.абс_ДатаПолученияСчета), СтрокаТабЧасти.ДокументРасчетов.абс_ДатаПолученияСчета, СтрокаТабЧасти.ДокументРасчетов.Дата);
		ОбластьМакета.Параметры.ДатаДоставки = Формат(ДатаДоставки, "ДФ=dd.MM.yy");
		ОбластьМакета.Параметры.ПериодНачисления = Формат(ДатаДоставки, "ДФ=dd.MM.yy") + " - " + Формат(Дата, "ДФ=dd.MM.yy");
		ОбластьМакета.Параметры.ДнейПросрочено = СтрокаТабЧасти.ДнейПросрочено;
		ОбластьМакета.Параметры.ПроцентПеней = Строка(ДоговорКонтрагента.абс_ПроцентПеней) + "%";
		ОбластьМакета.Параметры.Ограничение = "6%";
		ОбластьМакета.Параметры.СуммаПени = СтрокаТабЧасти.СуммаПени;
		ОбластьМакета.Параметры.СуммаВсего = СтрокаТабЧасти.СуммаЗадолженности + СтрокаТабЧасти.СуммаПени;
		
		СуммаЗадолженностиИтого = СуммаЗадолженностиИтого + СтрокаТабЧасти.СуммаЗадолженности;
		СуммаПениИтого = СуммаПениИтого + СтрокаТабЧасти.СуммаПени;
		СуммаВсегоИтого = СуммаВсегоИтого + СтрокаТабЧасти.СуммаЗадолженности + СтрокаТабЧасти.СуммаПени;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Итоги");
	ОбластьМакета.Параметры.СуммаЗадолженности = СуммаЗадолженностиИтого;
	ОбластьМакета.Параметры.СуммаПени = СуммаПениИтого;
	ОбластьМакета.Параметры.СуммаВсего = СуммаВсегоИтого;
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат  ТабДокумент;
	
КонецФункции
