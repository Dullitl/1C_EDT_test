
Процедура НайтиЦенуСтоимость(Номенклатура,Цена, Ссылка, Счет)
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ЗабалансовыйУчетОборотыДтКт.КоличествоОборотДт КАК Количество,
	|	ЗабалансовыйУчетОборотыДтКт.СуммаОборот КАК Стоимость
	|ИЗ
	|	РегистрБухгалтерии.ЗабалансовыйУчет.ОборотыДтКт(
	|			,
	|			,
	|			Регистратор,
	|			СчетДт = &Счет,
	|			,
	|			СчетКт = &Счет,
	|			,
	|			СубконтоДт1 = &Номенклатура
	|				И СубконтоКт1 = &Номенклатура) КАК ЗабалансовыйУчетОборотыДтКт
	|ГДЕ
	|	ЗабалансовыйУчетОборотыДтКт.Регистратор = &ТекДок";
	
	Запрос.УстановитьПараметр("ТекДок", Ссылка);
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	Запрос.УстановитьПараметр("Счет", Счет);
	
	Цена = 0;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если выборка.Количество=0 Тогда
			возврат;
		КонецЕсли;	
		Цена = Выборка.Стоимость/Выборка.Количество;
		прервать;
	КонецЦикла;	
	
КонецПроцедуры	

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст ошибки)
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в которой был выведен объект)
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М11") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М11", "M-11 (Требование-накладная)", ПечатьМ11(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

// Функция формирует печатную форму М-11
//
Функция ПечатьМ11(МассивОбъектов, ОбъектыПечати)
		
	ТекстЗапросаШапка = 
	"ВЫБРАТЬ
	|	абс_ПеремещениеОСВАренде.Номер КАК Номер,
	|	абс_ПеремещениеОСВАренде.Дата КАК ДатаДокумента,
	|	абс_ПеремещениеОСВАренде.Дата КАК ДатаСоставления,
	|	абс_ПеремещениеОСВАренде.Организация,
	|	абс_ПеремещениеОСВАренде.Комментарий КАК Комментарий,
	|	ВЫБОР
	|		КОГДА ФИО_Затребовал.Фамилия ЕСТЬ NULL 
	|			ТОГДА абс_ПеремещениеОСВАренде.Затребовал
	|		ИНАЧЕ ФИО_Затребовал.Фамилия + "" "" + ПОДСТРОКА(ФИО_Затребовал.Имя, 1, 1) + "". "" + ПОДСТРОКА(ФИО_Затребовал.Отчество, 1, 1) + "".""
	|	КОНЕЦ КАК Затребовал,
	|	ВЫБОР
	|		КОГДА ФИО_Разрешил.Фамилия ЕСТЬ NULL 
	|			ТОГДА абс_ПеремещениеОСВАренде.Разрешил
	|		ИНАЧЕ ФИО_Разрешил.Фамилия + "" "" + ПОДСТРОКА(ФИО_Разрешил.Имя, 1, 1) + "". "" + ПОДСТРОКА(ФИО_Разрешил.Отчество, 1, 1) + "".""
	|	КОНЕЦ КАК Разрешил,
	|	ВЫБОР
	|		КОГДА ФИО_ЧерезКого.Фамилия ЕСТЬ NULL 
	|			ТОГДА абс_ПеремещениеОСВАренде.ЧерезКого
	|		ИНАЧЕ ФИО_ЧерезКого.Фамилия + "" "" + ПОДСТРОКА(ФИО_ЧерезКого.Имя, 1, 1) + "". "" + ПОДСТРОКА(ФИО_ЧерезКого.Отчество, 1, 1) + "".""
	|	КОНЕЦ КАК ЧерезКого,
	|	ВЫБОР
	|		КОГДА ФИО_Отправитель.Фамилия ЕСТЬ NULL 
	|			ТОГДА МОЛ_Отправитель.ФизическоеЛицо
	|		ИНАЧЕ ФИО_Отправитель.Фамилия + "" "" + ПОДСТРОКА(ФИО_Отправитель.Имя, 1, 1) + "". "" + ПОДСТРОКА(ФИО_Отправитель.Отчество, 1, 1) + "".""
	|	КОНЕЦ КАК Отпустил,
	|	ВЫБОР
	|		КОГДА ФИОПолучатель.Фамилия ЕСТЬ NULL 
	|			ТОГДА МОЛ_Получатель.ФизическоеЛицо
	|		ИНАЧЕ ФИОПолучатель.Фамилия + "" "" + ПОДСТРОКА(ФИОПолучатель.Имя, 1, 1) + "". "" + ПОДСТРОКА(ФИОПолучатель.Отчество, 1, 1) + "".""
	|	КОНЕЦ КАК Получил,
	|	ДолжностьОтпустил.Должность КАК Должность_Отпустил,
	|	ДолжностьПолучил.Должность КАК Должность_Получил,
	|	абс_ПеремещениеОСВАренде.Отправитель КАК Склад,
	|	абс_ПеремещениеОСВАренде.Получатель КАК Подразделение
	|ИЗ
	|	Документ.абс_ПеремещениеОСВАренде КАК абс_ПеремещениеОСВАренде
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ) КАК ФИО_Затребовал
	|		ПО абс_ПеремещениеОСВАренде.Затребовал = ФИО_Затребовал.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ) КАК ФИО_ЧерезКого
	|		ПО абс_ПеремещениеОСВАренде.ЧерезКого = ФИО_ЧерезКого.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ) КАК ФИО_Разрешил
	|		ПО абс_ПеремещениеОСВАренде.Разрешил = ФИО_Разрешил.ФизЛицо,
	|	РегистрСведений.ОтветственныеЛица.СрезПоследних(&Дата, ) КАК МОЛ_Отправитель
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ) КАК ФИО_Отправитель
	|		ПО МОЛ_Отправитель.ФизическоеЛицо = ФИО_Отправитель.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних КАК ДолжностьОтпустил
	|		ПО МОЛ_Отправитель.ФизическоеЛицо = ДолжностьОтпустил.Сотрудник.Физлицо,
	|	РегистрСведений.ОтветственныеЛица.СрезПоследних(&Дата, ) КАК МОЛ_Получатель
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ) КАК ФИОПолучатель
	|		ПО МОЛ_Получатель.ФизическоеЛицо = ФИОПолучатель.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних КАК ДолжностьПолучил
	|		ПО МОЛ_Получатель.ФизическоеЛицо = ДолжностьПолучил.Сотрудник.Физлицо
	|ГДЕ
	|	абс_ПеремещениеОСВАренде.Ссылка = &ТекущийДокумент";
	                    
	ТекстЗапросаТовары = "ВЫБРАТЬ
	                     |	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	                     |	ВложенныйЗапрос.Номенклатура.Наименование КАК МатериалНаименование,
	                     |	ВложенныйЗапрос.Номенклатура.ИнвНомерАрендодателя КАК НоменклатурныйНомер,
	                     |	ВложенныйЗапрос.Количество КАК Количество,
	                     |	ВложенныйЗапрос.Счет КАК Счет,
	                     |	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмеренияНаименование,
	                     |	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки
	                     |ИЗ
	                     |	(ВЫБРАТЬ
	                     |		абс_ПеремещениеОСВАрендеОС.АрендованноеОС КАК Номенклатура,
	                     |		""шт"" КАК ЕдиницаИзмерения,
	                     |		абс_ПеремещениеОСВАрендеОС.Счет КАК Счет,
	                     |		СУММА(абс_ПеремещениеОСВАрендеОС.Количество) КАК Количество,
	                     |		МИНИМУМ(абс_ПеремещениеОСВАрендеОС.НомерСтроки) КАК НомерСтроки
	                     |	ИЗ
	                     |		Документ.абс_ПеремещениеОСВАренде.ОС КАК абс_ПеремещениеОСВАрендеОС
	                     |	ГДЕ
	                     |		абс_ПеремещениеОСВАрендеОС.Ссылка = &ТекущийДокумент
	                     |	
	                     |	СГРУППИРОВАТЬ ПО
	                     |		абс_ПеремещениеОСВАрендеОС.Счет,
	                     |		абс_ПеремещениеОСВАрендеОС.АрендованноеОС) КАК ВложенныйЗапрос";
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_абс_ПеремещениеОСВАренде_М11";
	
	// Вывод заголовка
	//Макет = ?(Уполномоченные, ПолучитьМакет("М11"), ПолучитьОбщийМакет("М11"));
	Макет = ПолучитьМакет("М11");

	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
		Запрос.УстановитьПараметр("Дата", ТекущаяДата());
		Запрос.Текст = ТекстЗапросаШапка;

		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);

		Запрос.Текст = ТекстЗапросаТовары;

		ЗапросПоНоменклатуре = Запрос.Выполнить();
		
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заголовок     = "ТРЕБОВАНИЕ-НАКЛАДНАЯ № " + ОбщегоНазначения.ПолучитьНомерНаПечать(Шапка);
		Область.Параметры.Заполнить(Шапка);
		
		СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента);

		Область.Параметры.ПредставлениеОрганизации   = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование");
		Область.Параметры.ПредставлениеПодразделения = Шапка.Подразделение;
		Область.Параметры.КодОКПО                    = СведенияОбОрганизации.КодПоОКПО;
		
		ТабДокумент.Вывести(Область);
		
		ВыборкаПоСтрокам = ЗапросПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоСтрокам.Следующий() Цикл

			Область = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.Заполнить(ВыборкаПоСтрокам);
			Область.Параметры.МатериалНаименование = СокрЛП(ВыборкаПоСтрокам.МатериалНаименование);
			
			НайтиЦенуСтоимость(ВыборкаПоСтрокам.Номенклатура, Область.Параметры.Цена, Ссылка, ВыборкаПоСтрокам.Счет);
		    Область.Параметры.Сумма = ВыборкаПоСтрокам.Количество*Область.Параметры.Цена;
	
			ТабДокумент.Вывести(Область);

		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.Заполнить(Шапка);
		ТабДокумент.Вывести(Область);
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
	КонецЦикла; 

	Возврат ТабДокумент;
	
КонецФункции // ПечатьМ11()

