Перем мУдалятьДвижения;

// Строки, хранят реквизиты имеющие смысл только для бух. учета и упр. соответственно
// в случае если документ не отражается в каком-то виде учета, делаются невидимыми
Перем мСтрокаРеквизитыБухУчета Экспорт; // (Регл)
Перем мСтрокаРеквизитыУпрУчета Экспорт; // (Упр)
Перем мСтрокаРеквизитыНалУчета Экспорт; // (Регл)

Перем мУчетнаяПолитика;                 // (Общ)
Перем мУчетнаяПолитикаБух;				// (Регл)

Перем мВалютаРегламентированногоУчета Экспорт;
Перем мИндексСтрокиСписанныеТовары;
Перем мУказаниеПроектовВТабличнойЧастиДокументов Экспорт;

Перем мУказаниеСкладовВТЧ Экспорт;

Перем ИспользоватьРегистрСвободныеОстатки Экспорт;

#Если Клиент Тогда

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	Если ЭтоНовый() Тогда
		Предупреждение(НСтр("ru = 'Документ можно распечатать только после его записи'"));
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение(НСтр("ru = 'Недостаточно полномочий для печати непроведенного документа!'"));
		Возврат;
	КонецЕсли; 

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Получить экземпляр документа на печать
	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
	Иначе
		ПараметрКоманды = Новый Массив;
		ПараметрКоманды.Добавить(Ссылка);
		
		Если НаПринтер Тогда
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер("Документ.ТребованиеНакладная", ИмяМакета, 
										ПараметрКоманды, Неопределено);
		Иначе
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.ТребованиеНакладная", ИмяМакета, 
										ПараметрКоманды, Неопределено, Неопределено);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер,
	ttk_ОбщегоНазначения.СформироватьЗаголовокДокумента(Ссылка), Ссылка);
	
КонецПроцедуры // Печать()

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов = Новый Структура("Накладная", "Требование-накладная");
	СтруктураМакетов.Вставить("М11", "М-11 (Требование накладная)");
	
	Возврат СтруктураМакетов;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА


//АБС ВСТАВКА Проектный учет 171213 Родин
Процедура ПеренестиПроектВРБП()
	
	Для Каждого Строка Из Материалы Цикл
		Если Строка.СчетЗатрат = ПланыСчетов.Хозрасчетный.ПрочиеРасходыБудущихПериодов и ТипЗнч(Строка.Субконто1) = Тип("СправочникСсылка.РасходыБудущихПериодов") Тогда
			Если ЗначениеЗаполнено(Строка.Субконто1) и Не ЗначениеЗаполнено(Строка.Субконто1.Проект) и ЗначениеЗаполнено(Строка.Проект) Тогда
				РБП = Строка.Субконто1.ПолучитьОбъект();
				РБП.Проект = Строка.Проект;
				РБП.ОбменДанными.Загрузка = Истина;
				РБП.Записать();
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
//АБС ВСТАВКА Проектный учет 171213 Родин


// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для определенного вида учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета() Экспорт
	
	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр();
	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл();
	
КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета()

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для управленческого учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр()
	
	мСтрокаРеквизитыУпрУчета = "Подразделение, НадписьПодразделение";
	
КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр()

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для регламентированного учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл()
	
	мСтрокаРеквизитыБухУчета = "ПодразделениеОрганизации, НадписьПодразделениеОрганизации";
	
	мСтрокаРеквизитыНалУчета = "";
	
КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл()

// Процедура проверяет правильность заполнения реквизитов документа
//
Процедура ПроверкаРеквизитовТЧ(ТаблицаМатериалов, СтруктураШапкиДокумента, Отказ, Заголовок) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	// Проверим наличие заказа в зависимости от выбранных статей затрат
	ЗаказОбязателен = Ложь;
	Для Каждого Строка Из ТаблицаМатериалов Цикл
		Если Строка.Услуга Тогда
			ttk_ОбщегоНазначения.СообщитьОбОшибке(Нстр("ru = 'Нельзя использовать услугу (строка № " + Строка.НомерСтроки + " таблица ""Материалы"")'"), Отказ, Заголовок);
		КонецЕсли;
		//Заполнение склада в табличной части проверяем здесь, т.к. склад в ТЧможет быть заполнен в процедуре ПередЗаписью
		//		в этом случае его нельзя проверять в ОбработкаПроверкиЗаполнения
		Если мУказаниеСкладовВТЧ И НЕ ЗначениеЗаполнено(Строка.Склад) Тогда
			ttk_ОбщегоНазначения.СообщитьОбОшибке(Нстр("ru = 'Не указан склад (строка № " + Строка.НомерСтроки + " таблица ""Материалы"")'"), Отказ, Заголовок);
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(Строка.СтатьяЗатрат) Тогда
			ttk_ОбщегоНазначения.СообщитьОбОшибке(Нстр("ru = 'Не указана статья затрат (строка № " + Строка.НомерСтроки + " таблица ""Материалы"")'"), Отказ, Заголовок);
		КонецЕсли;
		
		Если Строка.СтатусМатериальныхЗатрат = Перечисления.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку Тогда
			Если НЕ ЗначениеЗаполнено(Строка.Заказ) Тогда
				ttk_ОбщегоНазначения.СообщитьОбОшибке(Нстр("ru = 'При использовании статей затрат по переработке давальческого сырья заказ обязателен (строка № " + Строка.НомерСтроки + ")'"), Отказ, Заголовок);
			ИначеЕсли ТипЗнч(Строка.Заказ) <> Тип("ДокументСсылка.ЗаказПокупателя") ИЛИ  Не Строка.Заказ.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.Переработка Тогда
				ttk_ОбщегоНазначения.СообщитьОбОшибке(Нстр("ru = 'При использовании статей затрат по переработке давальческого сырья можно использовать только заказ на переработку (строка № " + Строка.НомерСтроки + ")'"), Отказ, Заголовок);
			КонецЕсли;
			Если Строка.ХарактерЗатрат <> Перечисления.ХарактерЗатрат.ПроизводственныеРасходы Тогда
				ttk_ОбщегоНазначения.СообщитьОбОшибке(Нстр("ru = 'В статье затрат статус материальных затрат ""Принятые в переработку"" можно указывать только для производственных расходов (строка № " + Строка.НомерСтроки + ")'"), Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
		
		//Если Строка.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы
		//   И Строка.ВидЗатрат <> Перечисления.ВидыЗатрат.Материальные Тогда
		//	ttk_ОбщегоНазначения.СообщитьОбОшибке(Нстр("ru = 'Для производственных расходов можно использовать только материальные статьи затрат! (строка № " + Строка.НомерСтроки + ")'"), Отказ, Заголовок);
		//КонецЕсли;

		Если ЗначениеЗаполнено(Строка.ВнутреннийЗаказ) Тогда
			СтрокаНачалаСообщенияОбОшибке = Нстр("ru = 'В строке номер """+ СокрЛП(Строка.НомерСтроки) +
				""" табличной части "" Материалы "": '");

			Если Строка.ВнутреннийЗаказ.ВидЗаказа <> Перечисления.ВидыВнутреннегоЗаказа.ВПодразделение Тогда
				//неправильный внутренний заказ
				ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + Нстр("ru = 'указан Внутренний заказ с видом заказа """+Строка.ВнутреннийЗаказ.ВидЗаказа+""". Может быть указан только заказ с видом ""В подразделение"" '") , Отказ, Заголовок);
			КонецЕсли;
			Если Строка.ВнутреннийЗаказ.Заказчик <> Подразделение Тогда
				// неправильное подразделение
				ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + Нстр("ru = 'указан Внутренний заказ, в котором подразделение-заказчик отличается от подразделения, указанного в шапке документа'") , Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
		//АБС задача 191 ИСУП ТТК
		СтрокаНачалаСообщенияОбОшибке = Нстр("ru = 'В строке номер """+ СокрЛП(Строка.НомерСтроки) +
				""" табличной части "" Материалы "": '");

		Если Не ЗначениеЗаполнено(Строка.абс_ЦФУ) Тогда
			ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+"Не заполнено ЦФУ",Отказ,Заголовок);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.абс_ЦФО) Тогда
			ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+"Не заполнено ЦФО",Отказ,Заголовок);
		КонецЕсли;
		
		//Крамаренко Д.М.
		//НФС 2018
		//Если Не ЗначениеЗаполнено(Строка.абс_БюджетнаяСтатья) Тогда
		//	ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+"Не заполнена Бюджетная Статья",Отказ,Заголовок);
		//КонецЕсли;
		//Крамаренко Д.М.
		//НФС 2018
		
		Если Не ЗначениеЗаполнено(Строка.абс_ТипКонтрагента) Тогда
			ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+"Не заполнен Тип контрагента ",Отказ,Заголовок);
		КонецЕсли;

		//\\АБС задача 191 ИСУП ТТК
		
		
		//АБС ВСТАВКА Проектный учет 171213 Родин
		
		ИспользоватьПроектныйУчет =  глЗначениеПеременной("абс_ПроектныйУчет");
		Если ИспользоватьПроектныйУчет Тогда
			Если Строка.СчетЗатрат = ПланыСчетов.Хозрасчетный.ПрочиеРасходыБудущихПериодов Тогда
				Если Не ЗначениеЗаполнено(Строка.Проект) Тогда
					ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+"Не заполнен Проект ",Отказ,Заголовок);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//АБС ВСТАВКА Проектный учет 171213 Родин
		
	КонецЦикла;
	
	// Здесь наборов-пакетов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетНаборов(ЭтотОбъект, "Материалы", ТаблицаМатериалов, Отказ, Заголовок);

	// Здесь наборов-комплектов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетКомплектов(ЭтотОбъект, "Материалы", ТаблицаМатериалов, Отказ, Заголовок);
	

КонецПроцедуры // ПроверкаРеквизитов()

//Проверяет корректность счетов учета ТЧ материалы, а также корректность значений реквизитов, 
//зависящих от выбранных счетов учета.
Процедура ПроверитьКорректностьСчетовУчетаМатериалы(ТаблицаМатериалов, СтруктураШапкиДокумента, Отказ, Заголовок)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из ТаблицаМатериалов Цикл
	
		Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете И Строка.СтатусМатериальныхЗатрат <> Перечисления.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку Тогда 
			СтрокаНачалаСообщенияОбОшибке = НСтр("ru = 'В строке номер """+ СокрЛП(Строка.НомерСтроки) +
									   """ табличной части "" Материалы "": '");
			Если Строка.ВидРасходовНУ = Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения
			  И НЕ Строка.СчетЗатратНУ.ПринадлежитЭлементу(ПланыСчетов.Налоговый.ПрочиеРасходы) Тогда
				//Выводим предупреждение (без запрета проведения). Поэтому Отказ не передаем
				СтрокаСообщения = НСтр("ru = 'Указана статья затрат, не учитываемая в целях налогообложения и счет налогового учета не принадлежащий счету ""Прочие расходы""'");
				ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, , Заголовок);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполняет счета учета по бухгалтерскому и налоговому учету.
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл(СтрокаТЧ, ИмяТабЧасти, ЗаполнятьБУ, ЗаполнятьНУ, ЗаполнятьСчетаУчетаНоменклатуры=Истина, ЗаполнятьСчетаУчетаЗатрат=Истина) Экспорт
	
	ЗаполнитьСчетаУчетаВТабЧасти(СтрокаТЧ, ИмяТабЧасти, ЗаполнятьБУ, ЗаполнятьНУ, ЗаполнятьСчетаУчетаНоменклатуры, ЗаполнятьСчетаУчетаЗатрат);
	
КонецПроцедуры // ЗаполнитьСчетаУчетаВСтрокеТабЧасти()

// Заполняет счета БУ и НУ в табличной части документа
//
Процедура ЗаполнитьСчетаУчетаВТабЧасти(ТабличнаяЧасть, ИмяТабЧасти, ЗаполнятьБУ, ЗаполнятьНУ, ЗаполнятьСчетаУчетаНоменклатуры=Истина, ЗаполнятьСчетаУчетаЗатрат=Истина) Экспорт
	
	СчетаУчетаВДокументах.ЗаполнитьСчетаУчетаТабличнойЧасти(ИмяТабЧасти, ТабличнаяЧасть, ЭтотОбъект, ЗаполнятьБУ, ЗаполнятьНУ, ЗаполнятьСчетаУчетаЗатрат, ЗаполнятьСчетаУчетаНоменклатуры);
	
КонецПроцедуры // ЗаполнитьСчетаУчетаВТабЧасти()

// Процедура выполняет заполнение табличной части "Товары" по документу основанию.
// При заполнении копируется состав документа.
//
// Параметры:
//  ДокументОснование - ДокументСсылка - ссылка на документ основание
//
Процедура ЗаполнитьТоварыПоДокументуПоступлениеТоваровУслуг(ДокументОснование)

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Номенклатура.СтатьяЗатрат КАК СтатьяЗатрат,
	|	Товары.Номенклатура.НоменклатурнаяГруппаЗатрат КАК НоменклатурнаяГруппа,
	|	Товары.ЕдиницаИзмерения,
	|	Товары.ЕдиницаИзмеренияМест,
	|	Товары.Коэффициент,
	|	Товары.Количество,
	|	Товары.КоличествоМест,
	|	Товары.ХарактеристикаНоменклатуры,
	|	Товары.СерияНоменклатуры,
	|	Товары.Склад,
	|	Товары.Заказ КАК ЗаказРезерв,
	|	ВЫБОР КОГДА (Товары.Заказ ССЫЛКА Документ.ВнутреннийЗаказ) ТОГДА
	|   	Неопределено
	|	ИНАЧЕ
	|		Товары.Заказ
	|	КОНЕЦ КАК Заказ,
	|	Товары.ОтражениеВУСН,
	|	Товары.СчетУчетаБУ КАК Счет,
	|	Товары.СчетУчетаНУ КАК СчетНУ
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК Товары
	|
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование 
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки
	|";

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезЗапроса = Запрос.Выполнить();

	Выборка = РезЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
	
		НоваяСтрока = Материалы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);

		НоваяСтрока.Качество = Справочники.Качество.Новый;
		
		ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл(НоваяСтрока, "Материалы", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете, Ложь, Истина);

	КонецЦикла;

КонецПроцедуры // ЗаполнитьТоварыПоДокументуПоступлениеТоваровУслуг()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Дополняет полями, нужными для регл. учета.
//
Процедура ДополнитьСтруктуруПолейТабличнойЧастиТоварыРегл(СтруктураПолей)

	СтруктураПолей.Вставить("Счет", 		"Счет");
	СтруктураПолей.Вставить("СчетНУ", 		"СчетНУ");
	
	СтруктураПолей.Вставить("СчетУчетаБУ", 	"Счет");
	СтруктураПолей.Вставить("СчетУчетаНУ",  "СчетНУ");
	СтруктураПолей.Вставить("СчетЗатрат", 	"СчетЗатрат");
	СтруктураПолей.Вставить("СчетЗатратБУ", "СчетЗатрат");
	СтруктураПолей.Вставить("СчетЗатратНУ", "СчетЗатратНУ");
	
	СтруктураПолей.Вставить("КорСчетБУ", 	"СчетЗатрат");
	СтруктураПолей.Вставить("КорСчетНУ", 	"СчетЗатратНУ");
	
	СтруктураПолей.Вставить("Субконто1", 	"Субконто1");
	СтруктураПолей.Вставить("Субконто2", 	"Субконто2");
	СтруктураПолей.Вставить("Субконто3", 	"Субконто3");
	СтруктураПолей.Вставить("СубконтоНУ1", 	"СубконтоНУ1");
	СтруктураПолей.Вставить("СубконтоНУ2", 	"СубконтоНУ2");
	СтруктураПолей.Вставить("СубконтоНУ3", 	"СубконтоНУ3");
	
	СтруктураПолей.Вставить("КорСубконтоБУ1", "Субконто1");
	СтруктураПолей.Вставить("КорСубконтоБУ2", "Субконто2");
	СтруктураПолей.Вставить("КорСубконтоБУ3", "Субконто3");
	СтруктураПолей.Вставить("КорСубконтоНУ1", "СубконтоНУ1");
	СтруктураПолей.Вставить("КорСубконтоНУ2", "СубконтоНУ2");
	СтруктураПолей.Вставить("КорСубконтоНУ3", "СубконтоНУ3");
	
КонецПроцедуры // ДополнитьСтруктуруПолейТабличнойЧастиТоварыРегл()

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
// Параметры: 
//  РезультатЗапросаПоТоварам - результат запроса по табличной части "Товары",
//  СтруктураШапкиДокумента   - выборка по результату запроса по шапке документа.
//
// Возвращаемое значение:
//  Сформированная таблица значений.
//
Функция ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента)

	ТаблицаТоваров = РезультатЗапросаПоТоварам.Выгрузить();

	ТаблицаТоваров.Колонки.Добавить("СуммаПродажная", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	ТаблицаТоваров.Колонки.Добавить("ПринадлежностьНоменклатуры",Новый ОписаниеТипов("ПеречислениеСсылка.ПринадлежностьНоменклатуры"));


	ЕстьРозничныйСклад = УправлениеРозничнойТорговлей.ОпределитьНаличиеРозничногоСклада(ТаблицаТоваров, "Склад", "ВидСклада");
	Если ЕстьРозничныйСклад Тогда
		ТаблицаПоЦенам = УправлениеРозничнойТорговлей.СформироватьЗапросПоПродажнымЦенам(Дата, ТаблицаТоваров.ВыгрузитьКолонку("Склад"),
		                 ТаблицаТоваров.ВыгрузитьКолонку("Номенклатура")).Выгрузить();

		УправлениеРозничнойТорговлей.ЗаполнитьКолонкуСуммаПродажная(ТаблицаТоваров, ТаблицаПоЦенам, "ВидСклада");
	КонецЕсли;
	
	Для каждого СтрокаТаблицы из ТаблицаТоваров Цикл
		Если СтрокаТаблицы.ОбособленныйУчетТоваровПоЗаказамПокупателей = NULL Тогда
			СтрокаТаблицы.ОбособленныйУчетТоваровПоЗаказамПокупателей = Ложь;
		КонецЕсли;
		
		СтрокаТаблицы.ПринадлежностьНоменклатуры = ?(СтрокаТаблицы.СтатусМатериальныхЗатрат = Перечисления.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку,
													Перечисления.ПринадлежностьНоменклатуры.Принятый,
													Перечисления.ПринадлежностьНоменклатуры.ПустаяСсылка());
	КонецЦикла;

	Возврат ТаблицаТоваров;

КонецФункции // ПодготовитьТаблицуТоваров()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ФОРМИРОВАНИЯ ДВИЖЕНИЙ ДОКУМЕНТА

// По результату запроса по шапке документа формируем движения по регистрам.
//
// Параметры: 
//  РежимПроведения           - режим проведения документа (оперативный или неоперативный),
//  СтруктураШапкиДокумента   - выборка из результата запроса по шапке документа,
//  ТаблицаПоТоварам          - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  ТаблицаПоТаре             - таблица значений, содержащая данные для проведения и проверки ТЧ "Возвратная тара",
//  Отказ                     - флаг отказа в проведении,
//  Заголовок                 - строка, заголовок сообщения об ошибке проведения.
//
Процедура ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок)
	
	ДвиженияПоРегистрамУпр(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, мУчетнаяПолитика, Отказ, Заголовок);
	
	//АБС ВСТАВКА  11.12.2013 Навценя2
	РегистрыНакопления.абс_ЗаявкиНаОбеспечение.ВыполнитьСписаниеПоЗаявке(Движения, СтруктураШапкиДокумента, ТаблицаПоТоварам);
	Если абс_ПроведениеПоОстаткам() Тогда 
		Возврат;
	КонецЕсли;
	//АБС ВСТАВКА  КОНЕЦ	
	
	//АБС ВСТАВКА №35863, 37395 НАЧАЛО 	
	Если ttk_ОбщегоНазначения.абс_НеВыполнятьДвиженияВСтатусе(СтруктураШапкиДокумента) или не ОтражатьВБухгалтерскомУчете или Отказ Тогда
		Возврат;
	КонецЕсли;	
	//\\АБС ВСТАВКА №35863, 37395 КОНЕЦ	
	
	ДвиженияПоРегистрамРегл(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, мУчетнаяПолитика, Отказ, Заголовок); 
	ДвиженияПоРегиструТоварыОрганизаций(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);  
	ДвиженияПоРегиструСписанныеТовары(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);    	
	Движения.СписанныеТовары.Записать(Истина);
	
	Если ТаблицаПоТоварам.Количество()>0 Тогда
	
		УправлениеЗапасами.ЗарегистрироватьДокументВПоследовательностяхПартионногоУчета(
			ЭтотОбъект,
			Дата, 
			СтруктураШапкиДокумента.Организация,
			ОтражатьВУправленческомУчете,
			СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете,
			СтруктураШапкиДокумента.ОтражатьВНалоговомУчете,
			СтруктураШапкиДокумента.СпособВеденияПартионногоУчетаПоОрганизации);
	
	КонецЕсли;

	// Проводки формируются и в модуле документа, и при списании партий
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		Движения.Хозрасчетный.Записать();
	КонецЕсли;
	Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда
		Движения.Налоговый.Записать();
	КонецЕсли;
	
	УправлениеЗапасамиПартионныйУчет.ДвижениеПартийТоваров(Ссылка, Движения.СписанныеТовары.Выгрузить());
	
	
	ДвиженияПоРегистрамУСНРегл(РежимПроведения, СтруктураШапкиДокумента, Отказ, Заголовок);

КонецПроцедуры // ДвиженияПоРегистрам()

// Формирование движений по регистрам по управленческому учету.
//
Процедура ДвиженияПоРегистрамУпр(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, УчетнаяПолитика, Отказ, Заголовок)

	Если Не СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда
		Возврат;
	КонецЕсли;

	Если РежимПроведения = РежимПроведенияДокумента.Оперативный Тогда
		МассивСерий = Новый Массив;
		Для Каждого СтрокаТЧ Из Материалы Цикл
			Если ЗначениеЗаполнено(СтрокаТЧ.СтатьяЗатрат)
			      И СтрокаТЧ.СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы Тогда
				МассивСерий.Добавить(СтрокаТЧ.СерияНоменклатуры);
			КонецЕсли;
		КонецЦикла;
		УправлениеСертификациейНоменклатуры.ПроверитьНаСертификацию( МассивСерий, Дата, Отказ, Заголовок);
	КонецЕсли;

	// ТОВАРЫ ПО РЕГИСТРУ ТоварыНаСкладах.
	//Бобылев А.А. 21.05.2018 СППР00-00000088
	Если СтруктураШапкиДокумента.отражатьВБухгалтерскомУчете Тогда
		ТаблицаПоТоварам.Колонки.Добавить("СчетУчета");
		Для каждого Элемент Из ТаблицаПоТоварам Цикл
			Элемент.счетУчета = Элемент.СчетУчетаБУ;
		КонецЦикла;
	КонецЕсли;
	//Бобылев А.А.
	РезультатЗапроса = ОбщегоНазначения.ОтобратьСтрокиПоКритериям(ТаблицаПоТоварам,
	                   Новый Структура("ВидСклада", Перечисления.ВидыСкладов.Оптовый));

	Если Не РезультатЗапроса.Пустой() Тогда
	
		НаборДвижений = Движения.ТоварыНаСкладах;
		
		// Контроль остатков товара
		Если НЕ ИспользоватьРегистрСвободныеОстатки И Материалы.Количество() <> 0 Тогда
			ПроцедурыКонтроляОстатков.ТоварыНаСкладахКонтрольОстатков("Материалы", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
		КонецЕсли;

		Если Не Отказ Тогда
		
			СтруктТаблицДокумента = Новый Структура;
			СтруктТаблицДокумента.Вставить("Материалы", РезультатЗапроса.Выгрузить());
				
			ТаблицыДанныхДокумента = ttk_ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
				
			ttk_ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Расход, ТаблицыДанныхДокумента, Дата);
		
		КонецЕсли;
		
	КонецЕсли;

	РезультатЗапроса = ОбщегоНазначения.ОтобратьСтрокиПоКритериям(ТаблицаПоТоварам,
	                   Новый Структура("ВидСклада", Перечисления.ВидыСкладов.Розничный));

	Если Не РезультатЗапроса.Пустой() Тогда
		НаборДвижений = Движения.ТоварыВРознице;

		// Контроль остатков товара
		Если НЕ ИспользоватьРегистрСвободныеОстатки И Материалы.Количество() <> 0 Тогда
			ПроцедурыКонтроляОстатков.ТоварыВРозницеКонтрольОстатков("Материалы", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
		КонецЕсли;

		Если Не Отказ Тогда
		
			СтруктТаблицДокумента = Новый Структура;
			СтруктТаблицДокумента.Вставить("Материалы", РезультатЗапроса.Выгрузить());
				
			ТаблицыДанныхДокумента = ttk_ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
				
			ttk_ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Расход, ТаблицыДанныхДокумента, Дата);
			
		КонецЕсли;
		
	КонецЕсли;

	// Если есть списание из резерва, то надо списать резерв
	ТаблицаПоТоварамИзРезерва = ТаблицаПоТоварам.Скопировать();
	Сч = 0;
	Пока Сч < ТаблицаПоТоварамИзРезерва.Количество() Цикл
		СтрокаТаблицы = ТаблицаПоТоварамИзРезерва.Получить(Сч);
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ЗаказРезерв) Тогда
			 ТаблицаПоТоварамИзРезерва.Удалить(СтрокаТаблицы);
		Иначе 
			Сч = Сч + 1;
			СтрокаТаблицы.ДокументРезерва = СтрокаТаблицы.ЗаказРезерв;
		КонецЕсли; 
	КонецЦикла;
		
	Если ТаблицаПоТоварамИзРезерва.Количество() > 0 Тогда
	
		НаборДвижений = Движения.ТоварыВРезервеНаСкладах;

		// Контроль остатков товара
		Если Материалы.Количество() <> 0 Тогда
			ПроцедурыКонтроляОстатков.ТоварыВРезервеНаСкладахКонтрольОстатков("Материалы", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
		КонецЕсли;
		
		Если Не Отказ Тогда
		
			СтруктТаблицДокумента = Новый Структура;
			СтруктТаблицДокумента.Вставить("Материалы", ТаблицаПоТоварамИзРезерва);
				
			ТаблицыДанныхДокумента = ttk_ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
				
			ttk_ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Расход, ТаблицыДанныхДокумента, Дата);
			
		КонецЕсли;
		
	КонецЕсли;
	
	УчетЗатратПоЗаказамНаПроизводство = мУчетнаяПолитика.УчетЗатратПоЗаказамНаПроизводство;
	
	//АБС ВСТАВКА   11.12.2013 Навценя2
	Если абс_ПроведениеПоОстаткам() Тогда 
		Возврат;
	КонецЕсли;
	//АБС ВСТАВКА  КОНЕЦ	
		
	// ТОВАРЫ ПО РЕГИСТРУ МатериалыВПроизводстве.
	НаборДвижений = Движения.МатериалыВПроизводстве;
	
	ТаблицаПоТоварамОперативныйУчет = ТаблицаПоТоварам.Скопировать();
	
	ИспользоватьЗаказыНаПроизводство = УправлениеЗаказами.ИспользоватьЗаказыНаПроизводство();
	
	КолвоЭлементов = ТаблицаПоТоварамОперативныйУчет.Количество();
	Для ОбратныйИндекс = 1 По КолвоЭлементов Цикл
   		СтрокаТаблицы = ТаблицаПоТоварамОперативныйУчет[КолвоЭлементов - ОбратныйИндекс];
  
		Если Не СтрокаТаблицы.ВестиОперативныйУчетОстатковНЗП 
		 ИЛИ СтрокаТаблицы.ХарактерЗатрат <> Перечисления.ХарактерЗатрат.ПроизводственныеРасходы Тогда
      		ТаблицаПоТоварамОперативныйУчет.Удалить(СтрокаТаблицы);
		Иначе
			Если Не СтрокаТаблицы.ВестиУчетПоСериямВНЗП Тогда
				СтрокаТаблицы.СерияЗатраты = Справочники.СерииНоменклатуры.ПустаяСсылка();
			КонецЕсли;
			СтрокаТаблицы.Заказ = УправлениеПроизводством.ПолучитьЗаказДляУчетаЗатрат(СтрокаТаблицы.Заказ, , УчетЗатратПоЗаказамНаПроизводство, ИспользоватьЗаказыНаПроизводство);
		КонецЕсли;

	КонецЦикла;
	
	Если ТаблицаПоТоварамОперативныйУчет.Количество() > 0 И НЕ Отказ Тогда

		СтруктТаблицДокумента = Новый Структура;
		СтруктТаблицДокумента.Вставить("Материалы", ТаблицаПоТоварамОперативныйУчет);
				
		ТаблицыДанныхДокумента = ttk_ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
			
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Подразделение", Подразделение);
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "КодОперации",   Перечисления.КодыОперацийМатериалыВПроизводстве.СписаниеПартийВПроизводствоОперативно);
				
		ttk_ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Приход, ТаблицыДанныхДокумента, Дата);
			
	КонецЕсли;
	
	Если Не глЗначениеПеременной("ПараметрыПартионногоУчета").СписыватьПартииПриПроведенииДокументов
	   И Не СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику
	   И НЕ Отказ
	Тогда
		
		ТаблицаПоТоварамНЗП = ТаблицаПоТоварам.Скопировать();
		
		ИспользоватьЗаказыНаПроизводство = УправлениеЗаказами.ИспользоватьЗаказыНаПроизводство();
	
		КолвоЭлементов = ТаблицаПоТоварамНЗП.Количество();
		Для ОбратныйИндекс = 1 По КолвоЭлементов Цикл
	   		СтрокаТаблицы = ТаблицаПоТоварамНЗП[КолвоЭлементов - ОбратныйИндекс];
	  
	   		Если СтрокаТаблицы.ХарактерЗатрат <> Перечисления.ХарактерЗатрат.ПроизводственныеРасходы Тогда
	      		ТаблицаПоТоварамНЗП.Удалить(СтрокаТаблицы);
			Иначе
				Если Не СтрокаТаблицы.ВестиУчетПоСериямВНЗП Тогда
					СтрокаТаблицы.СерияЗатраты = Справочники.СерииНоменклатуры.ПустаяСсылка();
				КонецЕсли;
				СтрокаТаблицы.Заказ = УправлениеПроизводством.ПолучитьЗаказДляУчетаЗатрат(СтрокаТаблицы.Заказ, , УчетЗатратПоЗаказамНаПроизводство, ИспользоватьЗаказыНаПроизводство);
			КонецЕсли;

		КонецЦикла;
		
		// ПРИХОД ТОВАРОВ ПО РЕГИСТРУ НезавершенноеПроизводство.
		НаборДвижений = Движения.НезавершенноеПроизводство;
		
		СтруктТаблицДокумента = Новый Структура;
		СтруктТаблицДокумента.Вставить("Материалы", ТаблицаПоТоварамНЗП);
				
		ТаблицыДанныхДокумента = ttk_ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
			
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Подразделение", Подразделение);
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "КодОперации",   Перечисления.КодыОперацийНезавершенноеПроизводство.СписаниеКоличестваВПроизводствоОперативно);
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "СписаниеПартий",Истина);
				
		ttk_ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Приход, ТаблицыДанныхДокумента, Дата);

	КонецЕсли;
	
	// Движения по регистру ЛимитноЗаборныеКарты.
	Если УправлениеПроизводством.ИспользоватьЛимитыОтпускаМатериалов() Тогда
		
		РезультатЗапросаЛимиты = УправлениеПроизводством.СформироватьЗапросЛимитыОтпускаМатериаловПоТабличнойЧасти(ЭтотОбъект);
			
		Если НЕ РезультатЗапросаЛимиты.Пустой() Тогда
			
			НаборДвижений = Движения.ЛимитноЗаборныеКарты;
			ТаблицаДвижений = НаборДвижений.Выгрузить();

			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(
				РезультатЗапросаЛимиты.Выгрузить(), 
				ТаблицаДвижений);

			Если Не СтруктураШапкиДокумента.РазрешитьПревышениеЛимита Тогда
				ТаблицаДвижений.ЗаполнитьЗначения(0, "ОтпущеноСверхЛимита");
			КонецЕсли;
		
			НаборДвижений.мПериод            = Дата;
			НаборДвижений.мТаблицаДвижений   = ТаблицаДвижений;

			// Проверка лимитов при проведении.
			НаборДвижений.КонтрольЛимитов(ЭтотОбъект, "Материалы", СтруктураШапкиДокумента, Отказ, Заголовок);
						
			Если Не Отказ Тогда
				Движения.ЛимитноЗаборныеКарты.ДобавитьДвижение();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Движение по внутренним заказам
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ИмяТабЧасти",       "Материалы");
	ДопПараметры.Вставить("СтатусПартии",      Перечисления.СтатусыПартийТоваров.Купленный);
	ДопПараметры.Вставить("РежимПроведения",   РежимПроведения);
	ДопПараметры.Вставить("ИмяРеквизитаЗаказ", "ВнутреннийЗаказ");
	ДопПараметры.Вставить("ЗаказВШапке",       Ложь);
	
	ТабИсходная = ТаблицаПоТоварам.Скопировать();
		
	ПодготовитьТаблицуДляДвиженийПоВнутреннимЗаказам(ТабИсходная, Перечисления.СтатусыПартийТоваров.Купленный);
	Если ТабИсходная.Количество() > 0 Тогда
		УправлениеЗаказами.ДвижениеПоВнутреннимЗаказам( ЭтотОбъект, ТабИсходная, ДопПараметры, Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ДвиженияПоРегистрамУпр()

Процедура  ПодготовитьТаблицуДляДвиженийПоВнутреннимЗаказам(ТабИсходная, знач СтатусПартии)
	Сч = 0;

	Пока Сч < ТабИсходная.Количество() Цикл
		СтрокаТаблицы = ТабИсходная.Получить(Сч);
		Если ЗначениеЗаполнено(СтрокаТаблицы.ВнутреннийЗаказ)  // Считается исполнением внутреннего заказа.
			  И СтрокаТаблицы.ВнутреннийЗаказ.Заказчик = Подразделение Тогда
			// Проверим остаток по регистру "Внутренние заказы", если в остатках не хватает количества, 
			// то, вероятно, заказаны комплектующие для комплектов, по ним движений не делаем
			КоличествоОстаток = УправлениеЗаказами.ПолучитьОстатокПоВнутреннемуЗаказу(СтрокаТаблицы.ВнутреннийЗаказ, 
																   СтрокаТаблицы.Количество, 
																   СтрокаТаблицы.Номенклатура, 
																   СтрокаТаблицы.ХарактеристикаНоменклатуры,
																   СтрокаТаблицы.ЕдиницаИзмерения,
																   СтатусПартии);
			Если КоличествоОстаток > 0 Тогда
				СтрокаТаблицы.Количество = Мин(СтрокаТаблицы.Количество, КоличествоОстаток);
				Сч = Сч + 1;
			Иначе
				ТабИсходная.Удалить(СтрокаТаблицы);
			КонецЕсли;
		Иначе
			ТабИсходная.Удалить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры



// Формирование движений по регистрам по регламентированному учету.
//
Процедура ДвиженияПоРегистрамРегл(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, УчетнаяПолитика, Отказ, Заголовок)
	
	Если Не СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		Возврат;
	КонецЕсли;
		
	Если Не глЗначениеПеременной("ПараметрыПартионногоУчета").СписыватьПартииПриПроведенииДокументовБух
	   И Не Отказ 
	   И Не СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику
	Тогда
		
		УчетЗатратПоЗаказамНаПроизводство = мУчетнаяПолитикаБух.УчетЗатратПоЗаказамНаПроизводство;
		
		ТаблицаПоТоварамНЗП = ТаблицаПоТоварам.Скопировать();
		
		ИспользоватьЗаказыНаПроизводство = УправлениеЗаказами.ИспользоватьЗаказыНаПроизводство();
		
		КолвоЭлементов = ТаблицаПоТоварамНЗП.Количество();
		Для ОбратныйИндекс = 1 По КолвоЭлементов Цикл
	   		СтрокаТаблицы = ТаблицаПоТоварамНЗП[КолвоЭлементов - ОбратныйИндекс];
	  
	   		Если СтрокаТаблицы.ХарактерЗатрат <> Перечисления.ХарактерЗатрат.ПроизводственныеРасходы Тогда
	      		ТаблицаПоТоварамНЗП.Удалить(СтрокаТаблицы);
			Иначе
				Если Не СтрокаТаблицы.ВестиУчетПоСериямВНЗП Тогда
					СтрокаТаблицы.СерияЗатраты = Справочники.СерииНоменклатуры.ПустаяСсылка();
				КонецЕсли;
				СтрокаТаблицы.Заказ = УправлениеПроизводством.ПолучитьЗаказДляУчетаЗатрат(СтрокаТаблицы.Заказ, , УчетЗатратПоЗаказамНаПроизводство, ИспользоватьЗаказыНаПроизводство);
			КонецЕсли;

		КонецЦикла;
		
		// ПРИХОД ТОВАРОВ ПО РЕГИСТРУ НезавершенноеПроизводство.
		НаборДвижений = Движения.НезавершенноеПроизводствоБухгалтерскийУчет;
		
		ТабИмен = Неопределено;
		ttk_ОбщегоНазначения.ПереименоватьКолонкуТаблицыЗначений(ТаблицаПоТоварамНЗП, ТабИмен, "СчетЗатратБУ", "СчетУчета");
						
		СтруктТаблицДокумента = Новый Структура;
		СтруктТаблицДокумента.Вставить("Материалы", ТаблицаПоТоварамНЗП);
				
		ТаблицыДанныхДокумента = ttk_ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
				
		ОбщегоНазначения.ВосстановитьИменаКолонокТаблицыЗначений(ТаблицаПоТоварамНЗП, ТабИмен);
					
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Организация",	 Организация);
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Подразделение", ПодразделениеОрганизации);
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "КодОперации",   Перечисления.КодыОперацийНезавершенноеПроизводство.СписаниеКоличестваВПроизводствоОперативно);
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "СписаниеПартий",Истина);
						
		ttk_ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Приход, ТаблицыДанныхДокумента, Дата);
		
		Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете
			И Не глЗначениеПеременной("ПараметрыПартионногоУчета").СписыватьПартииПриПроведенииДокументовНал	
		Тогда
			
			// ПРИХОД ТОВАРОВ ПО РЕГИСТРУ НезавершенноеПроизводство.
			НаборДвижений = Движения.НезавершенноеПроизводствоНалоговыйУчет;
			
			ТабИмен = Неопределено;
			ttk_ОбщегоНазначения.ПереименоватьКолонкуТаблицыЗначений(ТаблицаПоТоварамНЗП, ТабИмен, "СчетЗатратНУ", "СчетУчета");
								
			СтруктТаблицДокумента = Новый Структура;
			СтруктТаблицДокумента.Вставить("Материалы", ТаблицаПоТоварамНЗП);
						
			ТаблицыДанныхДокумента = ttk_ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
						
			ОбщегоНазначения.ВосстановитьИменаКолонокТаблицыЗначений(ТаблицаПоТоварамНЗП, ТабИмен);
							
			ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Организация",	 Организация);
			ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Подразделение", ПодразделениеОрганизации);
			ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "КодОперации",   Перечисления.КодыОперацийНезавершенноеПроизводство.СписаниеКоличестваВПроизводствоОперативно);
			ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "СписаниеПартий",Истина);
								
			ttk_ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Приход, ТаблицыДанныхДокумента, Дата);
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ДвиженияПоРегистрамРегл()

Процедура ДвиженияПоРегистрамУСНРегл(РежимПроведения, СтруктураШапкиДокумента,Отказ, Заголовок)
	
	Если НЕ СтруктураШапкиДокумента.ОтражатьВНалоговомУчетеУСН Тогда
		Возврат;
	КонецЕсли;
	
	НалоговыйУчетУСН.ДвиженияУСН(Ссылка, РежимПроведения);
		
КонецПроцедуры

// Формирование движений по регистру "Товары организаций".
//
Процедура ДвиженияПоРегиструТоварыОрганизаций(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок)

	Если НЕ СтруктураШапкиДокумента.ОтражатьВРегламентированномУчете Тогда
		Возврат;
	КонецЕсли;
	
	// ТОВАРЫ ПО РЕГИСТРУ ТоварыОрганизаций.
	НаборДвижений = Движения.ТоварыОрганизаций;
	
	// Проверка остатков при оперативном проведении.
	НаборДвижений.КонтрольОстатков(ЭтотОбъект, "Материалы", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);

	Если Не Отказ Тогда
	
		СтруктТаблицДокумента = Новый Структура;
		СтруктТаблицДокумента.Вставить("Материалы", ТаблицаПоТоварам);
						
		ТаблицыДанныхДокумента = ttk_ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
							
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Организация", Организация);
		ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Комиссионер", Неопределено);
		
		Если НЕ СтруктураШапкиДокумента.ВестиУчетТоваровОрганизацийВРазрезеСкладов Тогда
			ttk_ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Склад", Неопределено);
		КонецЕсли;
								
		ttk_ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Расход, ТаблицыДанныхДокумента, Дата);
			
	КонецЕсли;
	
КонецПроцедуры // ДвиженияПоРегиструТоварыОрганизаций()

Процедура ЗаполнитьКолонкиРегистраСписанныеТоварыПоТоварамУпр(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам)
	
	УчетЗатратПоЗаказамНаПроизводство = мУчетнаяПолитика.УчетЗатратПоЗаказамНаПроизводство;

	ТаблицаДвижений.ЗаполнитьЗначения(Подразделение,"Подразделение");
	ТаблицаДвижений.ЗаполнитьЗначения(ОтражатьВУправленческомУчете,"ОтражатьВУправленческомУчете");
	
	ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СтатусыПартийТоваров.Продукция,"ДопустимыйСтатус1");
	ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СтатусыПартийТоваров.ВозвратнаяТара,"ДопустимыйСтатус3");
	
	Для Каждого Строка Из ТаблицаДвижений Цикл
		
		СтрокаТЧ = ТаблицаПоТоварам.Получить(ТаблицаДвижений.Индекс(Строка));
		
		Если СтрокаТЧ.СтатусМатериальныхЗатрат = Перечисления.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку Тогда
			Строка.ДопустимыйСтатус2 = Перечисления.СтатусыПартийТоваров.ВПереработку;
		Иначе
			Строка.ДопустимыйСтатус2 = Перечисления.СтатусыПартийТоваров.Купленный;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры // ЗаполнитьКолонкиРегистраСписанныеТоварыПоТоварамУпр()

Процедура ЗаполнитьКолонкиРегистраСписанныеТоварыПоТоварамРегл(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам)
	
	УчетЗатратПоЗаказамНаПроизводство = мУчетнаяПолитикаБух.УчетЗатратПоЗаказамНаПроизводство;

	ТаблицаДвижений.ЗаполнитьЗначения(ПодразделениеОрганизации,		"ПодразделениеОрганизации");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете, 	"ОтражатьВБухгалтерскомУчете");
	ТаблицаДвижений.ЗаполнитьЗначения(Организация,                	"Организация");
	
	Для Каждого Строка Из ТаблицаДвижений Цикл
		
		СтрокаТабличнойЧасти = ТаблицаПоТоварам.Получить(ТаблицаДвижений.Индекс(Строка));
		ХарактерЗатратБУ = УправлениеЗатратами.ПолучитьХарактерЗатратПоСчетуЗатрат(СтрокаТабличнойЧасти.СчетЗатратБУ, СтрокаТабличнойЧасти.СтатьяЗатрат);
		ХарактерЗатратНУ = УправлениеЗатратами.ПолучитьХарактерЗатратПоСчетуЗатрат(СтрокаТабличнойЧасти.СчетЗатратНУ, СтрокаТабличнойЧасти.СтатьяЗатрат, "Налоговый");
				
		Если ХарактерЗатратБУ = Перечисления.ХарактерЗатрат.ВложенияВоВнеоборотныеАктивы Тогда
			Строка.КорСубконтоБУ1 = Строка.ОбъектСтроительства;
			Строка.КорСубконтоБУ2 = Строка.СтатьяЗатрат;
			Строка.КорСубконтоБУ3 = СтрокаТабличнойЧасти.СпособСтроительства;
		//АБС Закомментировано - для 25 счета КорСубконто3 надо заполнять	
		//ИначеЕсли ХарактерЗатратБУ <> Перечисления.ХарактерЗатрат.Прочие Тогда
		//	Строка.КорСубконтоБУ1 = Неопределено;
		//	Строка.КорСубконтоБУ2 = Неопределено;
		//	Строка.КорСубконтоБУ3 = Неопределено;
		КонецЕсли;
		//АБС Закомментировано - для 25 счета КорСубконто3 надо заполнять
		//Если ХарактерЗатратНУ <> Перечисления.ХарактерЗатрат.Прочие Тогда
		//	Строка.КорСубконтоНУ1 = Неопределено;
		//	Строка.КорСубконтоНУ2 = Неопределено;
		//	Строка.КорСубконтоНУ3 = Неопределено;
		//КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьКолонкиРегистраСписанныеТоварыПоТоварамРегл()

// Формирование движений по регистру "Списанные товары".
//
Процедура ДвиженияПоРегиструСписанныеТовары(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);
	
	// ТОВАРЫ ПО РЕГИСТРУ СписанныеТовары.
	НаборДвижений = Движения.СписанныеТовары;

	// Получим таблицу значений, совпадающую со структурой набора записей регистра.
	ТаблицаДвижений = НаборДвижений.Выгрузить();
	ТаблицаДвижений.Очистить();

	// Заполним таблицу движений.
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(УправлениеЗапасами.ПолучитьТаблицуСобственныхТоваров(СтруктураШапкиДокумента, ТаблицаПоТоварам), ТаблицаДвижений);
	
	// Недостающие поля.
	ТаблицаДвижений.ЗаполнитьЗначения( СтруктураШапкиДокумента.ОтражатьВНалоговомУчете, "ОтражатьВНалоговомУчете");
	
	Инд = 0;
	Для каждого Строка Из ТаблицаДвижений Цикл
		Инд = Инд+1;
		Строка.НомерСтрокиДокумента = Инд;
		
		СтатьяЗатрат = Строка.СтатьяЗатрат;
				
		Если СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы Тогда
			Строка.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.СписаниеПартийВПроизводствоОперативно;
		ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.БракВПроизводстве Тогда
			Строка.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.СписаниеНаБрак;
		ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ВложенияВоВнеоборотныеАктивы Тогда
			Строка.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.СписаниеНаВложенияВоВнеоборотныеАктивы;
		Иначе
			Строка.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.СписаниеНаЗатраты;
		КонецЕсли;
		Если Строка.СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы 
		   И Строка.СтатьяЗатрат.СтатусМатериальныхЗатрат = Перечисления.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку Тогда
      		Строка.ОтражатьВНалоговомУчете = Ложь;
		КонецЕсли;
		Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчетеУСН И Строка.ОтражатьВНалоговомУчете Тогда
			Строка.ОтражатьВНалоговомУчете = Истина;
			Строка.СчетУчетаНУ = Строка.СчетУчетаБУ;
		КонецЕсли;
		
		Если СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ВложенияВоВнеоборотныеАктивы Тогда
			Строка.КорСубконтоБУ1 = Строка.ОбъектСтроительства;
			Строка.КорСубконтоБУ2 = СтатьяЗатрат;
			Строка.КорСубконтоБУ3 = ТаблицаПоТоварам[инд-1].СпособСтроительства;
		КонецЕсли;
		
		Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчетеУСН Тогда
			Строка.ОтражатьВНалоговомУчете = Истина;
			Строка.СчетУчетаНУ = Строка.СчетУчетаБУ;
		КонецЕсли; 
		
	КонецЦикла;
	
	мИндексСтрокиСписанныеТовары = Инд;
	
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ВедениеУчетаПоПроектам,   "ВедениеУчетаПоПроектам");

	ТаблицаДвижений.ЗаполнитьЗначения(Дата,   "Период");
	ТаблицаДвижений.ЗаполнитьЗначения(Ссылка, "Регистратор");
	ТаблицаДвижений.ЗаполнитьЗначения(Истина, "Активность");

	ЗаполнитьКолонкиРегистраСписанныеТоварыПоТоварамУпр(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам);
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		ЗаполнитьКолонкиРегистраСписанныеТоварыПоТоварамРегл(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам);
	КонецЕсли;
	
	НаборДвижений.мПериод          = Дата;
	НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;

	Если Не Отказ Тогда
		Движения.СписанныеТовары.ВыполнитьДвижения();
	КонецЕсли;
	
КонецПроцедуры // ДвиженияПоРегиструСписанныеТовары()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаПроведения"
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Дерево значений, содержащее имена необходимых полей в запросе по шапке.
	Перем ДеревоПолейЗапросаПоШапке;
	
	Если мУдалятьДвижения Тогда
		ttk_ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ, Истина, РежимПроведения);
	КонецЕсли;

	Если абс_Статус = Перечисления.абс_СтатусыПервичныхДокументов.Отказ Тогда //абсо - заявка 9747
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета();
	
	Заголовок = ttk_ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ttk_ОбщегоНазначения.СформироватьСтруктуруШапкиДокументаИПроверитьОтражениеВУчете(ЭтотОбъект, Отказ, Заголовок);
	
	// Заполним по шапке документа дерево параметров, нужных при проведении.
	ДеревоПолейЗапросаПоШапке = УправлениеЗапасами.СформироватьДеревоПолейЗапросаПоШапке();
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Склад" 				, "ВидСклада" 						, "ВидСклада");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика"       , "ВестиПартионныйУчетПоСкладам"    , "ВестиПартионныйУчетПоСкладам");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Организация",          "ОтражатьВРегламентированномУчете"      , "ОтражатьВРегламентированномУчете");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика"     , "ВедениеУчетаПоПроектам"                     , "ВедениеУчетаПоПроектам");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика"     , "ВестиУчетТоваровОрганизацийВРазрезеСкладов"   , "ВестиУчетТоваровОрганизацийВРазрезеСкладов");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "НастройкаСпособовВеденияУправленческогоПартионногоУчета", "СпособВеденияПартионногоУчетаПоОрганизации", "СпособВеденияПартионногоУчетаПоОрганизации");

	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуруШапкиПодразделениемОрганизации(Заголовок, СтруктураШапкиДокумента, Отказ);
	
	ИспользоватьРасширеннуюАналитику = глЗначениеПеременной("ИспользоватьРасширеннуюАналитикуУчетаНоменклатурыИЗатрат") 
		И (глЗначениеПеременной("ДатаНачалаИспользованияРасширеннойАналитикиУчетаНоменклатурыИЗатрат") <= Дата);
	СтруктураШапкиДокумента.Вставить("ИспользоватьРасширеннуюАналитику", ИспользоватьРасширеннуюАналитику);

	мУчетнаяПолитика = ttk_ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиУпр(Дата);
    Если НЕ ЗначениеЗаполнено(мУчетнаяПолитика) Тогда
		Отказ = Истина;
	КонецЕсли; 
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		мУчетнаяПолитикаБух = ttk_ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Дата, Организация);
		Если НЕ ЗначениеЗаполнено(мУчетнаяПолитикаБух) Тогда
			Отказ = Истина;
		КонецЕсли; 
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Получим необходимые данные для проведения и проверки заполнения данные по табличной части "Материалы".
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("Номенклатура"              	, "Номенклатура");
	СтруктураПолей.Вставить("ЕдиницаИзмерения"              , "ЕдиницаИзмерения");
	СтруктураПолей.Вставить("Услуга"                    	, "Номенклатура.Услуга");
	СтруктураПолей.Вставить("Набор"                     	, "Номенклатура.Набор");
	СтруктураПолей.Вставить("ВестиОперативныйУчетОстатковНЗП", "Номенклатура.ВестиОперативныйУчетОстатковНЗП");
	СтруктураПолей.Вставить("Количество"                	, "Количество * Коэффициент /Номенклатура.ЕдиницаХраненияОстатков.Коэффициент");
	
	СтруктураПолей.Вставить("ХарактеристикаНоменклатуры"	, "ХарактеристикаНоменклатуры");
	СтруктураПолей.Вставить("СерияНоменклатуры"         	, "СерияНоменклатуры");
	СтруктураПолей.Вставить("ВестиУчетПоСериямВНЗП"         , "Номенклатура.ВестиУчетПоСериямВНЗП");
	СтруктураПолей.Вставить("Склад"                         , "Склад");
	СтруктураПолей.Вставить("ВидСклада"                     , "Склад.ВидСклада");
	
	СтруктураПолей.Вставить("Качество"         				, "Качество");
	СтруктураПолей.Вставить("ДокументРезерва"         		, "ЗаказРезерв");
	СтруктураПолей.Вставить("Заказ"         				, "Заказ");
	СтруктураПолей.Вставить("ВнутреннийЗаказ"         		, "ВнутреннийЗаказ");
	СтруктураПолей.Вставить("ЗаказСписания"         		, "Заказ");
	СтруктураПолей.Вставить("ЗаказПартии"         			, "ЗаказРезерв");
	СтруктураПолей.Вставить("ЗаказРезерв"         	        , "ЗаказРезерв");
	СтруктураПолей.Вставить("ДоговорКонтрагента"         	, "Заказ.ДоговорКонтрагента");
	
	СтруктураПолей.Вставить("Затрата"              			, "Номенклатура");
	СтруктураПолей.Вставить("ХарактеристикаЗатраты"			, "ХарактеристикаНоменклатуры");
	СтруктураПолей.Вставить("СерияЗатраты"         			, "СерияНоменклатуры");
	
	СтруктураПолей.Вставить("СтатьяЗатрат"         			, "СтатьяЗатрат");
	СтруктураПолей.Вставить("СтатьяЗатратНДС"         		, "СтатьяЗатратНДС");
	СтруктураПолей.Вставить("ХарактерЗатрат"      			, "СтатьяЗатрат.ХарактерЗатрат");
	СтруктураПолей.Вставить("ВидЗатрат"      				, "СтатьяЗатрат.ВидЗатрат");
	СтруктураПолей.Вставить("СтатусМатериальныхЗатрат"      , "СтатьяЗатрат.СтатусМатериальныхЗатрат");
	СтруктураПолей.Вставить("НоменклатурнаяГруппа"         	, "НоменклатурнаяГруппа");
	СтруктураПолей.Вставить("Продукция"         			, "Продукция");
	СтруктураПолей.Вставить("ХарактеристикаПродукции"       , "ХарактеристикаПродукции");
	СтруктураПолей.Вставить("СерияПродукции"         		, "СерияПродукции");
	СтруктураПолей.Вставить("ОбъектСтроительства"         	, "ОбъектСтроительства");
	СтруктураПолей.Вставить("СпособСтроительства"         	, "СпособСтроительства");
	СтруктураПолей.Вставить("Проект"         				, "Проект");
	
	СтруктураПолей.Вставить("ХарактеристикаНоменклатурыНовая", "ХарактеристикаПродукции");
	СтруктураПолей.Вставить("СерияНоменклатурыНовая"        , "СерияПродукции");
	СтруктураПолей.Вставить("ОбособленныйУчетТоваровПоЗаказамПокупателей" , "Заказ.ДоговорКонтрагента.ОбособленныйУчетТоваровПоЗаказамПокупателей");
	
	СтруктураПолей.Вставить("Комплект", 						"Номенклатура.Комплект");
	СтруктураПолей.Вставить("ВидРасходовНУ",    				"СтатьяЗатрат.ВидРасходовНУ");
	
	//АБС
	СтруктураПолей.Вставить("абс_ЦФУ"         				, "абс_ЦФУ");
	СтруктураПолей.Вставить("абс_ЦФО"         				, "абс_ЦФО");
	СтруктураПолей.Вставить("абс_БюджетнаяСтатья"         	, "абс_БюджетнаяСтатья");
	СтруктураПолей.Вставить("абс_ТипКонтрагента"         	, "абс_ТипКонтрагента");
	СтруктураПолей.Вставить("абс_ТипСети"         			, "абс_ТипСети");
	СтруктураПолей.Вставить("абс_ТЭО"         				, "абс_ТЭО");
	СтруктураПолей.Вставить("абс_КВ"         				, "абс_КВ");
	СтруктураПолей.Вставить("абс_ТипРасхода"         		, "абс_ТипРасхода");
    СтруктураПолей.Вставить("абс_ОсновныеСредства"         	, "абс_ОсновныеСредства");
	СтруктураПолей.Вставить("НомерСтрокиДокумента"         , "НомерСтроки");
	//\\АБС
	
	//Крамаренко Д.М.
	//НФС 2018
	СтруктураПолей.Вставить("СчетЗатрат"         , "СчетЗатрат");
	//Крамаренко Д.М.
	//НФС 2018

	СтруктураОбрабатываемыхКолонок = Новый Структура();
	ДополнитьСтруктуруПолейДаннымиНоменклатуры(СтруктураПолей, СтруктураОбрабатываемыхКолонок);
	
	ДополнитьСтруктуруПолейТабличнойЧастиТоварыРегл(СтруктураПолей);
	
	РезультатЗапросаПоТоварам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Материалы", СтруктураПолей);
	
	// Подготовим таблицу товаров для проведения.
	ТаблицаПоТоварам = ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента);
	
	//Заполнение незаполненных СтатьиЗатрат и НоменклатурнойГруппы по Номенклатуре в Таблице товаров
	ОбщегоНазначенияКлиентСервер.ЗаполнитьНоменклатурнуюГруппуИСтатьюЗатратВТаблицеДокумента(ТаблицаПоТоварам, СтруктураОбрабатываемыхКолонок);
	
	ПроверкаРеквизитовТЧ(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	//Заполнение и проверка заполнения счетов учета номенклатуры и затрат
	СчетаУчетаВДокументах.ЗаполнитьИПроверитьЗаполнениеСчетовУчетаТабличнойЧасти("Материалы",	ТаблицаПоТоварам, 	СтруктураШапкиДокумента, Отказ, Заголовок);
	
	ПроверитьКорректностьСчетовУчетаМатериалы(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	//Крамаренко Д.М.
	//НФС 2018
	//дублирование проверки счетов учета на всякий случай если выше прибили наши предопределенные счета
	ПроверкаРеквизитовТЧ(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);
	//Крамаренко Д.М.
	//НФС 2018
	
	// Движения по документу
	Если Не Отказ Тогда
		ИспользоватьРегистрСвободныеОстатки = глЗначениеПеременной("ИспользоватьРегистрСвободныеОстатки"); 

		ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);
		
		Если ИспользоватьРегистрСвободныеОстатки И 
			Материалы.Количество() <> 0 
			И НЕ Отказ Тогда
			Если глЗначениеПеременной("ИспользоватьБлокировкуДанных")  Тогда
				Движения.СвободныеОстатки.БлокироватьДляИзменения = Истина;
			КонецЕсли;
			Движения.Записать();
			РегистрыНакопления.СвободныеОстатки.КонтрольОстатков(
				"Материалы", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
		КонецЕсли;

	КонецЕсли;
	
	//Сделаем переменные доступными из подписок на события
	ДополнительныеСвойства.Вставить("СтруктураШапкиДокумента", СтруктураШапкиДокумента);
	ДополнительныеСвойства.Вставить("СтруктураТабличныхЧастей", Новый Структура("ТаблицаПоТоварам", ТаблицаПоТоварам));

	//АБС ВСТАВКА №2080 НАЧАЛО
	абс_СписанныеМатериалыНаОСДвижения(СтруктураШапкиДокумента,ТаблицаПоТоварам,Отказ);
	//\\АБС ВСТАВКА №2080 КОНЕЦ	
	
	//АБС ВСТАВКА №18514 НАЧАЛО
	КорректируемПроводкиПоДаннымПрошлогоПериода(Отказ);
	//\\АБС ВСТАВКА №18514 КОНЕЦ		
	
КонецПроцедуры	// ОбработкаПроведения()

//АБС ВСТАВКА №18514 НАЧАЛО
Процедура КорректируемПроводкиПоДаннымПрошлогоПериода(Отказ)
	
	Если НЕ абс_ПрошлыйНалоговыйПериод или Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Счет_91_02_7    = ПланыСчетов.Налоговый.НайтиПоКоду("91.02.7");
	Счет_91_02_1_БУ = ПланыСчетов.Хозрасчетный.НайтиПоКоду("91.02.1");
	Счет_ПВ         = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");   	
	
	//АБС ВСТАВКА №47258,45838 НАЧАЛО «29 сентября 2014 г.», Пополитов
	жСписокСчетов = Новый СписокЗначений;
	жСписокСчетов.Добавить(ПланыСчетов.Налоговый.НайтиПоКоду("20.01.1"));
	жСписокСчетов.Добавить(ПланыСчетов.Налоговый.НайтиПоКоду("25.01"));
	жСписокСчетов.Добавить(ПланыСчетов.Налоговый.НайтиПоКоду("44.02.1"));
	//\\АБС ВСТАВКА №47258,45838 КОНЕЦ
	
	Движения.Хозрасчетный.Прочитать();
	Движения.Налоговый.Прочитать();
	ТЗ = Движения.Налоговый.Выгрузить();
	абс_ПорядокФормированияВ_НУ = глЗначениеПеременной("абс_ПорядокФормированияВ_НУ_по_счету_91_02_1");
	ВидУчетаНУ = Перечисления.ВидыУчетаПоПБУ18.ВР;
	                                                 	
	Для каждого Проводка Из Движения.Хозрасчетный Цикл
		//АБС ИЗМЕНЕНИЕ   26.12.2014 9:37:57  Хазеев
		//ОбработкаПрерыванияПользователя();
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		//АБС ИЗМЕНЕНИЕ  КОНЕЦ		
		Если Проводка.СчетДт = Счет_91_02_1_БУ Тогда 			   
			Строка = Движения.Налоговый.Добавить();
			Строка.Активность  = Проводка.Активность;
			Строка.Период      = Проводка.Период;
			Строка.Содержание  = Проводка.Содержание;
			Строка.Сумма       = Проводка.Сумма;
			Строка.Регистратор = Проводка.Регистратор; 
			Строка.Организация = Проводка.Организация;
			Строка.СписаниеПартий = Проводка.СписаниеПартий; 			
			Строка.СчетДт = Счет_91_02_7;	
			Строка.СубконтоДт.Очистить();
			Для каждого ВидыСубконто Из Строка.СчетДт.ВидыСубконто Цикл  				
				Строка.СубконтоДт.Вставить(ВидыСубконто.ВидСубконто,  ВидыСубконто.ВидСубконто.ТипЗначения.ПривестиЗначение()); 				
			КонецЦикла;	
			БухгалтерскийУчет.УстановитьСубконто(Строка.СчетДт, Строка.СубконтоДт, 1, Проводка.СубконтоДт.ПрочиеДоходыИРасходы);
			Если не абс_ПорядокФормированияВ_НУ.Пустая() 
				и (абс_ПорядокФормированияВ_НУ = Проводка.СубконтоДт.ПрочиеДоходыИРасходы
					или  Проводка.СубконтоДт.ПрочиеДоходыИРасходы.ПринадлежитЭлементу(абс_ПорядокФормированияВ_НУ)) Тогда
				ВидУчетаНУ = Перечисления.ВидыУчетаПоПБУ18.ПР;	
			КонецЕсли;
			Строка.ВидУчетаДт = ВидУчетаНУ;
		КонецЕсли;
	КонецЦикла;
	          		
	Для каждого Проводка Из ТЗ Цикл
		//АБС ИЗМЕНЕНИЕ   26.12.2014 9:37:57  Хазеев
		//ОбработкаПрерыванияПользователя();
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		//АБС ИЗМЕНЕНИЕ  КОНЕЦ		
		Если не жСписокСчетов.НайтиПоЗначению(Проводка.СчетДт) = Неопределено Тогда 
			Строка = Движения.Налоговый.Добавить();
			Строка.Сумма       = -Проводка.Сумма;
			Строка.Активность  = Проводка.Активность;
			Строка.Период      = Проводка.Период;	
			Строка.Содержание  = Проводка.Содержание;
			Строка.ВидУчетаДт  = ВидУчетаНУ;
			Строка.Регистратор = Проводка.Регистратор; 
			Строка.Организация = Проводка.Организация;
			Строка.СписаниеПартий = Проводка.СписаниеПартий;
			Строка.СчетДт      = Проводка.СчетДт;	
			Строка.СубконтоДт.Очистить();
			Для каждого ВидыСубконто Из Строка.СчетДт.ВидыСубконто Цикл  				
				Строка.СубконтоДт.Вставить(ВидыСубконто.ВидСубконто,  ВидыСубконто.ВидСубконто.ТипЗначения.ПривестиЗначение()); 				
			КонецЦикла;	
			БухгалтерскийУчет.УстановитьСубконто(Строка.СчетДт, Строка.СубконтоДт, 1, Проводка.СубконтоДт1);
			БухгалтерскийУчет.УстановитьСубконто(Строка.СчетДт, Строка.СубконтоДт, 3, Проводка.СубконтоДт2);
			БухгалтерскийУчет.УстановитьСубконто(Строка.СчетДт, Строка.СубконтоДт, 2, Проводка.СубконтоДт3);	
			
			//АБС ВСТАВКА №28365 НАЧАЛО «15 октября 2014 г.», Пополитов
			//В рабочей базе тип субконто может отличаться, от типа этого же номера проводки
			КолСубконто = Строка.СчетДт.ВидыСубконто.Количество(); 			
			Для ИндексСтроки = 0 по КолСубконто - 1 цикл
				ТекЗначениеСтроки = Строка.СубконтоДт[ИндексСтроки];
				Если ЗначениеЗаполнено(ТекЗначениеСтроки) Тогда
					Продолжить;
				КонецЕсли;				
				Для ИндексПроводки = 1 по 3 цикл
					ТекЗначениеПроводки = Проводка["СубконтоДт" + ИндексПроводки];
					Если ТипЗнч(Строка.СчетДт.ВидыСубконто[ИндексСтроки].ВидСубконто.ТипЗначения.ПривестиЗначение()) = ТипЗнч(ТекЗначениеПроводки) Тогда
						БухгалтерскийУчет.УстановитьСубконто(Строка.СчетДт, Строка.СубконтоДт, ИндексСтроки + 1, ТекЗначениеПроводки);
					КонецЕсли;	
				КонецЦикла;
			КонецЦикла;	
			//\\АБС ВСТАВКА №28365 КОНЕЦ
			
		КонецЕсли;
	КонецЦикла;	
	Движения.Налоговый.Записать();	
	
КонецПроцедуры	
//\\АБС ВСТАВКА №18514 КОНЕЦ

//Процедура добавляет в структуру полей сведения о СтатьеЗатрат и НоменклатурнойГруппе из Номенклатуры
//	Эти сведения впоследствии могут пригодиться для заполнения незаполненных Статьи и НоменклатурнойГруппы
//	Также процедура готовит структуру, сопоставляющую поля из табличной части документа и поля из номенклатуры
Процедура ДополнитьСтруктуруПолейДаннымиНоменклатуры(СтруктураПолей, СтруктураОбрабатываемыхКолонок)
	СтруктураПолей.Вставить("СтатьяЗатратНоменклатуры"				, "Номенклатура.СтатьяЗатрат");
	СтруктураПолей.Вставить("ХарактерЗатратНоменклатуры"			, "Номенклатура.СтатьяЗатрат.ХарактерЗатрат");
	СтруктураПолей.Вставить("ВидЗатратНоменклатуры"					, "Номенклатура.СтатьяЗатрат.ВидЗатрат");
	СтруктураПолей.Вставить("СтатусМатериальныхЗатратНоменклатуры"	, "Номенклатура.СтатьяЗатрат.СтатусМатериальныхЗатрат");
	СтруктураПолей.Вставить("НоменклатурнаяГруппаНоменклатуры"		, "Номенклатура.НоменклатурнаяГруппаЗатрат");
	СтруктураПолей.Вставить("ВидРасходовНУНоменклатуры"				, "Номенклатура.СтатьяЗатрат.ВидРасходовНУ");
	
	СтруктураОбрабатываемыхКолонок = Новый Структура();
	СтруктураОбрабатываемыхКолонок.Вставить("СтатьяЗатрат", 			"СтатьяЗатратНоменклатуры");
	СтруктураОбрабатываемыхКолонок.Вставить("НоменклатурнаяГруппа", 	"НоменклатурнаяГруппаНоменклатуры");
	СтруктураОбрабатываемыхКолонок.Вставить("ХарактерЗатрат", 			"ХарактерЗатратНоменклатуры");
	СтруктураОбрабатываемыхКолонок.Вставить("ВидЗатрат", 				"ВидЗатратНоменклатуры");
	СтруктураОбрабатываемыхКолонок.Вставить("СтатусМатериальныхЗатрат", "СтатусМатериальныхЗатратНоменклатуры");
	СтруктураОбрабатываемыхКолонок.Вставить("ВидРасходовНУ", 			"ВидРасходовНУНоменклатуры");
КонецПроцедуры


// Процедура заполнения на основании потребностей производства по заданию на производство
//
Процедура ОбработкаЗаполненияПоЗаданиюНаПроизводство(ДокЗадание) Экспорт
	
	// Подготовим переменные.
	ТабПродукция = Новый ТаблицаЗначений;
	ТабПродукция.Колонки.Добавить("Номенклатура");
	ТабПродукция.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТабПродукция.Колонки.Добавить("ЕдиницаИзмерения");
	ТабПродукция.Колонки.Добавить("Количество");
	ТабПродукция.Колонки.Добавить("Спецификация");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗаданиеНаПроизводствоВыпускТехПроцесс.Номенклатура,
	|	ЗаданиеНаПроизводствоВыпускТехПроцесс.ХарактеристикаНоменклатуры,
	|	ЗаданиеНаПроизводствоВыпускТехПроцесс.ЕдиницаИзмерения,
	|	ЗаданиеНаПроизводствоВыпускТехПроцесс.Количество КАК Количество,
	|	ЗаданиеНаПроизводствоВыпускТехПроцесс.Спецификация,
	|	ЗаданиеНаПроизводствоВыпускТехПроцесс.Заказ КАК Заказ,
	|	ЗаданиеНаПроизводствоВыпускТехПроцесс.НомерСтроки
	|ИЗ
	|	Документ.ЗаданиеНаПроизводство.ВыпускТехПроцесс КАК ЗаданиеНаПроизводствоВыпускТехПроцесс
	|ГДЕ
	|	ЗаданиеНаПроизводствоВыпускТехПроцесс.Ссылка = &ДокОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаданиеНаПроизводствоВыпускТехПроцесс.НомерСтроки
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	Заказ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр( "ДокОснование", ДокЗадание);
	
	РезЗапроса = Запрос.Выполнить();
	ОбходПоЗаказам = РезЗапроса.Выбрать( ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ОбходПоЗаказам.Следующий() Цикл
		
		ТабПродукция.Очистить();
		Обход = ОбходПоЗаказам.Выбрать( ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Обход.Следующий() Цикл
			НоваяСтрока = ТабПродукция.Добавить();
			НоваяСтрока.Номенклатура               = Обход.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = Обход.ХарактеристикаНоменклатуры;
			НоваяСтрока.ЕдиницаИзмерения           = Обход.ЕдиницаИзмерения;
			НоваяСтрока.Спецификация               = Обход.Спецификация;
			НоваяСтрока.Количество                 = Обход.Количество;
		КонецЦикла;
		
		СтруктРезультат = Новый Структура;
		СтруктРезультат.Вставить( "Потребности");
				
		СтруктПараметры = Новый Структура;
		СтруктПараметры.Вставить( "ДатаСпецификации", Дата);
		СтруктПараметры.Вставить( "ПараметрыВыпуска", Неопределено);
		
		Если РазузлованиеНоменклатуры.ПолучитьПотребность(ТабПродукция, СтруктРезультат, СтруктПараметры).Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
			
		ТабМатериалы = СтруктРезультат["Потребности"];
		
		Для Каждого СтрокаМат Из ТабМатериалы Цикл
			
			НоваяСтрока = Материалы.Добавить();
			НоваяСтрока.Номенклатура               = СтрокаМат.Материал;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаМат.ХарактеристикаМатериала;
			НоваяСтрока.ЕдиницаИзмерения           = СтрокаМат.ЕдиницаИзмеренияМатериала;
			НоваяСтрока.Количество                 = СтрокаМат.КоличествоМатериала;
			НоваяСтрока.СтатьяЗатрат               = СтрокаМат.СтатьяЗатрат;
			
			НоваяСтрока.Коэффициент = НоваяСтрока.ЕдиницаИзмерения.Коэффициент;
			НоваяСтрока.Качество    = Справочники.Качество.Новый;
			НоваяСтрока.Заказ       = ОбходПоЗаказам.Заказ;
			
			ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти( НоваяСтрока, Ссылка);
			ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл  ( НоваяСтрока, "Материалы", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете);
		
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры // ОбработкаЗаполненияПоЗаданиюНаПроизводство()

// Процедура заполнения на основании плана производства по сменам
//
Процедура ОбработкаЗаполненияПоПлануПроизводстваПоСменам(ДокПлан)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ПланПроизводстваПоСменамПотребностиПроизводства.Номенклатура,
	|	ПланПроизводстваПоСменамПотребностиПроизводства.ХарактеристикаНоменклатуры,
	|	ПланПроизводстваПоСменамПотребностиПроизводства.Заказ,
	|	ПланПроизводстваПоСменамПотребностиПроизводства.Проект,
	|	СУММА(ПланПроизводстваПоСменамПотребностиПроизводства.КоличествоИзвне) КАК Количество,
	|	ПланПроизводстваПоСменамПотребностиПроизводства.Номенклатура.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ПланПроизводстваПоСменамПотребностиПроизводства.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|	ПланПроизводстваПоСменамПотребностиПроизводства.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Коэффициент
	|ИЗ
	|	Документ.ПланПроизводстваПоСменам.ПотребностиПроизводства КАК ПланПроизводстваПоСменамПотребностиПроизводства
	|ГДЕ
	|	ПланПроизводстваПоСменамПотребностиПроизводства.Ссылка = &ДокПлан
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланПроизводстваПоСменамПотребностиПроизводства.Номенклатура,
	|	ПланПроизводстваПоСменамПотребностиПроизводства.ХарактеристикаНоменклатуры,
	|	ПланПроизводстваПоСменамПотребностиПроизводства.Заказ,
	|	ПланПроизводстваПоСменамПотребностиПроизводства.Проект";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр( "ДокПлан", ДокПлан);
	
	РезультатЗапроса = Запрос.Выполнить();
	Обход = РезультатЗапроса.Выбрать();
	Пока Обход.Следующий() Цикл
		
		НоваяСтрока = Материалы.Добавить();
		НоваяСтрока.Номенклатура               = Обход.Номенклатура;
		НоваяСтрока.ХарактеристикаНоменклатуры = Обход.ХарактеристикаНоменклатуры;
		НоваяСтрока.ЕдиницаИзмерения           = Обход.ЕдиницаИзмерения;
		НоваяСтрока.Количество                 = Обход.Количество;
		НоваяСтрока.СтатьяЗатрат               = Обход.СтатьяЗатрат;
			
		НоваяСтрока.Коэффициент = Обход.Коэффициент;
		НоваяСтрока.Качество    = Справочники.Качество.Новый;
		НоваяСтрока.Заказ       = Обход.Заказ;
			
		ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти( НоваяСтрока, Ссылка);
		ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл  ( НоваяСтрока, "Материалы", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете);
		
	КонецЦикла;
	
КонецПроцедуры // ОбработкаЗаполненияПоПлануПроизводстваПоСменам()

// Процедура - обработчик события "ОбработкаЗаполнения"
//
Процедура ОбработкаЗаполнения(Основание)
	
	ОтражатьВУправленческомУчете = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОтражатьДокументыВУправленческомУчете");
	ОтражатьВБухгалтерскомУчете  = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОтражатьДокументыВБухгалтерскомУчете");
	ОтражатьВНалоговомУчете      = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОтражатьДокументыВНалоговомУчете")
		И ОтражатьВБухгалтерскомУчете;
		
	ДокументОснование = Основание;
		
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		
		// Заполнение шапки
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		Склад = Основание.СкладОрдер;
		
		Если НЕ ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
			ПодразделениеОрганизации = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделениеОрганизации");
		КонецЕсли;
		
		Если ПодразделениеОрганизации.Владелец <> Организация Тогда
			ПодразделениеОрганизации = Неопределено;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Основание.Сделка)
		   И Не Основание.Сделка.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
			Заказ = Основание.Сделка;
		КонецЕсли;
			
	    ЗаполнитьТоварыПоДокументуПоступлениеТоваровУслуг(Основание);
				
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетПроизводстваЗаСмену")
		  ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг") Тогда
		
		// Заполнение шапки
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект);

		Организация                  = Основание.Организация;
		ОтражатьВБухгалтерскомУчете  = Основание.ОтражатьВБухгалтерскомУчете;
		ОтражатьВНалоговомУчете      = Основание.ОтражатьВНалоговомУчете;
		ОтражатьВУправленческомУчете = Основание.ОтражатьВУправленческомУчете;
		Подразделение                = Основание.Подразделение;
		ПодразделениеОрганизации     = Основание.ПодразделениеОрганизации;
		
		Если ПодразделениеОрганизации.Владелец <> Организация Тогда
			ПодразделениеОрганизации = Неопределено;
		КонецЕсли;
		
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетПроизводстваЗаСмену") Тогда
			Склад                    = Основание.Склад;
		КонецЕсли;
		
		ТаблицаМатериалы = Материалы.Выгрузить();
		ТаблицаМатериалы.Очистить();
		
		Если Основание.РаспределениеМатериалов.Количество() > 0 Тогда
			
			ЕстьСчетЗатратВТЧ = Истина;
			
			Для Каждого ТекСтрокаМатериалы Из Основание.РаспределениеМатериалов Цикл
				
				НоваяСтрока = ТаблицаМатериалы.Добавить();
				НоваяСтрока.Номенклатура               = ТекСтрокаМатериалы.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = ТекСтрокаМатериалы.ХарактеристикаНоменклатуры;
				НоваяСтрока.СерияНоменклатуры          = ТекСтрокаМатериалы.СерияНоменклатуры;
				НоваяСтрока.ЕдиницаИзмерения           = ТекСтрокаМатериалы.ЕдиницаИзмерения;
				НоваяСтрока.Количество                 = ТекСтрокаМатериалы.Количество;
				НоваяСтрока.Коэффициент                = ТекСтрокаМатериалы.Коэффициент;
				НоваяСтрока.НоменклатурнаяГруппа       = ТекСтрокаМатериалы.НоменклатурнаяГруппа;
				НоваяСтрока.Качество                   = Справочники.Качество.Новый;
				НоваяСтрока.ОтражениеВУСН              = Перечисления.ОтражениеВУСН.Принимаются;
				
				Если ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетПроизводстваЗаСмену") Тогда
					НоваяСтрока.СтатьяЗатрат           = ?( Основание.ВводитьСтатьиЗатратПоСтрокам, ТекСтрокаМатериалы.СтатьяЗатрат, Основание.СтатьяЗатрат);
					НоваяСтрока.Заказ                  = ТекСтрокаМатериалы.Заказ;
				Иначе
					НоваяСтрока.СтатьяЗатрат           = ТекСтрокаМатериалы.СтатьяЗатрат;
					НоваяСтрока.Заказ                  = Основание.Сделка;
				КонецЕсли;
				
				//Если переработка, то счета затрат нельзя брать из документа основания, так как
				//в документе-основании это балансовые счета, а в требовании-накладной забалансовые.
				//Здесь мы заполняем всегда, а ниже для переработки перезаполним.
				НоваяСтрока.СчетЗатрат                 = ТекСтрокаМатериалы.СчетЗатрат;
				НоваяСтрока.СчетЗатратНУ               = ТекСтрокаМатериалы.СчетЗатратНУ;
				
			КонецЦикла;
			
		Иначе
			
			ЕстьСчетЗатратВТЧ = Ложь;
			
			Для Каждого ТекСтрокаМатериалы Из Основание.Материалы Цикл
				
				НоваяСтрока = ТаблицаМатериалы.Добавить();
				НоваяСтрока.Номенклатура               = ТекСтрокаМатериалы.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = ТекСтрокаМатериалы.ХарактеристикаНоменклатуры;
				НоваяСтрока.СерияНоменклатуры          = ТекСтрокаМатериалы.СерияНоменклатуры;
				НоваяСтрока.ЕдиницаИзмеренияМест       = ТекСтрокаМатериалы.ЕдиницаИзмеренияМест;
				НоваяСтрока.ЕдиницаИзмерения           = ТекСтрокаМатериалы.ЕдиницаИзмерения;
				НоваяСтрока.Количество                 = ТекСтрокаМатериалы.Количество;
				НоваяСтрока.Коэффициент                = ТекСтрокаМатериалы.Коэффициент;
				НоваяСтрока.КоличествоМест 			   = ?(НоваяСтрока.Коэффициент <> 0, НоваяСтрока.Количество * НоваяСтрока.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / НоваяСтрока.Коэффициент, 0);
				НоваяСтрока.НоменклатурнаяГруппа       = ТекСтрокаМатериалы.Номенклатура.НоменклатурнаяГруппаЗатрат;
				НоваяСтрока.Качество                   = Справочники.Качество.Новый;
				НоваяСтрока.ОтражениеВУСН              = Перечисления.ОтражениеВУСН.Принимаются;
				
				Если ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетПроизводстваЗаСмену") Тогда
					НоваяСтрока.СтатьяЗатрат           = ?( Основание.ВводитьСтатьиЗатратПоСтрокам, ТекСтрокаМатериалы.СтатьяЗатрат, Основание.СтатьяЗатрат);
					НоваяСтрока.Заказ                  = ТекСтрокаМатериалы.Заказ;
				Иначе
					НоваяСтрока.СтатьяЗатрат           = ТекСтрокаМатериалы.СтатьяЗатрат;
					НоваяСтрока.Заказ                  = Основание.Сделка;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ТаблицаМатериалы.ЗаполнитьЗначения(Склад, "Склад");
		ТаблицаМатериалы.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры, ЕдиницаИзмеренияМест, ЕдиницаИзмерения, Коэффициент, 
								|НоменклатурнаяГруппа, СтатьяЗатрат, Заказ, ЗаказРезерв, Качество, Счет, СчетНУ, СчетЗатрат, СчетЗатратНУ, ОтражениеВУСН, ЗаказРезерв, Склад",
								  "КоличествоМест, Количество");
		Материалы.Загрузить(ТаблицаМатериалы);
		
		Для Каждого ДанныеСтроки Из Материалы Цикл
			НадоЗаполнятьСчетаЗатрат = 
			//1 случай - его не было в ТЧ совсем (не использовалась для заполнения ТЧ РаспределениеМатериалов)
			НЕ ЕстьСчетЗатратВТЧ  
			//2 случай - для переработки нельзя брать счет затрат из РаспределенияМатериалов, т.к. он там балансовый, а здесь нужен забалансовый
			ИЛИ	ДанныеСтроки.СтатьяЗатрат.СтатусМатериальныхЗатрат = Перечисления.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку;
	
			ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл(ДанныеСтроки, "Материалы", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете, , НадоЗаполнятьСчетаЗатрат);
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВнутреннийЗаказ") Тогда
		
		Если Не УправлениеЗаказами.ИспользоватьВнутренниеЗаказы() Тогда
			Возврат;
		КонецЕсли;
		Если Основание.ВидЗаказа = Перечисления.ВидыВнутреннегоЗаказа.НаСклад Тогда
			Возврат;
		КонецЕсли;
		
		// Заполнение шапки
		ОтражатьВУправленческомУчете = Истина;
		Подразделение = Основание.Заказчик;
		Организация   = Основание.Организация;
		Ответственный = Основание.Ответственный;
		Комментарий   = Основание.Комментарий;
		
		// Для корректного заполнения счетов БУ и НУ, которые зависят от подразеделения организации
		ПодразделениеОрганизации = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделениеОрганизации");
		
		Если ПодразделениеОрганизации.Владелец <> Организация Тогда
			ПодразделениеОрганизации = Неопределено;
		КонецЕсли;
		
		РезультатЗапроса = ОстаткиТоваровПоВнутреннемуЗаказу_СУчетомРезервов(Основание, Дата);
		ЗаполнитьМатериалыПоВнутреннемуЗаказу(РезультатЗапроса);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.АктОтбораПробНоменклатуры") Тогда
		
		// Заполнение шапки
		Организация   = Основание.Организация;
		Склад         = Основание.Склад;
		Подразделение = Основание.Подразделение;
		
		// Для корректного заполнения счетов БУ и НУ, которые зависят от подразеделения организации
		ПодразделениеОрганизации = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделениеОрганизации");
		
		Если ПодразделениеОрганизации.Владелец <> Организация Тогда
			ПодразделениеОрганизации = Неопределено;
		КонецЕсли;

		ТаблицаМатериалы = Материалы.Выгрузить();
		
		Если Основание.ВидОперации = Перечисления.ВидыОперацийАктОтбораПробНоменклатуры.ДляВнешнейСертификации Тогда
			
			НоваяСтрока = ТаблицаМатериалы.Добавить();
			НоваяСтрока.ЕдиницаИзмерения           = Основание.Номенклатура.ЕдиницаХраненияОстатков;
			НоваяСтрока.Коэффициент                = Основание.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент;
			НоваяСтрока.Количество                 = Основание.Испытания;
			НоваяСтрока.Номенклатура               = Основание.Номенклатура;
			НоваяСтрока.СерияНоменклатуры          = Основание.СерияНоменклатуры;
			НоваяСтрока.СтатьяЗатрат = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяСтатьяЗатратОтбораПробВнешняя");
			НоваяСтрока.Качество                   = Справочники.Качество.Новый;
			НоваяСтрока.ОтражениеВУСН              = Перечисления.ОтражениеВУСН.Принимаются;
			
			НоваяСтрока = ТаблицаМатериалы.Добавить();
			НоваяСтрока.ЕдиницаИзмерения           = Основание.Номенклатура.ЕдиницаХраненияОстатков;
			НоваяСтрока.Коэффициент                = Основание.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент;
			НоваяСтрока.Количество                 = Основание.КонтрольнаяПроба;
			НоваяСтрока.Номенклатура               = Основание.Номенклатура;
			НоваяСтрока.СерияНоменклатуры          = Основание.СерияНоменклатуры;
			НоваяСтрока.СтатьяЗатрат = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяСтатьяЗатратОтбораПробВнешняя");
			НоваяСтрока.Качество                   = Справочники.Качество.Новый;
			НоваяСтрока.ОтражениеВУСН              = Перечисления.ОтражениеВУСН.Принимаются;
			
		Иначе
		
			ТабЧастьКопия = Основание.РаспределениеПоЛабораториям.Выгрузить();
			ТабЧастьКопия.Свернуть("Подразделение");
			Если ТабЧастьКопия.Количество() = 1 Тогда
				// Первое, оно же единственное подразделение из таб.части.
				Подразделение = ТабЧастьКопия[0].Подразделение;
			ИначеЕсли ТабЧастьКопия.Количество() = 0 Тогда
				Подразделение = Основание.Подразделение;
			Иначе
				// Первое подразделение из таб.части.
				СписокВыбора = Новый СписокЗначений;
				СписокВыбора.ЗагрузитьЗначения( ТабЧастьКопия.ВыгрузитьКолонку("Подразделение"));
				СписокВыбора.СортироватьПоПредставлению();
				Выбор = СписокВыбора.ВыбратьЭлемент("Выберите тестирующее подразделение (лабораторию).");
				Если Выбор = Неопределено Тогда // Не заполнять таб.часть.
					Подразделение = Неопределено;
				Иначе
					Подразделение = Выбор.Значение;
				КонецЕсли;
			КонецЕсли;
			
			Для Каждого ТекСтрокаТовары Из Основание.РаспределениеПоЛабораториям Цикл
			
				Если Не ТекСтрокаТовары.Подразделение = Подразделение Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ТаблицаМатериалы.Добавить();
				НоваяСтрока.ЕдиницаИзмерения           = Основание.Номенклатура.ЕдиницаХраненияОстатков;
				НоваяСтрока.Коэффициент                = Основание.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент;
				НоваяСтрока.Количество                 = ТекСтрокаТовары.Количество;
				НоваяСтрока.КоличествоМест             = ТекСтрокаТовары.Количество;
				НоваяСтрока.Номенклатура               = Основание.Номенклатура;
				НоваяСтрока.СерияНоменклатуры          = Основание.СерияНоменклатуры;
				НоваяСтрока.СтатьяЗатрат = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяСтатьяЗатратОтбораПробВнутренняя");
				НоваяСтрока.Качество                   = Справочники.Качество.Новый;
				НоваяСтрока.ОтражениеВУСН              = Перечисления.ОтражениеВУСН.Принимаются;
				
			КонецЦикла;
												
		КонецЕсли;
		
		ТаблицаМатериалы.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры, ЕдиницаИзмерения, Коэффициент, НоменклатурнаяГруппа, СтатьяЗатрат, Заказ, Качество, Счет, СчетНУ, СчетЗатрат, СчетЗатратНУ, ЗаказРезерв, ОтражениеВУСН",
								  "КоличествоМест, Количество");
		Материалы.Загрузить(ТаблицаМатериалы);

		ЗаполнитьСчетаУчетаВТабЧасти(Материалы, "Материалы", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете);

	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ИнвентаризацияТоваровНаСкладе") Тогда
		
		// Заполнение шапки
		Организация = Основание.Организация;
		Склад       = Основание.Склад;
		
		// Для корректного заполнения счетов БУ и НУ, которые зависят от подразеделения организации
		ПодразделениеОрганизации = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделениеОрганизации");
		
		Если ПодразделениеОрганизации.Владелец <> Организация Тогда
			ПодразделениеОрганизации = Неопределено;
		КонецЕсли;
		
		Для Каждого СтрокаТЧ Из Основание.Товары Цикл
			
			Недостача = СтрокаТЧ.КоличествоУчет - СтрокаТЧ.Количество;
			Если Недостача <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = Материалы.Добавить();
			НоваяСтрока.Склад                        = Основание.Склад;
			НоваяСтрока.Номенклатура                 = СтрокаТЧ.Номенклатура;
			НоваяСтрока.ЕдиницаИзмерения             = СтрокаТЧ.ЕдиницаИзмерения;
			НоваяСтрока.ЕдиницаИзмеренияМест         = СтрокаТЧ.ЕдиницаИзмеренияМест;
			НоваяСтрока.Коэффициент                  = СтрокаТЧ.Коэффициент;
			НоваяСтрока.Качество                     = СтрокаТЧ.Качество;
			НоваяСтрока.СерияНоменклатуры            = СтрокаТЧ.СерияНоменклатуры;
			НоваяСтрока.НоменклатурнаяГруппа         = НоваяСтрока.Номенклатура.НоменклатурнаяГруппа;
			НоваяСтрока.СтатьяЗатрат                 = НоваяСтрока.Номенклатура.СтатьяЗатрат;
			НоваяСтрока.Количество                   = Недостача;
			НоваяСтрока.ХарактеристикаНоменклатуры   = СтрокаТЧ.ХарактеристикаНоменклатуры;
			
			// Рассчитать реквизиты табличной части.
			ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти( НоваяСтрока, ЭтотОбъект);
			ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл  ( НоваяСтрока, "Материалы", Истина, Истина);
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаданиеНаПроизводство") Тогда
		
		// Заполнение шапки
		Организация   = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
		Подразделение = Основание.Подразделение;
		ПодразделениеОрганизации = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделениеОрганизации");
		
		Если ПодразделениеОрганизации.Владелец <> Организация Тогда
			ПодразделениеОрганизации = Неопределено;
		КонецЕсли;
		
		ОбработкаЗаполненияПоЗаданиюНаПроизводство(Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПланПроизводстваПоСменам") Тогда
		
		Организация   = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
		Подразделение = Основание.Подразделение;
		ПодразделениеОрганизации = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделениеОрганизации");
		
		Если ПодразделениеОрганизации.Владелец <> Организация Тогда
			ПодразделениеОрганизации = Неопределено;
		КонецЕсли;
		
		ОбработкаЗаполненияПоПлануПроизводстваПоСменам(Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		
		Организация              = Основание.Организация;
		Подразделение            = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделение");
		ПодразделениеОрганизации = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделениеОрганизации");
		Склад = Основание.СкладПолучатель;
		
		Если ПодразделениеОрганизации.Владелец <> Организация Тогда
			ПодразделениеОрганизации = Неопределено;
		КонецЕсли;
		
		ОтражатьВУправленческомУчете = Основание.ОтражатьВУправленческомУчете;
		ОтражатьВБухгалтерскомУчете  = Основание.ОтражатьВБухгалтерскомУчете;
		ОтражатьВНалоговомУчете      = Основание.ОтражатьВНалоговомУчете;
		
		Для Каждого СтрокаТЧ Из Основание.Товары Цикл
			
			НоваяСтрока = Материалы.Добавить();
			НоваяСтрока.Номенклатура               = СтрокаТЧ.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТЧ.ХарактеристикаНоменклатуры;
			НоваяСтрока.СерияНоменклатуры          = СтрокаТЧ.СерияНоменклатуры;
			
			НоваяСтрока.Количество                 = СтрокаТЧ.Количество;
			НоваяСтрока.КоличествоМест             = СтрокаТЧ.КоличествоМест;
			НоваяСтрока.Коэффициент                = СтрокаТЧ.Коэффициент;
			НоваяСтрока.ЕдиницаИзмерения           = СтрокаТЧ.ЕдиницаИзмерения;
			НоваяСтрока.ЕдиницаИзмеренияМест       = СтрокаТЧ.ЕдиницаИзмеренияМест;
			НоваяСтрока.Склад                      = Основание.СкладПолучатель;
			
			НоваяСтрока.Заказ                      = СтрокаТЧ.ДокументРезерва;
			НоваяСтрока.Качество                   = СтрокаТЧ.Качество;
			
			НоваяСтрока.Счет                       = СтрокаТЧ.НовыйСчетУчетаБУ;
			НоваяСтрока.СчетНУ                     = СтрокаТЧ.НовыйСчетУчетаНУ;
			
			// Выполнить общие действия для всех документов при изменении номенклатуры.
			ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(НоваяСтрока, ЭтотОбъект);

			УправлениеЗатратами.ЗаполнитьНоменклатурнуюГруппуИСтатьюЗатратВСтрокеТабличногоПоля(НоваяСтрока);
			ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл  (НоваяСтрока, "Материалы", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете);
			УправлениеЗатратами.ЗаполнитьСчетЗатратВСтрокеТабличногоПоля(НоваяСтрока, ПодразделениеОрганизации, НоваяСтрока.СтатьяЗатрат,,,"Счет");

			//СтрокаТабличнойЧасти.Качество = Справочники.Качество.Новый;
		    НоваяСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
						
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаПроизводство") Тогда
	
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект);

		// Попробуем заполнить подразделение организации.
		ПодразделениеОрганизации = УправлениеЗатратами.ПолучитьПодразделениеОрганизации(
			Организация,
			Подразделение,
			ОтражатьВБухгалтерскомУчете
		);
												  
		Если ПодразделениеОрганизации.Владелец <> Организация Тогда
			ПодразделениеОрганизации = Неопределено;
		КонецЕсли;
		ЗаказыНаПроизводствоИПереработку.ЗаполнитьТребованиеНакладнаяПоРезервамЗаказаНаПроизводство(ЭтотОбъект, Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВыпускПродукции") Тогда
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		ПодразделениеОрганизации = Основание.ПодразделениеОрганизации;
		
		ТабПродукция = Основание.Продукция.Выгрузить();
		ТабМатериалы = Материалы.Выгрузить();
		СтруктураДопКолонок = Новый Структура("Заказ, НоменклатурнаяГруппа");
		
		УправлениеПроизводством.ЗаполнитьМатериалыПоСпецификациям(ТабМатериалы, ТабПродукция, СтруктураДопКолонок,, Дата);
		ТабМатериалы.ЗаполнитьЗначения(Справочники.Качество.Новый, "Качество");
		
		ТабМатериалы.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, НоменклатурнаяГруппа, Заказ, Коэффициент, Качество", "Количество");
		
		Для Каждого СтрокаМат Из ТабМатериалы Цикл
			НоваяСтрока = Материалы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМат);
			НоваяСтрока.СтатьяЗатрат = СтрокаМат.Номенклатура.СтатьяЗатрат;
			ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти( НоваяСтрока, Ссылка);
		КонецЦикла;
		ЗаполнитьСчетаУчетаВТабЧасти(Материалы, "Материалы", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете);
		
	//АБС Коломиец+
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда

		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		Склад = Основание.СкладОрдер;
		
		ПодразделениеОрганизации = УправлениеЗатратами.ПолучитьПодразделениеОрганизации(
			Организация,
			Подразделение,
			ОтражатьВБухгалтерскомУчете
		);
		
		Если НЕ ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
			ПодразделениеОрганизации = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделениеОрганизации");
		КонецЕсли;
		
		Если ПодразделениеОрганизации.Владелец <> Организация Тогда
			ПодразделениеОрганизации = Неопределено;
		КонецЕсли;
					
	    ЗаполнитьТоварыПоДокументуПоступлениеТоваровУслуг(Основание);
		
		Для Каждого ТекСтрокаТовары Из Основание.Товары Цикл
			НоваяСтрока = Материалы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаТовары);
			
			//АБС ВСТАВКА 38227  04.02.2014 15:10:48  Стрельцов
			НоваяСтрока.Качество = Справочники.Качество.Новый;
			//\\АБС ВСТАВКА 38227 КОНЕЦ
			
			УправлениеЗатратами.ЗаполнитьНоменклатурнуюГруппуИСтатьюЗатратВСтрокеТабличногоПоля(НоваяСтрока);
			ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл  (НоваяСтрока, "Материалы", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете);
			УправлениеЗатратами.ЗаполнитьСчетЗатратВСтрокеТабличногоПоля(НоваяСтрока, ПодразделениеОрганизации, НоваяСтрока.СтатьяЗатрат,,,"Счет");			
		КонецЦикла;
	//АБС-
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("СправочникСсылка.НастройкиЗаполненияФорм") Тогда
		// Заполнение по настройке
		ХранилищаНастроек.ДанныеФорм.ЗаполнитьОбъектПоНастройке(ЭтотОбъект, Основание, Документы.ТребованиеНакладная.СтруктураДополнительныхДанныхФормы());
		
	Иначе
		
		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект);
		Если НЕ ЗначениеЗаполнено(Проект) Тогда
			Проект = Подразделение.ОсновнойПроект;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ЗаполнитьМатериалыПоВнутреннемуЗаказу(РезультатЗапроса) Экспорт
	СоотвСтрок     = Новый Соответствие;
	МассивОстатков = Новый Массив;

	ОбходНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ОбходНоменклатура.Следующий() Цикл
		ОбходХарактеристика = ОбходНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ОбходХарактеристика.Следующий() Цикл
		
			МассивОстатков.Очистить();
			
			Обход = ОбходХарактеристика.Выбрать();
			Пока Обход.Следующий() Цикл
				
				Если Обход.Источник = 1 Тогда // Это строка с остатком. Добавляем в документ
					
					НоваяСтрока = Материалы.Добавить();
					НоваяСтрока.Номенклатура = Обход.Номенклатура;
					
					НоваяСтрока.ХарактеристикаНоменклатуры = Обход.ХарактеристикаНоменклатуры;
					НоваяСтрока.ЕдиницаИзмерения = Обход.ЕдиницаИзмерения;
					НоваяСтрока.Коэффициент      = Обход.Коэффициент;
					НоваяСтрока.Количество 		 = Обход.КолОстаток * Обход.ЕдиницаХраненияКоэффициент / НоваяСтрока.Коэффициент;
					НоваяСтрока.Качество 		 = Справочники.Качество.Новый;
					НоваяСтрока.ВнутреннийЗаказ	 = Обход.ВнутреннийЗаказ;
					
					МассивОстатков.Добавить(НоваяСтрока);
					СоотвСтрок.Вставить(НоваяСтрока);
					
					ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти( НоваяСтрока, ЭтотОбъект); 
					ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл( НоваяСтрока, "Материалы", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете);
					НоваяСтрока.ОтражениеВУСН             = Перечисления.ОтражениеВУСН.Принимаются;

				Иначе
				
					НадоРазместить = Обход.КолРезерв;
					
					Пока НадоРазместить > 0 И МассивОстатков.Количество() > 0 Цикл
						Если не мУказаниеСкладовВТЧ Тогда
							Если ЗначениеЗаполнено(Склад) Тогда
								Если Склад<>Обход.Склад Тогда
									Прервать;
								КонецЕсли;	
							Иначе
								Склад = Обход.Склад;
							КонецЕсли;
						КонецЕсли;
						
						НадоРазместить = НадоРазместить * МассивОстатков[0].Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / МассивОстатков[0].Коэффициент;
						
						МожноРазместить = Мин( Макс(МассивОстатков[0].Количество, 0), НадоРазместить);
						
						НоваяСтрока = Материалы.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, МассивОстатков[0]);
						СоотвСтрок.Вставить(НоваяСтрока);
						
						НоваяСтрока.Количество         = МожноРазместить;
						НоваяСтрока.ЗаказРезерв 	   = Обход.ВнутреннийЗаказ;
						НоваяСтрока.Склад 			   = Обход.Склад;
						
						ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти( НоваяСтрока, ЭтотОбъект); 
						ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл( НоваяСтрока, "Материалы", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете);
						НоваяСтрока.ОтражениеВУСН             = Перечисления.ОтражениеВУСН.Принимаются;

						// Скорректируем исходную строку на размещенное количество
						МассивОстатков[0].Количество = МассивОстатков[0].Количество - МожноРазместить;
						Если МассивОстатков[0].Количество <= 0 Тогда
							СоотвСтрок.Удалить(МассивОстатков[0]);
							Материалы.Удалить(МассивОстатков[0]);
							МассивОстатков.Удалить(0);
						КонецЕсли;
						
						НадоРазместить = НадоРазместить - МожноРазместить;
					
					КонецЦикла;
					
				КонецЕсли;
			
			КонецЦикла;
		
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры


Функция ОстаткиТоваровПоВнутреннемуЗаказу_СУчетомРезервов( Заказ, Знач КонДата) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 															 КАК Источник,
	|	ВнутренниеЗаказыОстатки.Номенклатура                         КАК Номенклатура,
	|	ВнутренниеЗаказыОстатки.ХарактеристикаНоменклатуры           КАК ХарактеристикаНоменклатуры,
	|	ВнутренниеЗаказыОстатки.КоличествоОстаток          			 КАК КолОстаток,
	|	ВнутренниеЗаказыОстатки.ЕдиницаИзмерения                     КАК ЕдиницаИзмерения,
	|	ВнутренниеЗаказыОстатки.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаХранения,
	|	ВнутренниеЗаказыОстатки.ЕдиницаИзмерения.Коэффициент         КАК Коэффициент,
	|	ВнутренниеЗаказыОстатки.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК ЕдиницаХраненияКоэффициент,
	|	ВнутренниеЗаказыОстатки.ВнутреннийЗаказ                      КАК ВнутреннийЗаказ,
	|	0 															 КАК КолРезерв,
	|	NULL 														 КАК Склад
	|ИЗ
	|	РегистрНакопления.ВнутренниеЗаказы.Остатки(&КонДата, ВнутреннийЗаказ = &Заказ ) КАК ВнутренниеЗаказыОстатки
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ 
	|	2 													КАК Источник,
	|	РезервыОстатки.Номенклатура                         КАК Номенклатура,
	|	РезервыОстатки.ХарактеристикаНоменклатуры           КАК ХарактеристикаНоменклатуры,
	|	0          			 								КАК КолОстаток,
	|	NULL                     							КАК ЕдиницаИзмерения,
	|	РезервыОстатки.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаХранения,
	|	1         											КАК Коэффициент,
	|	РезервыОстатки.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК ЕдиницаХраненияКоэффициент,
	|	РезервыОстатки.ДокументРезерва                      КАК ВнутреннийЗаказ,
	|	РезервыОстатки.КоличествоОстаток          			КАК КолРезерв,
	|	РезервыОстатки.Склад 								КАК Склад
	|ИЗ
    |	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(&КонДата,ДокументРезерва = &Заказ %Условие_Склад%)  КАК РезервыОстатки
	|УПОРЯДОЧИТЬ ПО Источник
	|ИТОГИ
	|	СУММА(КолОстаток),
	|	СУММА(КолРезерв)
	|ПО
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры
	|";
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр( "КонДата", КонДата);
	Запрос.УстановитьПараметр( "Заказ",   Заказ);
	Если не мУказаниеСкладовВТЧ и ЗначениеЗаполнено(Склад) Тогда
		ТекстЗапроса = стрЗаменить(ТекстЗапроса,"%Условие_Склад%","И Склад = &Склад");
		Запрос.УстановитьПараметр( "Склад",   Склад);
	Иначе
		ТекстЗапроса = стрЗаменить(ТекстЗапроса,"%Условие_Склад%","");
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();
	
КонецФункции // ОстаткиТоваровПоВнутреннемуЗаказу_СУчетомРезервов()

// Процедура - обработчик события "ПриЗаписи"
//
Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	// АБС ВСТАВКА Согласование первичных документов
	Попытка
	
		СтатусПоРегистру = абс_БизнесПроцессы.ПолучитьСтатусПервичногоДокументаПоРегистру(Ссылка);
		
		Если НЕ абс_Статус = СтатусПоРегистру Тогда
			ЗаписатьНовыйСтатус(абс_Статус, абс_ПричинаИзмененияСтатуса);		
		КонецЕсли;
		
		
		// АБС ВСТАВКА 20120412 Попов
		// Временно отключаем формирование задач по ППД
		Возврат;
		// АБС ВСТАВКА 20120412 Попов 
		// Запустим БП если он еще не запущен
		Если абс_Статус = Перечисления.абс_СтатусыПервичныхДокументов.СогласованиеОФК Тогда
			БПСогласование = НайтиБПСогласование();
			
			Если Не ЗначениеЗаполнено(БПСогласование) Тогда
				абс_БизнесПроцессы.ЗапуститьБПСогласованияПервичногоДокумента(Ссылка);	
			КонецЕсли;
			
		КонецЕсли;
		
		Если абс_Статус = Перечисления.абс_СтатусыПервичныхДокументов.УточнениеБухгалтерией Тогда
			
			БПСогласование = НайтиБПСогласование();
			
			Если Не ЗначениеЗаполнено(БПСогласование) Тогда
				абс_БизнесПроцессы.ЗапуститьБПСогласованияПервичногоДокументаУточнениеБухгалтером(Ссылка);
			КонецЕсли;
			
		КонецЕсли;
		
			
		// Попробуем закрыть задачу согласования
		ЗадачаСогласование = ПолучитьЗадачуПоПервичномуДокументуСогласование();
		
		Если ЗадачаСогласование = Неопределено Тогда
			Возврат;
		КонецЕсли;
				
		Если НЕ ЗадачаСогласование.Выполнена Тогда

			ЗадачаОбъект = ЗадачаСогласование.ПолучитьОбъект();			
			ЗадачаОбъект.ВыполнитьЗадачу();
		КонецЕсли;
	
	Исключение

		ttk_ОбщегоНазначения.СообщитьОбОшибке("Ошибка при записи документа: " + ОписаниеОшибки() + ".", Отказ);
		
		Возврат;
	КонецПопытки;	
	
	// АБС ВСТАВКА Согласование первичных документов КОНЕЦ	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	//Горелик Р.В. 10.10.2018 [CHG448/525] +++
	Если ttk_ИнтеграцияБП30Сервер.ПроверкаИспользованияКонстантыИнтеграцияБП30(Дата) Тогда
		Если ЭтотОбъект.ПометкаУдаления = Истина Тогда
			ЭтотОбъект.ПометкаУдаления = Ложь;
			Сообщить("Запрет на установку пометки на удаление!");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//Горелик Р.В. 10.10.2018 [CHG448/525] ---
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	//Крамаренко Д.М.
	//НФС 2018
	Для Каждого Строка из Материалы Цикл
		
		Если НЕ ЗначениеЗаполнено(Строка.Склад) Тогда
			Строка.Склад = Склад;
		КонецЕсли;
		
		СтрокаНачалаСообщенияОбОшибке = Нстр("ru = 'В строке номер """+ СокрЛП(Строка.НомерСтроки) +
		""" табличной части "" Материалов "": '");
		Заголовок = "Ошибка аналитик";
		Если НЕ ЗначениеЗаполнено(Строка.абс_БюджетнаяСтатья) Тогда
			ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+"не заполнена Бюджетная Статья",Отказ,"Ошибка аналитик");
		КонецЕсли;
		//проверка счетов группы 07 или 08
		Если ЗначениеЗаполнено(Строка.СчетЗатрат) Тогда
			СчетаПроверочнойГруппы07 = Строка.СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке) ИЛИ Строка.СчетЗатрат = ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке;
			СчетаПроверочнойГруппы08 = Строка.СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы) ИЛИ Строка.СчетЗатрат = ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы;
		КонецЕсли;
		//проверка статьи КВ и аналитик
		Если СокрЛП(Строка.абс_БюджетнаяСтатья.Код) = "КВпоПост" Тогда
			Если НЕ ЗначениеЗаполнено(Строка.абс_ТЭО) ИЛИ НЕ ЗначениеЗаполнено(Строка.абс_КВ) Тогда
				ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не заполнены аналитики " +
				"ТЭО и КВ - они обязательны при указанной статье!", Отказ, "Ошибка аналитик");				
			КонецЕсли;
		КонецЕсли;
		
		//проверка статьи КВ и счетов учета
		Если абс_Статус = Перечисления.абс_СтатусыПервичныхДокументов.ДокументыПринятыБухгалтером или абс_Статус = Перечисления.абс_СтатусыПервичныхДокументов.Завершен Тогда
			Если СокрЛП(Строка.абс_БюджетнаяСтатья.Код) = "КВпоПост" Тогда
				Если НЕ (СчетаПроверочнойГруппы07 ИЛИ СчетаПроверочнойГруппы08) Тогда
					ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + Нстр("ru = 'Необходимо выбрать при установленной бюджетной статье только счета группы 07 или 08'") , Отказ, Заголовок);			
				КонецЕсли;
			КонецЕсли;
			Если СчетаПроверочнойГруппы07 ИЛИ СчетаПроверочнойГруппы08  Тогда
				Если СокрЛП(Строка.абс_БюджетнаяСтатья.Код) <> "КВпоПост" Тогда
					ttk_ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + Нстр("ru = 'Необходимо выбрать при установленных счетах группы 07 или 08 только статью Единая КВ'") , Отказ, Заголовок);
				КонецЕсли;			
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	//Крамаренко Д.М.
	//НФС 2018

	
	//АБС ИЗМЕНЕНИЕ 11.12.2013  Навценя2
	Если НЕ ЗначениеЗаполнено(абс_ЗаявкаНаТМЦ) Тогда 
		ttk_ОбщегоНазначения.абс_УстановитьРежимЗаписиДокумента(абс_Статус, Проведен, РежимЗаписи);		
		//АБС ВСТАВКА   28.03.2014 11:44:57  Гетц. Движения не делаются, если не менялось ничего, кроме статуса
		СтруктураПараметров = Новый Структура("Ссылка, ТекущийСтатус, НовыйСтатус, ДокументОбъект, Проведен", Ссылка, Ссылка.абс_Статус, абс_Статус, ЭтотОбъект, Проведен);
		РежимЗаписи = абс_СлужебныеФункции.ПолучитьРежимЗаписиПриСменеСтатуса(СтруктураПараметров, РежимЗаписи);
		//АБС ВСТАВКА  КОНЕЦ	
	Иначе
		абс_УстановитьРежимЗаписиДокументаПоЗаявкеНаТМЦ(абс_Статус, Проведен, РежимЗаписи);
	КонецЕсли;
	//АБС ИЗМЕНЕНИЕ  КОНЕЦ
 	         	
    //Перенес выше так так делаю выше перебор                               
	//Крамаренко Д.М.
	//НФС 2018
	//Для Каждого СтрокаТЧ Из Материалы Цикл
	//	Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Склад) Тогда
	//		СтрокаТЧ.Склад = Склад;
	//	КонецЕсли;
	//КонецЦикла;
	
	
	// Заполним субконто затрат
	СчетаУчетаВДокументах.ЗаполнитьСубконтоТабличнойЧасти("Материалы", ЭтотОбъект, ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете);
	
	мУдалятьДвижения = НЕ ЭтоНовый();
	
	// АБС ВСТАВКА 
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		ttk_ОбщегоНазначения.СообщитьОбОшибке("Не указан ответственный", Отказ);
	КонецЕсли;	
	// АБС ВСТАВКА КОНЕЦ
	
	//АБС ВСТАВКА Проектный учет 171213 Родин
	ИспользоватьПроектныйУчет =  глЗначениеПеременной("абс_ПроектныйУчет");
	Если ИспользоватьПроектныйУчет Тогда
		ПеренестиПроектВРБП();
	КонецЕсли;	
	//АБС ВСТАВКА Проектный учет 171213 Родин

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ОтражатьВУправленческомУчете И НЕ ОтражатьВБухгалтерскомУчете Тогда
		СтрокаСообщения = Нстр("ru = 'Документ должен принадлежать хотя бы одному из видов учета: ""Управленческий"" и (или)  ""Бухгалтерский"".'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ЭтотОбъект,,, Отказ);
	КонецЕсли;

	Если мУказаниеСкладовВТЧ Тогда
		//Склад в шапке не надо проверять, а надо проверять склад в ТЧ
		НомерУдаляемогоЭлемента = ПроверяемыеРеквизиты.Найти("Склад");
		Если НомерУдаляемогоЭлемента <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(НомерУдаляемогоЭлемента);
		КонецЕсли;
	КонецЕсли;
	Если НЕ ОтражатьВУправленческомУчете Тогда
		//Заполнение подразделения проверять не требуется
		НомерУдаляемогоЭлемента = ПроверяемыеРеквизиты.Найти("Подразделение");
		Если НомерУдаляемогоЭлемента <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(НомерУдаляемогоЭлемента);
		КонецЕсли;
	КонецЕсли;
	Если ОтражатьВБухгалтерскомУчете Тогда
		ПроверяемыеРеквизиты.Добавить("НДСвСтоимостиТоваров");	  
	КонецЕсли;
	
	УправлениеЗатратами.ПроверитьПодразделениеОрганизации(ЭтотОбъект, Отказ, "");
	Если Склад.ВидСклада = Перечисления.ВидыСкладов.НТТ Тогда
		СтрокаСообщения = Нстр("ru = 'Документ нельзя оформлять на НТТ!");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ЭтотОбъект,,, Отказ);
	КонецЕсли;
	
	// Единица измерения мест должна быть заполнена, если указано количество мест
	ОбработкаТабличныхЧастейСервер.ПроверитьЗаполненаЕдиницаИзмеренияМест(Материалы, ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	//Превышение лимита в документе должно быть установлено явно
	РазрешитьПревышениеЛимита = Ложь;
	
	//АБС ВСТАВКА   11.12.2013 Навценя2
	абс_ЗаявкаНаТМЦ = Документы.абс_ЗаявкаНаТМЦ.ПустаяСсылка();
	//АБС ВСТАВКА  КОНЕЦ
	
	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект,,ОбъектКопирования.Ссылка);
КонецПроцедуры

// АБС ВСТАВКА Согласование первичных документов
Процедура ЗаписатьНовыйСтатус(НовыйСтатус, Комментарий = Неопределено) Экспорт
	
	ТекПользователь = глЗначениеПеременной("глТекущийПользователь");
	
	НаборЗаписей = РегистрыСведений.абс_ИзменениеСтатусовПервичныхДокументов.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.ПервичныйДокумент.Установить(Ссылка);
	НаборЗаписей.Прочитать();
	
	Запись = НаборЗаписей.Добавить();
	Запись.Период = абс_СерверныеФункции.ПолучитьДатуСервера();

	Запись.ПервичныйДокумент		= Ссылка;
	Запись.Пользователь 			= ТекПользователь;	
	Запись.СтатусДокумента			= НовыйСтатус;
	
	Запись.Комментарий 				= Комментарий;
	
	ОтветственныйСотрудник = абс_БизнесПроцессы.ПолучитьСотрудникаПользователя(ТекПользователь);
	
	Если НЕ ОтветственныйСотрудник = Неопределено Тогда
		Запись.ДолжностьОтветственного	= ОтветственныйСотрудник.Должность;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция НайтиБПСогласование() Экспорт
	
	Возврат абс_БизнесПроцессы.НайтиБизнесПроцессПоПервичномуДокументу(Ссылка, "абсСогласованиеПервичныхДокументов");	
	
КонецФункции

Функция ПолучитьЗадачуПоПервичномуДокументуСогласование() Экспорт
	
	БП = НайтиБПСогласование();
	
	Если БП = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СпТочек = Новый Массив;
	
	Статус = абс_Статус;
	
	Если Статус = Перечисления.абс_СтатусыПервичныхДокументов.Подготовка Тогда
		
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеУточнениеОФК);
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеУточнениеБухгалтером);
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеОтказ);
		
	ИначеЕсли Статус = Перечисления.абс_СтатусыПервичныхДокументов.УточнениеОФК Тогда

		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеСогласованиеОФК);
		
	ИначеЕсли Статус = Перечисления.абс_СтатусыПервичныхДокументов.УточнениеБухгалтерией Тогда

		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеОбработкаПринятыхДокументов);
				
	ИначеЕсли Статус = Перечисления.абс_СтатусыПервичныхДокументов.Отказ Тогда		
		
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеОбработкаБухгалтерией);
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеСогласованиеОФК);
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеОбработкаПринятыхДокументов);
		
	ИначеЕсли Статус = Перечисления.абс_СтатусыПервичныхДокументов.Завершен Тогда
		
		//СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеОбработкаБухгалтерией);
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеОбработкаПринятыхДокументов);
				
	ИначеЕсли Статус = Перечисления.абс_СтатусыПервичныхДокументов.ОбработкаБухгалтером Тогда
		
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеСогласованиеОФК);	
		
	ИначеЕсли Статус = Перечисления.абс_СтатусыПервичныхДокументов.ДокументыПринятыБухгалтером Тогда
		
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеОбработкаБухгалтерией);
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеУточнениеБухгалтером);
		
	ИначеЕсли Статус = Перечисления.абс_СтатусыПервичныхДокументов.СогласованиеОФК Тогда
		
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеУточнениеОФК);
		СпТочек.Добавить(БизнесПроцессы.абсСогласованиеПервичныхДокументов.ТочкиМаршрута.ДействиеУточнениеБухгалтером);		
		
	КонецЕсли;
			
	Возврат абс_БизнесПроцессы.НайтиЗадачуЗЗ(БП, СпТочек ,, Ложь);

КонецФункции

Функция ПолучитьРежимЗаписиДокумента() Экспорт
	
	РежимЗаписи = Неопределено;
	
	Если 	абс_Статус = Перечисления.абс_СтатусыПервичныхДокументов.Подготовка
		ИЛИ абс_Статус = Перечисления.абс_СтатусыПервичныхДокументов.Отмена Тогда 
			
		Если Проведен Тогда
			
			РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
		Иначе
			
			РежимЗаписи = РежимЗаписиДокумента.Запись;
		КонецЕсли;
		
	Иначе
		
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
		
	КонецЕсли;
	
	//АБС ВСТАВКА 11.12.2013 Навценя2
	Если ЗначениеЗаполнено(абс_ЗаявкаНаТМЦ) И (абс_Статус = Перечисления.абс_СтатусыПервичныхДокументов.Подготовка) Тогда 
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;	
	//АБС ВСТАВКА  КОНЕЦ
	
	Возврат РежимЗаписи;
	
КонецФункции
// АБС ВСТАВКА Согласование первичных документов КОНЕЦ

//АБС ВСТАВКА №2080 НАЧАЛО
Процедура абс_СписанныеМатериалыНаОСДвижения(СтруктураШапкиДокумента,ТаблицаПоМатериалам,Отказ) Экспорт 

	Если НЕ СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		Возврат;
	КонецЕсли;    	
	
	Если НЕ Отказ Тогда
		
		НаборЗаписей = РегистрыНакопления.ПартииТоваровНаСкладахБухгалтерскийУчет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
		НаборЗаписей.Прочитать();		
		
		ТаблицаЗначений = Новый ТаблицаЗначений;
		ТаблицаЗначений.Колонки.Добавить("Период");
		ТаблицаЗначений.Колонки.Добавить("Организация");
		ТаблицаЗначений.Колонки.Добавить("ОсновноеСредство");
		ТаблицаЗначений.Колонки.Добавить("Номенклатура");
		ТаблицаЗначений.Колонки.Добавить("ХарактеристикаНоменклатуры");
		ТаблицаЗначений.Колонки.Добавить("СерияНоменклатуры");	
		ТаблицаЗначений.Колонки.Добавить("Количество");
		ТаблицаЗначений.Колонки.Добавить("Стоимость");
			
		Для Каждого Строка Из НаборЗаписей Цикл
			
			НайденнаяСтрока = ТаблицаПоМатериалам.Найти(Строка.НомерСтрокиСписанныхТоваров,"НомерСтрокиДокумента");
			Если НайденнаяСтрока = Неопределено Тогда
            	Продолжить;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(НайденнаяСтрока.абс_ОсновныеСредства) Тогда
				СТР = ТаблицаЗначений.Добавить();
				ЗаполнитьЗначенияСвойств(СТР,Строка);
				СТР.ОсновноеСредство = НайденнаяСтрока.абс_ОсновныеСредства;
			КонецЕсли;
			
		КонецЦикла;
		
		ТаблицаЗначений.Свернуть("Период,Организация,ОсновноеСредство,Номенклатура,ХарактеристикаНоменклатуры,СерияНоменклатуры","Количество,Стоимость");
		
		НаборЗаписей = РегистрыНакопления.абс_СписанныеМатериалыНаОС.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ЭтотОбъект.Ссылка);
		НаборЗаписей.Прочитать();
		
		Для Каждого СтрокаТЧ Из ТаблицаЗначений Цикл
			
			Строка = НаборЗаписей.Добавить();
			Строка.Период                     = СтрокаТЧ.Период;
			Строка.Организация                = СтрокаТЧ.Организация;
			Строка.ОсновноеСредство           = СтрокаТЧ.ОсновноеСредство;
			Строка.Номенклатура               = СтрокаТЧ.Номенклатура;
			Строка.ХарактеристикаНоменклатуры = СтрокаТЧ.ХарактеристикаНоменклатуры;
			Строка.СерияНоменклатуры          = СтрокаТЧ.СерияНоменклатуры;
			Строка.Количество                 = СтрокаТЧ.Количество;
			Строка.Сумма                      = СтрокаТЧ.Стоимость;
			
		КонецЦикла;
		
		НаборЗаписей.Записать(Истина);
		
	КонецЕсли;	
	
КонецПроцедуры
//\\АБС ВСТАВКА №2080 КОНЕЦ

//АБС ВСТАВКА  11.12.2013  Навценя2 
Процедура абс_УстановитьРежимЗаписиДокументаПоЗаявкеНаТМЦ(СтатусДокумента, Проведен, РежимЗаписи) Экспорт 
	
	Если не РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли;
	
	СписокСтатусовДляКонтроля = Новый СписокЗначений;
	СписокСтатусовДляКонтроля.Добавить(Перечисления.абс_СтатусыПервичныхДокументов.Отказ);
	СписокСтатусовДляКонтроля.Добавить(Перечисления.абс_СтатусыПервичныхДокументов.Отмена);
		
	Если не СписокСтатусовДляКонтроля.НайтиПоЗначению(СтатусДокумента) = Неопределено Тогда 		
		Если Проведен Тогда			
			РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
		Иначе                			
			РежимЗаписи = РежимЗаписиДокумента.Запись;
		КонецЕсли;  		  		
	Иначе         		
		РежимЗаписи = РежимЗаписиДокумента.Проведение; 		
	КонецЕсли; 	
	
	Если СтатусДокумента = Перечисления.абс_СтатусыПервичныхДокументов.Подготовка И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ДополнительныеСвойства.Вставить("абс_ПроведениеПоОстаткам", Истина);
	Иначе
		ДополнительныеСвойства.Вставить("абс_ПроведениеПоОстаткам", Ложь);	 
	КонецЕсли;
			
КонецПроцедуры	

Функция абс_ПроведениеПоОстаткам()  
	
	Если ДополнительныеСвойства.Свойство("абс_ПроведениеПоОстаткам") Тогда
		Если ТипЗнч(ДополнительныеСвойства.абс_ПроведениеПоОстаткам) = Тип("Булево") Тогда 
			Возврат ДополнительныеСвойства.абс_ПроведениеПоОстаткам;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции
//АБС ВСТАВКА  КОНЕЦ


мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
мИндексСтрокиСписанныеТовары = 0;

мУказаниеПроектовВТабличнойЧастиДокументов = УправлениеПроектами.УказаниеПроектовВТабличнойЧастиДокументов();

УказаниеСкладов = глЗначениеПеременной("УказаниеСкладовВТабличнойЧастиДокументов");

мУказаниеСкладовВТЧ = (УказаниеСкладов = Перечисления.ВариантыУказанияСкладовВТабличнойЧастиДокументов.ДляДокументовПоступленияРеализации)
                  Или (УказаниеСкладов = Перечисления.ВариантыУказанияСкладовВТабличнойЧастиДокументов.ДляДокументовРеализации);
		  
