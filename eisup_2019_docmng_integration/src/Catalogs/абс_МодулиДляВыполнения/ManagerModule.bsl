Процедура ВыполнитьМодуль(Ссылка, СтруктураПараметров = Неопределено) Экспорт
	
	Если СтруктураПараметров = Неопределено Тогда
		СтруктураПараметров = ПолучитьСтруктуруПараметров(Ссылка);
	КонецЕсли;
	
	Если СтруктураПараметров = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекстыЗапросов = Неопределено;
	
	Попытка 
		ТекстыЗапросов = ПолучитьСтруктуруТекстовЗапросов(Ссылка);
	Исключение
		Сообщить("Ошибка при заполнении структуры запросов:" + Символы.ПС + ОписаниеОшибки());		
	КонецПопытки;
	
	Если Ссылка.ВидВыполнения = Перечисления.абс_ВидыВыполненияМодулей.НаСервере Тогда
		
		абс_СерверныеФункции.ВыполнитьКодНаСервере(Ссылка.ТекстМодуля, СтруктураПараметров, ТекстыЗапросов);
		
	Иначе
		
		ВыполнитьКодНаКлиенте(Ссылка.ТекстМодуля, СтруктураПараметров, ТекстыЗапросов);
		
	КонецЕсли;	
	
КонецПроцедуры

Функция ПолучитьСтруктуруТекстовЗапросов(Ссылка, Отказ = Ложь) Экспорт
	
	СтруктураТекстовЗапросов = Новый Структура;
	
	Для Каждого СтрокаЗапрос Из Ссылка.Запросы Цикл
		
		СтруктураТекстовЗапросов.Вставить(СтрокаЗапрос.ИмяЗапроса, СтрокаЗапрос.ТекстЗапроса);
		
	КонецЦикла;
	
	Возврат СтруктураТекстовЗапросов;
	
КонецФункции

Функция ПолучитьСтруктуруПараметров(Ссылка, Отказ = Ложь) Экспорт
	
	СтруктураПараметров = Новый Структура;
	
	Для Каждого СтрокаПараметра Из Ссылка.Параметры Цикл
		
		Попытка 
			
			СтруктураПараметров.Вставить(СтрокаПараметра.ИмяПараметра, СтрокаПараметра.ЗначениеПараметра);
			
		Исключение
			
			Отказ = Истина;
			Сообщить("Ошибка при установке параметра № " + СтрокаПараметра.НомерСтроки + ": " + СтрокаПараметра.ИмяПараметра + "");
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция ПолучитьТаблицуПараметров(Ссылка, Отказ = Ложь) Экспорт
	
	ТаблицаПараметров = Новый ТаблицаЗначений;
	ТаблицаПараметров.Колонки.Добавить("ИмяПараметра");
	ТаблицаПараметров.Колонки.Добавить("ЭтоВыражение");
	ТаблицаПараметров.Колонки.Добавить("ЗначениеПараметра");
	
	Для Каждого СтрокаПараметраМодуля Из Ссылка.Параметры Цикл
		
		ЗаполнитьЗначенияСвойств(ТаблицаПараметров.Добавить(), СтрокаПараметраМодуля);
		
	КонецЦикла;
	
	Возврат ТаблицаПараметров;
	
КонецФункции


Процедура ВыполнитьКодНаКлиенте(ТекстМодуля, СтруктураПараметров = Неопределено, СтруктураЗапросов = Неопределено) Экспорт
	
	Выполнить(ТекстМодуля);
	
КонецПроцедуры


