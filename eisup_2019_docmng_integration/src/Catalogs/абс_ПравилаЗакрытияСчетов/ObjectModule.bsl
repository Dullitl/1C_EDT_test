
Процедура ПередЗаписью(Отказ)
	
	Если НЕ ОбменДанными.Загрузка Тогда
		
		КонтрольЗаполненияИстории(Отказ);
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура КонтрольЗаполненияИстории(Отказ)
	
	Если НЕ Модифицированность() или ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;	
	
	//Для истории записи обработок
	Если ПометкаУдаления и не ссылка.ПометкаУдаления Тогда
		
		врСтрока = абс_ИсторияИзменения.Добавить();
		врСтрока.Период = ТекущаяДата();
		врСтрока.Владелец = глЗначениеПеременной("глТекущийПользователь");
		врСтрока.Разработчик = глЗначениеПеременной("глТекущийПользователь");
		врСтрока.Описание = "Пометил на удаление элемент.";
		
	КонецЕсли;
	
	Если не ПометкаУдаления и ссылка.ПометкаУдаления Тогда
		
		врСтрока = абс_ИсторияИзменения.Добавить();
		врСтрока.Период = ТекущаяДата();
		врСтрока.Владелец = глЗначениеПеременной("глТекущийПользователь");
		врСтрока.Разработчик = глЗначениеПеременной("глТекущийПользователь");
		врСтрока.Описание = "Снял пометку удаления элемента.";
		
	КонецЕсли;		
	
	Если не ПометкаУдаления и не ссылка.ПометкаУдаления Тогда
		
		//Проверка, что есть хотя бы одна строка в тч
		Если Не ЗначениеЗаполнено(абс_ИсторияИзменения) Тогда
			Сообщить("Заполните поле владелец и описание (не менее 20 символов) в табличной части:""История изменения""",СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;	
		
		//Проверка полей
		НайденаСтрокаВПределахЧаса = Ложь;
		Для каждого Строка Из абс_ИсторияИзменения Цикл
			
			//Проверяем только записи которые были сегодня в пределах 1-го часа
			Если ЗначениеЗаполнено(Строка.Период) и Строка.Период < (ТекущаяДата() - 3600) Тогда
				Продолжить;
			КонецЕсли;	
			
			Если не Строка.Разработчик = глЗначениеПеременной("глТекущийПользователь") Тогда
				Продолжить;
			КонецЕсли;	
			
			НайденаСтрокаВПределахЧаса = Истина;
			
			Если СтрДлина(Строка.Описание) < 20 
				или НЕ ЗначениеЗаполнено(Строка.Владелец) 
				или НЕ ЗначениеЗаполнено(Строка.Разработчик)
				или НЕ ЗначениеЗаполнено(Строка.Период)Тогда
				
				Сообщить("Заполните поле владелец и описание (не менее 20 символов) в табличной части:""История изменения""",СтатусСообщения.Важное);
				Отказ = Истина;
				
			КонецЕсли;
			
		КонецЦикла;	
		
		Если НЕ НайденаСтрокаВПределахЧаса Тогда 
			
			Сообщить("Не найдено ни одной новой строки в тч ""История изменения"" по разработчику: "+глЗначениеПеременной("глТекущийПользователь")+"!",СтатусСообщения.Важное);
			Отказ = Истина;
			
		КонецЕсли;	
		
	КонецЕсли;   		
	
КонецПроцедуры	