
&НаКлиенте
Процедура НазначениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Попытка 
		ЭлектроннаяПодписьКлиент.ОткрытьСертификат(Объект.Отпечаток);
	Исключение
		Предупреждение(НСтр("ru = 'Невозможно открыть сертификат. Возможно он не установлен на локальный компьютер.'"));
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТабличнуюЧастьПоВидамДокументов();
	ОпределитьДоступностьСоставаИсполнителей();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьПоВидамДокументов()
	
	Для Каждого ЗначениеПеречисления Из Перечисления.ВидыЭД Цикл
		Если ЗначениеПеречисления = Перечисления.ВидыЭД.СчетФактура ИЛИ
			ЗначениеПеречисления = Перечисления.ВидыЭД.ПроизвольныйЭД ИЛИ 
			ЗначениеПеречисления = Перечисления.ВидыЭД.Извещение ИЛИ
			ЗначениеПеречисления = Перечисления.ВидыЭД.Подтверждение Тогда
				Продолжить;
		КонецЕсли;
		МассивСтрок = Объект.ВидыДокументов.НайтиСтроки(Новый Структура("ВидДокумента",ЗначениеПеречисления));
		Если МассивСтрок.Количество()=0 Тогда 
			НоваяСтрокаТЧ = Объект.ВидыДокументов.Добавить();
			НоваяСтрокаТЧ.ВидДокумента = ЗначениеПеречисления;
			НоваяСтрокаТЧ.ИспользоватьДляПодписи = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Для Каждого ЭлементТаблицы Из Объект.ВидыДокументов Цикл
		ЭлементТаблицы.ИспользоватьДляПодписи = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	
	Для Каждого ЭлементТаблицы Из Объект.ВидыДокументов Цикл
		ЭлементТаблицы.ИспользоватьДляПодписи = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьДоступностьСоставаИсполнителей()
	
	Элементы.СоставИсполнителей.Доступность = Объект.ПроверятьСоставИсполнителей;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьСоставИсполнителейПриИзменении(Элемент)
	
	ОпределитьДоступностьСоставаИсполнителей();
	
КонецПроцедуры

