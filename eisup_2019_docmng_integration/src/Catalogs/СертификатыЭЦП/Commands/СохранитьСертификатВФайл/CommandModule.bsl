
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
#Если ВебКлиент Тогда
	Если НЕ ПодключитьРасширениеРаботыСКриптографией() Тогда
		Предупреждение(НСтр("ru = 'Расширение для работы с криптографией не подключено, операция прервана.'"));
		Возврат;
	КонецЕсли;
#КонецЕсли

    СертификатДляСохранения = ПараметрКоманды;
	ПараметрыСертификата = ВернутьПараметрыСертификата(СертификатДляСохранения);
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Фильтр = НСтр("ru = 'Все файлы(*.cer)|*.cer'");
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ИмяФайлаПоУмолчанию = ПараметрыСертификата.Наименование;
	ЭлектронныеДокументыКлиент.СкорректироватьИмяФайла(ИмяФайлаПоУмолчанию);
	ДиалогОткрытияФайла.ПолноеИмяФайла = ИмяФайлаПоУмолчанию;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файл для сохранения сертификата'");
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		// если были переданы двоичные данные сертификата, то сохраним их, иначе - из хранилища сертификатов компьютера
		Если ПараметрыСертификата.Свойство("ДвоичныеДанныеСертификата") 
			И ЗначениеЗаполнено(ПараметрыСертификата.ДвоичныеДанныеСертификата) Тогда
			ПараметрыСертификата.ДвоичныеДанныеСертификата.Записать(ДиалогОткрытияФайла.ПолноеИмяФайла);
		Иначе
			Попытка
				МенеджерКриптографии = ЭлектроннаяПодписьКлиент.ПолучитьМенеджерКриптографии();
			Исключение
				ЭлектронныеДокументыКлиент.ОбработатьИсключениеПоЭДНаКлиенте(НСтр("ru = 'сохранение сертификата подписи в файл'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				Возврат;
			КонецПопытки;
			ДвоичныеДанныеОтпечатка = Base64Значение(ПараметрыСертификата.Отпечаток);
			Сертификат = Неопределено;
			ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов();
			Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
			Если Сертификат = Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сертификат не найден'"));
				Возврат;
			КонецЕсли;
			Сертификат.Выгрузить(ДиалогОткрытияФайла.ПолноеИмяФайла);
		КонецЕсли;
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сертификат ЭЦП сохранен в файл ""%1""'"),
																			ДиалогОткрытияФайла.ПолноеИмяФайла);
		Состояние(Текст);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ВернутьПараметрыСертификата(СсылкаНаСертификат)
	
	ПараметрыСертификата = Новый Структура;
	ПараметрыСертификата.Вставить("Отпечаток", СсылкаНаСертификат.Отпечаток);
	ПараметрыСертификата.Вставить("Наименование", СсылкаНаСертификат.Наименование);
	ПараметрыСертификата.Вставить("ДвоичныеДанныеСертификата", СсылкаНаСертификат.ФайлСертификата.Получить());
	Возврат ПараметрыСертификата;
	
КонецФункции
