
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	#Если ВебКлиент Тогда
		Если НЕ ПодключитьРасширениеРаботыСКриптографией() Тогда
			Предупреждение(НСтр("ru = 'Расширение для работы с криптографией не подключено, операция прервана.'"));
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.МножественныйВыбор = Ложь;
	Диалог.ПроверятьСуществованиеФайла = Истина;
	Диалог.Заголовок 	= "Выберите файл с сертификатом подписи";
	Фильтр = НСтр("ru = 'Все файлы(*.cer)|*.cer'");
	Диалог.Фильтр		= Фильтр;
	Диалог.Каталог 		= ФайловыеФункцииКлиент.ПолучитьПутьККаталогуДанныхПользователя();
	Если Диалог.Выбрать() Тогда
		ФайлСертификата = Диалог.ПолноеИмяФайла;
	Иначе 
		Возврат;
	КонецЕсли;

	Попытка
		МенеджерКриптографии = ЭлектроннаяПодписьКлиент.ПолучитьМенеджерКриптографии();
	Исключение
		ЭлектронныеДокументыКлиент.ОбработатьИсключениеПоЭДНаКлиенте(НСтр("ru = 'загрузка сертификата из файла'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	Попытка 
		НовыйСертификат = Новый СертификатКриптографии(ФайлСертификата);
	Исключение
		Предупреждение(НСтр("ru = 'Файл сертификата должен быть в формате DER X.509, операция прервана.'"));
		Возврат;
	КонецПопытки;
	
	СтруктураСертификата = ЭлектроннаяПодписьКлиентСервер.ЗаполнитьСтруктуруСертификата(НовыйСертификат);
	ДвоичныеДанныеСертификата = Новый ДвоичныеДанные(ФайлСертификата);
	СтруктураСертификата.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
	
	Организация = ОткрытьФормуМодально("Справочник.СертификатыЭЦП.Форма.ВыборОрганизации");
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") И ЗначениеЗаполнено(Организация) Тогда
		СтруктураСертификата.Вставить("Организация", Организация);
	Иначе
		Возврат;
	КонецЕсли;
	
	ОписаниеОшибки = "";
	СсылкаНаОбъект = ЭлектронныеДокументы.ЗагрузитьСертификат(СтруктураСертификата, ОписаниеОшибки);
	Если НЕ ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Предупреждение(ОписаниеОшибки);
		Возврат;
	КонецЕсли;

	ПараметрыФормы = Новый Структура("Ключ", СсылкаНаОбъект);
	ОткрытьФорму("Справочник.СертификатыЭЦП.Форма.ФормаЭлемента",ПараметрыФормы);
	ОповеститьОбИзменении(СсылкаНаОбъект);
	Оповестить("ОбновитьСписокСертификатов");

КонецПроцедуры
