
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
#Если ВебКлиент Тогда
	Если НЕ ПодключитьРасширениеРаботыСКриптографией() Тогда
		Предупреждение(НСтр("ru = 'Расширение для работы с криптографией не подключено, операция прервана.'"));
		Возврат;
	КонецЕсли;
#КонецЕсли
	Попытка
		МенеджерКриптографии = ЭлектроннаяПодписьКлиент.ПолучитьМенеджерКриптографии();
	Исключение
		ЭлектронныеДокументыКлиент.ОбработатьИсключениеПоЭДНаКлиенте(НСтр("ru = 'загрузка сертификата подписи из реестра'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	МассивСтруктурЛичныхСертификатов = ЭлектроннаяПодписьКлиент.ПолучитьМассивСтруктурСертификатов(Истина); // ТолькоЛичные
	
	ПараметрыФормы = Новый Структура("МассивСтруктурСертификатов", МассивСтруктурЛичныхСертификатов);
	ПараметрыФормы.Вставить("ЗагрузкаСертификата", Истина);
	СтруктураВозврата = ОткрытьФормуМодально("ОбщаяФорма.ВыборСертификата", ПараметрыФормы);
	
	Если ТипЗнч(СтруктураВозврата) = Тип("Структура") Тогда
		
		Отпечаток = СтруктураВозврата.Отпечаток;
		
		СтруктураСертификата = ЭлектроннаяПодписьКлиент.ЗаполнитьСтруктуруСертификатаПоОтпечатку(Отпечаток);
		
		Сертификат = ЭлектроннаяПодписьКлиент.ПолучитьСертификатПоОтпечатку(Отпечаток, Истина); // ТолькоЛичные
		
		ДвоичныеДанныеСертификата = Сертификат.Выгрузить();
		СтруктураСертификата.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
		
		Организация = ОткрытьФормуМодально("Справочник.СертификатыЭЦП.Форма.ВыборОрганизации");
		Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") И ЗначениеЗаполнено(Организация) Тогда
			СтруктураСертификата.Вставить("Организация", Организация);
		Иначе
			Возврат;
		КонецЕсли;
		
		ОписаниеОшибки = "";
		СсылкаНаОбъект = ЭлектронныеДокументы.ЗагрузитьСертификат(СтруктураСертификата, ОписаниеОшибки);
		Если НЕ ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
			Предупреждение(ОписаниеОшибки);
			Возврат;
		КонецЕсли;
		ПараметрыФормы = Новый Структура("Ключ", СсылкаНаОбъект);
		ОткрытьФорму("Справочник.СертификатыЭЦП.Форма.ФормаЭлемента", ПараметрыФормы);
		ОповеститьОбИзменении(СсылкаНаОбъект);
		Оповестить("ОбновитьСписокСертификатов");
				
	КонецЕсли;	
	
КонецПроцедуры
