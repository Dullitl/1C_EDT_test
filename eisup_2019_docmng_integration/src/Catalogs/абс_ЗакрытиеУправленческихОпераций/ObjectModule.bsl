Перем мТекущийПользователь Экспорт;
Перем мРолиПользователя Экспорт;
Перем ПрошлыйСписокСтатей Экспорт;

Процедура ПередЗаписью(Отказ)
	
	Если мРолиПользователя.Найти(Справочники.РолиИсполнителей.ЗакрытиеОпераций_в_УУ) = Неопределено
		и мРолиПользователя.Найти(Справочники.РолиИсполнителей.СуперПользователь) = Неопределено Тогда
		ttk_ОбщегоНазначения.СообщитьОбОшибке("Записать элемент может сотрудник с ролью ""Закрытие операций в УУ""!",Отказ);
		Возврат;
	КонецЕсли;	
	
	Если ОбменДанными.Загрузка или не Модифицированность() Тогда
		Возврат;
	КонецЕсли;		
	
	Год = НачалоГода(Год);	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Год",Год);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	абс_ЗакрытиеУправленческихОпераций.Год
	|ИЗ
	|	Справочник.абс_ЗакрытиеУправленческихОпераций КАК абс_ЗакрытиеУправленческихОпераций
	|ГДЕ
	|	абс_ЗакрытиеУправленческихОпераций.Ссылка <> &Ссылка
	|	И абс_ЗакрытиеУправленческихОпераций.Год МЕЖДУ НАЧАЛОПЕРИОДА(&Год, ГОД) И КОНЕЦПЕРИОДА(&Год, ГОД)
	|	И (НЕ абс_ЗакрытиеУправленческихОпераций.ПометкаУдаления)
	|
	|СГРУППИРОВАТЬ ПО
	|	абс_ЗакрытиеУправленческихОпераций.Год";
	Если не Запрос.Выполнить().Пустой() Тогда
		ttk_ОбщегоНазначения.СообщитьОбОшибке("С данным годом "+СтрЗаменить(Год(Год),Символы.НПП,"")+" уже есть записи!",Отказ);
		Возврат;
	КонецЕсли;	
	
	История = "";
	Если Год(Ссылка.Год) = Год(Год) Тогда
		Для каждого Стр Из Ссылка.ПериодЗакрытия Цикл
			Строка = ПериодЗакрытия[Стр.НомерСтроки-1];
			Если Строка.Закрыт <> Стр.Закрыт Тогда
				История = История + "; " + Символы.ПС + ?(Строка.Закрыт,"Закрыл период: "+Формат(Строка.Период,"ДФ='ММMM yyyy ""г.""'"),"Открыл период: "+Формат(Строка.Период,"ДФ='ММMM yyyy ""г.""'")); 	
			КонецЕсли;	
		КонецЦикла;	
		История = Сред(История,4);
	Иначе
		Если Не ЗначениеЗаполнено(Ссылка.Год) Тогда
			История = "Установлен год "+СтрЗаменить(Год(Год),Символы.НПП,"");
		Иначе	
			История = "Изменен год с "+СтрЗаменить(Год(Ссылка.Год),Символы.НПП,"")+" на "+СтрЗаменить(Год(Год),Символы.НПП,"");
		КонецЕсли;
		Для каждого Строка Из ПериодЗакрытия Цикл
			История = История + "; " + Символы.ПС + ?(Строка.Закрыт,"Закрыл период: "+Формат(Строка.Период,"ДФ='ММMM yyyy ""г.""'"),"Открыл период: "+Формат(Строка.Период,"ДФ='ММMM yyyy ""г.""'")); 	
		КонецЦикла;			
	КонецЕсли;
	
	Если СокрЛП(История) = "" Тогда
		История = "Период не менялся";
	КонецЕсли;	
	
	//для хранения истории изменений
	Строка = ИсторияИзменений.Добавить();
	Строка.ДатаИзменения = абс_СерверныеФункции.ПолучитьДатуСервера();
	Строка.История       = История;
	Строка.Ответственный = глЗначениеПеременной("глТекущийПользователь");
	Строка.ПрошлыйСписокСтатей = ПрошлыйСписокСтатей;
	Строка.Комментарий   = "Изменил:";
	
КонецПроцедуры

мТекущийПользователь 	= глЗначениеПеременной("глТекущийПользователь");
мРолиПользователя 		= абс_БизнесПроцессы.ПолучитьСписокДоступныхРолейПользователя(мТекущийПользователь);
ПрошлыйСписокСтатей     = "";