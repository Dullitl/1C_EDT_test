Перем ПрошлыйИзмененныйРодительОбъектаДоступа;

Процедура ПередЗаписью(Отказ)
	
	Если НЕ ОбменДанными.Загрузка Тогда
		
		ПрошлыйИзмененныйРодительОбъектаДоступа = ?(Не ЭтоНовый() и Не Ссылка.Родитель = Родитель, Ссылка.Родитель, Неопределено);
		НастройкаПравДоступа.ПередЗаписьюНовогоОбъектаСПравамиДоступаПользователей(ЭтотОбъект, Отказ, Родитель);
		
		#Если Клиент Тогда
		Если ВидОбработки = Перечисления.ВидыДополнительныхВнешнихОбработок.ЗаполнениеТабличныхЧастей Тогда
				
			// Сообщить о неуказанных табличных частей для заполнения.
			Для Каждого СтрокаПринадлежность Из Принадлежность Цикл
					
				Если ПустаяСтрока(СтрокаПринадлежность.ТабличнаяЧастьИмя) ИЛИ ПустаяСтрока(СтрокаПринадлежность.ТабличнаяЧастьПредставление) Тогда
					Сообщить("Не указана табличная часть для заполнения: " + СтрокаПринадлежность.ПредставлениеОбъекта, СтатусСообщения.Внимание);
				КонецЕсли;
					
			КонецЦикла;
				
		КонецЕсли;
		#КонецЕсли
		
		//Для истории записи обработок
		Если Модифицированность() и ПометкаУдаления и не ссылка.ПометкаУдаления и НЕ ЭтоГруппа Тогда
			
			врСтрока = абс_ИсторияИзменения.Добавить();
			врСтрока.Период = ТекущаяДата();
			врСтрока.Владелец = глЗначениеПеременной("глТекущийПользователь");
			врСтрока.Разработчик = глЗначениеПеременной("глТекущийПользователь");
			врСтрока.Описание = "Пометил на удаление элемент.";
			
		КонецЕсли;
		
		Если Модифицированность() и не ПометкаУдаления и ссылка.ПометкаУдаления и НЕ ЭтоГруппа Тогда
			
			врСтрока = абс_ИсторияИзменения.Добавить();
			врСтрока.Период = ТекущаяДата();
			врСтрока.Владелец = глЗначениеПеременной("глТекущийПользователь");
			врСтрока.Разработчик = глЗначениеПеременной("глТекущийПользователь");
			врСтрока.Описание = "Снял пометку удаления элемента.";
			
		КонецЕсли;		
			
		Если Модифицированность() и не ПометкаУдаления и не ссылка.ПометкаУдаления и НЕ ЭтоГруппа Тогда
	                              			
			//Проверка, что есть хотя бы одна строка в тч
			Если Не ЗначениеЗаполнено(абс_ИсторияИзменения) Тогда
				Сообщить("Заполните поле владелец и описание (не менее 20 символов) в табличной части:""История изменения""",СтатусСообщения.Важное);
				Отказ = Истина;
			КонецЕсли;	
			
			//Проверка полей
			НайденаСтрокаВПределахЧаса = Ложь;
			Для каждого Строка Из абс_ИсторияИзменения Цикл
				
				//Проверяем только записи которые были сегодня
				Если ЗначениеЗаполнено(Строка.Период) и Строка.Период < (ТекущаяДата() - 3600) Тогда
					Продолжить;
				КонецЕсли;	
				
				Если не Строка.Разработчик = глЗначениеПеременной("глТекущийПользователь") Тогда
					Продолжить;
				КонецЕсли;				
				
				НайденаСтрокаВПределахЧаса = Истина;
				
				Если СтрДлина(Строка.Описание) < 20 
					или НЕ ЗначениеЗаполнено(Строка.Владелец) 
					или НЕ ЗначениеЗаполнено(Строка.Разработчик)
					или НЕ ЗначениеЗаполнено(Строка.Период)Тогда
					
					Сообщить("Заполните поле владелец и описание (не менее 20 символов) в табличной части:""История изменения""",СтатусСообщения.Важное);
					Отказ = Истина;
				
				КонецЕсли;
				
			КонецЦикла;	
			
			Если НЕ НайденаСтрокаВПределахЧаса Тогда 
				
				Сообщить("Не найдено ни одной новой строки в тч ""История изменения"" по разработчику: "+глЗначениеПеременной("глТекущийПользователь")+"!",СтатусСообщения.Важное);
				Отказ = Истина;
				
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОбменДанными.Загрузка Тогда
		
		НастройкаПравДоступа.ОбновитьПраваДоступаКИерархическимОбъектамПриНеобходимости(Ссылка,ПрошлыйИзмененныйРодительОбъектаДоступа, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры


