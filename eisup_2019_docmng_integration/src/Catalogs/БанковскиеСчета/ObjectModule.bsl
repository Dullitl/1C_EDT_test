Перем мВалютаРегламентированногоУчета Экспорт;
Перем ЭтоВалютныйСчет Экспорт;

Функция ПроверитьКорректностьНомераСчета(Номер, ТекстОшибки = "") Экспорт

	Если НЕ ТипЗнч(Номер) = Тип("Строка") Тогда
		ТекстОшибки = "Номер счета должен быть строкой.";
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(Номер) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЭтоВалютныйСчет И СтрДлина(Номер) <> 20 Тогда
			ТекстОшибки = "Номер счета должен состоять из 20 знаков.";
			Возврат Ложь;
	КонецЕсли;

	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(Номер) Тогда
		ТекстОшибки = "В номере счета есть не только цифры. Возможно, номер указан неверно.";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьНомераСчета()

Функция ПроверитьКорректностьБИК(БИК, ТекстОшибки = "") Экспорт

	Если НЕ ТипЗнч(БИК) = Тип("Строка") Тогда
		ТекстОшибки = "БИК банка должен быть строкой.";
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(БИК) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтрДлина(БИК) <> 9 Тогда
		ТекстОшибки = "БИК банка должен состоять из 9 знаков.";
		Возврат Ложь;
	КонецЕсли;

	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(БИК) Тогда
		ТекстОшибки = "В составе БИК банка должны быть только цифры.";
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ Лев(БИК, 2) = "04" Тогда
		ТекстОшибки = "Первые 2 цифры БИК банка должны быть ""04"".";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьБИК()

Функция ПроверитьКорректностьКоррсчета(Коррсчет, ТекстОшибки = "") Экспорт

	Если НЕ ТипЗнч(Коррсчет) = Тип("Строка") Тогда
		ТекстОшибки = "Корр.счет банка должен быть строкой.";
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(Коррсчет) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтрДлина(Коррсчет) <> 20 Тогда
		ТекстОшибки = "Корр.счет банка должен состоять из 20 знаков.";
		Возврат Ложь;
	КонецЕсли;

	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(Коррсчет) Тогда
		ТекстОшибки = "В составе корр.счета банка должны быть только цифры.";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьКоррсчета()

// Обработчик события ПередЗаписью формы.
//
Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПометкаУдаления ИЛИ Ссылка.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	// АБС ВСТАВКА Фролов 
	// По Пекину разрешаем создавать счета без контроля
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации") И Владелец.ИностраннаяОрганизация Тогда
		Возврат;		
	КонецЕсли;	
	// АБС ВСТАВКА Фролов КОНЕЦ
	
	//АБС ВСТАВКА 33082  25.11.2013 17:08:30  Поставка
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Контрагенты") Тогда
		СтатусКонтрагента = Владелец.абс_СтатусКонтрагента;
		
		Если СтатусКонтрагента = Перечисления.абсСтатусыКонтрагентов.Подготовка ИЛИ 
			 СтатусКонтрагента = Перечисления.абсСтатусыКонтрагентов.Отказ ИЛИ
			 СтатусКонтрагента = Перечисления.абсСтатусыКонтрагентов.Закрыт Тогда
			 
			 ttk_ОбщегоНазначения.СообщитьОбОшибке("По контрагентам в статусах ""Подготовка"", ""Отказ"", ""Закрыт"" запрещено вводить банковские счета!", Отказ);
			 
		КонецЕсли;		
	КонецЕсли;
	//АБС ВСТАВКА 33082 КОНЕЦ 
	
	Заголовок = "Запись банковского счета (" + Владелец + ") " + Наименование + ", код " + Код;
	
	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(СокрЛП(НомерСчета)) Тогда 
		ttk_ОбщегоНазначения.СообщитьОбОшибке("В номере банковского счета присутствуют не только цифры. 
		|Возможно, номер указан неправильно", , Заголовок);
	КонецЕсли;

	// Проверка заполнения обязательных реквизитов
	Если НЕ ЗначениеЗаполнено(ВалютаДенежныхСредств) Тогда
		ttk_ОбщегоНазначения.СообщитьОбОшибке("Не выбрана валюта.", Отказ, Заголовок);
	КонецЕсли;
	Если ВалютаДенежныхСредств = мВалютаРегламентированногоУчета Тогда
	
		Если НЕ ЗначениеЗаполнено(НомерСчета) Тогда
			ttk_ОбщегоНазначения.СообщитьОбОшибке("Не указан номер счета.", Отказ, Заголовок);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Банк) Тогда
			ttk_ОбщегоНазначения.СообщитьОбОшибке("Не выбран банк.", Отказ, Заголовок);
		КонецЕсли;
	
	КонецЕсли;
	
	Если абс_ОграничитьДоступПоОрганизации И НЕ ЗначениеЗаполнено(абс_Организация) Тогда  //АБС-Пр2 11341
		ttk_ОбщегоНазначения.СообщитьОбОшибке("Не выбрана организация.", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");

