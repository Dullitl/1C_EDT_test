Перем СохраненнаяНастройка Экспорт;        // Текущий вариант отчета

Перем ТаблицаВариантовОтчета Экспорт;      // Таблица вариантов доступных текущему пользователю
Перем ДатаНачалаНП, ДатаКонцаНП;
Перем  УчитыватьОбособленноеПодразделение Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция СформироватьОтчет(Результат = Неопределено, ДанныеРасшифровки = Неопределено, ВыводВФормуОтчета = Истина) Экспорт
	
	ЗначениеПанелипользователя = ТиповыеОтчеты.ПолучитьЗначенияНастроекПанелиПользователяОбъекта(ЭтотОбъект);
	НастрокаПоУмолчанию        = КомпоновщикНастроек.ПолучитьНастройки();
	ТиповыеОтчеты.ПолучитьПримененуюНастройку(ЭтотОбъект);
	
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("НачалоПериода"));
	Если ЗначениеПараметра <> Неопределено Тогда
		ДатаНачалаНП =  Дата(ЗначениеПараметра.Значение);
	КонецЕсли;
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("КонецПериода"));
	Если ЗначениеПараметра <> Неопределено Тогда
		ДатаКонцаНП = Дата(ЗначениеПараметра.Значение);
	КонецЕсли;
	
	Если ДатаНачалаНП = '00010101'  тогда
		ДатаНачалаНП = НачалоМесяца(ТекущаяДата());
	КонецЕсли;
	Если ДатаКонцаНП = '00010101' тогда
		ДатаКонцаНП = КонецМесяца(ТекущаяДата());
	КонецЕсли;
	
	Если ЗначениеПанелипользователя <> Неопределено тогда
		УчитыватьОбособленноеПодразделение = ЗначениеПанелипользователя.ГруппироватьПоОрганизации;
	КонецЕсли;
	
	Группировка = ТиповыеОтчеты.ДобавитьГруппировку(КомпоновщикНастроек, "");
	Группировка.ПоляГруппировки.Элементы.Очистить();
	
	КомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();
	ТиповыеОтчеты.ДобавитьВыбранноеПоле(КомпоновщикНастроек, "Организация");
	ТиповыеОтчеты.ДобавитьВыбранноеПоле(КомпоновщикНастроек, "ГоловнаяОрганизация");
	ТиповыеОтчеты.ДобавитьВыбранноеПоле(КомпоновщикНастроек, "ФизЛицо");
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Попытка 
		
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
		ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		
		ТаблицаЗначений =  Новый ТаблицаЗначений;
		ПроцессорВывода.УстановитьОбъект(ТаблицаЗначений);
		ПроцессорВывода.Вывести(ПроцессорКомпоновки);
		
	Исключение
		#Если Клиент тогда 
			Вопрос("Отчет не сформирован!" + Символы.ПС + ТиповыеОтчеты.ПолучитьОписаниеРодительскойПричиныИнформацииОбОшибке(ИнформацияОбОшибке()), РежимДиалогаВопрос.ОК);
		#КонецЕсли
		Возврат Результат;
	КонецПопытки;
	
	СформироватьМакет(Результат, ТаблицаЗначений);
	
	КомпоновщикНастроек.ЗагрузитьНастройки(НастрокаПоУмолчанию);
	Возврат Результат;
	
КонецФункции

// Заполняет табличный документ
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом,
//
Процедура СформироватьМакет(ДокументРезультат, ФизЛицаОрганизаций) Экспорт
	
	СоответствиеКодовТарифа = Новый Соответствие;
	СоответствиеКодовТарифа.Вставить(Перечисления.ТарифыСтраховыхВзносов.ОбщийНалоговыйРежим, "01");
	СоответствиеКодовТарифа.Вставить(Перечисления.ТарифыСтраховыхВзносов.СельХозПроизводители, "02");
	СоответствиеКодовТарифа.Вставить(Перечисления.ТарифыСтраховыхВзносов.ЕНВД, "05");
	СоответствиеКодовТарифа.Вставить(Перечисления.ТарифыСтраховыхВзносов.УпрощенныйНалоговыйРежим, "05");
	СоответствиеКодовТарифа.Вставить(Перечисления.ТарифыСтраховыхВзносов.ЕСХН, "04");
	СоответствиеКодовТарифа.Вставить(Перечисления.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов, "03");
	СоответствиеКодовТарифа.Вставить(Перечисления.ТарифыСтраховыхВзносов.РезидентТехникоВнедренческойЗоны, "03");
	
	ДокументРезультат.Очистить();
	Макет =	ПолучитьМакет("КарточкаСтраховыхВзносов");
	
	// Расчет вычисляемых параметров
	
	// Создание запроса и установка всех необходимых параметров
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
	Запрос.УстановитьПараметр("ТаблицаФизЛицОрганизаций", ФизЛицаОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(ДатаНачалаНП));
	Запрос.УстановитьПараметр("КонецПериода", КонецГода(ДатаКонцаНП));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаФизЛиц.ФизЛицо КАК ФизЛицо,
	|	ТаблицаФизЛиц.Организация КАК ОбособленноеПодразделение,
	|	ТаблицаФизЛиц.ГоловнаяОрганизация КАК Организация
	|ПОМЕСТИТЬ ВТФизЛицаОрганизаций
	|ИЗ
	|	&ТаблицаФизЛицОрганизаций КАК ТаблицаФизЛиц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	Организация";
	Запрос.Выполнить();
	
	//-----------------------------------------------------------------------------
	// ВЫБОРКА СВЕДЕНИЙ О ФИЗЛИЦЕ 
	// 	
	
	// ДанныеОПравеНаПенсию
	// Таблица Таблица Данные о праве на пенсию: - таблица это список иностранцев и периодов
	// Поля:
	//		Физлицо, 
	//		Месяц - месяц налогового периода
	// 
	// Описание:
	//	Выбираем Из Списка периодов
	//	Внутреннее соединение с ГражданствоФизЛиц.СрезПоследних
	//  по равенству периодов
	//  условие: что физлицо - не имеет права на пенсию
	//
	
	// УчетнаяПолитикаНалоговыйУчет
	// Таблица УчетнаяПолитикаНалоговыйУчетУСН - это список периодов, когда организация переходила на УСН
	// поля:
	//		УСН, 
	//		Месяц - месяц налогового периода
	
	// ДоходыПоМесяцамКодамТекст
	// Таблица доходов ЕСН по Месяцам налогового периода и кодам дохода
	// Поля:
	//		Месяц
	//		ФизЛицо
	//		КодДоходаЕСН
	//		Результат
	//		Скидка
	// Описание:
	// 	Выбираем зарегистрированные доходы из ЕСНСведенияОДоходах 	
	//  Запрос выполняется для списка обособленных подразделений.
	
	// ПоказателиДоходовПоМесяцам
	// Описание:
	//  Вычисляет показатели отчета, основанные на сведениях о доходах
	
	// ПоказателиДоходовНарастающимИтогом
	// Описание:
	//  Вычисляет показатели отчета, основанные на сведениях о доходах - нарастающим итогом
	
	// ПоказателиНалогПоМесяцам
	// Описание:
	//	Вычисляет показатели отчета, основанные на сведениях о налогах.
	//  Из по ЕСН автоматически отнимается налог, приходящийся  на налоговую льготу инвалидов.
	
	// ПоказателиНалоговНарастающимИтогом
	// Описание:
	//  Вычисляет показатели отчета, основанные на сведениях о налогах - нарастающим итогом
	
	//ДанныеРасчетаТекст
	// Описание: объединяет показатели доходов и налогов и рассчитывает колонку 29						   
	
	ДтНачМесяца = НачалоМесяца(ДатаНачалаНП);
	ТекстПериодов = "ВЫБРАТЬ
	|	ДАТАВРЕМЯ("+Формат(ДтНачМесяца, "ДФ=yyyy")+", "+Месяц(ДтНачМесяца)+", 1) КАК Период
	|ПОМЕСТИТЬ ВТПериоды";
	ДтНачМесяца = ДобавитьМесяц(ДтНачМесяца, 1);
	
	Пока ДтНачМесяца <= ДатаКонцаНП Цикл
		ТекстПериодов =  ТекстПериодов + "
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ДАТАВРЕМЯ("+Формат(ДтНачМесяца, "ДФ=yyyy")+", "+Месяц(ДтНачМесяца)+", "+День(ДтНачМесяца)+") КАК Период
		|";
		ДтНачМесяца = ДобавитьМесяц(ДтНачМесяца, 1);
	КонецЦИкла;
	ТекстПериодов = ТекстПериодов + ";";
	
	ДанныеРасчетаТекст = ТекстПериодов + 
	"ВЫБРАТЬ
	|	ФИОФизЛицСрезПоследних.ФизЛицо КАК ФизЛицо,
	|	ФИОФизЛицСрезПоследних.Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя,
	|	ФИОФизЛицСрезПоследних.Отчество
	|ПОМЕСТИТЬ ВТФИОФизлиц
	|ИЗ
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|			&КонецПериода,
	|			ФизЛицо В
	|				(ВЫБРАТЬ
	|					Организации.Физлицо
	|				ИЗ
	|					ВТФизЛицаОрганизаций КАК Организации)) КАК ФИОФизЛицСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрезПоследнихДат.ПериодРегистрации КАК ПериодРегистрации,
	|	СрезПоследнихДат.Физлицо КАК Физлицо,
	|	СведенияОбИнвалидностиФизлиц.Инвалидность,
	|	СведенияОбИнвалидностиФизлиц.ГруппаИнвалидности,
	|	СведенияОбИнвалидностиФизлиц.ДатаВыдачи,
	|	СведенияОбИнвалидностиФизлиц.СрокДействияСправки
	|ПОМЕСТИТЬ Инвалиды
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТПериоды.Период КАК ПериодРегистрации,
	|		СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо,
	|		МАКСИМУМ(СведенияОбИнвалидностиФизлиц.Период) КАК Период
	|	ИЗ
	|		ВТПериоды КАК ВТПериоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИнвалидностиФизлиц КАК СведенияОбИнвалидностиФизлиц
	|			ПО (КОНЕЦПЕРИОДА(ВТПериоды.Период, МЕСЯЦ) >= СведенияОбИнвалидностиФизлиц.Период)
	|	ГДЕ
	|		СведенияОбИнвалидностиФизлиц.Физлицо В
	|				(ВЫБРАТЬ
	|					Организации.Физлицо
	|				ИЗ
	|					ВТФизЛицаОрганизаций КАК Организации)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СведенияОбИнвалидностиФизлиц.Физлицо,
	|		ВТПериоды.Период) КАК СрезПоследнихДат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИнвалидностиФизлиц КАК СведенияОбИнвалидностиФизлиц
	|		ПО СрезПоследнихДат.Период = СведенияОбИнвалидностиФизлиц.Период
	|			И СрезПоследнихДат.Физлицо = СведенияОбИнвалидностиФизлиц.Физлицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	ПериодРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Инвалиды.Физлицо КАК Физлицо,
	|	Инвалиды.Инвалидность,
	|	ГОД(Инвалиды.ПериодРегистрации) КАК РасчетныйПериод,
	|	МАКСИМУМ(Инвалиды.ДатаВыдачи) КАК ДатаВыдачи,
	|	МАКСИМУМ(Инвалиды.СрокДействияСправки) КАК СрокДействияСправки
	|ПОМЕСТИТЬ ВТСправкиОбИнвалидности
	|ИЗ
	|	Инвалиды КАК Инвалиды
	|ГДЕ
	|	Инвалиды.Инвалидность
	|
	|СГРУППИРОВАТЬ ПО
	|	Инвалиды.Физлицо,
	|	Инвалиды.Инвалидность,
	|	ГОД(Инвалиды.ПериодРегистрации)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	РасчетныйПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрезПоследнихДат.ПериодРегистрации КАК ПериодРегистрации,
	|	СрезПоследнихДат.Физлицо КАК Физлицо,
	|	ГражданствоФизЛиц.НеЯвляетсяНалоговымРезидентомРФ,
	|	ГражданствоФизЛиц.Страна,
	|	ГражданствоФизЛиц.НеИмеетПравоНаМедицинскоеСтрахование,
	|	ГражданствоФизЛиц.НеИмеетПравоНаПенсию
	|ПОМЕСТИТЬ ВТГражданство
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТПериоды.Период КАК ПериодРегистрации,
	|		ГражданствоФизЛиц.ФизЛицо КАК Физлицо,
	|		МАКСИМУМ(ГражданствоФизЛиц.Период) КАК Период
	|	ИЗ
	|		ВТПериоды КАК ВТПериоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|			ПО (НАЧАЛОПЕРИОДА(ВТПериоды.Период, МЕСЯЦ) >= ГражданствоФизЛиц.Период)
	|	ГДЕ
	|		ГражданствоФизЛиц.ФизЛицо В
	|				(ВЫБРАТЬ
	|					Организации.Физлицо
	|				ИЗ
	|					ВТФизЛицаОрганизаций КАК Организации)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ГражданствоФизЛиц.ФизЛицо,
	|		ВТПериоды.Период) КАК СрезПоследнихДат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО СрезПоследнихДат.Период = ГражданствоФизЛиц.Период
	|			И СрезПоследнихДат.Физлицо = ГражданствоФизЛиц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	ПериодРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница КАК Организация
	|ПОМЕСТИТЬ ВТОтветственные
	|ИЗ
	|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
	|			&КонецПериода,
	|			ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер)
	|				И СтруктурнаяЕдиница В
	|					(ВЫБРАТЬ
	|						Организации.Организация
	|					ИЗ
	|						ВТФизЛицаОрганизаций КАК Организации)) КАК ОтветственныеЛицаОрганизацийСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФИОФизЛицСрезПоследних.ФизЛицо КАК ФизЛицо,
	|	"""" + ВЫБОР
	|		КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) <> """"
	|			ТОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) + "". ""
	|		ИНАЧЕ """"
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) <> """"
	|			ТОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) + "". ""
	|		ИНАЧЕ """"
	|	КОНЕЦ + ФИОФизЛицСрезПоследних.Фамилия КАК ФИО
	|ПОМЕСТИТЬ ВТФИООтветственных
	|ИЗ
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|			&КонецПериода,
	|			ФизЛицо В
	|				(ВЫБРАТЬ
	|					Ответственные.ФизическоеЛицо
	|				ИЗ
	|					ВТОтветственные КАК Ответственные)) КАК ФИОФизЛицСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.ОбособленноеПодразделение КАК Организация,
	|	НАЧАЛОПЕРИОДА(Организации.Период, МЕСЯЦ) КАК Месяц,
	|	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчет.ВидТарифаСтраховыхВзносов, ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОбщийНалоговыйРежим)) КАК ВидТарифаСтраховыхВзносов
	|ПОМЕСТИТЬ ВТВидыТарифныхСтавокОрганизаций
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТПериоды.Период КАК Период,
	|		Организации.Организация КАК Организация,
	|		Организации.ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|	ИЗ
	|		ВТПериоды КАК ВТПериоды,
	|		ВТФизЛицаОрганизаций КАК Организации) КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Периоды.Период КАК Период,
	|			УчетнаяПолитикаНалоговыйУчет.Организация КАК Организация,
	|			МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период) КАК ПериодРегистра
	|		ИЗ
	|			ВТПериоды КАК Периоды
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
	|				ПО Периоды.Период >= УчетнаяПолитикаНалоговыйУчет.Период
	|		ГДЕ
	|			УчетнаяПолитикаНалоговыйУчет.Организация В
	|					(ВЫБРАТЬ
	|						Организации.Организация
	|					ИЗ
	|						ВТФизЛицаОрганизаций КАК Организации)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Периоды.Период,
	|			УчетнаяПолитикаНалоговыйУчет.Организация) КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
	|			ПО Периоды.ПериодРегистра = УчетнаяПолитикаНалоговыйУчет.Период
	|				И Периоды.Организация = УчетнаяПолитикаНалоговыйУчет.Организация
	|		ПО Организации.Организация = Периоды.Организация
	|			И Организации.Период = Периоды.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТарифСтраховыхВзносов.ПФР,
	|	ТарифСтраховыхВзносов.ПФРНакопительная,
	|	ТарифСтраховыхВзносов.ФСС,
	|	ТарифСтраховыхВзносов.ФФОМС,
	|	ТарифСтраховыхВзносов.ТФОМС,
	|	ТарифСтраховыхВзносов.ВидТарифа КАК ВидТарифа,
	|	ГОД(ДатыПоследнихДвижений.ПериодРегистрации) КАК РасчетныйПериод
	|ПОМЕСТИТЬ ВТРазмерыТарифныхСтавок
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТарифСтраховыхВзносов.ВидТарифа КАК ВидТарифа,
	|		КОНЕЦПЕРИОДА(Месяцы.Период, ГОД) КАК ПериодРегистрации,
	|		МАКСИМУМ(ТарифСтраховыхВзносов.Период) КАК Период
	|	ИЗ
	|		ВТПериоды КАК Месяцы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТарифСтраховыхВзносов КАК ТарифСтраховыхВзносов
	|			ПО (КОНЕЦПЕРИОДА(Месяцы.Период, ГОД) >= ТарифСтраховыхВзносов.Период)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТарифСтраховыхВзносов.ВидТарифа,
	|		Месяцы.Период) КАК ДатыПоследнихДвижений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТарифСтраховыхВзносов КАК ТарифСтраховыхВзносов
	|		ПО ДатыПоследнихДвижений.ВидТарифа = ТарифСтраховыхВзносов.ВидТарифа
	|			И ДатыПоследнихДвижений.Период = ТарифСтраховыхВзносов.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидТарифа,
	|	РасчетныйПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтраховыеВзносыСведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|	СтраховыеВзносыСведенияОДоходах.Организация КАК Организация,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода КАК ВидДохода,
	|	СтраховыеВзносыСведенияОДоходах.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	СтраховыеВзносыСведенияОДоходах.РезультатОборот КАК Результат,
	|	СтраховыеВзносыСведенияОДоходах.СкидкаОборот КАК Скидка,
	|	НАЧАЛОПЕРИОДА(СтраховыеВзносыСведенияОДоходах.Период, МЕСЯЦ) КАК ПериодРегистрации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Инвалиды.Инвалидность, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов)
	|		КОГДА СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ЕНВД)
	|		ИНАЧЕ ВидыТарифныхСтавок.ВидТарифаСтраховыхВзносов
	|	КОНЕЦ КАК ВидТарифаСтраховыхВзносов,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
	|			ТОГДА СтраховыеВзносыСведенияОДоходах.РезультатОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПоДопТарифу,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу КАК ОблагаетсяПоДополнительномуТарифу,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносыСведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеОблагаетсяЦеликом)
	|				ИЛИ СтраховыеВзносыСведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС)
	|				ИЛИ СтраховыеВзносыСведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ДенежноеДовольствиеВоеннослужащих)
	|			ТОГДА СтраховыеВзносыСведенияОДоходах.РезультатОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НеОблагаетсяЦеликом,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносыСведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ДоговорыГПХ)
	|			ТОГДА СтраховыеВзносыСведенияОДоходах.РезультатОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ДоговорГПХ,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФСС
	|			ТОГДА СтраховыеВзносыСведенияОДоходах.РезультатОборот - СтраховыеВзносыСведенияОДоходах.СкидкаОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОблагаемаяБазаФСС,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФОМС
	|			ТОГДА СтраховыеВзносыСведенияОДоходах.РезультатОборот - СтраховыеВзносыСведенияОДоходах.СкидкаОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОблагаемаяБазаФОМС,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуПФР
	|			ТОГДА СтраховыеВзносыСведенияОДоходах.РезультатОборот - СтраховыеВзносыСведенияОДоходах.СкидкаОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОблагаемаяБазаПФР
	|ПОМЕСТИТЬ ВТДоходыВзносов
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			МЕСЯЦ,
	|			(Физлицо, ОбособленноеПодразделение) В
	|				(ВЫБРАТЬ
	|					Организации.Физлицо,
	|					Организации.ОбособленноеПодразделение
	|				ИЗ
	|					ВТФизЛицаОрганизаций КАК Организации)) КАК СтраховыеВзносыСведенияОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ Инвалиды КАК Инвалиды
	|		ПО (НАЧАЛОПЕРИОДА(СтраховыеВзносыСведенияОДоходах.Период, МЕСЯЦ) = Инвалиды.ПериодРегистрации)
	|			И СтраховыеВзносыСведенияОДоходах.ФизЛицо = Инвалиды.Физлицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыТарифныхСтавокОрганизаций КАК ВидыТарифныхСтавок
	|		ПО (НАЧАЛОПЕРИОДА(СтраховыеВзносыСведенияОДоходах.Период, МЕСЯЦ) = ВидыТарифныхСтавок.Месяц)
	|			И СтраховыеВзносыСведенияОДоходах.ОбособленноеПодразделение = ВидыТарифныхСтавок.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	ВидДохода,
	|	Организация,
	|	ОбособленноеПодразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредельнаяВеличинаБазыСтраховыхВзносов.Размер КАК Размер,
	|	ДатыПоследнихДвижений.ПериодРегистрации КАК ПериодРегистрации
	|ПОМЕСТИТЬ ВТПредельнаяВеличина
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ПредельнаяВеличинаБазыСтраховыхВзносов.Период) КАК Период,
	|		ВТПериоды.Период КАК ПериодРегистрации
	|	ИЗ
	|		ВТПериоды КАК ВТПериоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПредельнаяВеличинаБазыСтраховыхВзносов КАК ПредельнаяВеличинаБазыСтраховыхВзносов
	|			ПО ВТПериоды.Период >= ПредельнаяВеличинаБазыСтраховыхВзносов.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТПериоды.Период) КАК ДатыПоследнихДвижений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПредельнаяВеличинаБазыСтраховыхВзносов КАК ПредельнаяВеличинаБазыСтраховыхВзносов
	|		ПО ДатыПоследнихДвижений.Период = ПредельнаяВеличинаБазыСтраховыхВзносов.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ФизЛицо КАК ФизЛицо,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	ВложенныйЗапрос.ПериодРегистрации КАК ПериодРегистрации,
	|	СУММА(ВложенныйЗапрос.ОблагаемаяБазаФССЗаГод) КАК ОблагаемаяБазаФССЗаГод,
	|	СУММА(ВложенныйЗапрос.ОблагаемаяБазаФОМСЗаГод) КАК ОблагаемаяБазаФОМСЗаГод,
	|	СУММА(ВложенныйЗапрос.ОблагаемаяБазаПФРЗаГод) КАК ОблагаемаяБазаПФРЗаГод,
	|	СУММА(ВложенныйЗапрос.ОблагаемаяБазаФССЗаМесяц) КАК ОблагаемаяБазаФССЗаМесяц,
	|	СУММА(ВложенныйЗапрос.ОблагаемаяБазаФОМСЗаМесяц) КАК ОблагаемаяБазаФОМСЗаМесяц,
	|	СУММА(ВложенныйЗапрос.ОблагаемаяБазаПФРЗаМесяц) КАК ОблагаемаяБазаПФРЗаМесяц,
	|	СУММА(ВЫБОР
	|			КОГДА ВложенныйЗапрос.ОблагаемаяБазаФССЗаГод > Предел.Размер
	|				ТОГДА ВЫБОР
	|						КОГДА ВложенныйЗапрос.ОблагаемаяБазаФССЗаГод - Предел.Размер >= ВложенныйЗапрос.ОблагаемаяБазаФССЗаМесяц
	|							ТОГДА ВложенныйЗапрос.ОблагаемаяБазаФССЗаМесяц
	|						ИНАЧЕ ВложенныйЗапрос.ОблагаемаяБазаФССЗаГод - Предел.Размер
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПревысившаяПределФСС,
	|	СУММА(ВЫБОР
	|			КОГДА ВложенныйЗапрос.ОблагаемаяБазаФОМСЗаГод > Предел.Размер
	|				ТОГДА ВЫБОР
	|						КОГДА ВложенныйЗапрос.ОблагаемаяБазаФОМСЗаГод - Предел.Размер >= ВложенныйЗапрос.ОблагаемаяБазаФОМСЗаМесяц
	|							ТОГДА ВложенныйЗапрос.ОблагаемаяБазаФОМСЗаМесяц
	|						ИНАЧЕ ВложенныйЗапрос.ОблагаемаяБазаФОМСЗаГод - Предел.Размер
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПревысившаяПределФОМС,
	|	СУММА(ВЫБОР
	|			КОГДА ВложенныйЗапрос.ОблагаемаяБазаПФРЗаГод > Предел.Размер
	|				ТОГДА ВЫБОР
	|						КОГДА ВложенныйЗапрос.ОблагаемаяБазаПФРЗаГод - Предел.Размер >= ВложенныйЗапрос.ОблагаемаяБазаПФРЗаМесяц
	|							ТОГДА ВложенныйЗапрос.ОблагаемаяБазаПФРЗаМесяц
	|						ИНАЧЕ ВложенныйЗапрос.ОблагаемаяБазаПФРЗаГод - Предел.Размер
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПревысившаяПределПФР
	|ПОМЕСТИТЬ БазаПревышенияДохода
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТБазаВзносов.ФизЛицо КАК ФизЛицо,
	|		ВТБазаВзносов.Организация КАК Организация,
	|		ВТБазаВзносов.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|		ВТПериоды.Период КАК ПериодРегистрации,
	|		СУММА(ВТБазаВзносов.ОблагаемаяБазаФСС) КАК ОблагаемаяБазаФССЗаГод,
	|		СУММА(ВТБазаВзносов.ОблагаемаяБазаФОМС) КАК ОблагаемаяБазаФОМСЗаГод,
	|		СУММА(ВТБазаВзносов.ОблагаемаяБазаПФР) КАК ОблагаемаяБазаПФРЗаГод,
	|		СУММА(ВЫБОР
	|				КОГДА ВТПериоды.Период = ВТБазаВзносов.ПериодРегистрации
	|					ТОГДА ВТБазаВзносов.ОблагаемаяБазаФСС
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК ОблагаемаяБазаФССЗаМесяц,
	|		СУММА(ВЫБОР
	|				КОГДА ВТПериоды.Период = ВТБазаВзносов.ПериодРегистрации
	|					ТОГДА ВТБазаВзносов.ОблагаемаяБазаФОМС
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК ОблагаемаяБазаФОМСЗаМесяц,
	|		СУММА(ВЫБОР
	|				КОГДА ВТПериоды.Период = ВТБазаВзносов.ПериодРегистрации
	|					ТОГДА ВТБазаВзносов.ОблагаемаяБазаПФР
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК ОблагаемаяБазаПФРЗаМесяц
	|	ИЗ
	|		ВТПериоды КАК ВТПериоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыВзносов КАК ВТБазаВзносов
	|			ПО ВТПериоды.Период >= ВТБазаВзносов.ПериодРегистрации
	|				И (НАЧАЛОПЕРИОДА(ВТПериоды.Период, ГОД) <= ВТБазаВзносов.ПериодРегистрации)
	|	ГДЕ
	|		ВТПериоды.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТБазаВзносов.ФизЛицо,
	|		ВТБазаВзносов.Организация,
	|		ВТБазаВзносов.ОбособленноеПодразделение,
	|		ВТПериоды.Период) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПредельнаяВеличина КАК Предел
	|		ПО ВложенныйЗапрос.ПериодРегистрации = Предел.ПериодРегистрации
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ОбособленноеПодразделение,
	|	ВложенныйЗапрос.ФизЛицо,
	|	ВложенныйЗапрос.ПериодРегистрации,
	|	ВложенныйЗапрос.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	ПериодРегистрации,
	|	ОбособленноеПодразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыВзносов.ФизЛицо КАК ФизЛицо,
	|	ДоходыВзносов.Организация КАК ГоловнаяОрганизация,
	|	ДоходыВзносов.ОбособленноеПодразделение КАК Организация,
	|	ДоходыВзносов.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
	|	ДоходыВзносов.ОблагаетсяПоДополнительномуТарифу,
	|	ДоходыВзносов.ПериодРегистрации КАК ПериодРегистрации,
	|	СУММА(ДоходыВзносов.Скидка) КАК Скидка,
	|	СУММА(ВЫБОР
	|			КОГДА ДоходыВзносов.ВидДохода <> ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеЯвляетсяОбъектом)
	|				ТОГДА ДоходыВзносов.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НачисленоВсего,
	|	СУММА(ВЫБОР
	|			КОГДА ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеОблагаетсяЦеликом)
	|					ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС)
	|					ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ДенежноеДовольствиеВоеннослужащих)
	|				ТОГДА ДоходыВзносов.Результат
	|			КОГДА ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.МатпомощьПриРожденииРебенка)
	|				ТОГДА ДоходыВзносов.Скидка
	|			КОГДА ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь)
	|				ТОГДА ДоходыВзносов.Скидка
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НеОблагается,
	|	СУММА(ВЫБОР
	|			КОГДА ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС)
	|				ТОГДА ДоходыВзносов.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Пособие,
	|	СУММА(ВЫБОР
	|			КОГДА ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ДенежноеСодержаниеПрокуроров)
	|				ТОГДА ДоходыВзносов.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВыплатыПрокуроров,
	|	СУММА(ВЫБОР
	|			КОГДА ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеОткрытия)
	|					ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеАудиовизуальныеПроизведения)
	|					ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеДругиеМузыкальныеПроизведения)
	|					ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеЛитературныеПроизведения)
	|					ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеМузыкальноСценическиеПроизведение)
	|					ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеНаучныеТруды)
	|					ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеСкульптуры)
	|					ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеГрафическиеПроизведения)
	|					ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеИсполнениеПроизведений)
	|				ТОГДА ДоходыВзносов.Результат
	|			КОГДА ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ДоговорыГПХ)
	|				ТОГДА ДоходыВзносов.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВыплатыПоДоговорам,
	|	СУММА(ДоходыВзносов.ОблагаемаяБазаПФР - ВЫБОР
	|			КОГДА БазаПревышенияДохода.ОблагаемаяБазаПФРЗаМесяц > 0
	|				ТОГДА ДоходыВзносов.ОблагаемаяБазаПФР * ЕСТЬNULL(БазаПревышенияДохода.СуммаПревысившаяПределПФР / БазаПревышенияДохода.ОблагаемаяБазаПФРЗаМесяц, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НалоговаяБазаПФР,
	|	СУММА(ДоходыВзносов.ОблагаемаяБазаФСС - ВЫБОР
	|			КОГДА БазаПревышенияДохода.ОблагаемаяБазаФССЗаМесяц > 0
	|				ТОГДА ДоходыВзносов.ОблагаемаяБазаФСС * ЕСТЬNULL(БазаПревышенияДохода.СуммаПревысившаяПределФСС / БазаПревышенияДохода.ОблагаемаяБазаФССЗаМесяц, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НалоговаяБазаФСС,
	|	СУММА(ДоходыВзносов.ОблагаемаяБазаФОМС - ВЫБОР
	|			КОГДА БазаПревышенияДохода.ОблагаемаяБазаФОМСЗаМесяц > 0
	|				ТОГДА ДоходыВзносов.ОблагаемаяБазаФОМС * ЕСТЬNULL(БазаПревышенияДохода.СуммаПревысившаяПределФОМС / БазаПревышенияДохода.ОблагаемаяБазаФОМСЗаМесяц, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НалоговаяБазаФОМС,
	|	ВЫБОР
	|		КОГДА ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеОткрытия)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеАудиовизуальныеПроизведения)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеДругиеМузыкальныеПроизведения)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеЛитературныеПроизведения)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеМузыкальноСценическиеПроизведение)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеНаучныеТруды)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеСкульптуры)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеГрафическиеПроизведения)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеИсполнениеПроизведений)
	|			ТОГДА ДоходыВзносов.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РасходыПоАвторским
	|ПОМЕСТИТЬ ВТБазаДляОтчета
	|ИЗ
	|	ВТДоходыВзносов КАК ДоходыВзносов
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПревышенияДохода КАК БазаПревышенияДохода
	|		ПО ДоходыВзносов.ФизЛицо = БазаПревышенияДохода.ФизЛицо
	|			И ДоходыВзносов.Организация = БазаПревышенияДохода.Организация
	|			И ДоходыВзносов.ОбособленноеПодразделение = БазаПревышенияДохода.ОбособленноеПодразделение
	|			И ДоходыВзносов.ПериодРегистрации = БазаПревышенияДохода.ПериодРегистрации
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыВзносов.ФизЛицо,
	|	ДоходыВзносов.Организация,
	|	ДоходыВзносов.ОбособленноеПодразделение,
	|	ДоходыВзносов.ОблагаетсяПоДополнительномуТарифу,
	|	ДоходыВзносов.ПериодРегистрации,
	|	ДоходыВзносов.ВидТарифаСтраховыхВзносов,
	|	ВЫБОР
	|		КОГДА ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеОткрытия)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеАудиовизуальныеПроизведения)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеДругиеМузыкальныеПроизведения)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеЛитературныеПроизведения)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеМузыкальноСценическиеПроизведение)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеНаучныеТруды)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеСкульптуры)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеГрафическиеПроизведения)
	|				ИЛИ ДоходыВзносов.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.АвторскиеИсполнениеПроизведений)
	|			ТОГДА ДоходыВзносов.Скидка
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизЛица.ФизЛицо КАК ФизЛицо,
	|	ФизЛица.ФизЛицо.ИНН КАК ФизЛицоИНН,
	|	ФизЛица.ФизЛицо.СтраховойНомерПФР КАК ФизЛицоСтраховойНомерПФР,
	|	ФизЛица.ФизЛицо.ДатаРождения КАК ФизЛицоДатаРождения,
	|	ФизЛица.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	ФизЛица.Организация КАК Организация,
	|	Периоды.Период КАК Период,
	|	ТарифыСтраховыхВзносов.Ссылка КАК ВидТарифа
	|ПОМЕСТИТЬ ПериодыФизлицаСтавки
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК ФизЛица,
	|	ВТПериоды КАК Периоды,
	|	Перечисление.ТарифыСтраховыхВзносов КАК ТарифыСтраховыхВзносов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Взносы.ПериодРегистрации,
	|	Взносы.ФизЛицо,
	|	Взносы.ГоловнаяОрганизация,
	|	Взносы.Организация,
	|	Взносы.ВидТарифаСтраховыхВзносов,
	|	СУММА(Взносы.ПФРСтраховая) КАК ПФРСтраховая,
	|	СУММА(Взносы.ПФРНакопительная) КАК ПФРНакопительная,
	|	СУММА(Взносы.ФСС) КАК ФСС,
	|	СУММА(Взносы.ФФОМС) КАК ФФОМС,
	|	СУММА(Взносы.ТФОМС) КАК ТФОМС,
	|	СУММА(Взносы.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
	|	СУММА(Взносы.ПФРПоДополнительномуТарифу) КАК ПФРПоДополнительномуТарифу
	|ПОМЕСТИТЬ ВТВзносы
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(СтраховыеВзносыИсчисленные.Период, МЕСЯЦ) КАК ПериодРегистрации,
	|		СтраховыеВзносыИсчисленные.ФизЛицо КАК ФизЛицо,
	|		СтраховыеВзносыИсчисленные.Организация КАК ГоловнаяОрганизация,
	|		СтраховыеВзносыИсчисленные.ОбособленноеПодразделение КАК Организация,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(Инвалиды.Инвалидность, ЛОЖЬ)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов)
	|			ИНАЧЕ ВидыТарифныхСтавокОрганизаций.ВидТарифаСтраховыхВзносов
	|		КОНЕЦ КАК ВидТарифаСтраховыхВзносов,
	|		СтраховыеВзносыИсчисленные.ПФРСтраховаяОборот КАК ПФРСтраховая,
	|		СтраховыеВзносыИсчисленные.ПФРНакопительнаяОборот КАК ПФРНакопительная,
	|		СтраховыеВзносыИсчисленные.ФССОборот КАК ФСС,
	|		СтраховыеВзносыИсчисленные.ФФОМСОборот КАК ФФОМС,
	|		СтраховыеВзносыИсчисленные.ТФОМСОборот КАК ТФОМС,
	|		СтраховыеВзносыИсчисленные.ФССНесчастныеСлучаиОборот КАК ФССНесчастныеСлучаи,
	|		СтраховыеВзносыИсчисленные.ПФРПоДополнительномуТарифуОборот КАК ПФРПоДополнительномуТарифу
	|	ИЗ
	|		РегистрНакопления.СтраховыеВзносыИсчисленные.Обороты(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				МЕСЯЦ,
	|				(Физлицо, ОбособленноеПодразделение) В
	|					(ВЫБРАТЬ
	|						Организации.Физлицо,
	|						Организации.ОбособленноеПодразделение
	|					ИЗ
	|						ВТФизЛицаОрганизаций КАК Организации)) КАК СтраховыеВзносыИсчисленные
	|			ЛЕВОЕ СОЕДИНЕНИЕ Инвалиды КАК Инвалиды
	|			ПО (НАЧАЛОПЕРИОДА(СтраховыеВзносыИсчисленные.Период, МЕСЯЦ) = Инвалиды.ПериодРегистрации)
	|				И СтраховыеВзносыИсчисленные.ФизЛицо = Инвалиды.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыТарифныхСтавокОрганизаций КАК ВидыТарифныхСтавокОрганизаций
	|			ПО (НАЧАЛОПЕРИОДА(СтраховыеВзносыИсчисленные.Период, МЕСЯЦ) = ВидыТарифныхСтавокОрганизаций.Месяц)
	|				И СтраховыеВзносыИсчисленные.ОбособленноеПодразделение = ВидыТарифныхСтавокОрганизаций.Организация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(СтраховыеВзносыИсчисленные.Период, МЕСЯЦ),
	|		СтраховыеВзносыИсчисленные.ФизЛицо,
	|		СтраховыеВзносыИсчисленные.Организация,
	|		СтраховыеВзносыИсчисленные.ОбособленноеПодразделение,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(Инвалиды.Инвалидность, ЛОЖЬ)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ЕНВД)
	|		КОНЕЦ,
	|		СтраховыеВзносыИсчисленные.ПФРСтраховаяЕНВДОборот,
	|		СтраховыеВзносыИсчисленные.ПФРНакопительнаяЕНВДОборот,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.СтраховыеВзносыИсчисленные.Обороты(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				МЕСЯЦ,
	|				(Физлицо, ОбособленноеПодразделение) В
	|					(ВЫБРАТЬ
	|						Организации.Физлицо,
	|						Организации.ОбособленноеПодразделение
	|					ИЗ
	|						ВТФизЛицаОрганизаций КАК Организации)) КАК СтраховыеВзносыИсчисленные
	|			ЛЕВОЕ СОЕДИНЕНИЕ Инвалиды КАК Инвалиды
	|			ПО (НАЧАЛОПЕРИОДА(СтраховыеВзносыИсчисленные.Период, ДЕНЬ) = Инвалиды.ПериодРегистрации)
	|				И СтраховыеВзносыИсчисленные.ФизЛицо = Инвалиды.Физлицо) КАК Взносы
	|
	|СГРУППИРОВАТЬ ПО
	|	Взносы.ПериодРегистрации,
	|	Взносы.ФизЛицо,
	|	Взносы.ГоловнаяОрганизация,
	|	Взносы.Организация,
	|	Взносы.ВидТарифаСтраховыхВзносов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГОД(СписокФизЛиц.ПериодРегистрации) КАК Год,
	|	НЕОПРЕДЕЛЕНО КАК Организация,
	|	СписокФизЛиц.ФизЛицо КАК ФизЛицо,
	|	СписокФизЛиц.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
	|	СписокФизЛиц.ПериодРегистрации КАК ПериодРегистрации,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|				ТОГДА СписокФизЛиц.Результат
	|			КОГДА СписокФизЛиц.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.МатпомощьПриРожденииРебенка)
	|				ТОГДА СписокФизЛиц.Скидка
	|			КОГДА СписокФизЛиц.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь)
	|				ТОГДА СписокФизЛиц.Скидка
	|			ИНАЧЕ СписокФизЛиц.Результат
	|		КОНЕЦ) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|			ТОГДА ""Доходы иностр. граждан (лиц без гр-ва), временно пребывающих на территории РФ (п. 15 ч. 1 ст. 9)""
	|		ИНАЧЕ СписокФизЛиц.ВидДохода
	|	КОНЕЦ КАК ВидДохода
	|ИЗ
	|	ВТДоходыВзносов КАК СписокФизЛиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГражданство КАК Гражданство
	|		ПО СписокФизЛиц.ФизЛицо = Гражданство.Физлицо
	|			И СписокФизЛиц.ПериодРегистрации = Гражданство.ПериодРегистрации
	|ГДЕ
	|	СписокФизЛиц.ВидДохода <> ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеЯвляетсяОбъектом)
	|	И СписокФизЛиц.ВидДохода <> ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ДенежноеСодержаниеПрокуроров)
	|	И СписокФизЛиц.ВидДохода <> ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.РаспределятьПоБазовымНачислениям)
	|	И ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|				ТОГДА ""Доходы иностр. граждан (лиц без гр-ва), временно пребывающих на территории РФ (п. 15 ч. 1 ст. 9)""
	|			ИНАЧЕ СписокФизЛиц.ВидДохода
	|		КОНЕЦ <> ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ОблагаетсяЦеликом)
	|
	|СГРУППИРОВАТЬ ПО
	|	НЕОПРЕДЕЛЕНО,
	|	СписокФизЛиц.ФизЛицо,
	|	СписокФизЛиц.ВидТарифаСтраховыхВзносов,
	|	СписокФизЛиц.ПериодРегистрации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|			ТОГДА ""Доходы иностр. граждан (лиц без гр-ва), временно пребывающих на территории РФ (п. 15 ч. 1 ст. 9)""
	|		ИНАЧЕ СписокФизЛиц.ВидДохода
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Год,
	|	Организация,
	|	ФизЛицо,
	|	ВидТарифаСтраховыхВзносов,
	|	ВидДохода,
	|	ПериодРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГОД(СписокФизЛиц.Период) КАК Год,
	|	НЕОПРЕДЕЛЕНО КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК КПП,
	|	НЕОПРЕДЕЛЕНО КАК ИННОрг,
	|	НЕОПРЕДЕЛЕНО КАК НаименованиеОрганизации,
	|	ЕСТЬNULL(ФИООтветственных.ФИО, """") КАК ФИОГлБух,
	|	СписокФизЛиц.ФизЛицо КАК ФизЛицо,
	|	СписокФизЛиц.ФизЛицоИНН КАК ИНН,
	|	СписокФизЛиц.ФизЛицоСтраховойНомерПФР КАК СтраховойНомерПФР,
	|	СписокФизЛиц.ФизЛицоДатаРождения КАК ДатаРождения,
	|	СписокФизЛиц.ВидТарифа КАК ВидТарифаСтраховыхВзносов,
	|	СписокФизЛиц.Период КАК ПериодРегистрации,
	|	СУММА(ЕСТЬNULL(Взносы.ПФРСтраховая, 0)) КАК ИсчисленоПФРСтраховых,
	|	СУММА(ЕСТЬNULL(Взносы.ПФРНакопительная, 0)) КАК ИсчисленоПФРНакопительная,
	|	СУММА(ЕСТЬNULL(Взносы.ПФРПоДополнительномуТарифу, 0)) КАК ИсчисленоПФРПоДополнительномуТарифу,
	|	СУММА(ЕСТЬNULL(Взносы.ФСС, 0)) КАК ИсчисленоФСС,
	|	СУММА(ЕСТЬNULL(Взносы.ФФОМС, 0)) КАК ИсчисленоФФОМС,
	|	СУММА(ЕСТЬNULL(Взносы.ТФОМС, 0)) КАК ИсчисленоТФОМС,
	|	СУММА(ЕСТЬNULL(БазаДляОтчета.НачисленоВсего, 0)) КАК НачисленоВсего,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(БазаДляОтчета.РасходыПоАвторским, 0)
	|		КОНЕЦ) КАК РасходыПоАвторским,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|				ТОГДА ЕСТЬNULL(БазаДляОтчета.НачисленоВсего, 0)
	|			ИНАЧЕ ЕСТЬNULL(БазаДляОтчета.НеОблагается, 0)
	|		КОНЕЦ) КАК НеОблагается,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(БазаДляОтчета.Пособие, 0)
	|		КОНЕЦ) КАК Пособия,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаМедицинскоеСтрахование, ЛОЖЬ)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(БазаДляОтчета.ВыплатыПрокуроров, 0)
	|		КОНЕЦ) КАК ВыплатыПрокуроров,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(БазаДляОтчета.ВыплатыПоДоговорам, 0)
	|		КОНЕЦ) КАК ВыплатыПоДоговорам,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(БазаДляОтчета.НалоговаяБазаПФР, 0)
	|		КОНЕЦ) КАК НалоговаяБазаПФР,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(БазаДляОтчета.НалоговаяБазаФСС, 0)
	|		КОНЕЦ) КАК НалоговаяБазаФСС,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаМедицинскоеСтрахование, ЛОЖЬ)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(БазаДляОтчета.НалоговаяБазаФОМС, 0)
	|		КОНЕЦ) КАК НалоговаяБазаФОМС,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(Гражданство.НеИмеетПравоНаПенсию, ЛОЖЬ)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(БазаПревышенияДохода.СуммаПревысившаяПределПФР, 0)
	|		КОНЕЦ) КАК ПревышениеПредельнойВеличины,
	|	ВЫБОР
	|		КОГДА Гражданство.Страна ЕСТЬ NULL 
	|				ИЛИ Гражданство.Страна = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ""Гражданин РФ""
	|		КОГДА Гражданство.Страна = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА ""Лицо без гражданства""
	|		ИНАЧЕ Гражданство.Страна
	|	КОНЕЦ КАК Гражданство,
	|	ФИОФизлиц.Фамилия КАК Фамилия,
	|	ФИОФизлиц.Имя КАК Имя,
	|	ФИОФизлиц.Отчество КАК Отчество,
	|	ВЫБОР
	|		КОГДА СправкиОбИнвалидности.Инвалидность ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ СправкиОбИнвалидности.Инвалидность
	|	КОНЕЦ КАК ЕстьИнвалидность,
	|	СправкиОбИнвалидности.ДатаВыдачи КАК ДатаВыдачи,
	|	СправкиОбИнвалидности.СрокДействияСправки КАК СрокДействияСправки,
	|	СУММА(ВЫБОР
	|			КОГДА ГОД(СписокФизЛиц.ФизЛицоДатаРождения) < 1967
	|				ТОГДА ТарифныеСтавки.ПФР
	|			ИНАЧЕ ТарифныеСтавки.ПФР - ТарифныеСтавки.ПФРНакопительная
	|		КОНЕЦ) КАК СЧ,
	|	СУММА(ВЫБОР
	|			КОГДА ГОД(СписокФизЛиц.ФизЛицоДатаРождения) < 1967
	|				ТОГДА 0
	|			ИНАЧЕ ТарифныеСтавки.ПФРНакопительная
	|		КОНЕЦ) КАК НЧ,
	|	СУММА(ТарифныеСтавки.ФСС) КАК ФСС,
	|	СУММА(ТарифныеСтавки.ФФОМС) КАК ФФОМС,
	|	СУММА(ТарифныеСтавки.ТФОМС) КАК ТФОМС
	|ИЗ
	|	ПериодыФизлицаСтавки КАК СписокФизЛиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГражданство КАК Гражданство
	|		ПО СписокФизЛиц.Период = Гражданство.ПериодРегистрации
	|			И СписокФизЛиц.ФизЛицо = Гражданство.Физлицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИОФизлиц КАК ФИОФизлиц
	|		ПО СписокФизЛиц.ФизЛицо = ФИОФизлиц.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВзносы КАК Взносы
	|		ПО СписокФизЛиц.ФизЛицо = Взносы.ФизЛицо
	|			И СписокФизЛиц.ОбособленноеПодразделение = Взносы.Организация
	|			И СписокФизЛиц.ВидТарифа = Взносы.ВидТарифаСтраховыхВзносов
	|			И СписокФизЛиц.Период = Взносы.ПериодРегистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаДляОтчета КАК БазаДляОтчета
	|		ПО СписокФизЛиц.ФизЛицо = БазаДляОтчета.ФизЛицо
	|			И СписокФизЛиц.ОбособленноеПодразделение = БазаДляОтчета.Организация
	|			И СписокФизЛиц.ВидТарифа = БазаДляОтчета.ВидТарифаСтраховыхВзносов
	|			И СписокФизЛиц.Период = БазаДляОтчета.ПериодРегистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыТарифныхСтавок КАК ТарифныеСтавки
	|		ПО (ГОД(СписокФизЛиц.Период) = ТарифныеСтавки.РасчетныйПериод)
	|			И (ТарифныеСтавки.ВидТарифа = СписокФизЛиц.ВидТарифа)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСправкиОбИнвалидности КАК СправкиОбИнвалидности
	|		ПО СписокФизЛиц.ФизЛицо = СправкиОбИнвалидности.Физлицо
	|			И (ГОД(СписокФизЛиц.Период) = СправкиОбИнвалидности.РасчетныйПериод)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтветственные КАК Ответственные
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственных КАК ФИООтветственных
	|			ПО Ответственные.ФизическоеЛицо = ФИООтветственных.ФизЛицо
	|		ПО СписокФизЛиц.ОбособленноеПодразделение = Ответственные.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПревышенияДохода КАК БазаПревышенияДохода
	|		ПО СписокФизЛиц.ФизЛицо = БазаПревышенияДохода.ФизЛицо
	|			И СписокФизЛиц.ОбособленноеПодразделение = БазаПревышенияДохода.ОбособленноеПодразделение
	|			И СписокФизЛиц.Период = БазаПревышенияДохода.ПериодРегистрации
	|ГДЕ
	|	(БазаДляОтчета.ВидТарифаСтраховыхВзносов ЕСТЬ НЕ NULL 
	|			ИЛИ Взносы.ВидТарифаСтраховыхВзносов ЕСТЬ НЕ NULL )
	|
	|СГРУППИРОВАТЬ ПО
	|	НЕОПРЕДЕЛЕНО,
	|	ЕСТЬNULL(ФИООтветственных.ФИО, """"),
	|	СписокФизЛиц.ФизЛицо,
	|	СписокФизЛиц.ВидТарифа,
	|	СписокФизЛиц.Период,
	|	ВЫБОР
	|		КОГДА Гражданство.Страна ЕСТЬ NULL 
	|				ИЛИ Гражданство.Страна = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ""Гражданин РФ""
	|		КОГДА Гражданство.Страна = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)
	|			ТОГДА ""Лицо без гражданства""
	|		ИНАЧЕ Гражданство.Страна
	|	КОНЕЦ,
	|	ФИОФизлиц.Фамилия,
	|	ФИОФизлиц.Имя,
	|	ФИОФизлиц.Отчество,
	|	ВЫБОР
	|		КОГДА СправкиОбИнвалидности.Инвалидность ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ СправкиОбИнвалидности.Инвалидность
	|	КОНЕЦ,
	|	СправкиОбИнвалидности.ДатаВыдачи,
	|	СправкиОбИнвалидности.СрокДействияСправки,
	|	ГОД(СписокФизЛиц.Период),
	|	СписокФизЛиц.ФизЛицоИНН,
	|	СписокФизЛиц.ФизЛицоСтраховойНомерПФР,
	|	СписокФизЛиц.ФизЛицоДатаРождения
	|
	|УПОРЯДОЧИТЬ ПО
	|	Год,
	|	Организация,
	|	ФИОФизлиц.Фамилия,
	|	ВидТарифаСтраховыхВзносов,
	|	ПериодРегистрации";							   

	Если УчитыватьОбособленноеПодразделение тогда
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"НЕОПРЕДЕЛЕНО КАК Организация", "СписокФизЛиц.ОбособленноеПодразделение КАК Организация");
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"НЕОПРЕДЕЛЕНО КАК КПП",			"СписокФизЛиц.ОбособленноеПодразделение.КПП КАК КПП");
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"НЕОПРЕДЕЛЕНО КАК ИННОрг",		"СписокФизЛиц.ОбособленноеПодразделение.ИНН КАК ИННОрг");
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"НЕОПРЕДЕЛЕНО КАК НаименованиеОрганизации", "СписокФизЛиц.ОбособленноеПодразделение.НаименованиеСокращенное КАК НаименованиеОрганизации");
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"НЕОПРЕДЕЛЕНО", "СписокФизЛиц.ОбособленноеПодразделение");
	Иначе
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"НЕОПРЕДЕЛЕНО КАК Организация", "СписокФизЛиц.Организация КАК Организация");
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"НЕОПРЕДЕЛЕНО КАК КПП",			"СписокФизЛиц.Организация.КПП КАК КПП");
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"НЕОПРЕДЕЛЕНО КАК ИННОрг",		"СписокФизЛиц.Организация.ИНН КАК ИННОрг");
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"НЕОПРЕДЕЛЕНО КАК НаименованиеОрганизации", "СписокФизЛиц.Организация.НаименованиеСокращенное КАК НаименованиеОрганизации");
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"НЕОПРЕДЕЛЕНО", "СписокФизЛиц.Организация");
	КонецЕсли;
	
	//-----------------------------------------------------------------------------
	// ВЫПОЛНЕНИЕ ЗАПРОСОВ
	
	// Данные расчета
	Запрос.Текст = ДанныеРасчетаТекст;
	МассивЗапросов = Запрос.ВыполнитьПакет();
	Выборка = МассивЗапросов[МассивЗапросов.Количество() - 1].Выбрать();
	ВыборкаДоходов = МассивЗапросов[МассивЗапросов.Количество() - 2].Выбрать();
	
	//-----------------------------------------------------------------------------
	// ЗАПОЛНЕНИЕ ФОРМЫ
	
	// Области макета
	
	ОбластьЗаглавие    = Макет.ПолучитьОбласть("Заглавие");
	ОбластьШапка       = Макет.ПолучитьОбласть("Шапка");
	ОбластьМесяц       = Макет.ПолучитьОбласть("Месяц");
	ОбластьПустойМесяц = Макет.ПолучитьОбласть("ПустойМесяц");
	ОбластьПодвал      = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаВторойСтраницы			= Макет.ПолучитьОбласть("ШапкаВторойСтраницы");
	ОбластьНачалоСтрокиВторойСтраницы	= Макет.ПолучитьОбласть("НачалоСтрокиВторойСтраницы");
	ОбластьМесяцВторойСтраницы			= Макет.ПолучитьОбласть("МесяцВторойСтраницы");
	ОбластьПустойМесяцВторойСтраницы	= Макет.ПолучитьОбласть("ПустойМесяцВторойСтраницы");
	
	СНачалаГода = Новый Структура("НарастНачисленоВсего, 
								|	НарастПревышениеПредельнойВеличины, 
								|	НарастРасходыПоАвторским, 
								|	НарастНеОблагается, 
								|	НарастПособия, 
								|	НарастВыплатыПрокуроров,
								|	НарастВыплатыПоДоговорам,
								|	НарастНалоговаяБазаПФР,
								|	НарастНалоговаяБазаФСС,
								|	НарастНалоговаяБазаФОМС,
								|	НарастИсчисленоПФРСтраховых,
								|	НарастИсчисленоПФРНакопительная,
								|	НарастИсчисленоПФРПоДополнительномуТарифу,
								|	НарастИсчисленоФСС,
								|	НарастИсчисленоФФОМС,
								|	НарастИсчисленоТФОМС");
	
	СтруктураПоиска = Новый Структура("Год, Организация, ФизЛицо, ВидТарифаСтраховыхВзносов");
	
	Пока Выборка.СледующийПоЗначениюПоля("Год") Цикл
		
		ОбластьЗаглавие.Параметры.НалоговыйПериод = Формат(Выборка.Год,"ЧГ=");
		
		Пока Выборка.СледующийПоЗначениюПоля("Организация") Цикл
			
			ОбластьЗаглавие.Параметры.ИННиКПП = СокрЛП(Выборка.ИННОрг) + "/" + СокрЛП(Выборка.КПП);
			ОбластьПодвал.Параметры.ФИОГлБух = Выборка.ФИОГлБух;
			
			Пока Выборка.СледующийПоЗначениюПоля("ФизЛицо") Цикл
				
				Если Выборка.ЕстьИнвалидность Тогда
					 ОбластьЗаглавие.Области.НЕТ.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии); 
					 ОбластьЗаглавие.Области.ЕСТЬ.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1); 
				Иначе
					 ОбластьЗаглавие.Области.ЕСТЬ.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии); 
					 ОбластьЗаглавие.Области.НЕТ.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1); 
				 КонецЕсли;
				
				НомерРаздела = 1;
				
				Пока Выборка.СледующийПоЗначениюПоля("ВидТарифаСтраховыхВзносов") Цикл
					
					ЗаполнитьЗначенияСвойств(СтруктураПоиска, Выборка);
					
					ОбластьЗаглавие.Параметры.Заполнить(Выборка);
					ОбластьЗаглавие.Параметры.НомерСтр = НомерРаздела;
					ОбластьЗаглавие.Параметры.КодТарифа = СоответствиеКодовТарифа[Выборка.ВидТарифаСтраховыхВзносов];
					ДокументРезультат.Вывести(ОбластьЗаглавие);
					ДокументРезультат.Вывести(ОбластьШапка);
					
					СНачалаГода.НарастНачисленоВсего = 0;
					СНачалаГода.НарастРасходыПоАвторским = 0;
					СНачалаГода.НарастНеОблагается = 0;
					СНачалаГода.НарастПревышениеПредельнойВеличины = 0;
					СНачалаГода.НарастПособия = 0;
					СНачалаГода.НарастВыплатыПрокуроров = 0;
					СНачалаГода.НарастВыплатыПоДоговорам = 0;
					СНачалаГода.НарастНалоговаяБазаПФР = 0;
					СНачалаГода.НарастНалоговаяБазаФСС = 0;
					СНачалаГода.НарастНалоговаяБазаФОМС = 0;
					СНачалаГода.НарастИсчисленоПФРСтраховых = 0;
					СНачалаГода.НарастИсчисленоПФРНакопительная = 0;
					СНачалаГода.НарастИсчисленоПФРПоДополнительномуТарифу = 0;
					СНачалаГода.НарастИсчисленоФСС = 0;
					СНачалаГода.НарастИсчисленоФФОМС = 0;
					СНачалаГода.НарастИсчисленоТФОМС = 0;
					
					Месяц = 0;
					
					// Выводим данные за все 12 месяцев
					Пока Выборка.Следующий() Цикл
						
						МесяцНовый = Месяц(Выборка.ПериодРегистрации); // месяц, который выводим
						
						Если МесяцНовый > Месяц + 1 тогда
							Для СчМесяцев = Месяц + 1 По МесяцНовый - 1 Цикл
								ДокументРезультат.Присоединить(ОбластьПустойМесяц);
							КонецЦикла;	
						КонецЕсли;
						Месяц = МесяцНовый;

						ОбластьМесяц.Параметры.Заполнить(Выборка);
						
						СНачалаГода.НарастНачисленоВсего = СНачалаГода.НарастНачисленоВсего + ОбластьМесяц.Параметры.НачисленоВсего;
						СНачалаГода.НарастРасходыПоАвторским = СНачалаГода.НарастРасходыПоАвторским + ОбластьМесяц.Параметры.РасходыПоАвторским;
						СНачалаГода.НарастНеОблагается = СНачалаГода.НарастНеОблагается + ОбластьМесяц.Параметры.НеОблагается;
						СНачалаГода.НарастПревышениеПредельнойВеличины = СНачалаГода.НарастПревышениеПредельнойВеличины + ОбластьМесяц.Параметры.ПревышениеПредельнойВеличины;
						СНачалаГода.НарастПособия = СНачалаГода.НарастПособия + ОбластьМесяц.Параметры.Пособия;
						СНачалаГода.НарастВыплатыПрокуроров = СНачалаГода.НарастВыплатыПрокуроров + ОбластьМесяц.Параметры.ВыплатыПрокуроров;
						СНачалаГода.НарастВыплатыПоДоговорам = СНачалаГода.НарастВыплатыПоДоговорам + ОбластьМесяц.Параметры.ВыплатыПоДоговорам;
						СНачалаГода.НарастНалоговаяБазаПФР = СНачалаГода.НарастНалоговаяБазаПФР + ОбластьМесяц.Параметры.НалоговаяБазаПФР;
						СНачалаГода.НарастНалоговаяБазаФСС = СНачалаГода.НарастНалоговаяБазаФСС + ОбластьМесяц.Параметры.НалоговаяБазаФСС;
						СНачалаГода.НарастНалоговаяБазаФОМС = СНачалаГода.НарастНалоговаяБазаФОМС + ОбластьМесяц.Параметры.НалоговаяБазаФОМС;
						СНачалаГода.НарастИсчисленоПФРСтраховых = СНачалаГода.НарастИсчисленоПФРСтраховых + ОбластьМесяц.Параметры.ИсчисленоПФРСтраховых;
						СНачалаГода.НарастИсчисленоПФРНакопительная = СНачалаГода.НарастИсчисленоПФРНакопительная + ОбластьМесяц.Параметры.ИсчисленоПФРНакопительная;
						СНачалаГода.НарастИсчисленоПФРПоДополнительномуТарифу = СНачалаГода.НарастИсчисленоПФРПоДополнительномуТарифу + ОбластьМесяц.Параметры.ИсчисленоПФРПоДополнительномуТарифу;
						СНачалаГода.НарастИсчисленоФСС = СНачалаГода.НарастИсчисленоФСС + ОбластьМесяц.Параметры.ИсчисленоФСС;
						СНачалаГода.НарастИсчисленоФФОМС = СНачалаГода.НарастИсчисленоФФОМС + ОбластьМесяц.Параметры.ИсчисленоФФОМС;
						СНачалаГода.НарастИсчисленоТФОМС = СНачалаГода.НарастИсчисленоТФОМС + ОбластьМесяц.Параметры.ИсчисленоТФОМС;
						
						ОбластьМесяц.Параметры.Заполнить(СНачалаГода);
						
						// Выведем месяц
						ДокументРезультат.Присоединить(ОбластьМесяц);
						
					КонецЦикла; 
					
					Если Месяц < 12 Тогда  // дополним таблицу пустыми строками
						Для СчМесяцев = Месяц + 1 По 12 Цикл
							// Выведем пустой месяц
							ДокументРезультат.Присоединить(ОбластьПустойМесяц);
						КонецЦикла;		
					КонецЕсли;
					
					ДокументРезультат.Вывести(ОбластьПодвал);
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					
					// Выведем необлагаемые доходы, если есть такие доходы
					ЗаголовокВыводили = Ложь;
					ВидДохода = "";
					Месяц = 0;
					
					Пока ВыборкаДоходов.НайтиСледующий(СтруктураПоиска) Цикл
						Если Не ЗаголовокВыводили Тогда
							ОбластьШапкаВторойСтраницы.Параметры.Заполнить(ОбластьЗаглавие.Параметры);
							ДокументРезультат.Вывести(ОбластьШапкаВторойСтраницы);
							ЗаголовокВыводили = Истина;
						КонецЕсли;
						Если ВидДохода <> ВыборкаДоходов.ВидДохода Тогда
							
							Если ВидДохода <> "" Тогда
								Если Месяц < 12 Тогда  // дополним таблицу пустыми строками
									Для СчМесяцев = Месяц + 1 По 12 Цикл
										// Выведем пустой месяц
										ДокументРезультат.Присоединить(ОбластьПустойМесяцВторойСтраницы);
									КонецЦикла;		
								КонецЕсли;
							КонецЕсли;
					
							ОбластьНачалоСтрокиВторойСтраницы.Параметры.ВидДохода = ВыборкаДоходов.ВидДохода;
							ДокументРезультат.Вывести(ОбластьНачалоСтрокиВторойСтраницы);
					        ВидДохода = ВыборкаДоходов.ВидДохода;
							Месяц = 0;
						КонецЕсли;
						МесяцНовый = Месяц(ВыборкаДоходов.ПериодРегистрации); // месяц, который выводим
						
						Если МесяцНовый > Месяц + 1 тогда
							Для СчМесяцев = Месяц + 1 По МесяцНовый - 1 Цикл
								ДокументРезультат.Присоединить(ОбластьПустойМесяцВторойСтраницы);
							КонецЦикла;	
						КонецЕсли;
						
						Месяц = МесяцНовый;
						ОбластьМесяцВторойСтраницы.Параметры.НеОблагается = ВыборкаДоходов.Сумма;
						ДокументРезультат.Присоединить(ОбластьМесяцВторойСтраницы);
					
					КонецЦикла;
					
					Если ЗаголовокВыводили Тогда
						Если Месяц < 12 Тогда  // дополним таблицу пустыми строками
							Для СчМесяцев = Месяц + 1 По 12 Цикл
								// Выведем пустой месяц
								ДокументРезультат.Присоединить(ОбластьПустойМесяцВторойСтраницы);
							КонецЦикла;		
						КонецЕсли;
						ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					
					НомерРаздела = НомерРаздела + 1;
				КонецЦикла; 
				
			КонецЦикла; 
			
		КонецЦикла; 
		
	КонецЦикла; 
	
	//-----------------------------------------------------------------------------
	
	//Параметры документа
	ДокументРезультат.Автомасштаб 			= 	Истина;
	ДокументРезультат.ОриентацияСтраницы 	= 	ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ТолькоПросмотр		= 	Истина;
	ДокументРезультат.ПолеСверху			= 	0;
	ДокументРезультат.ПолеСнизу				= 	0;
	
КонецПроцедуры

Процедура СохранитьНастройку() Экспорт
	
	СтруктураНастроек = ТиповыеОтчеты.ПолучитьСтруктуруПараметровТиповогоОтчета(ЭтотОбъект);
	СохранениеНастроек.СохранитьНастройкуОбъекта(СохраненнаяНастройка, СтруктураНастроек);
	
КонецПроцедуры

Процедура ПрименитьНастройку() Экспорт
	
	Схема = ТиповыеОтчеты.ПолучитьСхемуКомпоновкиОбъекта(ЭтотОбъект);
	
	// Считываение структуры настроек отчета
	Если Не СохраненнаяНастройка.Пустая() Тогда
		
		СтруктураНастроек = СохраненнаяНастройка.ХранилищеНастроек.Получить();
		Если Не СтруктураНастроек = Неопределено Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(СтруктураНастроек.НастройкиКомпоновщика);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураНастроек);
		Иначе
			КомпоновщикНастроек.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
		КонецЕсли;
		
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередВыводомЭлементРезультата(МакетКомпоновки, ПроцессорКомпоновки, ЭлементРезультата) Экспорт
	
КонецПроцедуры

Процедура ПередВыводомОтчета(МакетКомпоновки, ПроцессорКомпоновки) Экспорт
	
	
КонецПроцедуры

Процедура ПриВыводеЗаголовкаОтчета(ОбластьЗаголовок) Экспорт
КонецПроцедуры

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	СписокПолейПодстановкиОтборовПоУмолчанию = Новый Соответствие;
	
	Возврат Новый Структура("ИспользоватьСобытияПриФормированииОтчета,
	|ПриВыводеЗаголовкаОтчета,
	|ПослеВыводаПанелиПользователя,
	|ПослеВыводаПериода,
	|ПослеВыводаПараметра,
	|ПослеВыводаГруппировки,
	|ПослеВыводаОтбора,
	|ДействияПанелиИзменениеФлажкаДопНастроек,
	|ПриПолучениеНастроекПользователя, 
	|ЗаполнитьОтборыПоУмолчанию, 
	|СписокПолейПодстановкиОтборовПоУмолчанию,
	|МинимальныйПериодОтчета", 
	ложь, ложь, ложь, ложь, ложь, ложь, ложь, ложь, ложь, истина, СписокПолейПодстановкиОтборовПоУмолчанию,"Год");
КонецФункции

Функция ПолучитьДополнительныеНастройкиОтчета() Экспорт
	МассивДополнительныхНастроек = Новый Массив;
	МассивДополнительныхНастроек.Добавить(Новый Структура("Имя, Заголовок, ЗначениеПоУмолчанию", "ГруппироватьПоОрганизации", "Выводить карточки по обособленным подразделениям", истина));
	Возврат МассивДополнительныхНастроек;
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

Если СохраненнаяНастройка = Неопределено Тогда
	СохраненнаяНастройка =  Справочники.СохраненныеНастройки.ПустаяСсылка();
КонецЕсли;

Если КомпоновщикНастроек = Неопределено Тогда
	КомпоновщикНастроек =  Новый КомпоновщикНастроекКомпоновкиДанных;
КонецЕсли;

УчитыватьОбособленноеПодразделение = ложь;

УправлениеОтчетами.ЗаменитьНазваниеПолейСхемыКомпоновкиДанных(СхемаКомпоновкиДанных);

