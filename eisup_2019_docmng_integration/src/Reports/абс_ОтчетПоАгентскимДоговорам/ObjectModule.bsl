//*************************************************************
//
Процедура СформироватьОтчет(ОсновнаяФормаОбработки = Неопределено) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ОстаткиИОбороты_БухгалтерскийУчет.Субконто1.абс_ОсновнойДоговор КАК АгентскийДоговор,
	               |	ОстаткиИОбороты_БухгалтерскийУчет.Субконто1.Владелец КАК Контрагент,
	               |	ОстаткиИОбороты_БухгалтерскийУчет.Субконто1.Ссылка КАК Договор,
	               |	СУММА(ОстаткиИОбороты_БухгалтерскийУчет.СуммаНачальныйОстатокДт) КАК НачальноеСальдоДебет,
	               |	СУММА(ОстаткиИОбороты_БухгалтерскийУчет.СуммаНачальныйОстатокКт) КАК НачальноеСальдоКредит,
	               |	СУММА(ОстаткиИОбороты_БухгалтерскийУчет.СуммаОборотДт) КАК ОборотДебет,
	               |	СУММА(ОстаткиИОбороты_БухгалтерскийУчет.СуммаОборотКт) КАК ОборотКредит,
	               |	СУММА(ОстаткиИОбороты_БухгалтерскийУчет.СуммаКонечныйОстатокДт) КАК КонечноеСальдоДебет,
	               |	СУММА(ОстаткиИОбороты_БухгалтерскийУчет.СуммаКонечныйОстатокКт) КАК КонечноеСальдоКредит
	               |ПОМЕСТИТЬ ВТ_Договора
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
	               |			&ДатаНачала,
	               |			&ДатаОкончания,
	               |			,
	               |			,
	               |			,
	               |			&МассивСубконтоДоговоры,
	               |			НЕ Субконто1.абс_ОсновнойДоговор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	               |				И Субконто1.абс_ОсновнойДоговор = &ВыбранныйАгентскийДоговор
	               |				И НЕ Субконто1.абс_ОсновнойДоговор.Владелец = Субконто1.Владелец
	               |				И Субконто1.абс_СтатусДоговора В (&СтатусДоговора)) КАК ОстаткиИОбороты_БухгалтерскийУчет
	               |ГДЕ
	               |	(ОстаткиИОбороты_БухгалтерскийУчет.СуммаНачальныйОстаток <> 0
	               |			ИЛИ ОстаткиИОбороты_БухгалтерскийУчет.СуммаКонечныйОстаток <> 0
	               |			ИЛИ ОстаткиИОбороты_БухгалтерскийУчет.СуммаОборотДт <> 0
	               |			ИЛИ ОстаткиИОбороты_БухгалтерскийУчет.СуммаОборотКт <> 0)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОстаткиИОбороты_БухгалтерскийУчет.Субконто1.абс_ОсновнойДоговор,
	               |	ОстаткиИОбороты_БухгалтерскийУчет.Субконто1.Владелец,
	               |	ОстаткиИОбороты_БухгалтерскийУчет.Субконто1.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_Договора.Контрагент КАК Контрагент,
	               |	ВТ_Договора.Контрагент.Наименование КАК КонтрагентНаименование,
	               |	ВТ_Договора.Контрагент.абс_СтатусКонтрагента КАК СтатусИсполнителя,
	               |	ВТ_Договора.Договор,
	               |	ВТ_Договора.Договор.Номер КАК ДоговорНомер,
	               |	ВТ_Договора.Договор.абс_СтатусДоговора КАК СтатусДоговора,
	               |	ВТ_Договора.НачальноеСальдоДебет КАК НачальноеСальдоДебет,
	               |	ВТ_Договора.НачальноеСальдоКредит КАК НачальноеСальдоКредит,
	               |	ВТ_Договора.ОборотДебет КАК ОборотДебет,
	               |	ВТ_Договора.ОборотКредит КАК ОборотКредит,
	               |	ВТ_Договора.КонечноеСальдоДебет КАК КонечноеСальдоДебет,
	               |	ВТ_Договора.КонечноеСальдоКредит КАК КонечноеСальдоКредит
	               |ИЗ
	               |	ВТ_Договора КАК ВТ_Договора
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	КонтрагентНаименование,
	               |	ДоговорНомер
	               |ИТОГИ
	               |	СУММА(НачальноеСальдоДебет),
	               |	СУММА(НачальноеСальдоКредит),
	               |	СУММА(ОборотДебет),
	               |	СУММА(ОборотКредит),
	               |	СУММА(КонечноеСальдоДебет),
	               |	СУММА(КонечноеСальдоКредит)
	               |ПО
	               |	ОБЩИЕ,
	               |	Контрагент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ОплатыПоАгентскомуДоговору.Контрагент КАК Контрагент,
	               |	ОплатыПоАгентскомуДоговору.ДокументДвижения КАК ДокументОплаты,
	               |	ОплатыПоАгентскомуДоговору.ДокументДвижения.Дата КАК ДатаДокументаОплаты,
	               |	ВЫРАЗИТЬ(ОплатыПоАгентскомуДоговору.СуммаОборот КАК ЧИСЛО(15, 2)) КАК СуммаОплаты,
	               |	ВЫРАЗИТЬ(ОплатыПоАгентскомуДоговору.СуммаОборот * 18 / 118 КАК ЧИСЛО(15, 2)) КАК СуммаНДСОплаты,
	               |	ОплатыПоАгентскомуДоговору.Контрагент.Наименование КАК КонтрагентНаименование
	               |ИЗ
	               |	РегистрНакопления.ДвиженияДенежныхСредств.Обороты(
	               |			&ДатаНачала,
	               |			&ДатаОкончания,
	               |			,
	               |			ДоговорКонтрагента В
	               |				(ВЫБРАТЬ
	               |					ТаблицаДоговоров.Договор
	               |				ИЗ
	               |					ВТ_Договора КАК ТаблицаДоговоров)) КАК ОплатыПоАгентскомуДоговору
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	КонтрагентНаименование,
	               |	ДатаДокументаОплаты
	               |ИТОГИ
	               |	СУММА(СуммаОплаты),
	               |	СУММА(СуммаНДСОплаты)
	               |ПО
	               |	ОБЩИЕ,
	               |	Контрагент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВложенныйЗапрос.Контрагент,
	               |	ВложенныйЗапрос.Договор,
	               |	ВложенныйЗапрос.Регистратор КАК ДокументПоступления,
	               |	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	               |	СУММА(ВложенныйЗапрос.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(ВложенныйЗапрос.Сумма + ВложенныйЗапрос.СуммаНДС) КАК СуммаСНДС
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК Контрагент,
	               |		ХозрасчетныйОборотыДтКт.СубконтоКт2 КАК Договор,
	               |		ХозрасчетныйОборотыДтКт.Регистратор КАК Регистратор,
	               |		ВЫБОР
	               |			КОГДА НЕ ХозрасчетныйОборотыДтКт.СчетДт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям))
	               |				ТОГДА ХозрасчетныйОборотыДтКт.СуммаОборот
	               |			ИНАЧЕ 0
	               |		КОНЕЦ КАК Сумма,
	               |		ВЫБОР
	               |			КОГДА ХозрасчетныйОборотыДтКт.СчетДт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям))
	               |				ТОГДА ХозрасчетныйОборотыДтКт.СуммаОборот
	               |			ИНАЧЕ 0
	               |		КОНЕЦ КАК СуммаНДС
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	               |				&ДатаНачала,
	               |				&ДатаОкончания,
	               |				Регистратор,
	               |				,
	               |				,
	               |				,
	               |				,
	               |				СубконтоКт2 В
	               |					(ВЫБРАТЬ
	               |						ТаблицаДоговоров.Договор
	               |					ИЗ
	               |						ВТ_Договора КАК ТаблицаДоговоров)) КАК ХозрасчетныйОборотыДтКт
	               |	ГДЕ
	               |		(ХозрасчетныйОборотыДтКт.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
	               |				ИЛИ ХозрасчетныйОборотыДтКт.Регистратор ССЫЛКА Документ.ПоступлениеДопРасходов
	               |				ИЛИ ХозрасчетныйОборотыДтКт.Регистратор ССЫЛКА Документ.КорректировкаПоступления)
	               |		И ВЫБОР
	               |				КОГДА ХозрасчетныйОборотыДтКт.СчетДт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками))
	               |							И ХозрасчетныйОборотыДтКт.СчетКт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками))
	               |						ИЛИ ХозрасчетныйОборотыДтКт.СчетДт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами))
	               |							И ХозрасчетныйОборотыДтКт.СчетКт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами))
	               |						ИЛИ ХозрасчетныйОборотыДтКт.СчетДт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами))
	               |							И ХозрасчетныйОборотыДтКт.СчетКт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками))
	               |						ИЛИ ХозрасчетныйОборотыДтКт.СчетДт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками))
	               |							И ХозрасчетныйОборотыДтКт.СчетКт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами))
	               |					ТОГДА ЛОЖЬ
	               |				ИНАЧЕ ИСТИНА
	               |			КОНЕЦ) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.Контрагент,
	               |	ВложенныйЗапрос.Договор,
	               |	ВложенныйЗапрос.Регистратор
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВложенныйЗапрос.Регистратор.Дата
	               |АВТОУПОРЯДОЧИВАНИЕ";
				   
	МассивСубконтоДоговоры = Новый Массив();
	МассивСубконтоДоговоры.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	Запрос.УстановитьПараметр("ДатаНачала", 				Новый Граница(НачалоДня(ДатаНачала),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаОкончания", 				Новый Граница(КонецДня(ДатаОкончания),ВидГраницы.Включая));
	
	//абс Родин Лимитный контроль	
	МассивСтатусовДоговоров = Новый Массив;
	МассивСтатусовДоговоров.Добавить(Перечисления.абсСтатусыДоговоров.Исполнение);	
	МассивСтатусовДоговоров.Добавить(Перечисления.абсСтатусыДоговоров.Корректировка);	
	МассивСтатусовДоговоров.Добавить(Перечисления.абсСтатусыДоговоров.СогласованиеОФКПослеКоррректировки);	
	МассивСтатусовДоговоров.Добавить(Перечисления.абсСтатусыДоговоров.ОтказПослеКорректировки);	
	//абс Родин Лимитный контроль	
	
	Запрос.УстановитьПараметр("СтатусДоговора", 			МассивСтатусовДоговоров);
	Запрос.УстановитьПараметр("ВыбранныйАгентскийДоговор", 	ВыбАгентскийДоговор);
	Запрос.УстановитьПараметр("МассивСубконтоДоговоры", 	МассивСубконтоДоговоры);
	
	ПакетыРезультатаЗапроса = Запрос.ВыполнитьПакет();
	
	Если ПакетыРезультатаЗапроса.Количество() > 1 Тогда
		РезультатДоговоры = ПакетыРезультатаЗапроса[1];
	Иначе
		РезультатДоговоры = Неопределено;
	КонецЕсли;
	
	Если ПакетыРезультатаЗапроса.Количество() > 2 Тогда
		РезультатОплатыПоДоговору = ПакетыРезультатаЗапроса[2];
	Иначе
		РезультатОплатыПоДоговору = Неопределено;
	КонецЕсли;
	
	Если ПакетыРезультатаЗапроса.Количество() > 1 Тогда
		РезультатОСВПоДоговору = ПакетыРезультатаЗапроса[1];
		ТаблицаОСВПоДоговору =   ПакетыРезультатаЗапроса[1].Выгрузить();
	Иначе
		РезультатОСВПоДоговору = Неопределено;
		ТаблицаОСВПоДоговору = Неопределено;
	КонецЕсли;
	
	Если ПакетыРезультатаЗапроса.Количество() > 3 Тогда
		РезультатПоступленияПоДоговорам = ПакетыРезультатаЗапроса[3].Выгрузить();
	Иначе
		РезультатПоступленияПоДоговорам = Неопределено;
	КонецЕсли;

	МакетОтчета = ПолучитьМакет("ОтчетАгента");
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	ОбластьЗаголовок = МакетОтчета.ПолучитьОбласть("Заголовок");
	
	СтрАгентскийДоговор = СокрЛП(ВыбАгентскийДоговор.Наименование);
	ЕСли НЕ ПустаяСтрока(ВыбАгентскийДоговор.Номер) Тогда
		СтрАгентскийДоговор = СокрЛП(ВыбАгентскийДоговор.Номер);
	КонецЕсли;
	
	СтрАгентскийДоговорДата = "";
	Если ЗначениеЗаполнено(ВыбАгентскийДоговор.Дата) Или (НЕ ВыбАгентскийДоговор.Дата = Дата(1, 1, 1)) Тогда
		СтрАгентскийДоговорДата = Формат(ВыбАгентскийДоговор.Дата, "ДЛФ=DD");
	КонецЕсли;                        
	
	ОбластьЗаголовок.Параметры.АгентскийДоговор = СтрАгентскийДоговор + ?(Не ПустаяСтрока(СтрАгентскийДоговорДата), " от " + СтрАгентскийДоговорДата, "");
	ОбластьЗаголовок.Параметры.ПериодОтчета 	= "за период с " + Формат(ДатаНачала, "ДЛФ=DD") + " по " + Формат(ДатаОкончания, "ДЛФ=DD");
	ТабДокумент.Вывести(ОбластьЗаголовок);
	
	Если РезультатДоговоры = Неопределено Или РезультатДоговоры.Пустой() Тогда
		Сообщить("Отсутствуют данные по контрагентам-исполнителям и принадлежащим им договорам, связанными с данным агентским договором!"+Символы.ПС+
				 "Эти данные в отчет выведены не будут!", СтатусСообщения.Внимание);
	Иначе
		
		Шапка1  = МакетОтчета.ПолучитьОбласть("Шапка1");
		Строка1 = МакетОтчета.ПолучитьОбласть("Строка1");
		
		ТабДокумент1 = Новый ТабличныйДокумент;
		
		Шапка2 						= МакетОтчета.ПолучитьОбласть("Шапка2");
		Строка2						= МакетОтчета.ПолучитьОбласть("Строка2");
		ИтогиПоКонтрагенту2			= МакетОтчета.ПолучитьОбласть("ИтогиПоКонтрагенту2");
		ИтогиОбщие2 				= МакетОтчета.ПолучитьОбласть("ИтогиОбщие2");
		СтрокаВознаграждениеАгента 	= МакетОтчета.ПолучитьОбласть("ВознаграждениеАгента");
		
		ТабДокумент2 = Новый ТабличныйДокумент;
		                                                 
		ТабДокумент1.Вывести(Шапка1);
		ТабДокумент2.Вывести(Шапка2);
				
		ИтогоСуммаНДСУслуг 		= 0;
		ИтогоСуммаБезНДСУслуг 	= 0;
		ИтогоСуммаСНДСУслуг		= 0;
		
		НомПП_1 = 1;
		
		ВыборкаОбщие = РезультатДоговоры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОБЩИЕ");
				
		ВыборкаКонтрагенты = ВыборкаОбщие.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКонтрагенты.Следующий() Цикл
			
			ИтогоСуммаНДСУслугПоКонтрагенту 		= 0;
			ИтогоСуммаБезНДСУслугПоКонтрагенту 		= 0;
			ИтогоСуммаСНДСУслугПоКонтрагенту		= 0;
			
			НомПП_2 = 1;
			
			ВыборкаДоговор = ВыборкаКонтрагенты.Выбрать();
			Пока ВыборкаДоговор.Следующий() Цикл
				
				//если нет оборотов за выбранный период, то выводить данные не нужно
				ЕСли ВыборкаДоговор.ОборотДебет = 0 И ВыборкаДоговор.ОборотКредит = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				СуммаБезНДСУслугПоДоговору = 0;
				СуммаНДСУслугПоДоговору = 0;
				СуммаСНДСУслугПоДоговору = 0;
				
				Строка1.Параметры.НомПП = НомПП_1;
				Строка1.Параметры.Заполнить(ВыборкаДоговор);
				Строка1.Параметры.КонтрагентРасшифровка = ВыборкаДоговор.Контрагент.Ссылка;
				Строка1.Параметры.ДоговорРасшифровка = ВыборкаДоговор.Договор.Ссылка;
				
				//АБС ВСТАВКА   19.03.2014  Навценя
				Если НЕ ПустаяСтрока(ВыборкаДоговор.Договор.Наименование) Тогда
					Строка1.Параметры.Договор = СокрЛП(ВыборкаДоговор.Договор.Наименование);
				КонецЕсли;
				//АБС ВСТАВКА  КОНЕЦ
			
				ТабДокумент1.Вывести(Строка1);
				
				НомПП_1 = НомПП_1 + 1;
												
				ЕСли НЕ РезультатПоступленияПоДоговорам = Неопределено И НЕ РезультатПоступленияПоДоговорам.Количество() = 0 Тогда
					
					МассивСтрокПоДоговору = РезультатПоступленияПоДоговорам.НайтиСтроки(Новый Структура("Договор", ВыборкаДоговор.Договор));
					
					Если МассивСтрокПоДоговору.Количество() = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					Строка2.Параметры.Заполнить(ВыборкаДоговор);
					Строка2.Параметры.КонтрагентРасшифровка = ВыборкаДоговор.Контрагент.Ссылка;
					Строка2.Параметры.ДоговорРасшифровка = ВыборкаДоговор.Договор.Ссылка;

					//АБС ВСТАВКА 19.03.2014 Навценя					
					Если НЕ ПустаяСтрока(ВыборкаДоговор.Договор.Наименование) Тогда
						Строка2.Параметры.Договор = СокрЛП(ВыборкаДоговор.Договор.Наименование);
					КонецЕсли;
					//АБС ВСТАВКА  КОНЕЦ
				
					Строка2.Параметры.НомПП = НомПП_2;
					Строка2.Параметры.ИНН_Исполнителя = ВыборкаДоговор.Контрагент.ИНН;
					
					Для каждого СтрокаПоДоговору Из МассивСтрокПоДоговору Цикл
						СуммаНДСУслугПоДоговору 	= СуммаНДСУслугПоДоговору + СтрокаПоДоговору.СуммаНДС;
						СуммаСНДСУслугПоДоговору 	= СуммаСНДСУслугПоДоговору + СтрокаПоДоговору.СуммаСНДС;
						СуммаБезНДСУслугПоДоговору 	= СуммаБезНДСУслугПоДоговору + СтрокаПоДоговору.Сумма;
					КонецЦикла;
					
					Строка2.Параметры.СуммаНДСУслугПоДоговору 		= СуммаНДСУслугПоДоговору;
					Строка2.Параметры.СуммаСНДСУслугПоДоговору 		= СуммаСНДСУслугПоДоговору;
					Строка2.Параметры.СуммаБезНДСУслугПоДоговору 	= СуммаБезНДСУслугПоДоговору;
									
					ТабДокумент2.Вывести(Строка2);
					
					НомПП_2 = НомПП_2 + 1;
				Иначе
					Продолжить;
				КонецЕСли;
				
				ИтогоСуммаНДСУслугПоКонтрагенту 	= ИтогоСуммаНДСУслугПоКонтрагенту 	 + СуммаНДСУслугПоДоговору;
				ИтогоСуммаБезНДСУслугПоКонтрагенту 	= ИтогоСуммаБезНДСУслугПоКонтрагенту + СуммаБезНДСУслугПоДоговору;
				ИтогоСуммаСНДСУслугПоКонтрагенту	= ИтогоСуммаСНДСУслугПоКонтрагенту	 + СуммаСНДСУслугПоДоговору;
								
			КонецЦикла;
			
			Если ИтогоСуммаНДСУслугПоКонтрагенту = 0 И ИтогоСуммаБезНДСУслугПоКонтрагенту = 0 И ИтогоСуммаСНДСУслугПоКонтрагенту = 0 Тогда
				Продолжить;
			КонецЕСли;
			
			ИтогиПоКонтрагенту2.Параметры.Контрагент = ВыборкаКонтрагенты.Контрагент;
			ИтогиПоКонтрагенту2.Параметры.ИтогоСуммаНДСУслугПоКонтрагенту 	 = ИтогоСуммаНДСУслугПоКонтрагенту;
			ИтогиПоКонтрагенту2.Параметры.ИтогоСуммаБезНДСУслугПоКонтрагенту = ИтогоСуммаБезНДСУслугПоКонтрагенту;
			ИтогиПоКонтрагенту2.Параметры.ИтогоСуммаСНДСУслугПоКонтрагенту 	 = ИтогоСуммаСНДСУслугПоКонтрагенту;
			ТабДокумент2.Вывести(ИтогиПоКонтрагенту2);
			
			ИтогоСуммаНДСУслуг 		= ИтогоСуммаНДСУслуг 	+ ИтогоСуммаНДСУслугПоКонтрагенту;
			ИтогоСуммаБезНДСУслуг 	= ИтогоСуммаБезНДСУслуг + ИтогоСуммаБезНДСУслугПоКонтрагенту;
			ИтогоСуммаСНДСУслуг		= ИтогоСуммаСНДСУслуг	+ ИтогоСуммаСНДСУслугПоКонтрагенту;
			
		КонецЦикла;
		
		//ИтогиОбщие2.Параметры.ИтогоСуммаНДСУслуг 	= ИтогоСуммаНДСУслуг;
		//ИтогиОбщие2.Параметры.ИтогоСуммаБезНДСУслуг = ИтогоСуммаБезНДСУслуг;
		ИтогиОбщие2.Параметры.ИтогоСуммаСНДСУслуг 	= ИтогоСуммаСНДСУслуг;		
		ТабДокумент2.Вывести(ИтогиОбщие2);
		
		ТабДокумент2.Вывести(СтрокаВознаграждениеАгента);
		
		ТабДокумент.Вывести(ТабДокумент1);
		ТабДокумент.Вывести(ТабДокумент2);
		
	КонецЕСли;
	
	Если РезультатОплатыПоДоговору = Неопределено Или РезультатОплатыПоДоговору.Пустой() Тогда
		Сообщить("За выбранный период отсутствуют данные по оплатам по данному агентскому договору!"+Символы.ПС+
				 "Эти данные в отчет выведены не будут!", СтатусСообщения.Внимание);
	Иначе
		
		Шапка3  			 = МакетОтчета.ПолучитьОбласть("Шапка3");
		Строка3 			 = МакетОтчета.ПолучитьОбласть("Строка3");
		ИтогиПоКонтрагенту3  = МакетОтчета.ПолучитьОбласть("ИтогиПоКонтрагенту3");
		Итоги3Общие			 = МакетОтчета.ПолучитьОбласть("ИтогиОбщие3");
		 
		ТабДокумент.Вывести(Шапка3);
		
		ВыборкаОбщие = РезультатОплатыПоДоговору.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Общие");
		ВыборкаОбщие.Следующий();
				
		ВыборкаКонтрагенты = ВыборкаОбщие.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Контрагент");
		Пока ВыборкаКонтрагенты.Следующий() Цикл
			
			НомПП_3 = 1;
			
			ВыборкаДокументОплаты = ВыборкаКонтрагенты.Выбрать();
			Пока ВыборкаДокументОплаты.Следующий() Цикл
				
				МетаданныеДокумента = ВыборкаДокументОплаты.ДокументОплаты.Метаданные();
				
				Строка3.Параметры.Заполнить(ВыборкаДокументОплаты);
				Строка3.Параметры.КонтрагентРасшифровка = ВыборкаКонтрагенты.Контрагент.Ссылка;
				Строка3.Параметры.НомПП = НомПП_3;
				Строка3.Параметры.ИНН_Исполнителя = ВыборкаДокументОплаты.Контрагент.ИНН;
				Строка3.Параметры.Признак = "оплата";
				Строка3.Параметры.НомерИДатаДокумента = ВыборкаДокументОплаты.ДокументОплаты.Номер + " от " + Формат(ВыборкаДокументОплаты.ДокументОплаты.Дата, "ДФ=dd.MM.yyyy; ДЛФ=D");
				Строка3.Параметры.ДокументРасшифровка = ВыборкаДокументОплаты.ДокументОплаты.Ссылка;
				ТабДокумент.Вывести(Строка3);
				
				НомПП_3 = НомПП_3 + 1;
				
			КонецЦикла;
			
			ИтогиПоКонтрагенту3.Параметры.Заполнить(ВыборкаКонтрагенты);
			ТабДокумент.Вывести(ИтогиПоКонтрагенту3);
			
		КонецЦикла;
		
		Итоги3Общие.Параметры.Заполнить(ВыборкаОбщие);
		ТабДокумент.Вывести(Итоги3Общие);
		
	КонецЕсли;
	
	//4
	Если РезультатОСВПоДоговору = Неопределено Или РезультатОСВПоДоговору.Пустой() Тогда
		//Сообщить("За выбранный период отсутствуют движения по бухгалтерским проводкам по данному агентскому договору!"+Символы.ПС+
		//		 "Эти данные в отчет выведены не будут!", СтатусСообщения.Внимание);
	Иначе
		
		Шапка4  			 = МакетОтчета.ПолучитьОбласть("Шапка4");
		Строка4 			 = МакетОтчета.ПолучитьОбласть("Строка4");
		ИтогиПоКонтрагенту4  = МакетОтчета.ПолучитьОбласть("ИтогиПоКонтрагенту4");
		//Итоги3Общие			 = МакетОтчета.ПолучитьОбласть("ИтогиОбщие3");
		 
		ТабДокумент.Вывести(Шапка4);
		
		ВыборкаОбщие = РезультатОСВПоДоговору.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Общие");
		ВыборкаОбщие.Следующий();
				
		ВыборкаКонтрагенты = ВыборкаОбщие.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Контрагент");
		Пока ВыборкаКонтрагенты.Следующий() Цикл
			
			НомПП_4 = 1;
			
			ВыборкаБУ = ВыборкаКонтрагенты.Выбрать();
			Пока ВыборкаБУ.Следующий() Цикл                                       
							
				Строка4.Параметры.Заполнить(ВыборкаБУ);
				
				//АБС ВСТАВКА   19.03.2014 18:26:32  Навценя						  
				Если НЕ ПустаяСтрока(ВыборкаБУ.Договор.Наименование) Тогда
					Строка4.Параметры.Договор = СокрЛП(ВыборкаБУ.Договор.Наименование);
				КонецЕсли;
				 //АБС ВСТАВКА  КОНЕЦ
				
				Строка4.Параметры.КонтрагентРасшифровка = ВыборкаБУ.Контрагент.Ссылка;
				Строка4.Параметры.ДоговорРасшифровка 	= ВыборкаБУ.Договор;
				Строка4.Параметры.НомПП = НомПП_4;
				Строка4.Параметры.ИНН_Исполнителя = ВыборкаБУ.Контрагент.ИНН;
				
				ТабДокумент.Вывести(Строка4);
				
				НомПП_4 = НомПП_4 + 1;
				
			КонецЦикла;
			
			ИтогиПоКонтрагенту4.Параметры.Заполнить(ВыборкаКонтрагенты);
			ТабДокумент.Вывести(ИтогиПоКонтрагенту4);
			
		КонецЦикла;
		
		//Итоги3Общие.Параметры.Заполнить(ВыборкаОбщие);
		//ТабДокумент.Вывести(Итоги3Общие);
		
	КонецЕсли;
	
	Подвал = МакетОтчета.ПолучитьОбласть("Подвал");
	Подвал.Параметры.АгентПолноеНаименование = ?(НЕ ПустаяСтрока(ВыбАгентскийДоговор.Владелец.НаименованиеПолное), ВыбАгентскийДоговор.Владелец.НаименованиеПолное, ВыбАгентскийДоговор.Владелец.Наименование);
	ТабДокумент.Вывести(Подвал);
	
	ФормаВыводаРезультата = ЭтотОбъект.ПолучитьФорму("ФормаВыводаРезультата", ОсновнаяФормаОбработки);
	ФормаВыводаРезультата.ЗакрыватьПриЗакрытииВладельца = Истина;
	ФормаВыводаРезультата.ЭлементыФормы.ДокументРезультат.Очистить();
	ФормаВыводаРезультата.ЭлементыФормы.ДокументРезультат.Вывести(ТабДокумент);
	ФормаВыводаРезультата.ЭлементыФормы.ДокументРезультат.Защита 		 = Ложь;
	ФормаВыводаРезультата.ЭлементыФормы.ДокументРезультат.ТолькоПросмотр = Истина;
	ФормаВыводаРезультата.Открыть();	
	
КонецПроцедуры                                           