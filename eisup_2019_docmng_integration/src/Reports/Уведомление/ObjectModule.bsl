Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДокументРезультат.Очистить();
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Документы.УведомлениеОКонтролируемыхСделках.ПолучитьВременныеТаблицыДляЗаполненияУведомления(Уведомление);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтролируемыеСделки.Организация,
	|	КонтролируемыеСделки.ОтчетныйГод,
	|	КонтролируемыеСделки.Контрагент,
	|	КонтролируемыеСделки.ДоговорКонтрагента,
	|	КонтролируемыеСделки.ПредметСделки,
	|	КонтролируемыеСделки.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли КАК СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли,
	|	СУММА(КонтролируемыеСделки.СуммаБезНДСВРублях) КАК СуммаБезНДСВРублях,
	|	КонтролируемыеСделки.КонтрагентЗарегистрированВРФ,
	|	КонтролируемыеСделки.ЗарегистрированВСтранеСЛьготнымНалогообложением,
	|	КонтролируемыеСделки.ТипВзаимозависимости,
	|	СУММА(ВЫБОР
	|			КОГДА КонтролируемыеСделки.ЗарегистрированВСтранеСЛьготнымНалогообложением
	|				ТОГДА КонтролируемыеСделки.СуммаБезНДСВРублях
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаБезНДСВРубляхСделкаСОфшором,
	|	СУММА(ВЫБОР
	|			КОГДА КонтролируемыеСделки.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли
	|				ТОГДА КонтролируемыеСделки.СуммаБезНДСВРублях
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаБезНДСВРубляхСделкаВнешнейТорговли,
	|	СУММА(ВЫБОР
	|			КОГДА КонтролируемыеСделки.СделкаОблагаетсяЕНВД
	|					ИЛИ КонтролируемыеСделки.СделкаСПлательщикомЕСХН
	|				ТОГДА КонтролируемыеСделки.СуммаБезНДСВРублях
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаБезНДСВРубляхСделкаВРамкахСпецрежима,
	|	СУММА(ВЫБОР
	|			КОГДА КонтролируемыеСделки.СделкаКонтролируетсяПоНалогуНаПрибыль
	|				ТОГДА КонтролируемыеСделки.СуммаБезНДСВРублях
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаБезНДСВРубляхСделкаКонтролируетсяПоПрибыли,
	|	СУММА(ВЫБОР
	|			КОГДА КонтролируемыеСделки.СделкаКонтролируетсяПоРегистрацииВОЭЗ
	|				ТОГДА КонтролируемыеСделки.СуммаБезНДСВРублях
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаБезНДСВРубляхСделкаКонтролируетсяПоОЭЗ,
	|	СУММА(ВЫБОР
	|			КОГДА КонтролируемыеСделки.СделкаКонтролируетсяПоНДПИ
	|				ТОГДА КонтролируемыеСделки.СуммаБезНДСВРублях
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаБезНДСВРубляхСделкаКонтролируетсяПоНДПИ
	|ИЗ
	|	КонтролируемыеСделки КАК КонтролируемыеСделки
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоОбщейСумме КАК КонтрагентыКонтролируемыеПоОбщейСумме
	|		ПО КонтролируемыеСделки.Контрагент = КонтрагентыКонтролируемыеПоОбщейСумме.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоНДПИ КАК КонтрагентыКонтролируемыеПоНДПИ
	|		ПО КонтролируемыеСделки.Контрагент = КонтрагентыКонтролируемыеПоНДПИ.Контрагент
	|			И (КонтролируемыеСделки.СделкаКонтролируетсяПоНДПИ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоСпецРежиму КАК КонтрагентыКонтролируемыеПоСпецРежиму
	|		ПО КонтролируемыеСделки.Контрагент = КонтрагентыКонтролируемыеПоСпецРежиму.Контрагент
	|			И (КонтролируемыеСделки.СделкаОблагаетсяЕНВД
	|				ИЛИ КонтролируемыеСделки.СделкаСПлательщикомЕСХН)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоПрибыли КАК КонтрагентыКонтролируемыеПоПрибыли
	|		ПО КонтролируемыеСделки.Контрагент = КонтрагентыКонтролируемыеПоПрибыли.Контрагент
	|			И (КонтролируемыеСделки.СделкаКонтролируетсяПоНалогуНаПрибыль)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоОЭЗ КАК КонтрагентыКонтролируемыеПоОЭЗ
	|		ПО КонтролируемыеСделки.Контрагент = КонтрагентыКонтролируемыеПоОЭЗ.Контрагент
	|			И (КонтролируемыеСделки.СделкаКонтролируетсяПоРегистрацииВОЭЗ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыКонтролируемыеПоРегистрацииНеВРФ КАК КонтрагентыКонтролируемыеПоРегистрацииНеВРФ
	|		ПО КонтролируемыеСделки.Контрагент = КонтрагентыКонтролируемыеПоРегистрацииНеВРФ.Контрагент
	|			И (КонтролируемыеСделки.ЗарегистрированВСтранеСЛьготнымНалогообложением
	|				ИЛИ КонтролируемыеСделки.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДоговорыУчастниковКонтролируемыхСделок КАК ДоговорыУчастниковКонтролируемыхСделок
	|		ПО КонтролируемыеСделки.Организация = ДоговорыУчастниковКонтролируемыхСделок.Организация
	|			И КонтролируемыеСделки.Контрагент = ДоговорыУчастниковКонтролируемыхСделок.Контрагент
	|			И КонтролируемыеСделки.ДоговорКонтрагента = ДоговорыУчастниковКонтролируемыхСделок.ДоговорКонтрагента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредметыКонтролируемыхСделок КАК ПредметыКонтролируемыхСделок
	|		ПО КонтролируемыеСделки.ПредметСделки = ПредметыКонтролируемыхСделок.ПредметСделки
	|ГДЕ
	|	КонтролируемыеСделки.Контрагент В
	|			(ВЫБРАТЬ
	|				КонтрагентыПоМинимальнойСумме.Контрагент
	|			ИЗ
	|				КонтрагентыПоМинимальнойСумме КАК КонтрагентыПоМинимальнойСумме)
	|	И (КонтролируемыеСделки.ТипВзаимозависимости = ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.НеВзаимозависимыйПосредник)
	|			ИЛИ КонтролируемыеСделки.СтороныВзаимозависимыПоКодексу
	|				И НЕ КонтролируемыеСделки.КонтрагентЗарегистрированВРФ
	|			ИЛИ НЕ КонтрагентыКонтролируемыеПоОбщейСумме.Контрагент ЕСТЬ NULL 
	|			ИЛИ НЕ КонтрагентыКонтролируемыеПоНДПИ.Контрагент ЕСТЬ NULL 
	|			ИЛИ НЕ КонтрагентыКонтролируемыеПоСпецРежиму.Контрагент ЕСТЬ NULL 
	|			ИЛИ НЕ КонтрагентыКонтролируемыеПоПрибыли.Контрагент ЕСТЬ NULL 
	|			ИЛИ НЕ КонтрагентыКонтролируемыеПоОЭЗ.Контрагент ЕСТЬ NULL 
	|			ИЛИ НЕ КонтрагентыКонтролируемыеПоРегистрацииНеВРФ.Контрагент ЕСТЬ NULL )
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтролируемыеСделки.Организация,
	|	КонтролируемыеСделки.ОтчетныйГод,
	|	КонтролируемыеСделки.Контрагент,
	|	КонтролируемыеСделки.ДоговорКонтрагента,
	|	КонтролируемыеСделки.ПредметСделки,
	|	КонтролируемыеСделки.СделкаВнешнейТорговлиТоваромМировойБиржевойТорговли,
	|	КонтролируемыеСделки.КонтрагентЗарегистрированВРФ,
	|	КонтролируемыеСделки.ЗарегистрированВСтранеСЛьготнымНалогообложением,
	|	КонтролируемыеСделки.ТипВзаимозависимости";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("КонтролируемыеСделки", Таблица);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки);
	
	//Создадим и инициализируем процессор компоновки
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных);
	
	//Создадим и инициализируем процессор вывода результата
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);

КонецПроцедуры
