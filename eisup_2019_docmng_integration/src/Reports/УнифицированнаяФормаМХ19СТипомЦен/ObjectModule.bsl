#ЕСЛИ КЛИЕНТ ТОГДА

// Процедура формирования печатной формы отчета
//
Процедура ОбновитьОтчет(ТабДокумент) Экспорт

    Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Предупреждение("Не указана организация");
		Возврат;
	КонецЕсли;
    Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Предупреждение("Не указан склад");
		Возврат;
	КонецЕсли;
	
	ТабДокумент.Очистить();
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РегПартииТоваров.Номенклатура,
		|	РегПартииТоваров.Номенклатура.Код КАК Код,
		|	ПРЕДСТАВЛЕНИЕ(РегПартииТоваров.Номенклатура) КАК ПечНоменклатура,
		|	РегПартииТоваров.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдИзм,
		|	РегПартииТоваров.Номенклатура.ЕдиницаХраненияОстатков.Код КАК ПечКодЕдИзм,
		|	ПРЕДСТАВЛЕНИЕ(РегПартииТоваров.Номенклатура.ЕдиницаХраненияОстатков) КАК ПечЕдИзм,
		|	СУММА(РегПартииТоваров.КолОст1) КАК КолОст1,
		|	СУММА(РегПартииТоваров.КолОст2) КАК КолОст2,
		|	СУММА(РегПартииТоваров.КолОст3) КАК КолОст3,
		|	СУММА(РегПартииТоваров.СумОст1) КАК СумОст1,
		|	СУММА(РегПартииТоваров.СумОст2) КАК СумОст2,
		|	СУММА(РегПартииТоваров.СумОст3) КАК СумОст3,
		|	РегПартииТоваров.ХарактеристикаНоменклатуры,
		|	ПРЕДСТАВЛЕНИЕ(РегПартииТоваров.ХарактеристикаНоменклатуры) КАК ПечХарактеристикаНоменклатуры
		|ИЗ
		|	(ВЫБРАТЬ
		|		РегТовары.Номенклатура КАК Номенклатура,
		|		РегТовары.КоличествоОстаток КАК КолОст1,
		|		0 КАК КолОст2,
		|		0 КАК КолОст3,
		|		РегТовары.КоличествоОстаток * ЦеныНоменклатуры.Цена * ЕСТЬNULL(КурсыВалют.Курс, 0) * ВЫБОР
		|			КОГДА ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент ЕСТЬ NULL 
		|					ИЛИ ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент = 0
		|				ТОГДА 1
		|			ИНАЧЕ ЦеныНоменклатуры.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент
		|		КОНЕЦ * ЕСТЬNULL(КурсВалютыУчета.Кратность, 1) / (ЕСТЬNULL(КурсВалютыУчета.Курс, 1) * ВЫБОР
		|			КОГДА КурсыВалют.Кратность ЕСТЬ NULL 
		|					ИЛИ КурсыВалют.Кратность = 0
		|				ТОГДА 1
		|			ИНАЧЕ КурсыВалют.Кратность
		|		КОНЕЦ) КАК СумОст1,
		|		0 КАК СумОст2,
		|		0 КАК СумОст3,
		|		РегТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций.Остатки(
		|				&ПериодС,
		|				Организация = &Орг
		|					И Склад = &Склад) КАК РегТовары
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ПериодС, ТипЦен = &ТипЦен) КАК ЦеныНоменклатуры
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ПериодС, ) КАК КурсыВалют
		|				ПО (КурсыВалют.Валюта = ЦеныНоменклатуры.Валюта)
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|						&ПериодС,
		|						ВАЛЮТА В
		|							(ВЫБРАТЬ
		|								Константы.ВалютаРегламентированногоУчета
		|							ИЗ
		|								Константы КАК Константы)) КАК КурсВалютыУчета
		|				ПО (ИСТИНА)
		|			ПО (ЦеныНоменклатуры.Номенклатура = РегТовары.Номенклатура)
		|				И РегТовары.ХарактеристикаНоменклатуры = ЦеныНоменклатуры.ХарактеристикаНоменклатуры
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РегТовары.Номенклатура,
		|		0,
		|		РегТовары.КоличествоОстаток,
		|		0,
		|		0,
		|		РегТовары.КоличествоОстаток * ЦеныНоменклатуры.Цена * ЕСТЬNULL(КурсыВалют.Курс, 0) * ВЫБОР
		|			КОГДА ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент ЕСТЬ NULL 
		|					ИЛИ ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент = 0
		|				ТОГДА 1
		|			ИНАЧЕ ЦеныНоменклатуры.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент
		|		КОНЕЦ * ЕСТЬNULL(КурсВалютыУчета.Кратность, 1) / (ЕСТЬNULL(КурсВалютыУчета.Курс, 1) * ВЫБОР
		|			КОГДА КурсыВалют.Кратность ЕСТЬ NULL 
		|					ИЛИ КурсыВалют.Кратность = 0
		|				ТОГДА 1
		|			ИНАЧЕ КурсыВалют.Кратность
		|		КОНЕЦ),
		|		0,
		|		РегТовары.ХарактеристикаНоменклатуры
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций.Остатки(
		|				&ПериодПо,
		|				Организация = &Орг
		|					И Склад = &Склад) КАК РегТовары
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ПериодПо, ТипЦен = &ТипЦен) КАК ЦеныНоменклатуры
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ПериодПо, ) КАК КурсыВалют
		|				ПО (КурсыВалют.Валюта = ЦеныНоменклатуры.Валюта)
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|						&ПериодПо,
		|						ВАЛЮТА В
		|							(ВЫБРАТЬ
		|								Константы.ВалютаРегламентированногоУчета
		|							ИЗ
		|								Константы КАК Константы)) КАК КурсВалютыУчета
		|				ПО (ИСТИНА)
		|			ПО (ЦеныНоменклатуры.Номенклатура = РегТовары.Номенклатура)
		|				И РегТовары.ХарактеристикаНоменклатуры = ЦеныНоменклатуры.ХарактеристикаНоменклатуры
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РегТовары.Номенклатура,
		|		0,
		|		0,
		|		РегТовары.КоличествоОстаток,
		|		0,
		|		0,
		|		РегТовары.КоличествоОстаток * ЦеныНоменклатуры.Цена * ЕСТЬNULL(КурсыВалют.Курс, 0) * ВЫБОР
		|			КОГДА ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент ЕСТЬ NULL 
		|					ИЛИ ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент = 0
		|				ТОГДА 1
		|			ИНАЧЕ ЦеныНоменклатуры.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент
		|		КОНЕЦ * ЕСТЬNULL(КурсВалютыУчета.Кратность, 1) / (ЕСТЬNULL(КурсВалютыУчета.Курс, 1) * ВЫБОР
		|			КОГДА КурсыВалют.Кратность ЕСТЬ NULL 
		|					ИЛИ КурсыВалют.Кратность = 0
		|				ТОГДА 1
		|			ИНАЧЕ КурсыВалют.Кратность
		|		КОНЕЦ),
		|		РегТовары.ХарактеристикаНоменклатуры
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций.Остатки(
		|				&КонГраница,
		|				Организация = &Орг
		|					И Склад = &Склад) КАК РегТовары
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&КонГраница, ТипЦен = &ТипЦен) КАК ЦеныНоменклатуры
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонГраница, ) КАК КурсыВалют
		|				ПО (КурсыВалют.Валюта = ЦеныНоменклатуры.Валюта)
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|						&КонГраница,
		|						ВАЛЮТА В
		|							(ВЫБРАТЬ
		|								Константы.ВалютаРегламентированногоУчета
		|							ИЗ
		|								Константы КАК Константы)) КАК КурсВалютыУчета
		|				ПО (ИСТИНА)
		|			ПО (ЦеныНоменклатуры.Номенклатура = РегТовары.Номенклатура)
		|				И РегТовары.ХарактеристикаНоменклатуры = ЦеныНоменклатуры.ХарактеристикаНоменклатуры) КАК РегПартииТоваров
		|
		|СГРУППИРОВАТЬ ПО
		|	РегПартииТоваров.Номенклатура,
		|	РегПартииТоваров.Номенклатура.Код,
		|	РегПартииТоваров.Номенклатура.ЕдиницаХраненияОстатков,
		|	РегПартииТоваров.Номенклатура.ЕдиницаХраненияОстатков.Код,
		|	РегПартииТоваров.ХарактеристикаНоменклатуры
		|
		|УПОРЯДОЧИТЬ ПО
		|	РегПартииТоваров.Номенклатура.Наименование
		|ИТОГИ
		|	СУММА(КолОст1),
		|	СУММА(КолОст2),
		|	СУММА(КолОст3),
		|	СУММА(СумОст1),
		|	СУММА(СумОст2),
		|	СУММА(СумОст3)
		|ПО
		|	ОБЩИЕ";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр( "КонГраница", Новый Граница(КонецДня(КонДата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр( "ПериодС", Новый Граница(КонецДня(ПериодС), ВидГраницы.Включая));
	Запрос.УстановитьПараметр( "ПериодПо", Новый Граница(КонецДня(ПериодПо), ВидГраницы.Включая));
	Запрос.УстановитьПараметр( "Орг",        Организация);
	Запрос.УстановитьПараметр( "Склад",      Склад);
	Запрос.УстановитьПараметр( "ТипЦен",     ТипЦен);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Макет = ПолучитьМакет("МХ19");
	Область = Макет.ПолучитьОбласть("ПервыйЛист");
	
	ИнфоОрг = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация, КонДата, "ОКПО");
	
	// Вывод заголовка
	Область.Параметры.Организация     = Организация;
	Область.Параметры.ПечОрганизация  = Строка(Организация);
	Область.Параметры.ПечКодОКПО      = ИнфоОрг.ОКПО;
	Область.Параметры.ПечДата  = КонДата;
	Область.Параметры.Склад    = Склад;
	Область.Параметры.ПечСклад = Строка(Склад);
	
	ТабДокумент.Вывести(Область);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	// Вывод шапки таблицы
	ОбластьТабШапка = Макет.ПолучитьОбласть("ТабШапка");
	ОбластьТабШапка.Параметры.Дата1 = """" + Формат(ПериодС,  "DF=dd") + """ " + Формат(ПериодС,  "DF=MMM.yyyy");
	ОбластьТабШапка.Параметры.Дата2 = """" + Формат(ПериодПо, "DF=dd") + """ " + Формат(ПериодПо, "DF=MMM.yyyy");
	ОбластьТабШапка.Параметры.Дата3 = """" + Формат(КонДата,  "DF=dd") + """ " + Формат(КонДата,  "DF=MMM.yyyy");
	
	ТабДокумент.Вывести(ОбластьТабШапка);
	
	// Вывод табличной части
	Область = Макет.ПолучитьОбласть("ТабСтрока");
	ТабДокумент.Вывести(Область);
	
	МассивОбластей = Новый Массив;
	МассивОбластей.Добавить(Область);
	МассивОбластей.Добавить(Макет.ПолучитьОбласть("ТабИтог"));
	МассивОбластей.Добавить(Макет.ПолучитьОбласть("Подвал"));
	
	ОбходИтого = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ОбходИтого.Следующий() Тогда
	
		Обход = ОбходИтого.Выбрать();
		НомПП = 0;
		Пока Обход.Следующий() Цикл
			НомПП = НомПП + 1;
			
			Если ЗначениеЗаполнено(Обход.ХарактеристикаНоменклатуры) Тогда
				ПечНоменклатура =  Обход.ПечНоменклатура + " (" + Обход.ПечХарактеристикаНоменклатуры + ")";
			Иначе
				ПечНоменклатура =  Обход.ПечНоменклатура;
			КонецЕсли;
			
			Область.Параметры.ПечНомПП        = Строка(НомПП);
			Область.Параметры.ПечНоменклатура = ПечНоменклатура;
			Область.Параметры.Номенклатура    = Обход.Номенклатура;
			Область.Параметры.ПечКод          = Обход.Код;
			Область.Параметры.ПечЕдИзм        = Обход.ПечЕдИзм;
			Область.Параметры.ЕдИзм           = Обход.ЕдИзм;
			Область.Параметры.ПечКодЕдизм     = Обход.ПечКодЕдизм;
			Область.Параметры.КолОст1 = Обход.КолОст1;
			Область.Параметры.КолОст2 = Обход.КолОст2;
			Область.Параметры.КолОст3 = Обход.КолОст3;
			Область.Параметры.СумОст1 = Обход.СумОст1;
			Область.Параметры.СумОст2 = Обход.СумОст2;
			Область.Параметры.СумОст3 = Обход.СумОст3;
			
			Если НЕ ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, МассивОбластей) Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабДокумент.Вывести(ОбластьТабШапка);
			КонецЕсли;
			ТабДокумент.Вывести(Область);
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("ПустТаб");
		Пока ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, МассивОбластей, Ложь) Цикл
			ТабДокумент.Вывести(Область);
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("ТабИтог");
		Область.Параметры.КолОст1 = ОбходИтого.КолОст1;
		Область.Параметры.КолОст2 = ОбходИтого.КолОст2;
		Область.Параметры.КолОст3 = ОбходИтого.КолОст3;
		Область.Параметры.СумОст1 = ОбходИтого.СумОст1;
		Область.Параметры.СумОст2 = ОбходИтого.СумОст2;
		Область.Параметры.СумОст3 = ОбходИтого.СумОст3;
		
		ТабДокумент.Вывести(Область);
		
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("Подвал");
	ТабДокумент.Вывести(Область);
	
КонецПроцедуры // ОбновитьОтчет()

#КОНЕЦЕСЛИ