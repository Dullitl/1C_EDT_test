
///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС


Процедура ВыполнитьПроверкуДокументаПроектныйУчет(ДокументОбъект, Организация, Отказ, Ошибки = Ложь) Экспорт 
	
	Если Не глЗначениеПеременной("абс_ПроектныйУчет") Тогда 
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Если ДокументОбъект.ЭтоНовый() Тогда 
		Возврат;
	КонецЕсли;
		
	СтруктураПараметровПроверки = НеобходимаПроверкаДляОрганизации(Организация, ДокументОбъект.Дата);
	
	Если НЕ СтруктураПараметровПроверки.КонтрольЗаполнения Тогда 
		Возврат;
	КонецЕсли;
	
	Если НЕ НеобходимостьПроверкиОтСтатуса(ДокументОбъект) Тогда 
		Возврат;	
	КонецЕсли;
	
	ЗаполненоВШапке 		  = Истина;
	ЗаполненоВТаблицах  	  = Истина;
	ЗаполненоВТаблицеПроектов = Истина;
	ЗаполненоВЗЗ			  = Истина;
	
	МассивСообщений 		  = Новый Массив;
	
	ПроверитьЗаполнениеВШапкеДокумента(ДокументОбъект, ЗаполненоВШапке, МассивСообщений);
	ПроверитьЗаполнениеВТабличныхЧастях(ДокументОбъект, ЗаполненоВТаблицах, МассивСообщений);
	ПроверитьЗаполнениеВТаблицеПроектов(ДокументОбъект, ЗаполненоВТаблицеПроектов, МассивСообщений);
	ПроверитьЗаполнениеВЗЗ(ДокументОбъект, ЗаполненоВЗЗ, МассивСообщений);
	
	ЕстьОшибки = НЕ РезультатЗаполнения(ДокументОбъект, ЗаполненоВШапке,ЗаполненоВТаблицах,ЗаполненоВТаблицеПроектов, ЗаполненоВЗЗ);  
	
	Если ЕстьОшибки Тогда
		ВывестиМассивОшибок(МассивСообщений);
		Отказ = СтруктураПараметровПроверки.ОтказВЗаписи;
	КонецЕсли;
	
КонецПроцедуры

Функция НеобходимаПроверкаДляОрганизации(Организация, Дата) Экспорт
	
	Результат = Новый Структура("КонтрольЗаполнения, ОтказВЗаписи", Ложь, Ложь);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	абс_КонтрольАналитикПроектныйУчетСрезПоследних.ОтказЗаписи КАК ОтказВЗаписи,
	|	абс_КонтрольАналитикПроектныйУчетСрезПоследних.КонтрольЗаполнения
	|ИЗ
	|	РегистрСведений.абс_КонтрольАналитикПроектныйУчет.СрезПоследних(&Период, Организация = &Организация) КАК абс_КонтрольАналитикПроектныйУчетСрезПоследних");
	
	Запрос.УстановитьПараметр("Период", 	 Новый Граница(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


 ///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПРОВЕРКИ ЗАПОЛНЕНИЯ


Процедура ПроверитьЗаполнениеВШапкеДокумента(ДокументОбъект, ЗаполненоВШапке, МассивСообщений)
	
	Если НЕ (ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.РеализацияТоваровУслуг")
		ИЛИ  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПолучениеУслугПоПереработке")
		ИЛИ  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ТребованиеНакладная")
		ИЛИ  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АктОбОказанииПроизводственныхУслуг")) Тогда 
		Возврат;	
	КонецЕсли;

	ИмяРеквизита = "Проект";
	Проект = ДокументОбъект[ИмяРеквизита];
	
	Если НЕ ЗначениеЗаполнено(Проект) Тогда
		МассивСообщений.Добавить("Необходимо заполнить проект на закладке ""Дополнительно"".");
		ЗаполненоВШапке = Ложь;
		Возврат;
	КонецЕсли;
	
	Если НЕ ДействующийСтатус(Проект) Тогда
		МассивСообщений.Добавить("На закладке ""Дополнительно"" необходимо указать проект в статусе ""Действует"" или ""Согласован"".");
		ЗаполненоВШапке = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Проект.абс_РуководительПроекта) Тогда
		МассивСообщений.Добавить("В проекте указанном на закладке ""Дополнительно"" необходимо заполнить реквизит ""Руководитель проекта"".");
		ЗаполненоВШапке = Ложь;
	КонецЕсли;
		
КонецПроцедуры

Процедура ПроверитьЗаполнениеВТабличныхЧастях(ДокументОбъект, ЗаполненоВТаблицах, МассивСообщений)
	
	Если НЕ (ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ТребованиеНакладная")
		ИЛИ ТипЗнч(ДокументОбъект) 	= Тип("ДокументОбъект.ПринятиеКУчетуОС"))Тогда 
		Возврат;	
	КонецЕсли;
		
	СтруктураТабличныхЧастей = ПолучитьТабличныеЧастиИРеквизитыСПроектом(ДокументОбъект);
	
	Для каждого табличнаяЧасть из СтруктураТабличныхЧастей Цикл	
		Для каждого строка из ДокументОбъект[табличнаяЧасть.Ключ] Цикл 
			
			проект = строка[табличнаяЧасть.Значение];
			
			Если НЕ ЗначениеЗаполнено(проект) Тогда
				МассивСообщений.Добавить("Не заполнен реквизит проект в строке № " + Строка(строка.НомерСтроки) + " табличной части """ + табличнаяЧасть.Ключ + """"); 
				ЗаполненоВТаблицах = Ложь;
				Продолжить;
			КонецЕсли;
			
			Если НЕ ДействующийСтатус(проект) Тогда
				МассивСообщений.Добавить("В строке № " + Строка(строка.НомерСтроки) + " табличной части """ + табличнаяЧасть.Ключ + """, необходимо указать проект в статусе ""Действует"" или ""Согласован"". ");
				ЗаполненоВТаблицах = Ложь;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(проект.абс_РуководительПроекта) Тогда
				МассивСообщений.Добавить("В проекте указанном в строке № " + Строка(строка.НомерСтроки) + " табличной части """ + табличнаяЧасть.Ключ + """, необходимо заполнить реквизит ""Руководитель проекта"". ");
				ЗаполненоВТаблицах = Ложь;
			КонецЕсли;
				
		КонецЦикла;	
	КонецЦикла;
		
КонецПроцедуры

Процедура ПроверитьЗаполнениеВТаблицеПроектов(ДокументОбъект, ЗаполненоВТаблицеПроектов, МассивСообщений)
	
	Если НЕ (ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПлатежноеПоручениеВходящее")
		ИЛИ ТипЗнч(ДокументОбъект)  = Тип("ДокументОбъект.абс_ЗакупочныйЗаказ")
		ИЛИ ТипЗнч(ДокументОбъект) 	= Тип("ДокументОбъект.абс_СчетНаОплату")) Тогда 
		Возврат;	
	КонецЕсли;
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.абс_СчетНаОплату") Тогда		
		Если (ДокументОбъект.Технический) ИЛИ (НЕ ЗначениеЗаполнено(ДокументОбъект.ЗакупочныйЗаказ)) Тогда
			Возврат;
		КонецЕсли;
		Если ДокументОбъект.РаспределениеПоПроектам.Количество() = 0 Тогда 
			МассивСообщений.Добавить("Необходимо заполнить табличную часть ""Доли распределения"" на закладке ""Аналитика"". "); 
			ЗаполненоВТаблицеПроектов = Ложь;
		КонецЕсли;
	КонецЕсли;

	СтруктураТабличныхЧастей = ПолучитьТабличныеЧастиПОПроектам(ДокументОбъект);
	
	Для каждого табличнаяЧасть из СтруктураТабличныхЧастей Цикл	
		Для каждого строка из ДокументОбъект[табличнаяЧасть.Ключ] Цикл 
			
			проект = строка[табличнаяЧасть.Значение];
			ТЧ = ДокументОбъект[табличнаяЧасть.Ключ];
			
			Если НЕ ЗначениеЗаполнено(проект) Тогда
				Если РедактируетсяСписком(ДокументОбъект, ТЧ) Тогда 
					МассивСообщений.Добавить("Не заполнен реквизит проект в строке № " + Строка(строка.НомерСтроки) + " табличной части """ + табличнаяЧасть.Ключ + """"); 
				Иначе
					МассивСообщений.Добавить("Необходимо заполнить проект " + ПолучитьПредставлениеСтраницы(ДокументОбъект) + ".");	
				КонецЕсли;
				ЗаполненоВТаблицеПроектов = Ложь;
				Продолжить;
			КонецЕсли;
			
			Если НЕ ДействующийСтатус(проект) Тогда
				Если РедактируетсяСписком(ДокументОбъект, ТЧ) Тогда 
					МассивСообщений.Добавить("В строке № " + Строка(строка.НомерСтроки) + " табличной части """ + табличнаяЧасть.Ключ + """, необходимо указать проект в статусе ""Действует"" или ""Согласован"". ");
				Иначе
					МассивСообщений.Добавить("Необходимо указать проект в статусе ""Действует"" или ""Согласован"" " + ПолучитьПредставлениеСтраницы(ДокументОбъект) + ". ");	
				КонецЕсли;				
				ЗаполненоВТаблицеПроектов = Ложь;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(проект.абс_РуководительПроекта) Тогда
				Если РедактируетсяСписком(ДокументОбъект, ТЧ) Тогда 
					МассивСообщений.Добавить("В проекте указанном в строке № " + Строка(строка.НомерСтроки) + " табличной части """ + табличнаяЧасть.Ключ + """, необходимо заполнить реквизит ""Руководитель проектов"". ");
				Иначе
					МассивСообщений.Добавить("В указанном проекте " + ПолучитьПредставлениеСтраницы(ДокументОбъект) + " необходимо заполнить реквизит ""Руководитель проектов"".");
				КонецЕсли;
				ЗаполненоВТаблицеПроектов = Ложь;
			КонецЕсли;

		КонецЦикла;	
	КонецЦикла;
		
КонецПроцедуры

Процедура ПроверитьЗаполнениеВЗЗ(ДокументОбъект, ЗаполненоВЗЗ, МассивСообщений)
	
	Если НЕ (ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АвансовыйОтчет")
		ИЛИ  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеТоваровУслуг")) Тогда 
		Возврат;	
	КонецЕсли;

	ИмяРеквизита = "абс_ЗакупочныйЗаказ";
	ЗЗ_Ссылка = ДокументОбъект[ИмяРеквизита];
	ТЧ = ЗЗ_Ссылка.РаспределениеПоПроектам;
	
	Если НЕ ЗначениеЗаполнено(ЗЗ_Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗЗ_Ссылка.Ресурс) Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТЧ.Количество() < 1 Тогда
		МассивСообщений.Добавить("В документе " + Строка(ДокументОбъект[ИмяРеквизита]) + " необходимо заполнить проект на закладке ""Аналитика"".");
		ЗаполненоВЗЗ = Ложь;
		Возврат;
	КонецЕсли;
	
	Для каждого строка из ТЧ Цикл 
		
		проект = строка.Проект;
		
		Если НЕ ЗначениеЗаполнено(проект) Тогда
			Если РедактируетсяСписком(ЗЗ_Ссылка, ТЧ) Тогда 
				МассивСообщений.Добавить("В документе " + ЗЗ_Ссылка + " не заполнен реквизит проект в строке № " + Строка(строка.НомерСтроки) + " табличной части ""Распределения по проектам""."); 
			Иначе
				МассивСообщений.Добавить("В документе " + ЗЗ_Ссылка + " необходимо заполнить проект " + ПолучитьПредставлениеСтраницы(ЗЗ_Ссылка) + ".");	
			КонецЕсли;
			ЗаполненоВЗЗ = Ложь;
			Продолжить;
		КонецЕсли;
		
		Если НЕ ДействующийСтатус(проект) Тогда
			Если РедактируетсяСписком(ЗЗ_Ссылка, ТЧ) Тогда 
				МассивСообщений.Добавить("В документе " + ЗЗ_Ссылка + " в строке № " + Строка(строка.НомерСтроки) + " табличной части ""Распределения по проектам"", необходимо указать проект в статусе ""Действует"" или ""Согласован"". ");
			Иначе
				МассивСообщений.Добавить("В документе " + ЗЗ_Ссылка + " необходимо указать проект в статусе ""Действует"" " + ПолучитьПредставлениеСтраницы(ЗЗ_Ссылка) + ". ");	
			КонецЕсли;				
			ЗаполненоВЗЗ = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(проект.абс_РуководительПроекта) Тогда
			Если РедактируетсяСписком(ЗЗ_Ссылка, ТЧ) Тогда 
				МассивСообщений.Добавить("В документе " + ЗЗ_Ссылка + " в проекте указанном в строке № " + Строка(строка.НомерСтроки) + " табличной части ""Распределения по проектам"", необходимо заполнить реквизит ""Руководитель проектов"". ");
			Иначе
				МассивСообщений.Добавить("В документе " + ЗЗ_Ссылка + " в указанном проекте " + ПолучитьПредставлениеСтраницы(ЗЗ_Ссылка) + " необходимо заполнить реквизит ""Руководитель проектов"".");
			КонецЕсли;
			ЗаполненоВЗЗ = Ложь;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


Функция ПолучитьТабличныеЧастиИРеквизитыСПроектом(ДокументОбъект)
	
	СтруктураТабличныхЧастей = Новый Структура;
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ТребованиеНакладная") Тогда 
		СтруктураТабличныхЧастей.Вставить("Материалы", "Проект");
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПринятиеКУчетуОС") Тогда 
		СтруктураТабличныхЧастей.Вставить("ОсновныеСредства", "Проект");
	КонецЕсли; 
	
	Возврат СтруктураТабличныхЧастей;
	
КонецФункции

Функция ПолучитьТабличныеЧастиПОПроектам(ДокументОбъект)
	
	СтруктураТабличныхЧастей = Новый Структура;
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПлатежноеПоручениеВходящее") Тогда 
		СтруктураТабличныхЧастей.Вставить("РасшифровкаПлатежа", "Проект");
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.абс_ЗакупочныйЗаказ") Тогда 
		СтруктураТабличныхЧастей.Вставить("РаспределениеПоПроектам", "Проект");
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.абс_СчетНаОплату") Тогда 
		СтруктураТабличныхЧастей.Вставить("РаспределениеПоПроектам", "Проект");
	КонецЕсли; 
	
	Возврат СтруктураТабличныхЧастей;
	
КонецФункции

Процедура ВывестиМассивОшибок(МассивСообщений)
	
	Для Каждого строка из МассивСообщений Цикл 
		ОбщегоНазначения.Сообщение(строка);	
	КонецЦикла;
	
КонецПроцедуры

Функция РезультатЗаполнения(ДокументОбъект, ЗаполненоВШапке, ЗаполненоВТаблицах, ЗаполненоВТаблицеПроектов, ЗаполненоВЗЗ)
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ТребованиеНакладная") Тогда 
		Возврат ЗаполненоВШапке ИЛИ ЗаполненоВТаблицах;
	КонецЕсли;
			
	Возврат ЗаполненоВШапке И ЗаполненоВТаблицах И ЗаполненоВТаблицеПроектов И ЗаполненоВЗЗ;
		
КонецФункции

Функция РедактируетсяСписком(ДокументОбъект, ТабличнаяЧасть)
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПлатежноеПоручениеВходящее") Тогда  
		Возврат ТабличнаяЧасть.Количество() > 1 Или ПараметрыСеанса.абс_ПользовательДЗО;
	ИначеЕсли  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.абс_СчетНаОплату") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат ТабличнаяЧасть.Количество() > 1
	
КонецФункции

Функция НеобходимостьПроверкиОтСтатуса(ДокументОбъект)
		
	Если (ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АвансовыйОтчет")
		ИЛИ  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПолучениеУслугПоПереработке")
		ИЛИ  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ТребованиеНакладная")
		ИЛИ  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеТоваровУслуг")
		ИЛИ  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.РеализацияТоваровУслуг")
		ИЛИ  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АктОбОказанииПроизводственныхУслуг")) Тогда 
		
		Если ДокументОбъект.Ссылка.абс_Статус <> Перечисления.абс_СтатусыПервичныхДокументов.Подготовка Тогда 
			Возврат Ложь;
		КонецЕсли;
		
		Если ДокументОбъект.абс_Статус = Перечисления.абс_СтатусыПервичныхДокументов.Подготовка Тогда 
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.абс_ЗакупочныйЗаказ") Тогда
			
		Если ДокументОбъект.Ссылка.Статус = Перечисления.абсСтатусЗакупочногоЗаказа.ИзменениеСпецификации
			И (ДокументОбъект.Статус = Перечисления.абсСтатусЗакупочногоЗаказа.Согласован
			ИЛИ (ДокументОбъект.Статус = Перечисления.абсСтатусЗакупочногоЗаказа.Исполнение)) Тогда 
			Возврат Истина;
		КонецЕсли;
	
		Если ДокументОбъект.Ссылка.Статус <> Перечисления.абсСтатусЗакупочногоЗаказа.Подготовка Тогда 
			Возврат Ложь;
		КонецЕсли;
		
		Если ДокументОбъект.Статус = Перечисления.абсСтатусЗакупочногоЗаказа.Подготовка Тогда 
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли  ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.абс_СчетНаОплату") Тогда
				
		Если ДокументОбъект.Ссылка.СтатусСчета <> Перечисления.абсСтатусыСчетов.Подготовка Тогда 
			Возврат Ложь;
		КонецЕсли;
		
		Если ДокументОбъект.СтатусСчета = Перечисления.абсСтатусыСчетов.Подготовка Тогда 
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьПредставлениеСтраницы(Документ)
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ПлатежноеПоручениеВходящее") Тогда 
		Возврат "";		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументОбъект.абс_ЗакупочныйЗаказ")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.абс_ЗакупочныйЗаказ")
		ИЛИ ТипЗнч(Документ) = Тип("ДокументОбъект.абс_СчетНаОплату")Тогда 
		Возврат "на закладке ""Аналитика""";	
	КонецЕсли;
	
	Возврат "";	
	
КонецФункции

Функция ДействующийСтатус(проект)
	
	Если проект.абс_Статус = Перечисления.абс_СтатусыПроектов.Действует 
		ИЛИ  проект.абс_Статус = Перечисления.абс_СтатусыПроектов.Согласован Тогда 
		Возврат Истина;	
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции




