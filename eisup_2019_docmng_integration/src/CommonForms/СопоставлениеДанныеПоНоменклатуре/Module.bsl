
////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ЭлектронныйДокумент") Тогда
		ЭлектронныйДокумент = Параметры.ЭлектронныйДокумент;
	КонецЕсли;
	
	Если Параметры.Свойство("НеОткрыватьФормуПриОтсутствииНесопоставленнойНоменклатуры") Тогда
		НеОткрыватьФормуПриОтсутствииНесопоставленнойНоменклатуры = Параметры.НеОткрыватьФормуПриОтсутствииНесопоставленнойНоменклатуры;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
		ТекстСообщения = НСтр("ru = 'Не выбран электронный документ'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЭлектронныйДокумент", , Отказ); 
		Возврат;
	КонецЕсли;
	
	ПрочитатьНоменклатуруКонтрагентаСервер();
	
	Если НеОткрыватьФормуПриОтсутствииНесопоставленнойНоменклатуры 
		И НесопоставленнаяНоменклатураКонтрагента.Количество() = 0 Тогда
		// Форму открывать не требуется
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НесопоставленнаяНоменклатураКонтрагентаПриИзменении(Элемент)
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД

&НаКлиенте
Процедура ПрочитатьНоменклатуруКонтрагента(Команда)
	
	Если ЭтаФорма.Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru = 'Несохраненные изменения будут утеряны.
			|Продолжить?'");
		
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, "Заполнение списка номенклатуры") <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
		
	ПрочитатьНоменклатуруКонтрагентаСервер();
	
	ЭтаФорма.Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНоменклатуруКонтрагента(Команда)
	
	Отказ = Ложь;
	ЗаписатьНоменклатуруКонтрагентаСервер(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЕРВЕРНЫЕ ПРОЦЕДУРЫ ЧТЕНИЯ-ЗАПИСИ ДАННЫХ

&НаСервере
Процедура ПрочитатьНоменклатуруКонтрагентаСервер()
	
	МассивЭД = Новый Массив;
	МассивЭД.Добавить(ЭлектронныйДокумент);
	
	ТаблицаИнформацияОТоваре = ЭлектронныеДокументыФорматОбмена.ПолучитьИнформациюОТоваре(МассивЭД);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаИнформацияОТоваре.Ид КАК Идентификатор,
	|	ТаблицаИнформацияОТоваре.Артикул КАК АртикулНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.Наименование КАК НаименованиеНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.БазоваяЕдиницаКод КАК БазоваяЕдиницаКод,
	|	ТаблицаИнформацияОТоваре.Описание КАК Описание
	|ПОМЕСТИТЬ ТаблицаИнформацияОТоваре
	|ИЗ
	|	&ТаблицаИнформацияОТоваре КАК ТаблицаИнформацияОТоваре
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИнформацияОТоваре.Идентификатор,
	|	ТаблицаИнформацияОТоваре.АртикулНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.НаименованиеНоменклатурыКонтрагента,
	|	ЕСТЬNULL(КлассификаторЕдиницИзмерения.Ссылка, НЕОПРЕДЕЛЕНО) КАК ЕдиницаНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.Описание
	|ИЗ
	|	ТаблицаИнформацияОТоваре КАК ТаблицаИнформацияОТоваре
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|	ПО 
	|		ТаблицаИнформацияОТоваре.Идентификатор = НоменклатураКонтрагентов.Идентификатор
	|		ИЛИ ТаблицаИнформацияОТоваре.АртикулНоменклатурыКонтрагента = НоменклатураКонтрагентов.АртикулНоменклатурыКонтрагента
	|		ИЛИ ТаблицаИнформацияОТоваре.АртикулНоменклатурыКонтрагента = НоменклатураКонтрагентов.КодНоменклатурыКонтрагента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.КлассификаторЕдиницИзмерения КАК КлассификаторЕдиницИзмерения
	|	ПО 
	|		ТаблицаИнформацияОТоваре.БазоваяЕдиницаКод = КлассификаторЕдиницИзмерения.Код
	|	
	|ГДЕ
	|	НоменклатураКонтрагентов.Номенклатура ЕСТЬ NULL";
	Запрос.УстановитьПараметр("ТаблицаИнформацияОТоваре", ТаблицаИнформацияОТоваре);
	
	НесопоставленнаяНоменклатураКонтрагента.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНоменклатуруКонтрагентаСервер(Отказ = Ложь)
	
	НаборЗаписей = РегистрыСведений.НоменклатураКонтрагентов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Контрагент.Установить(ЭлектронныйДокумент.Контрагент);
	
	Для Каждого Строка Из НесопоставленнаяНоменклатураКонтрагента Цикл
		
		Если ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Строка);
			
			Запись.Контрагент = ЭлектронныйДокумент.Контрагент;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НаборЗаписей.Количество() > 0 Тогда
		Попытка
			НаборЗаписей.Записать(Ложь);
		Исключение
			ttk_ОбщегоНазначения.СообщитьОбОшибке(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), Отказ);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры
